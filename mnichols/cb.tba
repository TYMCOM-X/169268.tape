  100 REM THIS PROGRAM WILL PLAY "CHARITY BINGO", A GAME PATTERNED AFTER &
          A CONTEST RECEIVED IN THE MAIL.
  101 GOSUB 10000 ! IS USER ON A 425?
  102 GOSUB 11000 ! DOES USER NEED INSTRUCTIONS?
  105 POS$=POS$+CHAR(X) FOR X=32 TO 111
  107 X=RND(-1) ! RANDOMIZE USING TIMER
  150 STRING CARD.HEAD.ADDR(8), CARD.NUM.ADDR(8,5,5), RUBOFF.ADDR(10,5)
  155 INTEGER BINGO.NUMS(8,5,5), RUBOFF.VALUE(10,5), CALLED.NUMS(50), &
              DIRECTION.CTL
  160 GOSUB 500 ! DEFINE TERMINAL VIDEO CONTROL STRINGS
  162 GOSUB 400 ! BUILD SCREEN ADDRESS ARRAYS
  165 GOSUB 600 ! BUILD NEW BINGO CARDS
  170 GOSUB 700 ! DISPLAY BINGO CARDS ON SCREEN
  175 GOSUB 800 ! BUILD NEW RUBOFF CARD ARRAY
  180 GOSUB 900 ! DISPLAY CLEAN RUBOFF CARD ON SCREEN
  185 GOSUB 1000 ! PLAY THE GAME
  190 END ""

  400 CARD.NUM=0
  402 FOR ROW=12 TO 19 STEP 7
  405   FOR COL=1 TO 52 STEP 17
  407     CARD.NUM=CARD.NUM+1
  410     CARD.HEAD.ADDR(CARD.NUM)=SCRNPOS(ROW,COL)
  415   NEXT COL
  420 NEXT ROW
  425 FOR CARD.NUM=1 TO 4
  430   FOR ROW=1 TO 5
  435     FOR COL=1 TO 5
  440       CARD.NUM.ADDR(CARD.NUM,ROW,COL)= &
                SCRNPOS(ROW+12,((CARD.NUM-1)*17)+((COL-1)*3)+1)
  441     NEXT COL
  442   NEXT ROW
  443 NEXT CARD.NUM
  444 FOR CARD.NUM=5 TO 8
  445   FOR ROW=1 TO 5
  446     FOR COL=1 TO 5
  447       CARD.NUM.ADDR(CARD.NUM,ROW,COL)= &
                SCRNPOS(ROW+19,((CARD.NUM-5)*17)+((COL-1)*3)+1)
  448     NEXT COL
  449   NEXT ROW
  450 NEXT CARD.NUM
  451 FOR ROW=1 TO 10
  452   FOR COL=1 TO 5
  453     RUBOFF.ADDR(ROW,COL)=SCRNPOS(ROW,((COL-1)*5)+52)
  454   NEXT COL
  455 NEXT ROW
  460 RETURN

  500 CLS$=CHAR(27)+';'
  501 STD.VIDEO$=CHAR(27)+'G0'
  502 REV.VIDEO$=CHAR(27)+'G4'
  503 REV.UL.VIDEO$=CHAR(27)+'G5'
  504 SET.PROT.MODE$=CHAR(27)+'&'
  505 CLR.PROT.MODE$=CHAR(27)+"'"
  506 PAGE.ERASE$=CHAR(27)+'Y'
  507 REV.BL.VIDEO$=CHAR(27)+'G7'
  508 PAGE.TURN$=CHAR(27)+'J'
  509 BL.VIDEO$=CHAR(27)+'G2'
  510 LINE.ERASE$=CHAR(27)+'T'
  511 DARK$=CHAR(27)+')'
  512 LIGHT$=CHAR(27)+'('
  513 RETURN

  600 FOR CARD.NUM=1 TO 8
  601   CALLED.NUMS=ZER
  602   FOR COL=1 TO 5
  606     ON COL GOTO 610, 615, 620, 625, 630
  610     LOW=1
  611     HIGH=10
  612     GOTO 635
  615     LOW=11
  616     HIGH=20
  617     GOTO 635
  620     LOW=21
  621     HIGH=30
  622     GOTO 635
  625     LOW=31
  626     HIGH=40
  627     GOTO 635
  630     LOW=41
  631     HIGH=50
  635     FOR ROW=1 TO 5
  637       SELECTED.NUM=RNDRANGE(LOW,HIGH)
  639       IF CALLED.NUMS(SELECTED.NUM)=0 THEN
  641         CALLED.NUMS(SELECTED.NUM)=1
  643         BINGO.NUMS(CARD.NUM,ROW,COL)=SELECTED.NUM
  645       ELSE
  647         GOTO 637
  649       ENDIF
  650     NEXT ROW
  655   NEXT COL
  660 NEXT CARD.NUM
  665 BINGO.NUMS(CARD.NUM,3,3)=0 FOR CARD.NUM=1 TO 8 ! FREE SPACE
  670 RETURN

  700 PRINT CLS$
  701 FOR CARD.NUM=1 TO 8
  703   PRINT IN FORM "7X 8X 2Z 4X 3X": &
	      CARD.HEAD.ADDR(CARD.NUM)+REV.UL.VIDEO$, &
	      '    CARD', CARD.NUM, '    ', STD.VIDEO$
  706   FOR ROW=1 TO 5
  709     FOR COL=1 TO 5
  712     IF BINGO.NUMS(CARD.NUM,ROW,COL)<>0 THEN
  713       PRINT IN FORM "7X 2Z 3X": &
	      CARD.NUM.ADDR(CARD.NUM,ROW,COL)+REV.UL.VIDEO$, &
	      BINGO.NUMS(CARD.NUM,ROW,COL), &
	      STD.VIDEO$
  714     ELSE
  715       PRINT IN FORM "9X 2X 5X": &
              CARD.NUM.ADDR(CARD.NUM,ROW,COL)+REV.UL.VIDEO$+DARK$, &
	      "**", &
	      STD.VIDEO$+LIGHT$
  716     ENDIF
  717     NEXT COL
  718     PRINT SCRNPOS(1,1)
  719   NEXT ROW
  721 NEXT CARD.NUM
  730 RETURN

  800 FOR ROW=1 TO 10
  805   FOR COL=1 TO 5
  810     RUBOFF.VALUE(ROW,COL)=((COL-1)*10)+ROW
  815   NEXT COL
  820 NEXT ROW
  825 FOR X=1 TO 2000
  830   RNDROW1=RNDRANGE(1,10)
  835   RNDROW2=RNDRANGE(1,10)
  840   RNDCOL1=RNDRANGE(1,5)
  845   RNDCOL2=RNDRANGE(1,5)
  850   RUBOFF.VALUE(RNDROW1,RNDCOL1)==RUBOFF.VALUE(RNDROW2,RNDCOL2)
  855 NEXT X
  860 RETURN

  900 FOR ROW=1 TO 10
  905   FOR COL=1 TO 5
  910     PRINT IN FORM "9X '????' 6X": &
            RUBOFF.ADDR(ROW,COL)+REV.UL.VIDEO$+DARK$, STD.VIDEO$+LIGHT$
  915   NEXT COL
  917   PRINT SCRNPOS(1,1)
  920 NEXT ROW
  925 PRINT SCRNPOS(4,3)+"Total number of selections made"
  930 PRINT SCRNPOS(6,9)+"to reach Bingo:"
  935 RETURN

 1000 CUR.ROW=5
 1005 CUR.COL=3
 1010 IF GOT.A.BINGO=TRUE THEN 
 1011   PRINT SCRNPOS(1,1)+LIGHT$
 1012   RETURN
 1013 ENDIF
 1014 PRINT RUBOFF.ADDR(CUR.ROW,CUR.COL)+REV.BL.VIDEO$+SCRNPOS(1,1)
 1015 WAIT -1
 1020 OPEN "TERMINAL" FOR BINARY INPUT AS FILE 0
 1025 EIGHTI DIRECTION.CTL
 1030 CLOSE 0
 1035 IF DIRECTION.CTL > 127 THEN DIRECTION.CTL=DIRECTION.CTL-127
 1040 IF DIRECTION.CTL = 10 THEN ! DOWN
 1045   PRINT RUBOFF.ADDR(CUR.ROW,CUR.COL)+REV.UL.VIDEO$+SCRNPOS(1,1)
 1050   CUR.ROW=CUR.ROW+1
 1055   IF CUR.ROW=11 THEN CUR.ROW=1
 1060   GOTO 1010
 1065 ORIF DIRECTION.CTL = 11 THEN ! UP
 1070   PRINT RUBOFF.ADDR(CUR.ROW,CUR.COL)+REV.UL.VIDEO$+SCRNPOS(1,1)
 1075   CUR.ROW=CUR.ROW-1
 1080   IF CUR.ROW=0 THEN CUR.ROW=10
 1085   GOTO 1010
 1090 ORIF DIRECTION.CTL = 8 THEN ! LEFT
 1095   PRINT RUBOFF.ADDR(CUR.ROW,CUR.COL)+REV.UL.VIDEO$+SCRNPOS(1,1)
 1100   CUR.COL=CUR.COL-1
 1105   IF CUR.COL=0 THEN CUR.COL=5
 1110   GOTO 1010
 1115 ORIF DIRECTION.CTL = 12 THEN ! RIGHT
 1120   PRINT RUBOFF.ADDR(CUR.ROW,CUR.COL)+REV.UL.VIDEO$+SCRNPOS(1,1)
 1125   CUR.COL=CUR.COL+1
 1130   IF CUR.COL=6 THEN CUR.COL=1
 1135   GOTO 1010
 1140 ORIF DIRECTION.CTL = 13 THEN ! CARRIAGE RETURN
 1145   IF RUBOFF.VALUE(CUR.ROW,CUR.COL)= &
	   ABS(RUBOFF.VALUE(CUR.ROW,CUR.COL)) THEN
 1146     PICKED.NUMS=PICKED.NUMS+1
 1147     PRINT SCRNPOS(6,25):
 1148     PRINT IN IMAGE "%%": PICKED.NUMS
 1150     PRINT IN FORM "9X B 2Z B 4X": &
	    RUBOFF.ADDR(CUR.ROW,CUR.COL)+REV.UL.VIDEO$+LIGHT$, &
	    RUBOFF.VALUE(CUR.ROW,CUR.COL), SCRNPOS(1,1)
 1155     SELECTED.NUM=RUBOFF.VALUE(CUR.ROW,CUR.COL)
 1156     RUBOFF.VALUE(CUR.ROW,CUR.COL)=-SELECTED.NUM
 1160     IF SELECTED.NUM>= 1 AND SELECTED.NUM<=10 THEN CHECK.COL=1
 1165     IF SELECTED.NUM>=11 AND SELECTED.NUM<=20 THEN CHECK.COL=2
 1170     IF SELECTED.NUM>=21 AND SELECTED.NUM<=30 THEN CHECK.COL=3
 1175     IF SELECTED.NUM>=31 AND SELECTED.NUM<=40 THEN CHECK.COL=4
 1180     IF SELECTED.NUM>=41 AND SELECTED.NUM<=50 THEN CHECK.COL=5
 1185     FOR CARD.NUM=1 TO 8
 1190       FOR ROW=1 TO 5
 1195         IF BINGO.NUMS(CARD.NUM,ROW,CHECK.COL)=SELECTED.NUM THEN
 1200           PRINT IN FORM "9X 2Z 4X": &
		  CARD.NUM.ADDR(CARD.NUM,ROW,CHECK.COL)+REV.UL.VIDEO$+DARK$, &
		  SELECTED.NUM, SCRNPOS(1,1)
 1202           PRINT
 1205           BINGO.NUMS(CARD.NUM,ROW,CHECK.COL)=-SELECTED.NUM
 1207           GOSUB 2000 ! CHECK FOR BINGO ON THIS CARD
 1210         ENDIF
 1215       NEXT ROW
 1220     NEXT CARD.NUM
 1225   ENDIF
 1230 ENDIF
 1235 GOTO 1010


 2000 THIS.CARD.WINS=FALSE
 2002 IF (BINGO.NUMS(CARD.NUM,ROW,1)<=0 AND &
	  BINGO.NUMS(CARD.NUM,ROW,2)<=0 AND &
	  BINGO.NUMS(CARD.NUM,ROW,3)<=0 AND &
	  BINGO.NUMS(CARD.NUM,ROW,4)<=0 AND &
	  BINGO.NUMS(CARD.NUM,ROW,5)<=0) THEN
 2004   GOT.A.BINGO=TRUE
 2008   PRINT CARD.HEAD.ADDR(CARD.NUM)+REV.BL.VIDEO$
 2010 ENDIF
 2012 IF (BINGO.NUMS(CARD.NUM,1,CHECK.COL)<=0 AND &
	  BINGO.NUMS(CARD.NUM,2,CHECK.COL)<=0 AND &
	  BINGO.NUMS(CARD.NUM,3,CHECK.COL)<=0 AND &
	  BINGO.NUMS(CARD.NUM,4,CHECK.COL)<=0 AND &
	  BINGO.NUMS(CARD.NUM,5,CHECK.COL)<=0) THEN
 2014   GOT.A.BINGO=TRUE
 2018   PRINT CARD.HEAD.ADDR(CARD.NUM)+REV.BL.VIDEO$
 2020 ENDIF
 2022 IF (ROW=CHECK.COL OR ROW+CHECK.COL=6) THEN
 2024   IF (BINGO.NUMS(CARD.NUM,1,1)<=0 AND &
            BINGO.NUMS(CARD.NUM,2,2)<=0 AND &
	    BINGO.NUMS(CARD.NUM,3,3)<=0 AND &
	    BINGO.NUMS(CARD.NUM,4,4)<=0 AND &
	    BINGO.NUMS(CARD.NUM,5,5)<=0) THEN
 2026     GOT.A.BINGO=TRUE
 2028     PRINT CARD.HEAD.ADDR(CARD.NUM)+REV.BL.VIDEO$
 2030   ENDIF
 2032   IF (BINGO.NUMS(CARD.NUM,1,5)<=0 AND &
	    BINGO.NUMS(CARD.NUM,2,4)<=0 AND &
	    BINGO.NUMS(CARD.NUM,3,3)<=0 AND &
	    BINGO.NUMS(CARD.NUM,4,2)<=0 AND &
	    BINGO.NUMS(CARD.NUM,5,1)<=0) THEN
 2034     GOT.A.BINGO=TRUE
 2036     PRINT CARD.HEAD.ADDR(CARD.NUM)+REV.BL.VIDEO$
 2040   ENDIF
 2045 ENDIF
 2050 RETURN

10000 PRINT 'Are you running on a model 425 terminal':
10010 INPUT RESPONSE$
10020 IF LEFT(RESPONSE$,1)<>'Y' AND LEFT(RESPONSE$,1)<>'y' THEN
10030   PRINT
10040   PRINT 'This game makes extensive use of the reverse video'
10050   PRINT 'capabilities of the 425 terminal... Sorry!'
10060   END ''
10070 ELSE
10080   RETURN
10090 ENDIF

11000 PRINT
11010 PRINT 'Do you need instructions':
11020 INPUT RESPONSE$
11030 IF RESPONSE$='Y' OR RESPONSE$='y' THEN
11040   PRINT
11045   PRINT 'Turn on your printer and press <return>...':
11048   INPUT IN FORM 'R': RESPONSE$
11049   ON FILE -4 GOTO 11080
11050   COPY 'SCARNY.INS' TO 'TERM'
11055   NO FILE -4 
11056   PRINT
11057   PRINT 'Hit <return> to continue...':
11058   INPUT IN FORM 'R': RESPONSE$
11060 ENDIF
11070 RETURN
11080 PRINT 'The file containing the rules for Scarney Baccarat has been'
11090 PRINT 'removed from the system.  The game is not difficult to play'
11100 PRINT 'and the rules can probably be learned as you go if you care'
11110 PRINT 'to jump blindly into your first game.  Good luck!'
11120 RESUME 11055

30035 DEF STRING SCRNPOS(INTEGER(R,C))
30040   STRING ROW,COL
30045   ROW=SUBSTR(POS$,R,1)
30050   COL=SUBSTR(POS$,C,1)
30055   EXIT CHAR(27)+'='+ROW+COL
30060 ENDF SCRNPOS
30065 DEF INTEGER RNDRANGE(INTEGER(L,H))
30070   RANGE=H-L+1
30075   OFFSET=H-RANGE
30080   EXIT INT((RND*RANGE)+OFFSET+1)
30085 ENDF RNDRANGE
   