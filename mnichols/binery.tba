1000 POS$=POS$+CHAR(X) FOR X=32 TO 111
1002 ON ESCAPE GOTO 1068
1005 X=RND(-1)
1010 INTEGER LOTTO.BALLS(49), SELECTED.BALLS(49), BONUS.NUMBER, &
             BOARD.NUMBERS(5,6), BOARDS(5,50), &
             DIRECTION.CTL, GAME.COUNT(5), MONEY.WINNERS(5)
1015 DOUBLE THRSHLD.50(5), WINNERS(5), LOSERS(5), CALC.TABLE(0:5), &
            CARRYOVER.AMTS(5), LEFT.OVERS, DIVIDED.AMT, TOTAL.PLAYERS
1020 STRING BOARD.ADDR(5,50)
1022 LOTTO.BALLS(X)=X FOR X=1 TO 49
1026 DATA 9692842, 1615473, 37568, 715, 39
1028 READ THRSHLD.50(X) FOR X=1 TO 5
1029 ! THRSHLD.50(X) is number of plays when there is a 50% chance of a winner
1030 DATA 1, 6, 258, 13545, 246820
1032 READ WINNERS(X) FOR X=1 TO 5
1034 DATA 13983815, 13983810, 13983558, 13970271, 13736996
1036 READ LOSERS(X) FOR X=1 TO 5
1045 GOSUB 4000 ! READ VIDEO CONTROL STRINGS
1047 OPEN (1,"TERM",IO)
1050 GOSUB 3000 ! PRINT SCREENS
1060 GOSUB 2000 ! GET PLAYERS NUMBER SELECTIONS
1062 GOSUB 5000 ! PICK WINNING NUMBERS AND DISPLAY PAYOFF AMOUNTS
1064 GOSUB 8000 ! DETERMINE NUMBER OF WINNERS AND DIVIDE THE MONEY
1066 GOTO 1060
1068 CLOSE 1
1070 END ''

2000 FOR CARD.NUM=1 TO 5
2005   FOR LOTTO.NUM=1 TO 49
2010     IF BOARDS(CARD.NUM, LOTTO.NUM)=1 THEN
2015       PRINT ON 1 IN FORM '6X 2Z': BOARD.ADDR(CARD.NUM, LOTTO.NUM)+DARK$: &
                 LOTTO.NUM
2020     ENDIF
2025     NEXT LOTTO.NUM
2030   NEXT CARD.NUM
2035 PRINT ON 1 IN FORM '88X': SCRNPOS(24,1)+BRIGHT$+SET.PROT.MODE$+ &
           "   Use the cursor keys to highlight your picks, "+ &
           "then press <return> to select"+CLR.PROT.MODE$
2040 BOARDS=ZER
2045 BOARD.NUMBERS=ZER
2050 SELECTED.BALLS=ZER
2055 PLAYERS.GAMES=0
2075 FOR CARD.NUM=1 TO 5
2080   PICKED.NUMS=0
2085   CUR.LOTTO.NUM=23
2090   IF CUR.LOTTO.NUM<>50 THEN
2095     IF BOARDS(CARD.NUM, CUR.LOTTO.NUM)=1 THEN
2100       PRINT ON 1 IN FORM '9X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
                 BRIGHT$+"**"+CHAR(8)
2105     ELSE
2110       PRINT ON 1 IN FORM '6X 2Z 1X':BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
                 BRIGHT$: CUR.LOTTO.NUM: CHAR(8)
2115     ENDIF
2120   ELSE
2121     IF LAST.FLIP=1 THEN
2122       PRINT ON 1 IN FORM '9X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
                 DARK$+'->'+CHAR(8)
2123     ELSE
2125       PRINT ON 1 IN FORM '9X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
                 BRIGHT$+"->"+CHAR(8)
2126     ENDIF
2130   ENDIF
2131   IF LAST.FLIP=1 THEN
2132     LAST.FLIP=0
2134     GOTO 2145
2135   ENDIF
2136   GOSUB 7000
2143   GOTO 2090
2145   NEXT CARD.NUM
2150 RETURN

3000 PRINT ON 1 IN FORM '16X': CHAR(27)+"-0  "+CHAR(27)+":"+CHAR(27)+"-1  "+ &
           CHAR(27)+":"+PAGE.TURN$
3010 PRINT ON 1 IN FORM '90X': SCRNPOS(1,1)+REV.UL.VIDEO$+DARK$+ &
           "       L O T T O    6 / 4 9"+ &
           "  ---  C A L I F O R N I A    L O T T E R Y        "+STD.VIDEO$
3015 FOR X=1 TO 5
3020   PRINT ON 1 IN FORM '24X': SCRNPOS(2,(X*16)-15)+REV.UL.VIDEO$+ &
             " $1 - BOARD "+SUBSTR("ABCDE",X,1)+" "+STD.VIDEO$
3025   NEXT X
3030 FOR ROW=3 TO 23 STEP 20
3035   FOR COL=1 TO 65 STEP 16
3040     PRINT ON 1 IN FORM '20X': SCRNPOS(ROW,COL)+"+--------------+"
3045     NEXT COL
3050   NEXT ROW
3055 FOR ROW=5 TO 21 STEP 2
3060   FOR COL=1 TO 65 STEP 16
3065     PRINT ON 1 IN FORM '20X': SCRNPOS(ROW,COL)+"|--|--|--|--|--|"
3070     NEXT COL
3075   NEXT ROW
3080 FOR ROW=4 TO 22 STEP 2
3085   FOR COL=1 TO 65 STEP 16
3090     PRINT ON 1 IN FORM '20X': SCRNPOS(ROW,COL)+"|  |  |  |  |  |"
3095     NEXT COL
3100   NEXT ROW
3115 FOR ROW=4 TO 22 STEP 2
3120   FOR COL=2 TO 14 STEP 3
3122     FOR REPS=0 TO 4
3124       IF ROW=22 AND COL=14 THEN
3126         PRINT ON 1 IN FORM '6X': SCRNPOS(ROW,(REPS*16)+COL)+"->"
3128       ELSE
3130         PRINT ON 1 IN FORM '4X 2Z': SCRNPOS(ROW,(REPS*16)+COL), &
                 ((ROW-4)/2)*5+(COL+1)/3
3133       ENDIF
3134       BOARD.ADDR(REPS+1,((ROW-4)/2)*5+(COL+1)/3)= &
             SCRNPOS(ROW,(REPS*16)+COL)
3135       NEXT REPS
3140     NEXT COL
3145   NEXT ROW
3155 PRINT ON 1 IN FORM '4X': PAGE.TURN$+BRIGHT$
3160 PRINT ON 1 IN FORM '43X': SCRNPOS(1,23)+REV.VIDEO$+ &
           '   LOTTO 6/49  WINNING NUMBERS   '+STD.VIDEO$
3165 PRINT ON 1 IN FORM '43X': SCRNPOS(5,23)+REV.VIDEO$+ &
           '        BONUS NUMBER:            '+STD.VIDEO$
3170 FOR ROW=2 TO 4
3175   FOR COL=23 TO 55 STEP 32
3180     PRINT ON 1 IN FORM '11X': SCRNPOS(ROW,COL)+REV.VIDEO$+' '+STD.VIDEO$
3185     NEXT COL
3190   NEXT ROW
3195 FOR COL=25 TO 50 STEP 5
3200   PRINT ON 1 IN FORM '7X': SCRNPOS(3,COL)+REV.UL.VIDEO$
3205   NEXT COL
3210 FOR ROW=7 TO 11
3215   PRINT ON 1 IN FORM '26X': SCRNPOS(ROW,17)+'GAME '+ &
             SUBSTR('ABCDE',ROW-6,1)+' -'+SCRNPOS(ROW,56)+ &
             '-->   of 6'
3220   NEXT ROW
3225 FOR ROW=7 TO 11
3230   FOR COL=25 TO 50 STEP 5
3235     PRINT ON 1 IN FORM '14X': SCRNPOS(ROW,COL)+REV.UL.VIDEO$+ &
               '    '+STD.VIDEO$
3240     NEXT COL
3245   NEXT ROW
3250 PRINT ON 1 IN FORM '60X': SCRNPOS(13,1)+REV.UL.VIDEO$+RPT(' ',24)+ &
           '6/49 PRIZE PAYOFF INFORMATION'
3255 PRINT ON 1 IN FORM '42X': SCRNPOS(15,2)+'Games this week:'+ &
           REV.UL.VIDEO$+'              '+STD.VIDEO$
3260 FOR ROW=17 TO 21
3265   PRINT ON 1 IN FORM '75X': SCRNPOS(ROW,18)+REV.UL.VIDEO$+ &
             '$             '+STD.VIDEO$+' / '+REV.UL.VIDEO$+'       '+ &
             STD.VIDEO$+'winners  = '+REV.UL.VIDEO$+'$             '+ &
             STD.VIDEO$+'each'
3270   NEXT ROW
3275 FOR ROW=17 TO 21
3280   ON ROW-16 GOTO 3285,3295,3305,3315,3325
3285   LABEL$='All 6 Numbers:'
3290   GOTO 3330
3295   LABEL$='Any 5 + Bonus:'
3300   GOTO 3330
3305   LABEL$='Any 5 Numbers:'
3310   GOTO 3330
3315   LABEL$='Any 4 Numbers:'
3320   GOTO 3330
3325   LABEL$='Any 3 Numbers:'
3330   PRINT ON 1 IN FORM '18X': SCRNPOS(ROW,4)+LABEL$
3335   NEXT ROW
3340 PRINT ON 1 IN FORM '18X': SCRNPOS(22,19)+'--------------'
3345 PRINT ON 1 IN FORM '36X': SCRNPOS(23,6)+'Total Pool:'+REV.UL.VIDEO$+ &
           '$              '+STD.VIDEO$
3350 PRINT ON 1 IN FORM '2X': PAGE.TURN$
3355 RETURN

4000 CLS$=CHAR(27)+';'
4010 STD.VIDEO$=CHAR(27)+'G0'
4020 REV.VIDEO$=CHAR(27)+'G4'
4030 REV.UL.VIDEO$=CHAR(27)+'G5'
4040 SET.PROT.MODE$=CHAR(27)+'&'
4050 CLR.PROT.MODE$=CHAR(27)+"'"
4060 PAGE.ERASE$=CHAR(27)+'Y'
4070 REV.BL.VIDEO$=CHAR(27)+'G7'
4080 PAGE.TURN$=CHAR(27)+'J'
4090 BL.VIDEO$=CHAR(27)+'G2'
4100 LINE.ERASE$=CHAR(27)+'T'
4110 BRIGHT$=CHAR(27)+'('
4120 DARK$=CHAR(27)+')'
4125 HOME$=CHAR(30)
4130 SET.BLOCK$=CHAR(27)+'B'
4135 SET.CONV$=CHAR(27)+'C'
4140 RETURN

5000 ! COPY NUMBERS ON SCREEN 1 TO BOXES ON SCREEN 2
5001 PRINT ON 1 IN FORM '2X': PAGE.TURN$
5002 GOSUB 6000 ! CLEAR OUT OLD NUMBERS FIRST
5005 FOR CARD.NUM=1 TO 5
5010   NEXT.NUM=1
5015   FOR LOTTO.NUM=1 TO 49
5020     IF BOARDS(CARD.NUM, LOTTO.NUM)=1 THEN
5025       BOARD.NUMBERS(CARD.NUM, NEXT.NUM)=LOTTO.NUM
5030       PRINT ON 1 IN FORM '7X B 2Z': SCRNPOS(CARD.NUM+6,(NEXT.NUM*5)+20)+ &
                 REV.UL.VIDEO$: LOTTO.NUM
5035       NEXT.NUM=NEXT.NUM+1
5040     ENDIF
5045     NEXT LOTTO.NUM
5050   NEXT CARD.NUM
5055 ! SCRAMBLE THE LOTTO BALLS
5060 X=INT(RND*20)+1
5065 FOR Y=1 TO 500+X
5070   X1=INT(RND*49)+1
5075   X2=INT(RND*49)+1
5080   LOTTO.BALLS(X1)==LOTTO.BALLS(X2)
5085   NEXT Y
5090 ! SHOW THE BALLS SCRAMBLING + PICK THE WINNING NUMBERS
5092 PRINT ON 1 IN FORM '6X': SCRNPOS(5,47)+"  "
5095 X=INT(RND*7)+1
5100 FOR Y=1 TO 20+X
5105   SELECTED.BALLS=ZER
5110   FOR X=1 TO 6
5115     PICK=INT(RND*49)+1
5120     IF SELECTED.BALLS(LOTTO.BALLS(PICK))=1 THEN GOTO 5115
5125     SELECTED.BALLS(LOTTO.BALLS(PICK))=1
5130     PRINT ON 1 IN FORM '4X 2Z': SCRNPOS(3,(X*5)+22): LOTTO.BALLS(PICK)
5135     NEXT X
5140   NEXT Y
5145 PICK=INT(RND*49)+1
5150 IF SELECTED.BALLS(LOTTO.BALLS(PICK))=1 THEN GOTO 5145
5155 BONUS.NUMBER=LOTTO.BALLS(PICK)
5160 PRINT ON 1 IN FORM '4X 2Z': SCRNPOS(5,47): BONUS.NUMBER
5165 ! BLINK THE NUMBERS SELECTED THAT MATCH
5170 FOR X=1 TO 5
5175   GAME.COUNT=ZER
5180   FOR Y=1 TO 6
5183     IF BOARD.NUMBERS(X,Y)=0 THEN GOTO 5200
5185     IF SELECTED.BALLS(BOARD.NUMBERS(X,Y))=1 THEN
5190       PRINT ON 1 IN FORM '7X': SCRNPOS(X+6,(Y*5)+20)+REV.BL.VIDEO$
5192       GAME.COUNT(X)=GAME.COUNT(X)+1
5195     ENDIF
5200     NEXT Y
5205   PRINT ON 1 IN FORM '4X 1D': SCRNPOS(X+6,60): GAME.COUNT(X)
5210   NEXT X
5215 ! CALCULATE TOTAL PLAYERS FOR THE WEEK & UPDATE $ TOTALS
5220 TOTAL.PLAYERS=INT(9000000+((WEEKS.CARRIED+1.5)^1.5* &
                            (INT(RND*150000)+300000))+PLAYERS.GAMES)
5222 PRINT ON 1 IN FORM '4X ZZ,ZZZ,ZZZ': SCRNPOS(15,20): TOTAL.PLAYERS
5225 CARRYOVER.AMTS(1)=CARRYOVER.AMTS(1)+DBL(TOTAL.PLAYERS/2D0*.4D0)
5230 CARRYOVER.AMTS(2)=CARRYOVER.AMTS(2)+DBL(TOTAL.PLAYERS/2D0*.2135D0)
5235 CARRYOVER.AMTS(3)=CARRYOVER.AMTS(3)+DBL(TOTAL.PLAYERS/2D0*.11D0)
5240 CARRYOVER.AMTS(4)=CARRYOVER.AMTS(4)+DBL(TOTAL.PLAYERS/2D0*.10D0)
5245 CARRYOVER.AMTS(5)=CARRYOVER.AMTS(5)+DBL(TOTAL.PLAYERS/2D0*.1765D0)
5250 FOR X=1 TO 5
5255   PRINT ON 1 IN FORM '4X $ZZ,ZZZ,ZZZ.DD': SCRNPOS(X+16,19): &
             CARRYOVER.AMTS(X)
5260   NEXT X
5265 PRINT ON 1 IN FORM '4X ZZZ,ZZZ,ZZZ.DD': SCRNPOS(23,19): &
           SUM(CARRYOVER.AMTS(X) FOR X=1 TO 5)
5270 PRINT ON 1 IN FORM 'X': HOME$
5275 RETURN

6000 FOR CARD.NUM=1 TO 5
6015   FOR LOTTO.PICK=1 TO 6
6020     IF BOARD.NUMBERS(CARD.NUM, LOTTO.PICK)<>0 THEN
6025       BOARD.NUMBERS(CARD.NUM, LOTTO.PICK)=0
6030       PRINT ON 1 IN FORM '10X': SCRNPOS(CARD.NUM+6,(LOTTO.PICK*5)+20)+ &
                 REV.UL.VIDEO$+"   "
6035     ENDIF
6045     NEXT LOTTO.PICK
6050   NEXT CARD.NUM
6055 RETURN

7000 WAIT -1
7005 OPEN "TERMINAL" FOR BINARY INPUT AS FILE 0
7010 EIGHTI DIRECTION.CTL
7015 CLOSE 0
7020 IF DIRECTION.CTL > 127 THEN DIRECTION.CTL=DIRECTION.CTL-127
7025 IF DIRECTION.CTL = 10 THEN ! DOWN
7030   GOSUB 7500
7035   CUR.LOTTO.NUM=CUR.LOTTO.NUM+5
7040   IF CUR.LOTTO.NUM>50 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM-50
7050 ORIF DIRECTION.CTL = 11 THEN ! UP
7055   GOSUB 7500
7060   CUR.LOTTO.NUM=CUR.LOTTO.NUM-5
7065   IF CUR.LOTTO.NUM<1 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM+50
7075 ORIF DIRECTION.CTL = 8 THEN ! LEFT
7080   GOSUB 7500
7085   CUR.LOTTO.NUM=CUR.LOTTO.NUM-1
7090   IF CUR.LOTTO.NUM<1 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM+50
7100 ORIF DIRECTION.CTL = 12 THEN ! RIGHT
7105   GOSUB 7500
7110   CUR.LOTTO.NUM=CUR.LOTTO.NUM+1
7115   IF CUR.LOTTO.NUM>50 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM-50
7125 ORIF DIRECTION.CTL = 13 THEN ! CARRIAGE RETURN
7130   IF CUR.LOTTO.NUM=50 THEN 
7131     IF PICKED.NUMS=6 OR PICKED.NUMS=0 THEN
7132       LAST.FLIP=1
7133       RETURN
7134     ELSE
7135       PRINT ON 1 IN FORM '81X': SCRNPOS(24,1)+LINE.ERASE$+ &
                 "     This board must have either 0 or 6 numbers selected "+ &
   "before advancing"
7136       RETURN
7137     ENDIF
7138   ENDIF
7139   IF BOARDS(CARD.NUM, CUR.LOTTO.NUM)=0 THEN
7140     PICKED.NUMS=PICKED.NUMS+1
7145     GOSUB 7600
7150     BOARDS(CARD.NUM, CUR.LOTTO.NUM)=1
7155   ELSE
7160     PICKED.NUMS=PICKED.NUMS-1
7165     GOSUB 7500
7170     BOARDS(CARD.NUM, CUR.LOTTO.NUM)=0
7175   ENDIF
7180 ORIF DIRECTION.CTL = 30 THEN ! HOME
7185   GOSUB 7500
7190   CUR.LOTTO.NUM=50
7200 ENDIF
7205 RETURN

7500 IF CUR.LOTTO.NUM<>50 THEN
7502   IF BOARDS(CARD.NUM, CUR.LOTTO.NUM)=1 THEN
7504     GOTO 7525
7506   ELSE
7508     PRINT ON 1 IN FORM '6X 2Z 1X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
               DARK$: CUR.LOTTO.NUM: CHAR(8)
7510   ENDIF
7512 ELSE
7515   PRINT ON 1 IN FORM '9X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
             DARK$+"->"+CHAR(8)
7520 ENDIF
7525 RETURN
7600 IF CUR.LOTTO.NUM<>50 THEN
7605   PRINT ON 1 IN FORM '6X 2Z 1X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
             BRIGHT$+"**"+CHAR(8)
7610 ELSE
7615   PRINT ON 1 IN FORM '9X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM)+ &
             DARK$+"  "+CHAR(8)
7620 ENDIF
7625 RETURN

8000 FOR GAME.LEVEL=1 TO 5
8002   MONEY.WINNERS(GAME.LEVEL)=0
8003   CARRY.OVER.FLAG=0
8004   GOSUB 8500 ! ADD PLAYERS' WINNING TICKETS TO MONEY.WINNERS(GAME.LEVEL)
8005   X1=TOTAL.PLAYERS DIV THRSHLD.50(GAME.LEVEL)
8010   LEFT.OVERS=INT(FP(TOTAL.PLAYERS / THRSHLD.50(GAME.LEVEL))* &
                  THRSHLD.50(GAME.LEVEL))
8015   IF X1>0 THEN 
8020     CALL CALCODDS(THRSHLD.50(GAME.LEVEL), GAME.LEVEL)
8025     FOR Y=1 TO X1
8030       Z=RND
8035       FOR Q=0 TO 5
8040         IF Z>=CALC.TABLE(Q) OR Q=5 THEN
8045           MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+Q
8047           GOTO 8060
8050         ENDIF
8055         NEXT Q
8060       NEXT Y
8065   ENDIF
8070   IF LEFT.OVERS>0 THEN
8075     CALL CALCODDS(LEFT.OVERS, GAME.LEVEL)
8080     Z=RND
8085     FOR Q=0 TO 5
8090       IF Z>=CALC.TABLE(Q) OR Q=5 THEN
8095         MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+Q
8100         GOTO 8115
8105       ENDIF
8110       NEXT Q
8115   ENDIF
8116   PRINT ON 1 IN FORM '4X ZZZ,ZZD': SCRNPOS(GAME.LEVEL+16,38): &
             MONEY.WINNERS(GAME.LEVEL)
8130   IF MONEY.WINNERS(GAME.LEVEL)=0 THEN
8135     DIVIDED.AMT=0
8140     CARRY.OVER.FLAG=1
8145   ELSE
8150     DIVIDED.AMT=CARRYOVER.AMTS(GAME.LEVEL)/MONEY.WINNERS(GAME.LEVEL)
8153     CARRYOVER.AMTS(GAME.LEVEL)=0
8155   ENDIF
8157   PRINT ON 1 IN FORM '4X $ZZ,ZZZ,ZZZ.DD': SCRNPOS(GAME.LEVEL+16,58): &
             DIVIDED.AMT
8160   NEXT GAME.LEVEL
8165 IF CARRY.OVER.FLAG=1 THEN
8170   WEEKS.CARRIED=WEEKS.CARRIED+1
8175 ELSE
8180   WEEKS.CARRIED=0
8185 ENDIF
8186 PRINT ON 1 IN FORM '67X': SCRNPOS(23,34)+ &
           "Please press <return> to play next weeks game"
8187 INPUT FROM 1 IN FORM 'R': XX$
8188 PRINT ON 1 IN FORM '2X': PAGE.TURN$
8190 RETURN

8500 ON GAME.LEVEL GOTO 8505, 8515, 8525, 8535, 8545
8505 NEED.TO.WIN=6
8510 GOTO 8550
8515 NEED.TO.WIN=5
8520 GOTO 8550
8525 NEED.TO.WIN=5
8530 GOTO 8550
8535 NEED.TO.WIN=4
8540 GOTO 8550
8545 NEED.TO.WIN=3
8550 FOR GAME.ID=1 TO 5
8555   IF GAME.LEVEL=2 THEN
8560     IF BOARDS(GAME.ID, BONUS.NUMBER)=1 AND &
            GAME.COUNT(GAME.ID)=NEED.TO.WIN THEN
8565       MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+1
8570     ENDIF
8575   ELSE
8580     IF GAME.COUNT(GAME.ID)=NEED.TO.WIN THEN
8585       MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+1
8590     ENDIF
8595   ENDIF
8600   NEXT GAME.ID
8605 RETURN

9000 DEF STRING SCRNPOS(INTEGER(R,C))
9005   STRING ROW,COL
9010   ROW=SUBSTR(POS$,R,1)
9015   COL=SUBSTR(POS$,C,1)
9020   EXIT CHAR(27)+'='+ROW+COL
9025 ENDF SCRNPOS
9030 DEF STRING GTFUNC
9035   INTEGER CHAR1, CHAR2, CHAR3
9037   WAIT -1
9040   OPEN 'TERMINAL' FOR BINARY INPUT AS FILE 0
9045   EIGHTI CHAR1, CHAR2, CHAR3
9047   CLOSE 0
9050   EXIT CHAR(CHAR2)
9055 ENDF GTFUNC

10000 DEF INTEGER CALCODDS(DOUBLE remndr, INTEGER win.level)
10005 ! REMNDR = portion of players less than amount needed to guarantee a
10010 !          50% chance of finding a winner for this win level
10015 ! WIN.LEVEL = integer in range of 1 through 5; 1 = 6 of 6, 2 = 5 + Bonus,
10020 !             3 = 5 of 6, 4 = 4 of 6, 5 = 3 of 6
10021   INTEGER X
10022   CALC.TABLE=ZER
10025   CALC.TABLE(0)=(LOSERS(win.level)/13983816D0)^remndr
10030   MAX.TABLE.ENTRY=MIN(5, remndr)
10035   FOR X=1 TO MAX.TABLE.ENTRY
10040     CALC.TABLE(X)=(LOSERS(win.level)/13983816D0)^DBL(remndr-X) * &
                        (WINNERS(win.level)/13983816D0)^DBL(X) * &
                        DBL((FACT(remndr,remndr-X))/(FACT(X,1)))
10045     NEXT X
10047   PREV.VALUE=1
10050   FOR X=0 TO MAX.TABLE.ENTRY
10055     CALC.TABLE(X)=PREV.VALUE-CALC.TABLE(X)
10060     PREV.VALUE=CALC.TABLE(X)
10065     NEXT X
10070 ENDF CALCODDS
11000 DEF DOUBLE FACT(INTEGER fact.num, small.num)
11005   DOUBLE FACTOR
11007   INTEGER X
11010   FACTOR = fact.num
11015   FOR X=FACTOR-1 TO small.num+1 STEP -1
11020     FACTOR = FACTOR*X
11025     NEXT X
11030   EXIT FACTOR
11035 ENDF FACT
    