1000 POS$=POS$+CHAR(X) FOR X=32 TO 111
1005 ON ESCAPE GOTO 1105
1010 X=RND(-1)
1015 INTEGER LOTTO.BALLS(49), SELECTED.BALLS(49), BONUS.NUMBER, &
             BOARD.NUMBERS(5,6), BOARDS(5,50), &
             DIRECTION.CTL, GAME.COUNT(5), MONEY.WINNERS(5)
1020 DOUBLE THRSHLD.50(5), WINNERS(5), LOSERS(5), CALC.TABLE(0:5), &
            CARRYOVER.AMTS(5), LEFT.OVERS, DIVIDED.AMT, TOTAL.PLAYERS, Z
1025 STRING BOARD.ADDR(5,50)
1030 LOTTO.BALLS(X)=X FOR X=1 TO 49
1035 DATA 9692842, 1615473, 38463, 715, 39
1040 READ THRSHLD.50(X) FOR X=1 TO 5
1045 ! THRSHLD.50(X) is number of plays when there is a 50% chance of a winner
1050 DATA 1, 6, 252, 13545, 246820
1055 READ WINNERS(X) FOR X=1 TO 5
1060 DATA 13983815, 13983810, 13983564, 13970271, 13736996
1065 READ LOSERS(X) FOR X=1 TO 5
1070 GOSUB 4000 ! READ VIDEO CONTROL STRINGS
1075 ! OPEN (1,"TERM",IO)
1080 GOSUB 3000 ! PRINT SCREENS
1085 GOSUB 2000 ! GET PLAYERS NUMBER SELECTIONS
1090 GOSUB 5000 ! PICK WINNING NUMBERS AND DISPLAY PAYOFF AMOUNTS
1092 CARRY.OVER.FLAG=0
1095 GOSUB 8000 ! DETERMINE NUMBER OF WINNERS AND DIVIDE THE MONEY
1100 GOTO 1085
1105 CLOSE 1
1110 PRINT HOME$+PAGE.ERASE$+PAGE.TURN$+HOME$+PAGE.ERASE$+BRIGHT$
1115 COPY "SYST.OUT" TO "TERMINAL"
1120 END ""


2000 FOR CARD.NUM=1 TO 5
2005   FOR LOTTO.NUM=1 TO 49
2010     IF BOARDS(CARD.NUM, LOTTO.NUM)=1 THEN
2015       PRINT IN FORM '4X 2X 2Z': &
                 BOARD.ADDR(CARD.NUM, LOTTO.NUM):DARK$:LOTTO.NUM
2020     ENDIF
2025     NEXT LOTTO.NUM
2030   NEXT CARD.NUM
2035 PRINT IN FORM '4X 2X 2X 48X 29X 2X': &
           SCRNPOS(24,1):BRIGHT$:SET.PROT.MODE$: &
           "   Use the cursor keys to highlight your picks, ": &
           "then press <return> to select":CLR.PROT.MODE$
2040 BOARDS=ZER
2045 BOARD.NUMBERS=ZER
2050 SELECTED.BALLS=ZER
2055 PLAYERS.GAMES=0
2060 FOR CARD.NUM=1 TO 5
2065   PICKED.NUMS=0
2070   CUR.LOTTO.NUM=23
2075   IF CUR.LOTTO.NUM<>50 THEN
2080     IF BOARDS(CARD.NUM, CUR.LOTTO.NUM)=1 THEN
2085       PRINT IN FORM '4X 2X 2X 1X': &
                 BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM):BRIGHT$:"**":CHAR(8)
2090     ELSE
2095       PRINT IN FORM '4X 2X 2Z 1X': &
                 BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM):BRIGHT$: &
                 CUR.LOTTO.NUM: CHAR(8)
2100     ENDIF
2105   ELSE
2110     IF LAST.FLIP=1 THEN
2115       PRINT IN FORM '4X 2X 2X 1X': &
                 BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM):DARK$:'->':CHAR(8)
2120     ELSE
2125       PRINT IN FORM '4X 2X 2X 1X': &
                 BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM):BRIGHT$:"->":CHAR(8)
2130     ENDIF
2135   ENDIF
2140   IF LAST.FLIP=1 THEN
2145     LAST.FLIP=0
2150     GOTO 2170
2155   ENDIF
2160   GOSUB 7000
2165   GOTO 2075
2170   NEXT CARD.NUM
2175 RETURN

3000 PRINT IN FORM '1X 4X 1X 1X 1X 4X 1X 1X 2X': &
           CHAR(27):"-0  ":CHAR(27):":":CHAR(27):"-1  ": &
           CHAR(27):":":PAGE.TURN$
3005 PRINT IN FORM '4X 3X 2X 27X 51X 3X': &
           SCRNPOS(1,1):REV.UL.VIDEO$:DARK$: &
           "       L O T T O    6 / 4 9": &
           "  ---  C A L I F O R N I A    L O T T E R Y        ":STD.VIDEO$
3010 FOR X=1 TO 5
3015   PRINT IN FORM '4X 3X 12X 1X 1X 3X': &
             SCRNPOS(2,(X*16)-15):REV.UL.VIDEO$: &
             " $1 - BOARD ":SUBSTR("ABCDE",X,1):" ":STD.VIDEO$
3020   NEXT X
3025 FOR ROW=3 TO 23 STEP 20
3030   FOR COL=1 TO 65 STEP 16
3035     PRINT IN FORM '4X 16X': SCRNPOS(ROW,COL):"+--------------+"
3040     NEXT COL
3045   NEXT ROW
3050 FOR ROW=5 TO 21 STEP 2
3055   FOR COL=1 TO 65 STEP 16
3060     PRINT IN FORM '4X 16X': SCRNPOS(ROW,COL):"|--|--|--|--|--|"
3065     NEXT COL
3070   NEXT ROW
3075 FOR ROW=4 TO 22 STEP 2
3080   FOR COL=1 TO 65 STEP 16
3085     PRINT IN FORM '4X 16X': SCRNPOS(ROW,COL):"|  |  |  |  |  |"
3090     NEXT COL
3095   NEXT ROW
3100 FOR ROW=4 TO 22 STEP 2
3105   FOR COL=2 TO 14 STEP 3
3110     FOR REPS=0 TO 4
3115       IF ROW=22 AND COL=14 THEN
3120         PRINT IN FORM '4X 2X': SCRNPOS(ROW,(REPS*16)+COL):"->"
3125       ELSE
3130         PRINT IN FORM '4X 2Z': SCRNPOS(ROW,(REPS*16)+COL): &
                 ((ROW-4)/2)*5+(COL+1)/3
3135       ENDIF
3140       BOARD.ADDR(REPS+1,((ROW-4)/2)*5+(COL+1)/3)= &
             SCRNPOS(ROW,(REPS*16)+COL)
3145       NEXT REPS
3150     NEXT COL
3155   NEXT ROW
3160 PRINT IN FORM '2X 2X': PAGE.TURN$:BRIGHT$
3165 PRINT IN FORM '4X 3X 33X 3X': SCRNPOS(1,23):REV.VIDEO$: &
           '   LOTTO 6/49  WINNING NUMBERS   ':STD.VIDEO$
3170 PRINT IN FORM '4X 3X 33X 3X': SCRNPOS(5,23):REV.VIDEO$: &
           '        BONUS NUMBER:            ':STD.VIDEO$
3175 FOR ROW=2 TO 4
3180   FOR COL=23 TO 55 STEP 32
3185     PRINT IN FORM '4X 3X 1X 3X': &
               SCRNPOS(ROW,COL):REV.VIDEO$:' ':STD.VIDEO$
3190     NEXT COL
3195   NEXT ROW
3200 FOR COL=25 TO 50 STEP 5
3205   PRINT IN FORM '4X 3X': SCRNPOS(3,COL):REV.UL.VIDEO$
3210   NEXT COL
3215 FOR ROW=7 TO 11
3220   PRINT IN FORM '4X 5X 1X 2X 4X 10X': SCRNPOS(ROW,17):'GAME ': &
             SUBSTR('ABCDE',ROW-6,1):' -':SCRNPOS(ROW,56): &
             '-->   of 6'
3225   NEXT ROW
3230 FOR ROW=7 TO 11
3235   FOR COL=25 TO 50 STEP 5
3240     PRINT IN FORM '4X 3X 4X 3X': SCRNPOS(ROW,COL):REV.UL.VIDEO$: &
               '    ':STD.VIDEO$
3245     NEXT COL
3250   NEXT ROW
3255 PRINT IN FORM '4X 3X 24X 29X': &
           SCRNPOS(13,1):REV.UL.VIDEO$:RPT(' ',24): &
           '6/49 PRIZE PAYOFF INFORMATION'
3260 PRINT IN FORM '4X 16X 3X 14X 3X': SCRNPOS(15,2):'Games this week:': &
           REV.UL.VIDEO$:'              ':STD.VIDEO$
3265 FOR ROW=17 TO 21
3270   PRINT IN FORM '4X 3X 14X 3X 3X 3X 7X 3X 11X 3X 14X 3X 4X': &
   SCRNPOS(ROW,18):REV.UL.VIDEO$: &
   '$             ':STD.VIDEO$:' / ':REV.UL.VIDEO$:'       ': &
   STD.VIDEO$:'winners  = ':REV.UL.VIDEO$:'$             ': &
   STD.VIDEO$:'each'
3275   NEXT ROW
3280 FOR ROW=17 TO 21
3285   ON ROW-16 GOTO 3290,3300,3310,3320,3330
3290   LABEL$='All 6 Numbers:'
3295   GOTO 3335
3300   LABEL$='Any 5 + Bonus:'
3305   GOTO 3335
3310   LABEL$='Any 5 Numbers:'
3315   GOTO 3335
3320   LABEL$='Any 4 Numbers:'
3325   GOTO 3335
3330   LABEL$='Any 3 Numbers:'
3335   PRINT IN FORM '4X 14X': SCRNPOS(ROW,4):LABEL$
3340   NEXT ROW
3345 PRINT IN FORM '4X 14X': SCRNPOS(22,19):'--------------'
3350 PRINT IN FORM '4X 11X 3X 15X 3X': &
           SCRNPOS(23,6):'Total Pool:':REV.UL.VIDEO$: &
           '$              ':STD.VIDEO$
3355 PRINT IN FORM '2X': PAGE.TURN$
3360 RETURN

4000 CLS$=CHAR(27)+';'
4005 STD.VIDEO$=CHAR(27)+'G0'
4010 REV.VIDEO$=CHAR(27)+'G4'
4015 REV.UL.VIDEO$=CHAR(27)+'G5'
4020 SET.PROT.MODE$=CHAR(27)+'&'
4025 CLR.PROT.MODE$=CHAR(27)+"'"
4030 PAGE.ERASE$=CHAR(27)+'Y'
4035 REV.BL.VIDEO$=CHAR(27)+'G7'
4040 PAGE.TURN$=CHAR(27)+'J'
4045 BL.VIDEO$=CHAR(27)+'G2'
4050 LINE.ERASE$=CHAR(27)+'T'
4055 BRIGHT$=CHAR(27)+'('
4060 DARK$=CHAR(27)+')'
4065 HOME$=CHAR(30)
4070 SET.BLOCK$=CHAR(27)+'B'
4075 SET.CONV$=CHAR(27)+'C'
4080 RETURN

5000 ! CALCULATE TOTAL PLAYERS FOR THE WEEK & UPDATE $ TOTALS
5002 PRINT IN FORM '2X': PAGE.TURN$
5005 TOTAL.PLAYERS=INT(7000000+((WEEKS.CARRIED+1.5)^1.85* &
          (INT(RND*150000)+300000))+PLAYERS.GAMES+INT(RND*1000000)-500000)
5010 PRINT IN FORM '4X ZZ,ZZZ,ZZZ': SCRNPOS(15,20): TOTAL.PLAYERS
5015 CARRYOVER.AMTS(1)=CARRYOVER.AMTS(1)+DBL(TOTAL.PLAYERS/2D0*.4D0)
5020 CARRYOVER.AMTS(2)=CARRYOVER.AMTS(2)+DBL(TOTAL.PLAYERS/2D0*.2135D0)
5025 CARRYOVER.AMTS(3)=CARRYOVER.AMTS(3)+DBL(TOTAL.PLAYERS/2D0*.11D0)
5030 CARRYOVER.AMTS(4)=CARRYOVER.AMTS(4)+DBL(TOTAL.PLAYERS/2D0*.10D0)
5035 CARRYOVER.AMTS(5)=CARRYOVER.AMTS(5)+DBL(TOTAL.PLAYERS/2D0*.1765D0)
5040 FOR X=1 TO 5
5045   PRINT IN FORM '4X $ZZ,ZZZ,ZZZ.DD 4X 7X 4X 13X': &
             SCRNPOS(X+16,19):CARRYOVER.AMTS(X):SCRNPOS(X+16,38): &
             '       ':SCRNPOS(X+16,59):'             '
5050   NEXT X
5055 PRINT IN FORM '4X ZZZ,ZZZ,ZZZ.DD': SCRNPOS(23,19): &
           SUM(CARRYOVER.AMTS(X) FOR X=1 TO 5)
5060 ! COPY NUMBERS ON SCREEN 1 TO BOXES ON SCREEN 2
5070 GOSUB 6000 ! CLEAR OUT OLD NUMBERS FIRST
5075 FOR CARD.NUM=1 TO 5
5080   NEXT.NUM=1
5085   FOR LOTTO.NUM=1 TO 49
5090     IF BOARDS(CARD.NUM, LOTTO.NUM)=1 THEN
5095       BOARD.NUMBERS(CARD.NUM, NEXT.NUM)=LOTTO.NUM
5100       PRINT IN FORM '4X 3X B 2Z': &
                 SCRNPOS(CARD.NUM+6,(NEXT.NUM*5)+20): &
                 REV.UL.VIDEO$: LOTTO.NUM
5105       NEXT.NUM=NEXT.NUM+1
5110     ENDIF
5115     NEXT LOTTO.NUM
5120   NEXT CARD.NUM
5125 ! SCRAMBLE THE LOTTO BALLS
5130 X=INT(RND*20)+1
5135 FOR Y=1 TO 500+X
5140   X1=INT(RND*49)+1
5145   X2=INT(RND*49)+1
5150   LOTTO.BALLS(X1)==LOTTO.BALLS(X2)
5155   NEXT Y
5160 ! SHOW THE BALLS SCRAMBLING + PICK THE WINNING NUMBERS
5165 PRINT IN FORM '4X 2X': SCRNPOS(5,47):"  "
5170 X=INT(RND*7)+1
5175 FOR Y=1 TO 8+X
5180   SELECTED.BALLS=ZER
5185   FOR X=1 TO 6
5190     PICK=INT(RND*49)+1
5195     IF SELECTED.BALLS(LOTTO.BALLS(PICK))=1 THEN GOTO 5190
5200     SELECTED.BALLS(LOTTO.BALLS(PICK))=1
5205     PRINT IN FORM '4X 2Z': SCRNPOS(3,(X*5)+22): LOTTO.BALLS(PICK)
5210     NEXT X
5215   NEXT Y
5220 PICK=INT(RND*49)+1
5225 IF SELECTED.BALLS(LOTTO.BALLS(PICK))=1 THEN GOTO 5220
5230 BONUS.NUMBER=LOTTO.BALLS(PICK)
5235 PRINT IN FORM '4X 2Z': SCRNPOS(5,47): BONUS.NUMBER
5240 ! BLINK THE NUMBERS SELECTED THAT MATCH
5245 FOR X=1 TO 5
5250   GAME.COUNT=ZER
5255   FOR Y=1 TO 6
5260     IF BOARD.NUMBERS(X,Y)=0 THEN GOTO 5285
5265     IF SELECTED.BALLS(BOARD.NUMBERS(X,Y))=1 THEN
5270       PRINT IN FORM '4X 3X': SCRNPOS(X+6,(Y*5)+20):REV.BL.VIDEO$
5275       GAME.COUNT(X)=GAME.COUNT(X)+1
5280     ENDIF
5285     NEXT Y
5290   PRINT IN FORM '4X 1D': SCRNPOS(X+6,60): GAME.COUNT(X)
5295   NEXT X
5300 PRINT IN FORM 'X': HOME$
5305 RETURN

6000 FOR CARD.NUM=1 TO 5
6005   FOR LOTTO.PICK=1 TO 6
6010     BOARD.NUMBERS(CARD.NUM, LOTTO.PICK)=0
6015     PRINT IN FORM '4X 3X 3X': &
               SCRNPOS(CARD.NUM+6,(LOTTO.PICK*5)+20): &
               REV.UL.VIDEO$:"   "
6020     NEXT LOTTO.PICK
6022   PRINT IN FORM '4X 1X':SCRNPOS(CARD.NUM+6,60):" "
6025   NEXT CARD.NUM
6030 RETURN

7000 WAIT -1
7005 OPEN "TERMINAL" FOR BINARY INPUT AS FILE 0
7010 EIGHTI DIRECTION.CTL
7015 CLOSE 0
7020 IF DIRECTION.CTL > 127 THEN DIRECTION.CTL=DIRECTION.CTL-127
7025 IF DIRECTION.CTL = 10 THEN ! DOWN
7030   GOSUB 7500
7035   CUR.LOTTO.NUM=CUR.LOTTO.NUM+5
7040   IF CUR.LOTTO.NUM>50 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM-50
7045 ORIF DIRECTION.CTL = 11 THEN ! UP
7050   GOSUB 7500
7055   CUR.LOTTO.NUM=CUR.LOTTO.NUM-5
7060   IF CUR.LOTTO.NUM<1 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM+50
7065 ORIF DIRECTION.CTL = 8 THEN ! LEFT
7070   GOSUB 7500
7075   CUR.LOTTO.NUM=CUR.LOTTO.NUM-1
7080   IF CUR.LOTTO.NUM<1 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM+50
7085 ORIF DIRECTION.CTL = 12 THEN ! RIGHT
7090   GOSUB 7500
7095   CUR.LOTTO.NUM=CUR.LOTTO.NUM+1
7100   IF CUR.LOTTO.NUM>50 THEN CUR.LOTTO.NUM=CUR.LOTTO.NUM-50
7105 ORIF DIRECTION.CTL = 13 THEN ! CARRIAGE RETURN
7110   IF CUR.LOTTO.NUM=50 THEN 
7115     IF PICKED.NUMS=6 OR PICKED.NUMS=0 THEN
7120       LAST.FLIP=1
7125       RETURN
7130     ELSE
7135       PRINT IN FORM '4X 2X 57X 16X': SCRNPOS(24,1):LINE.ERASE$: &
                 "     This board must have either 0 or 6 numbers selected ": &
                 "before advancing"
7140       RETURN
7145     ENDIF
7150   ENDIF
7155   IF BOARDS(CARD.NUM, CUR.LOTTO.NUM)=0 THEN
7160     PICKED.NUMS=PICKED.NUMS+1
7165     GOSUB 7600
7170     BOARDS(CARD.NUM, CUR.LOTTO.NUM)=1
7175   ELSE
7180     PICKED.NUMS=PICKED.NUMS-1
7185     GOSUB 7500
7190     BOARDS(CARD.NUM, CUR.LOTTO.NUM)=0
7195   ENDIF
7200 ORIF DIRECTION.CTL = 30 THEN ! HOME
7205   GOSUB 7500
7210   CUR.LOTTO.NUM=50
7212 ORIF DIRECCTION.CTL = 27 THEN ! ESCAPE
7213   GOTO 1105
7215 ENDIF
7220 RETURN

7500 IF CUR.LOTTO.NUM<>50 THEN
7505   IF BOARDS(CARD.NUM, CUR.LOTTO.NUM)=1 THEN
7510     GOTO 7545
7515   ELSE
7520     PRINT IN FORM '4X 2X 2Z 1X': &
               BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM): &
               DARK$: CUR.LOTTO.NUM: CHAR(8)
7525   ENDIF
7530 ELSE
7535   PRINT IN FORM '4X 2X 2X 1X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM): &
             DARK$:"->":CHAR(8)
7540 ENDIF
7545 RETURN
7600 IF CUR.LOTTO.NUM<>50 THEN
7605   PRINT IN FORM '4X 2X 2X 1X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM): &
             BRIGHT$:"**":CHAR(8)
7610 ELSE
7615   PRINT IN FORM '4X 2X 2X 1X': BOARD.ADDR(CARD.NUM, CUR.LOTTO.NUM): &
             DARK$:"  ":CHAR(8)
7620 ENDIF
7625 RETURN

8000 FOR GAME.LEVEL=1 TO 5
8005   MONEY.WINNERS(GAME.LEVEL)=0
8015   GOSUB 8500 ! ADD PLAYERS' WINNING TICKETS TO MONEY.WINNERS(GAME.LEVEL)
8020   X1=TOTAL.PLAYERS DIV THRSHLD.50(GAME.LEVEL)
8025   LEFT.OVERS=INT(FP(TOTAL.PLAYERS / THRSHLD.50(GAME.LEVEL))* &
                  THRSHLD.50(GAME.LEVEL))
8030   IF X1>0 THEN 
8035     CALL CALCODDS(THRSHLD.50(GAME.LEVEL), GAME.LEVEL)
8040     FOR Y=1 TO X1
8045       Z=DBL(RND)
8050       FOR Q=0 TO 5
8055         IF Z>=CALC.TABLE(Q) THEN
8065           GOTO 8080
8070         ENDIF
8075         NEXT Q
8080       MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+Q
8082       NEXT Y
8085   ENDIF
8090   IF LEFT.OVERS>0 THEN
8095     CALL CALCODDS(LEFT.OVERS, GAME.LEVEL)
8100     Z=DBL(RND)
8105     FOR Q=0 TO 5
8110       IF Z>=CALC.TABLE(Q) THEN
8120         GOTO 8135
8125       ENDIF
8130       NEXT Q
8135     MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+Q
8137   ENDIF
8140   PRINT IN FORM '4X ZZZ,ZZD': SCRNPOS(GAME.LEVEL+16,38): &
             MONEY.WINNERS(GAME.LEVEL)
8145   IF MONEY.WINNERS(GAME.LEVEL)=0 THEN
8150     DIVIDED.AMT=0
8155     CARRY.OVER.FLAG=1
8160   ELSE
8165     DIVIDED.AMT=CARRYOVER.AMTS(GAME.LEVEL)/MONEY.WINNERS(GAME.LEVEL)
8170     CARRYOVER.AMTS(GAME.LEVEL)=0
8175   ENDIF
8180   PRINT IN FORM '4X $ZZ,ZZZ,ZZZ.DD': SCRNPOS(GAME.LEVEL+16,58): &
             DIVIDED.AMT
8185   NEXT GAME.LEVEL
8190 IF CARRY.OVER.FLAG=1 THEN
8195   WEEKS.CARRIED=WEEKS.CARRIED+1
8200 ELSE
8205   WEEKS.CARRIED=0
8210 ENDIF
8215 PRINT IN FORM '4X 45X': SCRNPOS(15,34): &
           "Please press <return> to play next weeks game"
8220 INPUT IN FORM 'R': XX$
8225 PRINT IN FORM '4X 2X 2X': SCRNPOS(15,34):LINE.ERASE$:PAGE.TURN$
8230 RETURN

8500 ON GAME.LEVEL GOTO 8505, 8515, 8525, 8535, 8545
8505 NEED.TO.WIN=6
8510 GOTO 8550
8515 NEED.TO.WIN=5
8520 GOTO 8550
8525 NEED.TO.WIN=5
8530 GOTO 8550
8535 NEED.TO.WIN=4
8540 GOTO 8550
8545 NEED.TO.WIN=3
8550 FOR GAME.ID=1 TO 5
8555   IF GAME.LEVEL=2 THEN
8560     IF BOARDS(GAME.ID, BONUS.NUMBER)=1 AND &
            GAME.COUNT(GAME.ID)=NEED.TO.WIN THEN
8565       MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+1
8570     ENDIF
8575   ELSE
8580     IF GAME.COUNT(GAME.ID)=NEED.TO.WIN THEN
8585       MONEY.WINNERS(GAME.LEVEL)=MONEY.WINNERS(GAME.LEVEL)+1
8590     ENDIF
8595   ENDIF
8600   NEXT GAME.ID
8605 RETURN

9000 DEF STRING SCRNPOS(INTEGER(R,C))
9005   STRING ROW,COL
9010   ROW=SUBSTR(POS$,R,1)
9015   COL=SUBSTR(POS$,C,1)
9020   EXIT CHAR(27)+'='+ROW+COL
9025 ENDF SCRNPOS

9500 DEF INTEGER CALCODDS(DOUBLE remndr, INTEGER win.level)
9505 ! REMNDR = portion of players less than amount needed to guarantee a
9510 !          50% chance of finding a winner for this win level
9515 ! WIN.LEVEL = integer in range of 1 through 5; 1 = 6 of 6, 2 = 5 + Bonus,
9520 !             3 = 5 of 6, 4 = 4 of 6, 5 = 3 of 6
9525   INTEGER X
9530   CALC.TABLE=ZER
9535   CALC.TABLE(0)=(LOSERS(win.level)/13983816D0)^remndr
9540   MAX.TABLE.ENTRY=MIN(5, remndr)
9545   FOR X=1 TO MAX.TABLE.ENTRY
9550     CALC.TABLE(X)=(LOSERS(win.level)/13983816D0)^DBL(remndr-X) * &
                        (WINNERS(win.level)/13983816D0)^DBL(X) * &
                        DBL((FACT(remndr,remndr-X))/(FACT(X,1)))
9555     NEXT X
9560   PREV.VALUE=1
9565   FOR X=0 TO MAX.TABLE.ENTRY
9570     CALC.TABLE(X)=PREV.VALUE-CALC.TABLE(X)
9575     PREV.VALUE=CALC.TABLE(X)
9580     NEXT X
9585 ENDF CALCODDS
9600 DEF DOUBLE FACT(INTEGER fact.num, small.num)
9605   DOUBLE FACTOR
9610   INTEGER X
9615   FACTOR = fact.num
9620   FOR X=FACTOR-1 TO small.num+1 STEP -1
9625     FACTOR = FACTOR*X
9630     NEXT X
9635   EXIT FACTOR
9640 ENDF FACT
  