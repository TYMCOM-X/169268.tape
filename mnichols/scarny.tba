  100 REM THIS PROGRAM WILL PLAY SCARNEY BACCARAT, A VERSION OF BACCARAT &
          INCORPORATING FEATURES OF BANK CRAPS, BLACKJACK AND CHEMIN DE FER.
  101 GOSUB 10000 ! IS USER ON A 425?
  102 GOSUB 11000 ! DOES USER NEED INSTRUCTIONS?
  105 POS$=POS$+CHAR(X) FOR X=32 TO 111
  140 STRING CARD.NAME(0:12), CARD.ADDR(7,2,3), BET.ADDR(7), INSUR.ADDR(7), &
             SIDE.BET.ADDR(7,5:9), FIELD.BET.ADDR(7), SUIT(0:3), BAL.ADDR(7), &
	     DLRS.CARD.ADDR(3), PLAYER.ADDR(7), WIN.LOSE.ADDR(7,2)
  145 INTEGER CARD.DECK(208), HELD.CARDS(7,2,3), BET(7,2), DEALERS.CARDS(3), &
              CARD.VALUE(0:12), PLAYER.COUNT(7,2), FIELD.BET(7), &
	      SIDE.BET(7,5:9), CUT.DECK(208), NEXT.CARD
  150 REAL INSUR.BET(7,2), CURR.TOTAL(7)
  153 LOGICAL PLAYABLE.HAND(7,2), ACTIVE(7), MUST.SHUFFLE, BET.PLACED, &
              CAN.SPLIT, WALKED.AWAY
  155 GOSUB 500  ! DEFINE SCREEN CONTROL STRINGS
  200 GOSUB 1500 ! INITIALIZE ARRAYS
  205 GOSUB 1000 ! FORMAT SCREEN
  210 GOSUB 2000 ! PLAY THE GAME
  300 END ""

  500 CLS$=CHAR(27)+';'
  501 STD.VIDEO$=CHAR(27)+'G0'
  502 REV.VIDEO$=CHAR(27)+'G4'
  503 REV.UL.VIDEO$=CHAR(27)+'G5'
  504 SET.PROT.MODE$=CHAR(27)+'&'
  505 CLR.PROT.MODE$=CHAR(27)+"'"
  506 PAGE.ERASE$=CHAR(27)+'Y'
  507 REV.BL.VIDEO$=CHAR(27)+'G7'
  508 PAGE.TURN$=CHAR(27)+'J'
  509 BL.VIDEO$=CHAR(27)+'G2'
  510 LINE.ERASE$=CHAR(27)+'T'
  511 RETURN

 1000 PRINT CLS$+' PLAYERS  -->'
 1010 PRINT '   BET    -->'
 1020 PRINT 'INSURANCE -->'
 1025 PRINT
 1030 PRINT ' BALANCE  -->'
 1035 PRINT
 1040 PRINT 'SIDE BETS'
 1050 PRINT REV.UL.VIDEO$+'   5   '+STD.VIDEO$+' -->'
 1060 PRINT REV.UL.VIDEO$+'   6   '+STD.VIDEO$+' -->'
 1070 PRINT REV.UL.VIDEO$+'   7   '+STD.VIDEO$+' -->'
 1080 PRINT REV.UL.VIDEO$+'   8   '+STD.VIDEO$+' -->'
 1090 PRINT REV.UL.VIDEO$+'   9   '+STD.VIDEO$+' -->'
 1100 PRINT REV.UL.VIDEO$+' FIELD '+STD.VIDEO$+' -->'
 1105 PRINT
 1110 PRINT '  CARDS   -->'
 1120 PRINT SCRNPOS(ROW,11)+'-->' FOR ROW=16 TO 19
 1125 PRINT
 1130 PRINT '  HOUSE   -->'
 1140 X=0
 1150 FOR Y=1 TO 7
 1160   X = X+1
 1170   PRINT PLAYER.ADDR(Y)+REV.UL.VIDEO$:
 1180   PRINT IN IMAGE ' %      '+STD.VIDEO$: X
 1190 NEXT Y
 1195 FOR X=1 TO 7
 1200   PRINT BET.ADDR(X)+STD.VIDEO$
 1205   PRINT INSUR.ADDR(X)+STD.VIDEO$
 1210   PRINT BAL.ADDR(X)+STD.VIDEO$
 1215   PRINT SIDE.BET.ADDR(X,Y)+STD.VIDEO$ FOR Y=5 TO 9
 1220   PRINT FIELD.BET.ADDR(X)+STD.VIDEO$
 1222   PRINT WIN.LOSE.ADDR(X,1)+STD.VIDEO$
 1225   FOR Y=1 TO 2
 1230     FOR Z=1 TO 3
 1235       IF Y=2 AND Z=3 THEN GOTO 1245
 1240       PRINT CARD.ADDR(X,Y,Z)+STD.VIDEO$
 1245     NEXT Z
 1250   NEXT Y
 1255 NEXT X
 1260 PRINT DLRS.CARD.ADDR(X)+STD.VIDEO$ FOR X=1 TO 3
 1265 PRINT SCRNPOS(21,41)+STD.VIDEO$
 1270 PRINT SCRNPOS(ROW,77)+STD.VIDEO$ FOR ROW=2 TO 19
 1275 RETURN

 1500 DATA " A"," 2"," 3"," 4"," 5"," 6"," 7"," 8"," 9","10"," J"," Q"," K"
 1505 READ CARD.NAME(X) FOR X=0 TO 12
 1510 FOR X=1 TO 7
 1515   FOR Y=1 TO 2
 1520     FOR Z=1 TO 3
 1525       IF Y=2 AND Z=3 THEN GOTO 1535
 1530       CARD.ADDR(X,Y,Z)=SCRNPOS(11+(3*Y)+Z,5+(X*9))
 1535     NEXT Z
 1540   NEXT Y
 1545 NEXT X
 1550 BET.ADDR(X)=SCRNPOS(2,5+(X*9)) FOR X=1 TO 7
 1555 INSUR.ADDR(X)=SCRNPOS(3,5+(X*9)) FOR X=1 TO 7
 1560 FIELD.BET.ADDR(X)=SCRNPOS(13,5+(X*9)) FOR X=1 TO 7
 1565 FOR X=1 TO 7
 1570   FOR Y=5 TO 9
 1575     SIDE.BET.ADDR(X,Y)=SCRNPOS(Y+3,5+(X*9))
 1580   NEXT Y
 1585 NEXT X
 1590 DATA "HEART","SPADE","CLUBS","D'MND"
 1595 READ SUIT(X) FOR X=0 TO 3
 1600 CARD.DECK(X)=X FOR X=1 TO 208
 1605 DATA 1,2,3,4,5,6,7,8,9,10,10,10,10
 1610 READ CARD.VALUE(X) FOR X=0 TO 12
 1615 BAL.ADDR(X)=SCRNPOS(5,5+(X*9)) FOR X=1 TO 7
 1620 DLRS.CARD.ADDR(X)=SCRNPOS(21,5+(X*9)) FOR X=1 TO 3
 1625 PLAYER.ADDR(X)=SCRNPOS(1,5+(X*9)) FOR X=1 TO 7
 1630 ACTIVE(X)=FALSE FOR X=1 TO 7
 1635 MUST.SHUFFLE=TRUE
 1640 Z=RND(-1)
 1645 FOR X=1 TO 7
 1650   WIN.LOSE.ADDR(X,1)=SCRNPOS(14,5+(X*9))
 1655   WIN.LOSE.ADDR(X,2)=SCRNPOS(17,5+(X*9))
 1660 NEXT X
 1665 RETURN

 2000 GOSUB 2500 ! INPUT BETS UNTIL DEAL REQUEST IS MADE
 2005 GOSUB 3000 ! DEAL OUT 2 CARDS TO EACH PLAYER AND TO DEALER
 2010 GOSUB 3500 ! LET EACH PLAYER PLAY OUT HIS/HER HAND
 2015 GOSUB 4000 ! PLAY OUT DEALER'S HAND
 2020 GOSUB 5000 ! MAKE PAYOFFS AND GRAB THE LOSER'S MONEY
 2025 GOTO 2000  ! NEXT ROUND

 2500 ON ESCAPE GOTO 9000
 2501 PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2505 PRINT SCRNPOS(23,18)+'Enter player no. (1 - 7) or <CR> to deal: ':
 2510 INPUT IN FORM 'R': COMMAND$
 2515 IF LENGTH(COMMAND$) > 1 THEN GOTO 2500
 2517 IF LENGTH(COMMAND$) = 0 THEN 
 2518   PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2519   RETURN
 2520 ENDIF
 2521 IF INDEX('1234567',COMMAND$) = 0 THEN GOTO 2500
 2525 PLYR.NUM = VAL(COMMAND$)
 2530 IF NOT(ACTIVE(PLYR.NUM)) THEN
 2531   PRINT PLAYER.ADDR(PLYR.NUM)+STD.VIDEO$
 2540   PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$
 2545   PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$
 2550   PRINT BAL.ADDR(PLYR.NUM)+REV.UL.VIDEO$
 2551   ADD.BACK=0
 2552 ELSE
 2555   ADD.BACK=BET(PLYR.NUM,1)+BET(PLYR.NUM,2)
 2556   BET(PLYR.NUM,HAND)=0 FOR HAND=1 TO 2
 2557   PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 2560   ADD.BACK=ADD.BACK+SIDE.BET(PLYR.NUM,BET.NUM) FOR BET.NUM=5 TO 9
 2561   SIDE.BET(PLYR.NUM,BET.NUM)=0 FOR BET.NUM=5 TO 9
 2562   PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+REV.UL.VIDEO$ &
              +'        '+CHAR(8) FOR BET.NUM=5 TO 9
 2565   ADD.BACK=ADD.BACK+FIELD.BET(PLYR.NUM)
 2566   FIELD.BET(PLYR.NUM)=0
 2567   PRINT FIELD.BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 2570 ENDIF
 2575 IF ADD.BACK > 0 THEN
 2580   CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+ADD.BACK
 2585   CALL UPD.BAL
 2615 ENDIF
 2620 PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+REV.UL.VIDEO$ FOR BET.NUM=5 TO 9
 2625 PRINT FIELD.BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$
 2630 PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'   '+CHAR(8):
 2635 INPUT IN FORM 'R': VALUE$
 2640 IF LENGTH(VALUE$)=0 THEN
 2645   ACTIVE(PLYR.NUM)=FALSE
 2650   HOUSE.BAL=HOUSE.BAL+(CURR.TOTAL(PLYR.NUM)*-1)
 2652   CURR.TOTAL(PLYR.NUM)=0
 2655   BET(PLYR.NUM,HAND)=0 FOR HAND=1 TO 2
 2660   SIDE.BET(PLYR.NUM,BET.NUM)=0 FOR BET.NUM=5 TO 9
 2665   FIELD.BET(PLYR.NUM)=0
 2670   PRINT BET.ADDR(PLYR.NUM)+STD.VIDEO$+'        '+CHAR(8)
 2675   PRINT INSUR.ADDR(PLYR.NUM)+STD.VIDEO$+'        '+CHAR(8)
 2680   PRINT BAL.ADDR(PLYR.NUM)+STD.VIDEO$+'        '+CHAR(8)
 2685   PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+STD.VIDEO$+'        ' &
              +CHAR(8) FOR BET.NUM=5 TO 9
 2690   PRINT FIELD.BET.ADDR(PLYR.NUM)+STD.VIDEO$+'        '+CHAR(8)
 2691   PRINT PLAYER.ADDR(PLYR.NUM)+REV.UL.VIDEO$
 2692   GOTO 2500
 2695 ENDIF
 2700 IF VAL(VALUE$)>0 THEN
 2705   IF INT(VAL(VALUE$))=VAL(VALUE$) THEN
 2710     IF VAL(VALUE$)>1 AND VAL(VALUE$)<= 2000 THEN
 2715       BET(PLYR.NUM,1)=VAL(VALUE$)
 2720       PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 2725       PRINT IN IMAGE '$%%%%%% '+CHAR(8): BET(PLYR.NUM,1)
 2726       ACTIVE(PLYR.NUM)=TRUE
 2727       CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)-BET(PLYR.NUM,1)
 2728       CALL UPD.BAL
 2730     ELSE
 2735       PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2740       PRINT SCRNPOS(23,20)+'House betting limits are from $2 to $2000'
 2745       PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 2750       GOTO 2630
 2755     ENDIF
 2760   ELSE
 2765     PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2770     PRINT SCRNPOS(23,21)+'Please bet only in even dollar amounts'
 2775     GOTO 2745
 2780   ENDIF
 2785 ELSE
 2790   GOTO 2745
 2795 ENDIF
 2800 FOR BET.NUM=5 TO 9
 2805   PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+REV.UL.VIDEO$+'   '+CHAR(8):
 2810   INPUT IN FORM 'R': VALUE$
 2815   IF LENGTH(VALUE$)=0 OR VAL(VALUE$)<=0 THEN
 2820     SIDE.BET(PLYR.NUM,BET.NUM)=0
 2825     PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+STD.VIDEO$+'        '+CHAR(8)
 2830   ELSE
 2835     IF INT(VAL(VALUE$))=VAL(VALUE$) THEN
 2840       IF VAL(VALUE$)>1 AND VAL(VALUE$)<=2000 THEN
 2845         SIDE.BET(PLYR.NUM,BET.NUM)=VAL(VALUE$)
 2850         PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+REV.UL.VIDEO$:
 2855         PRINT IN IMAGE '$%%%%%% '+CHAR(8): SIDE.BET(PLYR.NUM,BET.NUM)
 2860         CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)- &
                                   SIDE.BET(PLYR.NUM,BET.NUM)
 2865         CALL UPD.BAL
 2870       ELSE
 2871         PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2872         PRINT SCRNPOS(23,20)+'House betting limits are from $2 to $2000'
 2873         PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+REV.UL.VIDEO$+ &
                    '        '+CHAR(8)
 2875         GOTO 2805
 2880       ENDIF
 2885     ELSE
 2886       PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2887       PRINT SCRNPOS(23,21)+'Please bet only in even dollar amounts'
 2890       GOTO 2873
 2895     ENDIF
 2900   ENDIF
 2905 NEXT BET.NUM
 2915 PRINT FIELD.BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'   '+CHAR(8):
 2920 INPUT IN FORM 'R': VALUE$
 2925 IF LENGTH(VALUE$)=0 OR VAL(VALUE$)<=0 THEN
 2930   FIELD.BET(PLYR.NUM)=0
 2935   PRINT FIELD.BET.ADDR(PLYR.NUM)+STD.VIDEO$+'        '+CHAR(8)
 2940 ELSE
 2945   IF INT(VAL(VALUE$))=VAL(VALUE$) THEN
 2950     IF VAL(VALUE$)>1 AND VAL(VALUE$)<=2000 THEN
 2955       FIELD.BET(PLYR.NUM)=VAL(VALUE$)
 2960       PRINT FIELD.BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 2965       PRINT IN IMAGE '$%%%%%% '+CHAR(8): FIELD.BET(PLYR.NUM)
 2966       CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)-FIELD.BET(PLYR.NUM)
 2967       CALL UPD.BAL
 2968     ELSE
 2969       PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2970       PRINT SCRNPOS(23,20)+'House betting limits are from $2 to $2000'
 2971       PRINT FIELD.BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 2972       GOTO 2915
 2973     ENDIF
 2974   ELSE
 2975     PRINT SCRNPOS(21,42)+PAGE.ERASE$
 2976     PRINT SCRNPOS(23,21)+'Please bet only in even dollar amounts'
 2980     GOTO 2971
 2981   ENDIF
 2982 ENDIF
 2985 GOTO 2500

 3000 BET.PLACED=FALSE
 3005 BET.PLACED=BET.PLACED OR BET(PLYR.NUM,1)>0 FOR PLYR.NUM=1 TO 7
 3010 IF NOT(BET.PLACED) THEN 
 3011   PRINT SCRNPOS(21,42)+PAGE.ERASE$
 3012   PRINT SCRNPOS(23,16)+'Dealer will not deal until a bet has been placed'
 3013   WAIT 5
 3014   RETURN
 3015 ENDIF
 3016 FOR PLYR.NUM=1 TO 7
 3017   FOR HAND=1 TO 2
 3018     IF HELD.CARDS(PLYR.NUM,HAND,1)#0 THEN
 3019       PRINT WIN.LOSE.ADDR(PLYR.NUM,HAND)+STD.VIDEO$+'      '+CHAR(8)
 3020     ENDIF
 3021     FOR CARD=1 TO 3
 3022       IF HELD.CARDS(PLYR.NUM,HAND,CARD)#0 THEN
 3023         PRINT CARD.ADDR(PLYR.NUM,HAND,CARD)+STD.VIDEO$+'        '+CHAR(8)
 3024         HELD.CARDS(PLYR.NUM,HAND,CARD)=0
 3025       ENDIF
 3026     NEXT CARD
 3044   NEXT HAND
 3045 NEXT PLYR.NUM
 3046 FOR CARD=1 TO 3
 3047   IF DEALERS.CARDS(CARD)#0 THEN
 3048     PRINT DLRS.CARD.ADDR(CARD)+STD.VIDEO$+'        '+CHAR(8)
 3049     DEALERS.CARDS(CARD)=0
 3050   ENDIF
 3051 NEXT CARD
 3052 PRINT SCRNPOS(4,14)+LINE.ERASE$
 3053 PRINT SCRNPOS(7,14)+LINE.ERASE$
 3054 IF MUST.SHUFFLE THEN GOSUB 3200
 3055 FOR CARD.NUM=1 TO 2
 3056   FOR PLYR.NUM=1 TO 7
 3057     IF BET(PLYR.NUM,1)>0 THEN
 3060       PRINT CARD.ADDR(PLYR.NUM,1,CARD.NUM)+REV.UL.VIDEO$+ &
                  CARD.STRING(CARD.DECK(NEXT.CARD))
 3065       HELD.CARDS(PLYR.NUM,1,CARD.NUM)=CARD.DECK(NEXT.CARD)
 3070       NEXT.CARD=NEXT.CARD+1
 3075       PLAYABLE.HAND(PLYR.NUM,1)=TRUE
 3080     ENDIF
 3085   NEXT PLYR.NUM
 3090   IF CARD.NUM=1 THEN
 3095     PRINT DLRS.CARD.ADDR(CARD.NUM)+REV.UL.VIDEO$+ &
                CARD.STRING(CARD.DECK(NEXT.CARD))
 3100     DEALERS.CARDS(CARD.NUM)=CARD.DECK(NEXT.CARD)
 3105     NEXT.CARD=NEXT.CARD+1
 3110   ELSE
 3115     PRINT DLRS.CARD.ADDR(CARD.NUM)+REV.UL.VIDEO$+'????????'
 3120     DEALERS.CARDS(CARD.NUM)=CARD.DECK(NEXT.CARD)
 3125     NEXT.CARD=NEXT.CARD+1
 3130   ENDIF
 3135 NEXT CARD.NUM
 3140 RETURN

 3200 PRINT SCRNPOS(21,42)+PAGE.ERASE$
 3205 PRINT SCRNPOS(23,18)+'Just one moment, while I shuffle the deck...'
 3206 WAIT 3
 3210 FOR X=1 TO 1500
 3215   POS.1=INT(RND(0)*208+1)
 3220   POS.2=INT(RND(0)*208+1)
 3225   IF POS.1=POS.2 THEN GOTO 3215
 3235   CARD.DECK(POS.1)==CARD.DECK(POS.2)
 3245 NEXT X
 3250 PRINT SCRNPOS(21,42)+PAGE.ERASE$
 3255 PRINT SCRNPOS(23,15)+'To cut the deck, enter a number from 1 to 208: ':
 3260 INPUT IN FORM 'R': VALUE$
 3265 IF VAL(VALUE$)>=1 AND VAL(VALUE$)<=208 THEN
 3270   DEST.ADDR=1
 3272   SOURCE.ADDR=VAL(VALUE$)
 3275   CUT.DECK(DEST.ADDR)=CARD.DECK(SOURCE.ADDR)
 3280   DEST.ADDR=DEST.ADDR+1
 3285   SOURCE.ADDR=SOURCE.ADDR+1
 3290   IF SOURCE.ADDR>208 THEN SOURCE.ADDR=1
 3291   IF DEST.ADDR>208 THEN GOTO 3296
 3292   GOTO 3275
 3293 ELSE
 3294   GOTO 3250
 3295 ENDIF
 3296 NEXT.CARD=1
 3297 MUST.SHUFFLE=FALSE
 3298 CARD.DECK=CUT.DECK
 3300 PRINT SCRNPOS(21,42)+PAGE.ERASE$
 3301 LAST.CARD=144+INT(RND(0)*11+1)
 3305 RETURN

 3500 IF NOT(BET.PLACED) THEN RETURN
 3505 FOR PLYR.NUM=1 TO 7
 3510   IF ACTIVE(PLYR.NUM) AND BET(PLYR.NUM,1) <> 0 THEN
 3511     PRINT SCRNPOS(21,42)+PAGE.ERASE$
 3512     PRINT SCRNPOS(22,36):
 3513     PRINT IN IMAGE 'Player %': PLYR.NUM
 3515     IF CARD.VALUE(HELD.CARDS(PLYR.NUM,1,1) MOD 13)= &
             CARD.VALUE(HELD.CARDS(PLYR.NUM,1,2) MOD 13) THEN
 3520       CAN.SPLIT=TRUE
 3525     ELSE
 3530       CAN.SPLIT=FALSE
 3535     ENDIF
 3540     IF CAN.SPLIT THEN
 3544       PRINT SCRNPOS(23,6)+'Enter <H> to Hit, <S> to Stand, '+ &
                  '<D> to Double Down or <2> to Split: ':
 3546       INPUT IN FORM 'R': DECISION$
 3548       IF LENGTH(DECISION$)#1 THEN GOTO 3540
 3550       IF INDEX('HSD2',DECISION$) = 0 THEN GOTO 3540
 3552     ELSE
 3556       PRINT SCRNPOS(23,13)+'Enter <H> to Hit, <S> to Stand '+ &
                  'or <D> to Double Down: ':
 3558       INPUT IN FORM 'R': DECISION$
 3560       IF LENGTH(DECISION$)#1 THEN GOTO 3540
 3562       IF INDEX('HSD',DECISION$) = 0 THEN GOTO 3540
 3564     ENDIF
 3566     ON INDEX('HSD2',DECISION$) GOSUB 3600,3700,3800,3900
 3568   ENDIF
 3570 NEXT PLYR.NUM
 3575 RETURN

 3600 PRINT CARD.ADDR(PLYR.NUM,1,3)+REV.UL.VIDEO$+ &
            CARD.STRING(CARD.DECK(NEXT.CARD))
 3605 HELD.CARDS(PLYR.NUM,1,3)=CARD.DECK(NEXT.CARD)
 3610 NEXT.CARD=NEXT.CARD+1
 3615 RETURN ! PLAYER TOOK 1 ADDITIONAL CARD

 3700 RETURN ! PLAYER DID NOT TAKE A THIRD CARD

 3800 PRINT CARD.ADDR(PLYR.NUM,1,3)+REV.UL.VIDEO$+'????????'
 3805 HELD.CARDS(PLYR.NUM,1,3)=CARD.DECK(NEXT.CARD)+208
 3810 NEXT.CARD=NEXT.CARD+1
 3815 BET(PLYR.NUM,1)=BET(PLYR.NUM,1)*2
 3820 PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 3825 PRINT IN IMAGE '$%%%%%% '+CHAR(8): BET(PLYR.NUM,1)
 3830 CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)-(BET(PLYR.NUM,1)/2)
 3840 CALL UPD.BAL
 3845 RETURN ! PLAYER DOUBLED DOWN

 3900 HELD.CARDS(PLYR.NUM,2,1)=HELD.CARDS(PLYR.NUM,1,2)
 3905 PRINT CARD.ADDR(PLYR.NUM,1,2)+STD.VIDEO$+'        '+CHAR(8)
 3910 PRINT CARD.ADDR(PLYR.NUM,2,1)+REV.UL.VIDEO$+ &
            CARD.STRING(HELD.CARDS(PLYR.NUM,2,1))
 3915 FOR HAND=1 TO 2
 3920   PRINT CARD.ADDR(PLYR.NUM,HAND,2)+REV.UL.VIDEO$+'????????'
 3925   HELD.CARDS(PLYR.NUM,HAND,2)=CARD.DECK(NEXT.CARD)+208
 3930   NEXT.CARD=NEXT.CARD+1
 3935 NEXT HAND
 3940 PLAYABLE.HAND(PLYR.NUM,2)=TRUE
 3945 BET(PLYR.NUM,2)=BET(PLYR.NUM,1)
 3950 PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 3955 PRINT IN IMAGE '$%%%%%% '+CHAR(8): BET(PLYR.NUM,1)+BET(PLYR.NUM,2)
 3960 CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)-BET(PLYR.NUM,2)
 3965 CALL UPD.BAL
 3970 RETURN ! PLAYER SPLIT HIS HAND

 4000 IF NOT(BET.PLACED) THEN RETURN
 4005 IF CARD.VALUE(DEALERS.CARDS(1) MOD 13)=9 THEN
 4010   FOR PLYR.NUM=1 TO 7
 4015     IF ACTIVE(PLYR.NUM) THEN
 4020       FOR HAND=1 TO 2
 4022         IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 4025           PRINT SCRNPOS(21,42)+PAGE.ERASE$
 4030           PRINT SCRNPOS(22,32):
 4035           PRINT IN IMAGE 'Player %   Hand %': PLYR.NUM, HAND
 4040           PRINT SCRNPOS(23,24)+'Do you want Scarney Insurance? ':
 4045           INPUT IN FORM 'R': RESPONSE$
 4046           IF LENGTH(RESPONSE$)=1 AND INDEX('YN',RESPONSE$)#0 THEN &
                   GOTO 4050 ELSE GOTO 4022
 4050           IF RESPONSE$='Y' THEN
 4051             INSUR.BET(PLYR.NUM,HAND)=BET(PLYR.NUM,HAND)/2
 4052           ELSE
 4055             INSUR.BET(PLYR.NUM,HAND)=0
 4056           ENDIF
 4060           PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 4065           IF INT(INSUR.BET(PLYR.NUM,HAND))= &
		       INSUR.BET(PLYR.NUM,HAND) THEN
 4070             PRINT IN IMAGE '$%%%%%% '+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
                                         INSUR.BET(PLYR.NUM,2)
 4075           ELSE
 4080             PRINT IN IMAGE '$%%%%%.%'+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
                                          INSUR.BET(PLYR.NUM,2)
 4085           ENDIF
 4086           CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)- &
                       INSUR.BET(PLYR.NUM,HAND)
 4087           CALL UPD.BAL
 4095         ENDIF
 4100       NEXT HAND
 4105     ENDIF
 4110   NEXT PLYR.NUM
 4115 ENDIF
 4120 PRINT DLRS.CARD.ADDR(2)+REV.UL.VIDEO$+CARD.STRING(DEALERS.CARDS(2))
 4125 IF CARD.VALUE(DEALERS.CARDS(1) MOD 13)=9 AND &
         CARD.VALUE(DEALERS.CARDS(2) MOD 13)=10 THEN
 4127   PRINT SCRNPOS(4,14)+LINE.ERASE$
 4128   PRINT SCRNPOS(4,20)+'Dealer has Scarney and pays off insurance '+ &
              'bets at 2 to 1!'
 4130   FOR PLYR.NUM=1 TO 7
 4135     IF ACTIVE(PLYR.NUM) THEN
 4140       FOR HAND=1 TO 2
 4141         IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 4142           INSUR.BET(PLYR.NUM,HAND)=INSUR.BET(PLYR.NUM,HAND)*3
 4143           PRINT INSUR.ADDR(PLYR.NUM)+REV.BL.VIDEO$:
 4144           IF INT(INSUR.BET(PLYR.NUM,1)+INSUR.BET(PLYR.NUM,2))= &
                       INSUR.BET(PLYR.NUM,1)+INSUR.BET(PLYR.NUM,2) THEN
 4145             PRINT IN IMAGE '$%%%%%% '+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
				         INSUR.BET(PLYR.NUM,2)
 4146           ELSE
 4147             PRINT IN IMAGE '$%%%%%.%'+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
				          INSUR.BET(PLYR.NUM,2)
 4148           ENDIF
 4155         ENDIF
 4160       NEXT HAND
 4165     ENDIF
 4170   NEXT PLYR.NUM
 4172   FOR PLYR.NUM=1 TO 7
 4173     IF ACTIVE(PLYR.NUM) THEN
 4174       FOR HAND=1 TO 2
 4175         IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 417       IF INSUR.BET(PLYR.NUM,HAND)>0 THEN
 4177             PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 4178             CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+ &
                                INSUR.BET(PLYR.NUM,HAND)
 4179             INSUR.BET(PLYR.NUM,HAND)=0
 4180           ENDIF
 4181         ENDIF
 4182       NEXT HAND
 4183       CALL UPD.BAL
 4184     ENDIF
 4185   NEXT PLYR.NUM
 4186 ENDIF
 4187 IF CARD.VALUE(DEALERS.CARDS(1) MOD 13)=9 AND &
         CARD.VALUE(DEALERS.CARDS(2) MOD 13)#10 THEN
 4188   PRINT SCRNPOS(4,14)+LINE.ERASE$
 4190   PRINT SCRNPOS(4,20)+'Dealer does not have Scarney and collects '+ &
              'all insurance bets.'
 4195   FOR PLYR.NUM=1 TO 7
 4197     IF ACTIVE(PLYR.NUM) THEN
 4200       INSUR.BET(PLYR.NUM,1)=0
 4205       INSUR.BET(PLYR.NUM,2)=0
 4210       PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 4215     ENDIF
 4220   NEXT PLYR.NUM
 4225 ENDIF
 4230 DEALERS.TOT=SUM(CARD.VALUE(DEALERS.CARDS(CARD) MOD 13) &
                  FOR CARD=1 TO 2) MOD 10
 4235 IF CARD.VALUE(DEALERS.CARDS(1) MOD 13)=10 AND &
         CARD.VALUE(DEALERS.CARDS(2) MOD 13)=10 THEN
 4240   FOR PLYR.NUM=1 TO 7
 4245     IF ACTIVE(PLYR.NUM) THEN
 4250       FOR HAND=1 TO 2
 4255         IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 4260           PRINT SCRNPOS(21,42)+PAGE.ERASE$
 4265           PRINT SCRNPOS(22,32):
 4270           PRINT IN IMAGE 'Player %   Hand %': PLYR.NUM, HAND
 4275           PRINT SCRNPOS(23,19)+'Do you want Scarney '+ &
                      'Baccarat Insurance? ':
 4280           INPUT IN FORM 'R': RESPONSE$
 4285           IF LENGTH(RESPONSE$)=1 AND INDEX('YN',RESPONSE$)#0 THEN &
                   GOTO 4290 ELSE GOTO 4255
 4290           IF RESPONSE$='Y' THEN
 4295             INSUR.BET(PLYR.NUM,HAND)=BET(PLYR.NUM,HAND)/2
 4296           ELSE
 4297             INSUR.BET(PLYR.NUM,HAND)=0
 4298           ENDIF
 4300           PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 4305           IF INT(INSUR.BET(PLYR.NUM,HAND))= &
		       INSUR.BET(PLYR.NUM,HAND) THEN
 4310             PRINT IN IMAGE '$%%%%%% '+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
                                         INSUR.BET(PLYR.NUM,2)
 4315           ELSE
 4320             PRINT IN IMAGE '$%%%%%.%'+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
                                          INSUR.BET(PLYR.NUM,2)
 4325           ENDIF
 4330           CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)- &
                       INSUR.BET(PLYR.NUM,HAND)
 4335           CALL UPD.BAL
 4345         ENDIF
 4350       NEXT HAND
 4355     ENDIF
 4360   NEXT PLYR.NUM
 4367   DEALERS.CARDS(3)=CARD.DECK(NEXT.CARD)
 4368   DEALERS.TOT=SUM(CARD.VALUE(DEALERS.CARDS(CARD) MOD 13) &
                    FOR CARD=1 TO 3) MOD 10
 4369   NEXT.CARD=NEXT.CARD+1
 4370   PRINT DLRS.CARD.ADDR(3)+REV.UL.VIDEO$+CARD.STRING(DEALERS.CARDS(3))
 4375   IF CARD.VALUE(DEALERS.CARDS(1) MOD 13)=10 AND &
           CARD.VALUE(DEALERS.CARDS(2) MOD 13)=10 AND &
	   CARD.VALUE(DEALERS.CARDS(3) MOD 13)=10 THEN
 4380     PRINT SCRNPOS(4,14)+LINE.ERASE$
 4385     PRINT SCRNPOS(4,16)+'Dealer has Scarney Baccarat and pays off '+ &
                'insurance at 2 to 1!'
 4390     FOR PLYR.NUM=1 TO 7
 4395       IF ACTIVE(PLYR.NUM) THEN
 4400         FOR HAND=1 TO 2
 4405           IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 4410             INSUR.BET(PLYR.NUM,HAND)=INSUR.BET(PLYR.NUM,HAND)*3
 4411             PRINT INSUR.ADDR(PLYR.NUM)+REV.BL.VIDEO$:
 4412             IF INT(INSUR.BET(PLYR.NUM,1)+INSUR.BET(PLYR.NUM,2))= &
                         INSUR.BET(PLYR.NUM,1)+INSUR.BET(PLYR.NUM,2) THEN
 4413               PRINT IN IMAGE '$%%%%%% '+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
		  		           INSUR.BET(PLYR.NUM,2)
 4414             ELSE
 4415               PRINT IN IMAGE '$%%%%%.%'+CHAR(8): INSUR.BET(PLYR.NUM,1)+ &
		  			    INSUR.BET(PLYR.NUM,2)
 4416             ENDIF
 4430           ENDIF
 4435         NEXT HAND
 4445       ENDIF
 4450     NEXT PLYR.NUM
 4452     FOR PLYR.NUM=1 TO 7
 4453       IF ACTIVE(PLYR.NUM) THEN
 4454         FOR HAND=1 TO 2
 4455           IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 4456             IF INSUR.BET(PLYR.NUM,HAND)>0 THEN
 4457               PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 4458               CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+ &
                                  INSUR.BET(PLYR.NUM,HAND)
 4459               INSUR.BET(PLYR.NUM,HAND)=0
 4460             ENDIF
 4461           ENDIF
 4462         NEXT HAND
 4463         CALL UPD.BAL
 4464       ENDIF
 4465     NEXT PLYR.NUM
 4466     GOTO 4235
 4467   ELSE
 4468     PRINT SCRNPOS(4,14)+LINE.ERASE$
 4469     PRINT SCRNPOS(4,14)+'Dealer does not have Scarney Baccarat '+ &
                'and collects all insurance'
 4470     FOR PLYR.NUM=1 TO 7
 4475       IF ACTIVE(PLYR.NUM) THEN
 4480         INSUR.BET(PLYR.NUM,1)=0
 4485         INSUR.BET(PLYR.NUM,2)=0
 4490         PRINT INSUR.ADDR(PLYR.NUM)+REV.UL.VIDEO$+'        '+CHAR(8)
 4495       ENDIF
 4500     NEXT PLYR.NUM
 4505   ENDIF
 4510 ENDIF
 4515 IF DEALERS.TOT >= 5 AND DEALERS.CARDS(3)=0 THEN
 4516   PRINT SCRNPOS(7,14)+LINE.ERASE$
 4517   PRINT SCRNPOS(7,19):
 4518   PRINT IN IMAGE 'Field bets pay even money and side bets '+ &
                'on % pay 10 for 1!': DEALERS.TOT
 4519 ELSE
 4520   PRINT SCRNPOS(7,14)+LINE.ERASE$
 4521   PRINT SCRNPOS(7,14)+"All side and field bets lose - "+ &
                "Dealer's two-card total was under 5"
 4522 ENDIF
 4523 IF DEALERS.TOT < 5 AND DEALERS.CARDS(3)=0 THEN
 4524   DEALERS.CARDS(3)=CARD.DECK(NEXT.CARD)
 4525   DEALERS.TOT=(DEALERS.TOT+CARD.VALUE(DEALERS.CARDS(3) MOD 13)) MOD 10
 4526   NEXT.CARD=NEXT.CARD+1
 4530   PRINT DLRS.CARD.ADDR(3)+REV.UL.VIDEO$+CARD.STRING(DEALERS.CARDS(3))
 4531 ENDIF
 4570 FOR PLYR.NUM=1 TO 7
 4575   FOR BET.NUM=5 TO 9
 4580     IF SIDE.BET(PLYR.NUM,BET.NUM) > 0 THEN
 4585       IF BET.NUM=DEALERS.TOT AND DEALERS.CARDS(3)=0 THEN
 4590         SIDE.BET(PLYR.NUM,BET.NUM)=SIDE.BET(PLYR.NUM,BET.NUM)*10
 4591       ELSE
 4592         SIDE.BET(PLYR.NUM,BET.NUM)=0
 4593       ENDIF
 4594       PRINT SIDE.BET.ADDR(PLYR.NUM,BET.NUM)+REV.BL.VIDEO$:
 4595       PRINT IN IMAGE '$%%%%%% '+CHAR(8): SIDE.BET(PLYR.NUM,BET.NUM)
 4615     ENDIF
 4620   NEXT BET.NUM
 4625   IF FIELD.BET(PLYR.NUM) > 0 THEN
 4630     IF DEALERS.TOT >= 5 AND DEALERS.TOT <= 9 AND DEALERS.CARDS(3)=0 THEN
 4635       FIELD.BET(PLYR.NUM)=FIELD.BET(PLYR.NUM)*2
 4636     ELSE
 4637       FIELD.BET(PLYR.NUM)=0
 4638     ENDIF
 4639     PRINT FIELD.BET.ADDR(PLYR.NUM)+REV.BL.VIDEO$:
 4640     PRINT IN IMAGE '$%%%%%% '+CHAR(8): FIELD.BET(PLYR.NUM)
 4660   ENDIF
 4665 NEXT PLYR.NUM
 4675 RETURN

 5000 IF NOT(BET.PLACED) THEN RETURN
 5001 FOR PLYR.NUM=1 TO 7
 5005   IF ACTIVE(PLYR.NUM) THEN
 5010     FOR HAND=1 TO 2
 5015       IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 5020         FOR CARD=1 TO 3
 5025           IF HELD.CARDS(PLYR.NUM,HAND,CARD)>208 THEN
 5030             PRINT CARD.ADDR(PLYR.NUM,HAND,CARD)+REV.UL.VIDEO$+ &
                        CARD.STRING(HELD.CARDS(PLYR.NUM,HAND,CARD))
 5035           ENDIF
 5040           PLAYER.COUNT(PLYR.NUM,HAND)=PLAYER.COUNT(PLYR.NUM,HAND)+ &
                       CARD.VALUE(HELD.CARDS(PLYR.NUM,HAND,CARD) MOD 13) &
		       UNLESS HELD.CARDS(PLYR.NUM,HAND,CARD)=0
 5045         NEXT CARD
 5050       ENDIF
 5055     NEXT HAND
 5060   ENDIF
 5065 NEXT PLYR.NUM
 5070 FOR PLYR.NUM=1 TO 7
 5075   IF ACTIVE(PLYR.NUM) THEN
 5080     FOR HAND=1 TO 2
 5085       IF PLAYABLE.HAND(PLYR.NUM,HAND) THEN
 5090         IF PLAYER.COUNT(PLYR.NUM,HAND) MOD 10 > DEALERS.TOT THEN
 5091           BET(PLYR.NUM,HAND)=BET(PLYR.NUM,HAND)*2
 5092           PRINT WIN.LOSE.ADDR(PLYR.NUM,HAND)+BL.VIDEO$+'  WIN!'
 5093         ORIF PLAYER.COUNT(PLYR.NUM,HAND) MOD 10 = DEALERS.TOT THEN
 5094           BET(PLYR.NUM,HAND)=BET(PLYR.NUM,HAND)*1
 5095           PRINT WIN.LOSE.ADDR(PLYR.NUM,HAND)+BL.VIDEO$+'  PUSH'
 5096         ELSE
 5097           BET(PLYR.NUM,HAND)=0
 5098           PRINT WIN.LOSE.ADDR(PLYR.NUM,HAND)+BL.VIDEO$+'  LOSE'
 5113         ENDIF
 5120       ENDIF
 5121     NEXT HAND
 5122     IF BET(PLYR.NUM,1)+BET(PLYR.NUM,2)>0 THEN
 5123       PRINT BET.ADDR(PLYR.NUM)+REV.BL.VIDEO$:
 5124     ELSE
 5125       PRINT BET.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
 5126     ENDIF
 5127     PRINT IN IMAGE '$%%%%%% '+CHAR(8): BET(PLYR.NUM,1)+BET(PLYR.NUM,2)
 5128   ENDIF
 5130 NEXT PLYR.NUM
 5135 FOR PLYR.NUM=1 TO 7
 5140   FOR HAND=1 TO 2
 5195     CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+BET(PLYR.NUM,HAND)
 5200     BET(PLYR.NUM,HAND)=0
 5205     PLAYABLE.HAND(PLYR.NUM,HAND)=FALSE
 5210     CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+INSUR.BET(PLYR.NUM,HAND)
 5215     INSUR.BET(PLYR.NUM,HAND)=0
 5220     PLAYER.COUNT(PLYR.NUM,HAND)=0
 5225   NEXT HAND
 5230   CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+FIELD.BET(PLYR.NUM)
 5235   FIELD.BET(PLYR.NUM)=0
 5236   FOR BET.NUM=5 TO 9
 5237     IF SIDE.BET(PLYR.NUM,BET.NUM)#0 THEN
 5240       CURR.TOTAL(PLYR.NUM)=CURR.TOTAL(PLYR.NUM)+ &
                 SIDE.BET(PLYR.NUM,BET.NUM)
 5242     ENDIF
 5243   NEXT BET.NUM
 5245   SIDE.BET(PLYR.NUM,BET.NUM)=0 FOR BET.NUM=5 TO 9
 5247   CALL UPD.BAL IF ACTIVE(PLYR.NUM)
 5250 NEXT PLYR.NUM
 5260 IF NEXT.CARD >= LAST.CARD THEN MUST.SHUFFLE=TRUE
 5265 RETURN

 9000 PRINT PAGE.TURN$+CLS$
 9001 WALKED.AWAY=FALSE
 9003 FOR PLYR.NUM=1 TO 7
 9005   WALKED.AWAY=WALKED.AWAY OR BET(PLYR.NUM,HAND) > 0 FOR HAND=1 TO 2
 9010   WALKED.AWAY=WALKED.AWAY OR INSUR.BET(PLYR.NUM,HAND) > 0 FOR HAND=1 TO 2
 9015   WALKED.AWAY=WALKED.AWAY OR SIDE.BET(PLYR.NUM,BET.NUM) > 0 &
		    FOR BET.NUM=5 TO 9
 9020   WALKED.AWAY=WALKED.AWAY OR FIELD.BET(PLYR.NUM) > 0
 9023 NEXT PLYR.NUM
 9025 IF WALKED.AWAY THEN
 9030   PRINT SCRNPOS(5,12)+'Seeing that you have chosen to walk away from '+ &
              'the table'
 9035   PRINT SCRNPOS(6,7)+'leaving some undecided bets thereon, the house '+ &
	      'exercises its option'
 9040   PRINT SCRNPOS(7,7)+'to claim all "unclaimed" funds for future '+ &
	      'support of its operation.'
 9045 ENDIF
 9047 HOUSE.BAL=HOUSE.BAL+(CURR.TOTAL(PLYR.NUM)*-1) FOR PLYR.NUM=1 TO 7
 9050 IF HOUSE.BAL < 0 THEN
 9055   PRINT SCRNPOS(9,4)+'Due to your superior play and excellent '+ &
	     'luck, this Scarney Baccarat table'
 9060   PRINT SCRNPOS(10,14):
 9065   PRINT IN IMAGE 'now closes in a deficit position of minus '+ &
	     '$%%%%%%.%%': HOUSE.BAL*-1
 9070 ELSE
 9075   PRINT SCRNPOS(9,8)+'Once again the house and probability triumph '+ &
	     'over the common man'
 9080   PRINT SCRNPOS(10,10):
 9085   PRINT IN IMAGE 'and are able to show a profit of '+ &
	     '$%%%%%%.%%.  Come back soon!': HOUSE.BAL
 9090 ENDIF
 9095 END ''

10000 PRINT 'Are you running on a model 425 terminal':
10010 INPUT RESPONSE$
10020 IF LEFT(RESPONSE$,1)<>'Y' AND LEFT(RESPONSE$,1)<>'y' THEN
10030   PRINT
10040   PRINT 'This game makes extensive use of the reverse video'
10050   PRINT 'capabilities of the 425 terminal... Sorry!'
10060   END ''
10070 ELSE
10080   RETURN
10090 ENDIF

11000 PRINT
11010 PRINT 'Do you need instructions':
11020 INPUT RESPONSE$
11030 IF RESPONSE$='Y' OR RESPONSE$='y' THEN
11040   PRINT
11045   PRINT 'Turn on your printer and press <return>...':
11048   INPUT IN FORM 'R': RESPONSE$
11049   ON FILE -4 GOTO 11080
11050   COPY 'SCARNY.INS' TO 'TERM'
11055   NO FILE -4 
11056   PRINT
11057   PRINT 'Hit <return> to continue...':
11058   INPUT IN FORM 'R': RESPONSE$
11060 ENDIF
11070 RETURN
11080 PRINT 'The file containing the rules for Scarney Baccarat has been'
11090 PRINT 'removed from the system.  The game is not difficult to play'
11100 PRINT 'and the rules can probably be learned as you go if you care'
11110 PRINT 'to jump blindly into your first game.  Good luck!'
11120 RESUME 11055

30000 DEF REAL UPD.BAL
30005   PRINT BAL.ADDR(PLYR.NUM)+REV.UL.VIDEO$:
30010   IF INT(CURR.TOTAL(PLYR.NUM))#CURR.TOTAL(PLYR.NUM) THEN
30015     PRINT IN IMAGE '$%%%%%.%'+CHAR(8): CURR.TOTAL(PLYR.NUM)
30020   ELSE
30025     PRINT IN IMAGE '$%%%%%% '+CHAR(8): CURR.TOTAL(PLYR.NUM)
30027   ENDIF
30030   ENDF UPD.BAL
30035 DEF STRING SCRNPOS(INTEGER(R,C))
30040   STRING ROW,COL
30045   ROW=SUBSTR(POS$,R,1)
30050   COL=SUBSTR(POS$,C,1)
30055   EXIT CHAR(27)+'='+ROW+COL
30060   ENDF SCRNPOS
30065 DEF STRING CARD.STRING(INTEGER CARD)
30070   EXIT CARD.NAME(CARD MOD 13)+' '+ SUIT(CARD MOD 4)
30075   ENDF CARD.STRING
   ~  K