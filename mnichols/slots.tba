  10 DIM WINDOW(3,3), SYMBOL.NO(3,20), SYMBOL$(8), WINDOW.ADDR$(3,3), &
         PAYOFF.ADDR$(3), PAYOFFS(3), PLAY.ARROW$(3), ARROW.ADDR$(3), &
         PAYOFF.COMBS$(3)
  15 DEF EXTERNAL SIMPL STUFIB(STRING S)
  17 LET X = RND(-3)
  20 GOSUB 1000
  25 GOSUB 1500
  27 GOSUB 1700
  30 GOSUB 2000
  40 ON ESCAPE GOTO 100
  50 GOSUB 5000
 100 END ''
1000 DATA 5,14,5,23,5,32,2,14,2,23,2,32,8,14,8,23,8,32
1010 FOR MACHINE = 1 TO 3
1020   FOR REEL = 1 TO 3
1030     READ ROW, COLUMN
1040     WINDOW.ADDR$(MACHINE,REEL)=CHAR(27)+'='+CHAR(ROW+31)+CHAR(COLUMN+31)
1050     NEXT REEL
1060   NEXT MACHINE
1070 DATA 'TYMSHARE',' TYMNET ',' MAGNUM ','TYMBASIC',' COMARS ', &
          ' UNITAX ',' TYMCOM ',' TYMSAT '
1080 FOR SYMB = 1 TO 8
1090   READ SYMBOL$(SYMB)
1100   LET SYMBOL$(SYMB) = CHAR(27)+'G4'+CHAR(10)+CHAR(8)+CHAR(27)+'G4'+ &
            SYMBOL$(SYMB)+CHAR(8) UNLESS SYMB<3
1110   NEXT SYMB
1120 LET SYMBOL$(1) = CHAR(27)+'G4'+CHAR(10)+CHAR(8)+CHAR(27)+'G4'+CHAR(27)+ &
            ')'+SYMBOL$(1)+CHAR(27)+'('
1130 LET SYMBOL$(2) = &
         CHAR(27)+'G5'+CHAR(10)+CHAR(8)+CHAR(27)+'G5'+SYMBOL$(2)+CHAR(8)
1140 DATA 6,49,4,49,8,49
1150 FOR X = 1 TO 3
1160   READ ROW, COLUMN
1170   LET PAYOFF.ADDR$(X) = CHAR(27)+'='+CHAR(ROW+31)+CHAR(COLUMN+31)
1180   NEXT X
1190 DATA 3,2,6,1,5,8,2,4,7,1,2,3,8,6,1,8,2,5,6,7
1200 DATA 4,3,2,5,7,3,1,4,8,2,7,1,5,4,8,2,7,3,6,8
1210 DATA 5,4,8,6,2,4,7,3,5,8,4,7,6,1,5,8,3,4,8,7
1220 READ SYMBOL.NO(X,Y) FOR Y=1 TO 20 FOR X = 1 TO 3
1230 DATA 6,9,3,9,9,9
1240 FOR X = 1 TO 3
1250   READ ROW, COLUMN
1260   LET ARROW.ADDR$(X) = CHAR(27)+'='+CHAR(ROW+31)+CHAR(COLUMN+31)
1270   NEXT X
1280 DATA '1-->', '2-->', '3-->'
1290 READ PLAY.ARROW$(X) FOR X = 1 TO 3
1300 RETURN
1500 PRINT 'Are you running on a ADM 31 (TYM425) terminal':
1510 INPUT RESPONSE$
1520 IF LEFT(RESPONSE$,1) <> 'Y' AND LEFT(RESPONSE$,1) <> 'y' THEN END ''
1530 PRINT CHAR(27)+';'
1540 PRINT &
'This program simulates a three-reel, multiple-payoff slot machine.'
1550 PRINT &
'Before this program will run correctly, you must enable the underline'
1560 PRINT &
'capability of the TYM425. To determine if that capability is already enabled'
1570 PRINT &
'in your terminal, place your terminal in local mode by typing <shift-'
1580 PRINT &
'function> <B>, and then typing <ESC> <G> <1>. If the underline character'
1590 PRINT &
'appears on the terminal screen, no modification to your terminal is '
1600 PRINT &
'necessary. If not, turn DIP switch 6 of switch group C-11 off (contained in'
1610 PRINT &
'second switch grouping from the rear of the terminal on the PC board).'
1620 PRINT
1630 PRINT &
'To place your terminal back in character mode, type <shift-function> <C>'
1640 PRINT &
'and type a carriage return to continue.':
1650 INPUT IN FORM 'R': RESPONSE$
1670 RETURN
1700 PRINT CHAR(27)+';'+ &
'Welcome to the Valley Green Hotel and Casino!  I am the cashier, ready to'
1710 PRINT
1720 PRINT &
'supply you with a roll of coins in your favorite denomination.'
1730 PRINT
1740 PRINT
1750 PRINT &
'Would you like <1> nickels'
1760 PRINT &
'               <2> dimes'
1770 PRINT &
'               <3> quarters'
1780 PRINT &
'               <4> half dollars'
1790 PRINT &
'            or <5> silver dollars?'
1800 PRINT
1810 PRINT
1820 PRINT 'Your choice: ':
1830 INPUT IN IMAGE '#': COINS
1840 IF COINS <1 OR COINS >5 THEN GOTO 1810
1850 ON COINS GOTO 1855, 1860, 1865, 1870, 1875
1855 LET DENOM$ = 'nickels'
1856 LET COINS.LEFT = COINS.LEFT + 40
1857 LET MONEY.REMAINING = MONEY.REMAINING + 2.00
1858 LET SINGLE.COIN = .05
1859 GOTO 1880
1860 LET DENOM$ = 'dimes'
1861 LET COINS.LEFT = COINS.LEFT + 50
1862 LET MONEY.REMAINING = MONEY.REMAINING + 5.00
1863 LET SINGLE.COIN = .10
1864 GOTO 1880
1865 LET DENOM$ = 'quarters'
1866 LET COINS.LEFT = COINS.LEFT + 40
1867 LET MONEY.REMAINING = MONEY.REMAINING + 10.00
1868 LET SINGLE.COIN = .25
1869 GOTO 1880
1870 LET DENOM$ = 'half dollars'
1871 LET COINS.LEFT = COINS.LEFT + 40
1872 LET MONEY.REMAINING = MONEY.REMAINING + 20.00
1873 LET SINGLE.COIN = .50
1874 GOTO 1880
1875 LET DENOM$ = 'silver dollars'
1876 LET COINS.LEFT =  COINS.LEFT + 50
1877 LET MONEY.REMAINING = MONEY.REMAINING + 50.00
1878 LET SINGLE.COIN = 1.00
1880 PRINT CHAR(27)+';'
1890 RETURN
2000 PRINT CHAR(27)+'='+CHAR(X+31)+CHAR(72)+CHAR(27)+'G0' FOR X = 2 TO 10
2010 FOR X = 2 TO 10
2020   FOR Y = 14 TO 32 STEP 9
2030     PRINT CHAR(27)+'='+CHAR(X+31)+CHAR(Y+31)+CHAR(27):
2040     IF X=4 OR X=7 OR X=10 THEN PRINT 'G5' ELSE PRINT 'G4'
2050     NEXT Y
2060   NEXT X
2070 DATA 13,14,16,17,18,19,20,21
2080 FOR X = 1 TO 8
2090   READ ROW
2100   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(65)+CHAR(27)+'G0'
2110   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(56)+CHAR(27)+'G5'
2120   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(47)+CHAR(27)+'G5'
2125   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(38)+CHAR(27)+'G5'
2130   NEXT X
2140 DATA 13,14,16,17,19,20,21
2150 FOR X = 1 TO 7
2160   READ ROW
2170   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(101)+CHAR(27)+'G0'
2180   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(92)+CHAR(27)+'G5'
2190   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(83)+CHAR(27)+'G5'
2200   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(74)+CHAR(27)+'G5'
2210   NEXT X
2220 PRINT CHAR(27)+'='+CHAR(42)+CHAR(32)+CHAR(27)+'G1'
2230 PRINT ' Payoff chart:'
2240 DATA 'TYMSHARE',7,13,8,13,17,13,26,17,26,19,26,21,26,14,62
2250 DATA ' TYMNET ',3,14,8,14,17,14,26
2260 DATA ' MAGNUM ',5,16,8,16,17,16,26,17,8,17,17
2270 DATA 'TYMBASIC',5,18,8,18,17,18,26,19,8,19,17
2280 DATA ' COMARS ',5,20,8,20,17,20,26,21,8,21,17
2290 DATA ' UNITAX ',5,13,44,13,53,13,62,14,44,14,53
2300 DATA ' TYMCOM ',5,16,44,16,53,16,62,17,44,17,53
2310 DATA ' TYMSAT ',6,19,44,19,53,19,62,20,44,20,53,21,44
2320 DATA ' ****** ',4,17,62,20,62,21,53,21,62
2330 FOR X=1 TO 9
2340   READ NAME$,MULT
2350   FOR Y=1 TO MULT
2360     READ ROW,COLUMN
2370     PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(COLUMN+31):
2380     IF NAME$='TYMSHARE' THEN PRINT CHAR(27)+')':
2390     PRINT NAME$+CHAR(27)+'('
2400     NEXT Y
2410   NEXT X
2420 DATA 13,200,14,100,16,18,17,18,18,14,19,14,20,10,21,10
2430 FOR X=1 TO 8
2440   READ ROW,AMT
2450   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(66):
2460   PRINT IN IMAGE '-> %%%':AMT
2470   NEXT X
2480 DATA 13,8,14,8,16,5,17,2,19,5,20,3,21,2
2490 FOR X=1 TO 7
2500   READ ROW,AMT
2510   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(102):
2520   PRINT IN IMAGE '-> %':AMT
2530   NEXT X
2540 FOR ROW=4 TO 10 STEP 2
2550   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(85)+CHAR(27)+'G0'
2560   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(79)+CHAR(27)+'G5'
2570   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(75)+'==>' UNLESS ROW = 10
2580   NEXT ROW
2600 PRINT CHAR(27)+'='+CHAR(33)+CHAR(80)+'Playing for '+DENOM$
2610 PRINT CHAR(27)+'='+CHAR(35)+CHAR(87)+'Coins left:'
2615 PRINT CHAR(27)+'='+CHAR(35)+CHAR(102):
2616 PRINT IN IMAGE '%%%': COINS.LEFT
2620 PRINT CHAR(27)+'='+CHAR(36)+CHAR(87)+'Coins played:'
2625 PRINT CHAR(27)+'='+CHAR(36)+CHAR(102):
2626 PRINT IN IMAGE '%%%': COINS.PLAYED
2630 PRINT CHAR(27)+'='+CHAR(38)+CHAR(87)+'Money remaining:'
2635 PRINT CHAR(27)+'='+CHAR(38)+CHAR(104):
2636 PRINT IN IMAGE '$%%%.%%': MONEY.REMAINING
2640 PRINT CHAR(27)+'='+CHAR(40)+CHAR(87)+'PLAY 1 TO 3 COINS ==>'
2650 DATA 4,75,5,75,7,80,9,79
2660 FOR X=1 TO 4
2670   READ ROW,COLUMN
2680   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(COLUMN+31)+CHAR(27)+'G0'
2690   NEXT X
2700 DATA 4,69,5,69,7,72,9,77
2710 FOR X=1 TO 4
2720   READ ROW,COLUMN
2730   PRINT CHAR(27)+'='+CHAR(ROW+31)+CHAR(COLUMN+31)+CHAR(27)+'G5'
2740   NEXT X
2745 PRINT CHAR(27)+'='+CHAR(40)+CHAR(80)+'-----'
2750 PRINT CHAR(27)+'='+CHAR(41)+CHAR(74)+'TOTAL'
2760 PRINT CHAR(27)+'='+CHAR(53)+CHAR(32)+CHAR(27)+'G1'
2770 PRINT CHAR(27)+'='+CHAR(41)+CHAR(80)+CHAR(27)+')'+RPT(' ',5)+CHAR(27)+'('
2780 PRINT CHAR(27)+'='+CHAR(54)+CHAR(39)+ &
      'Identical payoffs in 2 payout lines doubles the value of each payoff.'
2790 PRINT CHAR(27)+'='+CHAR(55)+CHAR(39)+ &
      'Identical payoffs in 3 payout lines triples the value of each payoff.':
2800 PRINT CHAR(30)
2810 RETURN
5000 PRINT CHAR(27)+'='+CHAR(40)+CHAR(109)+' '+CHAR(8):
5010 INPUT IN FORM 'R': COINS.INSERTED$
5015 IF INDEX('123',COINS.INSERTED$) = 0 THEN GOTO 5000
5020 IF VAL(COINS.INSERTED$) < 1 OR VAL(COINS.INSERTED$) > 3 THEN GOTO 5000
5021 IF MONEY.REMAINING >= SINGLE.COIN * VAL(COINS.INSERTED$) &
        THEN GOTO 5040 ELSE GOTO 5022
5022 GOSUB 1700
5023 RESTORE
5024 READ X1$ FOR X = 1 TO 101
5025 GOSUB 2000
5030 GOTO 5000
5040 FOR X = 1 TO 3
5050   PRINT ARROW.ADDR$(X):
5060   IF X <= VAL(COINS.INSERTED$) THEN PRINT PLAY.ARROW$(X) ELSE PRINT &
                '    '+CHAR(8)
5070   NEXT X
5080 LET COINS.PLAYED = COINS.PLAYED + VAL(COINS.INSERTED$)
5090 LET COINS.LEFT = COINS.LEFT - VAL(COINS.INSERTED$)
5100 PRINT CHAR(27)+'='+CHAR(36)+CHAR(102):
5110 PRINT IN IMAGE '%%%': COINS.PLAYED
5120 PRINT CHAR(27)+'='+CHAR(35)+CHAR(102):
5130 PRINT IN IMAGE '%%%': COINS.LEFT
5133 LET MONEY.REMAINING = SINGLE.COIN * COINS.LEFT
5135 PRINT CHAR(27)+'='+CHAR(38)+CHAR(104):
5137 PRINT IN IMAGE '$%%%.%%': MONEY.REMAINING
5140 FOR X = 1 TO 3
5150   FOR Y = 1 TO 3
5160     LET WINDOW(X,Y) = SYMBOL.NO(Y,INT((RND*20)+1))
5180     NEXT Y
5190   NEXT X
5200 FOR Y = 1 TO 3
5210   FOR X = 1 TO 3
5220     PRINT WINDOW.ADDR$(X,Y)+SYMBOL$(WINDOW(X,Y))
5230     NEXT X
5240   NEXT Y
5250 FOR PAYOFF.LINE = 1 TO 3
5260   LET PAYOFF.COMBS$(PAYOFF.LINE)=STR(WINDOW(PAYOFF.LINE,1)) + &
           STR(WINDOW(PAYOFF.LINE,2)) + STR(WINDOW(PAYOFF.LINE,3))
5270   NEXT PAYOFF.LINE
5280 FOR PAYOFF.LINE = 1 TO 3
5285   IF VAL(COINS.INSERTED$) >= PAYOFF.LINE THEN
5290     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='1 1 1' THEN
5291       PAYOFFS(PAYOFF.LINE) = 200
5292       GOSUB 6000
5295     GOTO 5430
5296     ENDIF
5300     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='2 2 2' THEN
5301       PAYOFFS(PAYOFF.LINE) = 100
5302       GOSUB 6000
5305     GOTO 5430
5306     ENDIF
5310     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='3 3 3' OR &
            LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='3 3 1' THEN
5311       PAYOFFS(PAYOFF.LINE) = 18
5312       GOSUB 6000
5315     GOTO 5430
5316     ENDIF
5320     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='4 4 4' OR &
            LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='4 4 1' THEN
5321       PAYOFFS(PAYOFF.LINE) = 14
5322       GOSUB 6000
5325     GOTO 5430
5326     ENDIF
5330     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='5 5 5' OR &
            LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='5 5 1' THEN
5331       PAYOFFS(PAYOFF.LINE) = 10
5332       GOSUB 6000
5335     GOTO 5430
5336     ENDIF
5340     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='6 6 6' OR &
            LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='6 6 1' THEN
5341       PAYOFFS(PAYOFF.LINE) = 8
5342       GOSUB 6000
5345     GOTO 5430
5346     ENDIF
5350     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='7 7 7' THEN
5351       PAYOFFS(PAYOFF.LINE) = 5
5352       GOSUB 6000
5355     GOTO 5430
5356     ENDIF
5360     IF SUBSTR(LJUST(PAYOFF.COMBS$(PAYOFF.LINE)),1,3)='7 7' THEN
5361       PAYOFFS(PAYOFF.LINE) = 2
5362       GOSUB 6000
5365     GOTO 5430
5366     ENDIF
5370     IF LJUST(PAYOFF.COMBS$(PAYOFF.LINE))='8 8 8' THEN
5371       PAYOFFS(PAYOFF.LINE) = 5
5372       GOSUB 6000
5375     GOTO 5430
5376     ENDIF
5380     IF SUBSTR(LJUST(PAYOFF.COMBS$(PAYOFF.LINE)),1,3)='8 8' THEN
5381       PAYOFFS(PAYOFF.LINE) = 3
5382       GOSUB 6000
5385     GOTO 5430
5386     ENDIF
5390     IF SUBSTR(LJUST(PAYOFF.COMBS$(PAYOFF.LINE)),1,1)='8' THEN
5391       PAYOFFS(PAYOFF.LINE) = 2
5392       GOSUB 6000
5393     ELSE
5394       PAYOFFS(PAYOFF.LINE) = 0
5396     ENDIF
5400   ELSE
5410     LET PAYOFFS(PAYOFF.LINE) = 0
5420   ENDIF
5430   NEXT PAYOFF.LINE
5440 IF PAYOFFS(1) = PAYOFFS(2) AND PAYOFFS(2) = PAYOFFS(3) THEN
5450   LET PAYOFFS(1) = PAYOFFS(1) * 3
5460   LET PAYOFFS(2) = PAYOFFS(2) * 3
5470   LET PAYOFFS(3) = PAYOFFS(3) * 3
5480 GOTO 5610
5485 ENDIF
5490 IF PAYOFFS(1) = PAYOFFS(2) THEN
5500   LET PAYOFFS(1) = PAYOFFS(1) * 2
5510   LET PAYOFFS(2) = PAYOFFS(2) * 2
5520 GOTO 5610
5525 ENDIF
5530 IF PAYOFFS(2) = PAYOFFS(3) THEN
5540   LET PAYOFFS(2) = PAYOFFS(2) * 2
5550   LET PAYOFFS(3) = PAYOFFS(3) * 2
5560 GOTO 5610
5565 ENDIF
5570 IF PAYOFFS(1) = PAYOFFS(3) THEN
5580   LET PAYOFFS(1) = PAYOFFS(1) * 2
5590   LET PAYOFFS(3) = PAYOFFS(3) * 2
5600 ENDIF
5610 FOR PAYOFF.LINE = 1 TO 3
5620   PRINT PAYOFF.ADDR$(PAYOFF.LINE):
5626   PRINT IN IMAGE '%%%% ': PAYOFFS(PAYOFF.LINE):
5630   NEXT PAYOFF.LINE
5640 LET TOTAL.WON = PAYOFFS(1) + PAYOFFS(2) + PAYOFFS(3)
5645 PRINT CHAR(27)+'='+CHAR(41)+CHAR(80)+CHAR(27)+')':
5650 PRINT IN IMAGE '%%%%': TOTAL.WON:
5655 PRINT CHAR(27)+'('
5660 IF TOTAL.WON <> 0 THEN
5670   LET COINS.LEFT = COINS.LEFT + TOTAL.WON
5675   PRINT CHAR(27)+'='+CHAR(35)+CHAR(102):
5680   PRINT IN IMAGE '%%%': COINS.LEFT
5685   LET MONEY.REMAINING = COINS.LEFT * SINGLE.COIN
5690   PRINT CHAR(27)+'='+CHAR(38)+CHAR(104):
5700   PRINT IN IMAGE '$%%%.%%': MONEY.REMAINING
5710 ENDIF
5730 GOTO 5000
6000 FOR Y = 1 TO 3
6010   PRINT WINDOW.ADDR$(PAYOFF.LINE,Y)+CHAR(10)+CHAR(27)+'G':
6020   IF WINDOW(PAYOFF.LINE,Y) = 2 THEN PRINT '7' ELSE PRINT '6'
6030   NEXT Y
6040 RETURN
