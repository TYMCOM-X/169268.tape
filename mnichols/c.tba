10 DEF LOGICAL EXTERNAL SIMPL BEGIN.BLOCK.OUT AKA ".BBO"
20 DEF EXTERNAL SIMPL END.BLOCK.OUT AKA ".EBO"
30 BASE 0
40 GOSUB 1200
50 PRINT CLEAR.SCREEN$:
60 DIM GAME.BOARD$(30,2),BOARD.ADDRESS(30,1),NUMBER.SELECTED(4),NAME$(2)
70 DIM CHECKMARK$(10,2),CHECK.ADDRESS(10,2,1),PRIZE.DESCR$(10,2),NA$(2)
80 DIM PRIZE.ADDRESS(10,2,1),ALPHABET$(27),PRIZE.LISTING$(13),PRIZE.CARD$(13)
90 DIM PRIZE.VALUE(13),INPUT.LETTERS$(4),DOOR.LINE(2),SIDE.SHOWN(30)
95 DIM CC$(2)
100 LET X=RND(-1)
101 PRINT "ARE YOU RUNNING ON A MODEL 444 TERMINAL":
102 INPUT RESPONSE$
103 IF LEFT(RESPONSE$,1)<>"Y" THEN END " "
104 PRINT CLEAR.SCREEN$
110 PRINT "STAND BY; GAME ARRAYS BEING INITIALIZED"
120 GOSUB 1260
130 PRINT "Would you like instructions":
140 INPUT RESPONSE$
150 IF LEFT(RESPONSE$,1)="Y" THEN GOSUB 1000
160 PRINT CLEAR.SCREEN$+"Please enter the names of our next two contestants:"
170 PRINT
180 FOR X=1 TO 2
190   PRINT "Player":X;
200   INPUT NA$(X)
210 NEXT X
220 PRINT
230 PRINT "Were both names entered correctly":
240 INPUT RESPONSE$
250 IF LEFT(RESPONSE$,1)="Y" THEN 260 ELSE 160
260 FOR X= 1 TO 2
270   NAME$(X)="|"+CPAD(NA$(X),15)+"|"
280 NEXT X
290 PRINT CLEAR.SCREEN$+"ENTER: < 1 >  TO USE ONE OF THE PROGRAM'S PUZZLES"
300 PRINT "       < 2 >  TO ENTER YOUR OWN PUZZLE ANSWER"
310 PRINT
320 PRINT "Your choice":
330 INPUT CHOICE$
340 IF VAL(CHOICE$) < 1 OR VAL(CHOICE$) > 2 THEN 290
350 ON VAL(CHOICE$) GOTO 360, 400
360 PRINT "INTERNAL PUZZLE WILL BE USED."
370 X2=INT(RND*30)+1
380 READ AN$ FOR X=1 TO X2
390 GOTO 580
400 PRINT CLEAR.SCREEN$+"ENTER YOUR ANSWER, USING ONLY THE LETTERS A - Z."
410 PRINT "NUMBERS OR PUNCTUATION MARKS MAKE THE ENTRY UNACCEPTABLE."
420 PRINT "LENGTH OF ANSWER MUST BE KEPT TO 30 CHARACTERS OR LESS."
430 PRINT
440 PRINT "YOUR ANSWER -> ":
450 INPUT AN$
460 IF LENGTH(AN$) > 30 OR LENGTH (AN$) < 1 THEN 550
470 IF LEFT(AN$,1)=" " THEN
480   AN$=RIGHT(AN$,LENGTH(AN$)-1)
490   GOTO 470
500 ENDIF
510 FOR X= 1 TO LENGTH(AN$)
520   IF SUBSTR(AN$,X,1)=" " OR (SUBSTR(AN$,X,1) >= "A" AND &
                                  SUBSTR(AN$,X,1) <= "Z") THEN 530 ELSE 550
530 NEXT X
540 GOTO 580
550 PRINT "PUZZLE AS ENTERED IS NOT ACCEPTABLE. PLEASE TRY AGAIN."
560 WAIT -3
565 WAIT 2
570 GOTO 400
580 PRINT "THE PUZZLE HAS BEEN SELECTED.  HAVE FUN!"
590 FOR X=1 TO LENGTH(AN$)
600   IF SUBSTR(AN$,X,1)=" " THEN A1$=A1$+" " ELSE A1$=A1$+"-"
610 NEXT X
620 WAIT 4
700 GOSUB 2300
710 TOTAL.CHOSEN=0
720 FOR CHOICE.NO=1 TO 2
730   GOSUB 800
740   GOSUB 900
750 NEXT CHOICE.NO
760 GOSUB 3000
770 GOTO 710

800 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"+CHAR(27)+"&a22r26C"+ &
          NA$(CP)+"'S TURN;   SELECT: ":
810 INPUT IN IMAGE "#":NO.SELECTED$
820 ON TRANSFORM GOTO 895
830 IF VAL(NO.SELECTED$)<1 OR VAL(NO.SELECTED$)>30 THEN 800
840 NUMBER.SELECTED(CHOICE.NO)=VAL(NO.SELECTED$)
850 IF SIDE.SHOWN(NUMBER.SELECTED(CHOICE.NO))<>0 THEN 800
860 SIDE.SHOWN(NUMBER.SELECTED(CHOICE.NO))=1
870 TOTAL.CHOSEN=TOTAL.CHOSEN+1
875 NO TRANSFORM
880 RETURN
895 RESUME 800

900 PRINT CHAR(27)+"&a"+STR(BOARD.ADDRESS(NUMBER.SELECTED(CHOICE.NO), &
          0))+"r"+STR(BOARD.ADDRESS(NUMBER.SELECTED(CHOICE.NO),1))+"C"+ &
          GAME.BOARD$(NUMBER.SELECTED(CHOICE.NO), &
          SIDE.SHOWN(NUMBER.SELECTED(CHOICE.NO)))
910 RETURN

1000 PRINT CLEAR.SCREEN$+CHAR(27)+"&a0r33C"+"CONCENTRATION"
1010 PRINT
1020 PRINT "This program is based upon the TV game show of the same name."
1030 PRINT "The object of the game is to find matching prizes on a board of 30"
1040 PRINT "squares.  The prizes do not become yours until you solve the"
1050 PRINT "puzzle.  The puzzle is a familiar phrase, movie title, the name of"
1060 PRINT "someone or just something you know.  Each time a pair of matching"
1070 PRINT "prizes is found, two letters of the alphabet will be revealed."
1080 PRINT "Each time those letters appear in the puzzle phrase, they will be"
1090 PRINT "revealed one by one until either someone guesses the puzzle or all"
1100 PRINT "of the blank spaces have been filled in.  Four of the spaces on"
1110 PRINT "the game board do not reveal letters of the alphabet, but contain"
1120 PRINT "question marks.  These squares allow the person who made the match"
1130 PRINT "to select the letter (or letters) he or she would like to see."
1140 PRINT
1150 PRINT "When you are ready to begin, please hit <return>."
1160 INPUT IN FORM "R": READY$
1170 RETURN

1200 LET HOME.UP$=CHAR(27)+"H"
1210 LET CLS$=CHAR(27)+"J"
1220 LET CLEAR.SCREEN$=HOME.UP$+CLS$
1250 RETURN

1260 TOP$=TOP$+"-" FOR X=1 TO 46
1270 SIDE$=SIDE$+"+"+RPT(CHAR(27)+"B"+CHAR(27)+"D"+"|",2)+CHAR(27) &
       +"B"+CHAR(27)+"D" FOR X=1 TO 6
1271 SIDE$=SIDE$+"+"
1275 PUZZLE.TOP$=RPT("=",34)
1280 NAME$(X)="|               |" FOR X=1 TO 2
1290 FOR X= 1 TO 10
1300 FOR Y= 1 TO 2
1310 FOR Z=0 TO 1
1320 READ PRIZE.ADDRESS(X,Y,Z)
1330 NEXT Z,Y,X
1340 FOR X=1 TO 10
1350 FOR Y=1 TO 2
1360 FOR Z=0 TO 1
1370 READ CHECK.ADDRESS(X,Y,Z)
1380 NEXT Z,Y,X
1390 NO.PRIZE$=NO.PRIZE$+"X" FOR X=1 TO 15
1400 NO.PRIZE$="|"+NO.PRIZE$+"|"
1410 FOR X=1 TO 10
1420 FOR Y=1 TO 2
1430 PRIZE.DESCR$(X,Y)=NO.PRIZE$
1440 NEXT Y,X
1450 NO.CHECK$="XX"
1460 FOR X=1 TO 10
1470 FOR Y=1 TO 2
1480 CHECKMARK$(X,Y)=NO.CHECK$
1490 NEXT Y,X
1500 DOOR.LINE(X)=1 FOR X=1 TO 2
1510 DOOR.TOP$="====="+CHAR(27)+"&dA"+"================="+CHAR(27)+"&d@"
1520 DOOR.TOP$=DOOR.TOP$+"==="+CHAR(27)+"&dA"+"================="+CHAR(27)+"&d@"
1530 DOOR.TOP$=DOOR.TOP$+"====="
1540 SECOND.LINE$="| "+CHAR(27)+"&dA"+"  "+CHAR(27)+"&d@"+" "+CHAR(27)+ &
                  "&dA"+"                 "+CHAR(27)+"&d@"+" | "+CHAR(27)+ &
                  "&dA"+"                 "+CHAR(27)+"&d@"+" "+CHAR(27)
1550 SECOND.LINE$=SECOND.LINE$+"&dA"+"  "+CHAR(27)+"&d@"+" |"
1560 DOOR.BOTTOM$=DOOR.BOTTOM$+"=" FOR X=1 TO 47
1570 DOOR.SIDE$=DOOR.SIDE$+"|"+CHAR(27)+"B"+CHAR(27)+"D" FOR X=1 TO 16
1580 DOWN.AND.BACK$=DOWN.AND.BACK$+CHAR(27)+"D" FOR X=1 TO 8
1590 DOWN.AND.BACK$=CHAR(27)+"B"+DOWN.AND.BACK$
1600 READ ALPHABET$(X) FOR X=1 TO 27
1610 R=INT(RND*4)
1620 R1=3-R
1630 IF R=0 THEN
1640   GOTO 1670
1650 ELSE
1660   READ X1$,X2$,V1 FOR X=1 TO 10 * R
1670   FOR X = 1 TO 10
1680     READ P1$,P2$,PRIZE.VALUE(X)
1690     PRIZE.LISTING$(X)=CPAD(P1$,8)+DOWN.AND.BACK$+CPAD(P2$,8)+"|"
1700     PRIZE.CARD$(X)=P1$+" "+P2$
1710     PRIZE.CARD$(X)="|"+CPAD(PRIZE.CARD$(X),15)+"|"
1720   NEXT X
1730 ENDIF
1740 IF R1=0 THEN
1750   GOTO 1790
1760 ELSE
1770   READ X1$,X2$,V1 FOR X=1 TO 10 * R1
1780 ENDIF
1790 FOR X = 11 TO 13
1800   READ P1$,P2$
1810   PRIZE.LISTING$(X)=CPAD(P1$,8)+DOWN.AND.BACK$+CPAD(P2$,8)+"|"
1820 NEXT X
1830 FOR X= 1 TO 30
1840   GAME.BOARD$(X,0)="        "+DOWN.AND.BACK$+RPT(" ",5-LENGTH(STR(X)))+ &
                       STR(X)+"   |"
1850 NEXT X
1860 Z=0
1870 FOR X=2 TO 17 STEP 3
1880   FOR Y=18 TO 54 STEP 9
1890     Z=Z+1
1900     BOARD.ADDRESS(Z,0)=X
1910     BOARD.ADDRESS(Z,1)=Y
1920 NEXT Y,X
1930 SIDE.SHOWN(X)=0 FOR X=1 TO 30
1940 FOR X=1 TO 10
1950   FOR Z = 1 TO 2
1960     Y=INT(RND*30)+1
1970     IF SIDE.SHOWN(Y)=0 THEN 1980 ELSE 1960
1980     GAME.BOARD$(Y,1)=PRIZE.LISTING$(X)
1990     SIDE.SHOWN(Y)=1
2000 NEXT Z,X
2010 FOR X=1 TO 6
2020   Y=INT(RND*30)+1
2030   IF SIDE.SHOWN(Y)=0 THEN 2040 ELSE 2020
2040   GAME.BOARD$(Y,1)=PRIZE.LISTING$(11)
2050   SIDE.SHOWN(Y)=1
2060 NEXT X
2070 FOR X=1 TO 2
2080   Y=INT(RND*30)+1
2090   IF SIDE.SHOWN(Y)=0 THEN 2100 ELSE 2080
2100   GAME.BOARD$(Y,1)=PRIZE.LISTING$(12)
2110   SIDE.SHOWN(Y)=1
2120 NEXT X
2130 FOR X= 1 TO 30
2140   IF SIDE.SHOWN(X)=0 THEN GAME.BOARD$(X,1)=PRIZE.LISTING$(13)
2150 NEXT X
2160 SIDE.SHOWN(X)=0 FOR X=1 TO 30
2170 FOR X=1 TO 26
2180   Y=INT(RND*30)+1
2190   IF SIDE.SHOWN(Y)=0 THEN 2200 ELSE 2180
2200   GAME.BOARD$(Y,2)="   "+ALPHABET$(X)+"    "+DOWN.AND.BACK$+"        "+"|"
2205   ALPHABET$(X)="   "+ALPHABET$(X)+"    "+DOWN.AND.BACK$+ &
           "        |"
2210   SIDE.SHOWN(Y)=1
2220 NEXT X
2230 FOR X=1 TO 30
2240   IF SIDE.SHOWN(X)=0 THEN
2242     GAME.BOARD$(X,2)="  "+ALPHABET$(27)+"   "+DOWN.AND.BACK$+ &
                "        |"
2244   ENDIF
2250 NEXT X
2255 ALPHABET$(27)="  "+ALPHABET$(27)+"   "+DOWN.AND.BACK$+"        |"
2260 SIDE.SHOWN(X)=0 FOR X=1 TO 30
2270 CP=1
2275 OP=2
2280 RETURN

2300 CALL BEGIN.BLOCK.OUT
2310 PRINT CLEAR.SCREEN$
2320 PRINT CHAR(27)+"&a"+STR(ROW)+"r"+STR(17)+"C"+TOP$: FOR ROW=1 TO 19 STEP 3
2330 PRINT CHAR(27)+"&a"+STR(1)+"r"+STR(COL)+"C"+SIDE$: FOR COL=17 TO 62 STEP 9
2340 PRINT CHAR(27)+"&a"+STR(BOARD.ADDRESS(X,0))+"r"+STR(BOARD.ADDRESS(X,1))+ &
           "C"+GAME.BOARD$(X,SIDE.SHOWN(X)): FOR X=1 TO 30
2345 PRINT
2350 CALL END.BLOCK.OUT
2360 RETURN

2400 CALL BEGIN.BLOCK.OUT
2410 PRINT CLEAR.SCREEN$+CHAR(27)+"&a1r17C"+DOOR.TOP$:
2420 PRINT CHAR(27)+"&a2r"+STR(COL)+"C"+DOOR.SIDE$: FOR COL=17 TO 63 STEP 23
2430 PRINT CHAR(27)+"&a18r17C"+DOOR.BOTTOM$:
2440 PRINT CHAR(27)+"&a2r22C"+CHAR(27)+"&dA"+NAME$(1)+CHAR(27)+"&d@"+CHAR(27)+ &
           "&a2r42C"+CHAR(27)+"&dA"+NAME$(2)+CHAR(27)+"&d@":
2450 PRINT CHAR(27)+"&a3r17C"+SECOND.LINE$:
2460 FOR X=1 TO 10
2470   PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(X,1,0))+"r"+ &
             STR(CHECK.ADDRESS(X,1,1))+"C"+CHAR(27)+"&dA"+CHECKMARK$(X,1)+ &
             CHAR(27)+"&d@":
2480   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(X,1,0))+"r"+ &
             STR(PRIZE.ADDRESS(X,1,1))+"C"+CHAR(27)+"&dA"+ &
             PRIZE.DESCR$(X,1)+CHAR(27)+"&d@":
2490   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(X,2,0))+"r"+ &
             STR(PRIZE.ADDRESS(X,2,1))+"C"+CHAR(27)+"&dA"+ &
             PRIZE.DESCR$(X,2)+CHAR(27)+"&d@":
2500   PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(X,2,0))+"r"+ &
             STR(CHECK.ADDRESS(X,2,1))+"C"+CHAR(27)+ &
             "&dA"+CHECKMARK$(X,2)+CHAR(27)+"&d@":
2510 NEXT X
2515 CALL END.BLOCK.OUT
2520 RETURN

3000 IF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN-1),1)=PRIZE.LISTING$(13) AND &
        GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1)=PRIZE.LISTING$(13) THEN &
        GOSUB 3100 ELSE GOTO 3020
3010 RETURN
3020 IF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN-1),1)=PRIZE.LISTING$(13) OR &
        GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1)=PRIZE.LISTING$(13) THEN &
        GOSUB 3200 ELSE GOTO 3040
3030 RETURN
3040 IF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN-1),1)= &
        GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1) THEN GOSUB 3300 &
        ELSE GOTO 3060
3050 RETURN
3060 SIDE.SHOWN(NUMBER.SELECTED(X))=0 FOR X=1 TO TOTAL.CHOSEN
3065 GOSUB 3400
3070 GOSUB 3500
3080 GOSUB 3600
3090 RETURN

3100 CHOICE.NO=3
3110 GOSUB 800
3120 GOSUB 900
3130 EARLY.EXIT=1
3140 GOSUB 3300
3150 CHOICE.NO=4
3160 GOSUB 800
3170 GOSUB 900
3180 EARLY.EXIT=0
3190 GOSUB 3300
3195 RETURN

3200 IF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1)=PRIZE.LISTING$(13) THEN
3210   TEMP=NUMBER.SELECTED(TOTAL.CHOSEN-1)
3220   NUMBER.SELECTED(TOTAL.CHOSEN-1)=NUMBER.SELECTED(TOTAL.CHOSEN)
3230   NUMBER.SELECTED(TOTAL.CHOSEN)=TEMP
3240 ENDIF
3250 GOSUB 3300
3260 RETURN

3300 IF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1)=PRIZE.LISTING$(11) THEN
3310   GOSUB 4000
3320   RETURN
3330 ORIF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1)=PRIZE.LISTING$(12) THEN
3340   GOSUB 4500
3350   RETURN
3360 ELSE
3370   GOSUB 5000
3380   RETURN
3390 ENDIF

3400 WAIT -3
3405 WAIT 2
3410 RETURN

3500 IF CP=1 THEN
3510   CP=2
3520   OP=1
3530 ELSE
3540   CP=1
3550   OP=2
3560 ENDIF
3570 RETURN

3600 PRINT CHAR(27)+"&a"+STR(BOARD.ADDRESS(NUMBER.SELECTED(X5),0))+ &
            "r"+STR(BOARD.ADDRESS(NUMBER.SELECTED(X5),1))+"C"+ &
            GAME.BOARD$(NUMBER.SELECTED(X5),SIDE.SHOWN(NUMBER.SELECTED( &
            X5))): FOR X5=1 TO TOTAL.CHOSEN
3610 RETURN

4000 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4005 FOR X=1 TO 10
4010   IF PRIZE.DESCR$(X,CP)="|XXXXXXXXXXXXXXX|" THEN
4015     NEXT X
4020     PRINT CHAR(27)+"&a22r26C"+NA$(CP)+" HAS NO PRIZES TO FORFEIT"
4025     GOTO 4255
4030   ENDIF
4035 GOSUB 2400
4040 PRINT CHAR(27)+"&a22r20C"+NA$(CP)+", WHICH PRIZE WILL YOU GIVE TO "+ &
         NA$(OP):
4045 INPUT GP$
4050 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4055 IF CP=2 THEN
4060   X7=16
4065   Y7=0
4070   Z7=-1
4075 ELSE
4080   X7=0
4085   Y7=16
4090   Z7=1
4095 ENDIF
4100 FOR X=1 TO 10
4105   IF INDEX(PRIZE.DESCR$(X,CP),GP$) THEN
4110     SAVEX=X
4115     GOTO 4155
4120   ELSE
4125     NEXT X
4130     PRINT CHAR(27)+"&a22r20C"+"YOU DON'T HAVE THAT PRIZE, "+ &
            NA$(CP)
4135     WAIT -3
4137     WAIT 2
4140     PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4145     GOTO 4040
4150   ENDIF
4155 IF CHECKMARK$(SAVEX,CP)="**" THEN 
4160   GOTO 4315
4165 ELSE
4170   FOR Y = 1 TO 10
4175     IF PRIZE.DESCR$(Y,OP)=PRIZE.DESCR$(SAVEX,CP) THEN 
4180       GOTO 4365
4185     ELSE
4190       NEXT Y
4195     ENDIF
4200 ENDIF
4205 PRIZE.DESCR$(DOOR.LINE(OP),OP)=PRIZE.DESCR$(SAVEX,CP)
4210 PRIZE.DESCR$(SAVEX,CP)="|XXXXXXXXXXXXXXX|"
4215 FOR Y=X7 TO Y7 STEP Z7
4220   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(DOOR.LINE(OP),OP,0))+ &
         "r"+STR(PRIZE.ADDRESS(DOOR.LINE(OP),OP,1)+Y)+"C"+CHAR(27)+"&dA"+ &
         SUBSTR(PRIZE.DESCR$(DOOR.LINE(OP),OP),Y+1,1)+CHAR(27)+"&d@":
4225   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(SAVEX,CP,0))+"r"+ &
         STR(PRIZE.ADDRESS(SAVEX,CP,1)+Y)+"C"+CHAR(27)+"&dA"+ &
         SUBSTR("|XXXXXXXXXXXXXXX|",Y+1,1)+CHAR(27)+"&d@"
4230 NEXT Y
4235 GOSUB 3500
4240 GOSUB 6000
4245 GOSUB 3500
4250 GOSUB 2300
4255 WAIT -3
4257 WAIT 2
4260 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4265 IF EARLY.EXIT=1 THEN
4270   RETURN
4275 ELSE
4280   FOR Y=1 TO TOTAL.CHOSEN
4285     SIDE.SHOWN(NUMBER.SELECTED(Y))=2
4290   NEXT Y
4295 ENDIF
4300 GOSUB 3400
4305 GOSUB 3600
4310 GOTO 5350
4315 FOR Z=X7 TO Y7 STEP Z7
4320   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(DOOR.LINE(OP),OP,0))+"r"+ &
          STR(PRIZE.ADDRESS(DOOR.LINE(OP),OP,1)+Z)+"C"+CHAR(27)+"&dA"+ &
          SUBSTR(PRIZE.DESCR$(SAVEX,CP),Z+1,1)+CHAR(27)+"&d@":
4325 NEXT Z
4330 CHECKMARK$(SAVEX,CP)="XX"
4335 PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(SAVEX,CP,0))+"r"+ &
        STR(CHECK.ADDRESS(SAVEX,CP,1))+"C"+CHAR(27)+"&dA"+ &
        "XX"+CHAR(27)+"&d@":
4340 PRIZE.DESCR$(DOOR.LINE(OP),OP)=PRIZE.DESCR$(SAVEX,CP)
4345 GOSUB 3500
4350 GOSUB 6000
4355 GOSUB 3500
4360 GOTO 4250
4365 FOR Z=X7 TO Y7 STEP Z7
4370   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(SAVEX,CP,0))+"r"+ &
          STR(PRIZE.ADDRESS(SAVEX,CP,1)+Z)+"C"+CHAR(27)+"&dA"+ &
          SUBSTR("|XXXXXXXXXXXXXXX|",Z+1,1)+CHAR(27)+"&d@":
4375 NEXT Z
4380 CHECKMARK$(Y,OP)="**"
4385 PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(Y,OP,0))+"r"+ &
        STR(CHECK.ADDRESS(Y,OP,1))+"C"+CHAR(27)+"&dA"+"**"+CHAR(27)+"&d@"
4390 PRIZE.DESCR$(SAVEX,CP)="|XXXXXXXXXXXXXXX|"
4395 GOTO 4250

4500 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4505 FOR X=1 TO 10
4510   IF PRIZE.DESCR$(X,OP)="|XXXXXXXXXXXXXXX|" THEN
4515     NEXT X
4520     PRINT CHAR(27)+"&a22r26C"+NA$(OP)+" HAS NO PRIZES TO CHOOSE FROM"
4525     GOTO 4755
4530   ENDIF
4535 GOSUB 2400
4540 PRINT CHAR(27)+"&a22r20C"+NA$(CP)+", WHICH PRIZE WILL YOU TAKE FROM "+ &
         NA$(OP):
4545 INPUT TP$
4550 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4555 IF CP=2 THEN
4560   X7=0
4565   Y7=16
4570   Z7=1
4575 ELSE
4580   X7=16
4585   Y7=0
4590   Z7=-1
4595 ENDIF
4600 FOR X=1 TO 10
4605   IF INDEX(PRIZE.DESCR$(X,OP),TP$) THEN
4610     SAVEX=X
4615     GOTO 4655
4620   ELSE
4625     NEXT X
4630     PRINT CHAR(27)+"&a22r20C"+NA$(OP)+" DOESN'T HAVE THAT PRIZE, "+ &
            NA$(CP)
4635     WAIT -3
4637     WAIT 2
4640     PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4645     GOTO 4540
4650   ENDIF
4655 IF CHECKMARK$(SAVEX,OP)="**" THEN 
4660   GOTO 4815
4665 ELSE
4670   FOR Y = 1 TO 10
4675     IF PRIZE.DESCR$(Y,CP)=PRIZE.DESCR$(SAVEX,OP) THEN 
4680       GOTO 4865
4685     ELSE
4690       NEXT Y
4695     ENDIF
4700 ENDIF
4705 PRIZE.DESCR$(DOOR.LINE(CP),CP)=PRIZE.DESCR$(SAVEX,OP)
4710 PRIZE.DESCR$(SAVEX,OP)="|XXXXXXXXXXXXXXX|"
4715 FOR Y=X7 TO Y7 STEP Z7
4720   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(DOOR.LINE(CP),CP,0))+ &
         "r"+STR(PRIZE.ADDRESS(DOOR.LINE(CP),CP,1)+Y)+"C"+CHAR(27)+"&dA"+ &
         SUBSTR(PRIZE.DESCR$(DOOR.LINE(CP),CP),Y+1,1)+CHAR(27)+"&d@":
4725   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(SAVEX,OP,0))+"r"+ &
           STR(PRIZE.ADDRESS(SAVEX,OP,1)+Y)+"C"+CHAR(27)+"&dA"+ &
           SUBSTR("|XXXXXXXXXXXXXXX|",Y+1,1)+CHAR(27)+"&d@":
4730 NEXT Y
4735 PRINT
4740 GOSUB 6000
4745 GOSUB 3400
4750 GOSUB 2300
4755 GOSUB 3400
4760 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"
4765 IF EARLY.EXIT=1 THEN
4770   RETURN
4775 ELSE
4780   FOR Y=1 TO TOTAL.CHOSEN
4785     SIDE.SHOWN(NUMBER.SELECTED(Y))=2
4790   NEXT Y
4795 ENDIF
4800 GOSUB 3400
4805 GOSUB 3600
4810 GOTO 5350
4815 FOR Z=X7 TO Y7 STEP Z7
4820   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(DOOR.LINE(CP),CP,0))+"r"+ &
          STR(PRIZE.ADDRESS(DOOR.LINE(OP),OP,1)+Z)+"C"+CHAR(27)+"&dA"+ &
          SUBSTR(PRIZE.DESCR$(SAVEX,OP),Z+1,1)+CHAR(27)+"&d@":
4825 NEXT Z
4830 CHECKMARK$(SAVEX,OP)="XX"
4835 PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(SAVEX,OP,0))+"r"+ &
        STR(CHECK.ADDRESS(SAVEX,OP,1))+"C"+CHAR(27)+"&dA"+ &
        "XX"+CHAR(27)+"&d@":
4840 PRIZE.DESCR$(DOOR.LINE(CP),CP)=PRIZE.DESCR$(SAVEX,OP)
4850 GOSUB 6000
4860 GOTO 4250
4865 FOR Z=X7 TO Y7 STEP Z7
4870   PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(SAVEX,OP,0))+"r"+ &
          STR(PRIZE.ADDRESS(SAVEX,OP,1)+Z)+"C"+CHAR(27)+"&dA"+ &
          SUBSTR("|XXXXXXXXXXXXXXX|",Z+1,1)+CHAR(27)+"&d@":
4875 NEXT Z
4880 CHECKMARK$(Y,CP)="**"
4885 PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(Y,OP,0))+"r"+ &
        STR(CHECK.ADDRESS(Y,OP,1))+"C"+CHAR(27)+"&dA"+"**"+CHAR(27)+"&d@"
4890 PRIZE.DESCR$(SAVEX,OP)="|XXXXXXXXXXXXXXX|"
4895 GOTO 4250

5000 FOR X=1 TO 10
5020   IF GAME.BOARD$(NUMBER.SELECTED(TOTAL.CHOSEN),1)=PRIZE.LISTING$(X) THEN
5030     SAVEX=X
5040   ELSE
5050     NEXT X
5060   ENDIF
5070 FOR Y=1 TO 10
5080   IF PRIZE.DESCR$(Y,CP)=PRIZE.CARD$(SAVEX) THEN
5090     GOSUB 2400
5100     CHECKMARK$(Y,CP)="**"
5110     PRINT CHAR(27)+"&a"+STR(CHECK.ADDRESS(Y,CP,0))+"r"+ &
           STR(CHECK.ADDRESS(Y,CP,1))+"C"+CHAR(27)+"&dA"+ &
           CHECKMARK$(Y,CP)+CHAR(27)+"&d@"
5120     5290
5130   ELSE
5140     NEXT Y
5150   ENDIF
5160 GOSUB 2400
5170 PRIZE.DESCR$(DOOR.LINE(CP),CP)=PRIZE.CARD$(SAVEX)
5180 IF CP=1 THEN
5190   X7=16
5200   Y7=0
5210   Z7=-1
5220 ELSE
5230   X7=0
5240   Y7=16
5250   Z7=1
5260 ENDIF
5270 PRINT CHAR(27)+"&a"+STR(PRIZE.ADDRESS(DOOR.LINE(CP),CP,0))+"r"+ &
           STR(PRIZE.ADDRESS(DOOR.LINE(CP),CP,1)+Y)+"C"+CHAR(27)+ &
           "&dA"+SUBSTR(PRIZE.CARD$(SAVEX),Y+1,1)+CHAR(27)+"&d@": &
           FOR Y=X7 TO Y7 STEP Z7
5275 PRINT
5280 GOSUB 6000
5290 WAIT -3
5295 WAIT 2
5300 GOSUB 2300
5310 IF EARLY.EXIT=1 THEN RETURN
5320 SIDE.SHOWN(NUMBER.SELECTED(Y))=2 FOR Y=1 TO TOTAL.CHOSEN
5330 GOSUB 3400
5340 GOSUB 3600
5350 GOSUB 7000
5360 GOSUB 7500
5370 GOSUB 8000
5380 GOSUB 8100
5390 GOSUB 2300
5400 RETURN

6000 DOOR.LINE(CP)=DOOR.LINE(CP)+1
6010 IF DOOR.LINE(CP) > 10 THEN 
6020   DOOR.LINE(CP) = 0
6030   GOTO 6070
6040 ELSE
6050   IF PRIZE.DESCR$(DOOR.LINE(CP),CP) <> "|XXXXXXXXXXXXXXX|" THEN 6000 &
           ELSE RETURN
6060 ENDIF
6070 FOR X = 1 TO 10
6080   IF PRIZE.DESCR$(X,CP) <> "|XXXXXXXXXXXXXXX|" THEN
6090     NEXT X
6100     DOOR.LINE(CP)=X
6110   ELSE
6120     DOOR.LINE(CP)=X
6130   ENDIF
6140 RETURN

7000 FOR X=1 TO TOTAL.CHOSEN
7005   FOR Y=1 TO 27
7015     IF GAME.BOARD$(NUMBER.SELECTED(X),2)=ALPHABET$(Y) THEN &
            INPUT.LETTERS$(X)=CHAR(Y+64)
7025   NEXT Y
7035 NEXT X
7045 FOR X=1 TO TOTAL.CHOSEN
7055   IF INPUT.LETTERS$(X)=CHAR(91) THEN
7065     PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"+CHAR(27)+"&a22r8C"+ &
            "ENTER ANY LETTER YOU THINK IS IN THE PUZZLE":
7070     INPUT INPUT.LETTERS$(X)
7080     IF INPUT.LETTERS$(X) < "A" OR INPUT.LETTERS$(X) > "Z" OR &
            LENGTH(INPUT.LETTERS$(X))>1 THEN
7090       GOTO 7065
7100     ELSE 
7105       WAIT -3
7110       WAIT 2
7115     ENDIF
7120   ENDIF
7130 NEXT X
7140 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"+CHAR(27)+"&a22r10C"+ &
       "LETTERS ADDED TO PUZZLE:";
7150 PRINT INPUT.LETTERS$(X); FOR X=1 TO TOTAL.CHOSEN
7155 PRINT
7160 GOSUB 3400
7165 A2$=""
7170 FOR X=1 TO LENGTH(AN$)
7175   A3$=""
7180   FOR Y=1 TO TOTAL.CHOSEN
7190     IF SUBSTR(AN$,X,1)=INPUT.LETTERS$(Y) THEN
7200       A3$=INPUT.LETTERS$(Y)
7230     ENDIF
7240   NEXT Y
7242   IF A3$="" THEN A2$=A2$+SUBSTR(A1$,X,1) ELSE A2$=A2$+A3$
7250 NEXT X
7260 A1$=A2$
7265 RETURN

7500 TOTAL.MATCHED=TOTAL.MATCHED+TOTAL.CHOSEN
7505 IF TOTAL.MATCHED <> 28 THEN RETURN
7510 CC=0
7515 FOR X= 1 TO 30
7520   IF SIDE.SHOWN(X)=0 THEN
7525     CC=CC+1
7530     CC$(CC)=GAME.BOARD$(X,1)
7535   ENDIF
7540 NEXT X
7545 IF CC$(CC)=CC$(CC-1) THEN RETURN
7550 PRINT CHAR(27)+"&a22r0C"+CHAR(27)+"K"+CHAR(27)+"&a21r12C"+ &
       "THE LAST 2 PARTS OF THE PUZZLE DO NOT MATCH - "+ &
       CHAR(27)+"&a22r12C"+"YOU EACH GET ONE LAST CHANCE TO WIN THE GAME"
7555 WAIT -1
7560 WAIT 5
7565 FOR Y=1 TO 2
7570   GOSUB 8000
7575   GOSUB 8100
7580   GOSUB 3500
7585 NEXT Y
7590 PRINT CLEAR.SCREEN$
7595 PRINT "SORRY!  NO ONE SOLVED THE PUZZLE!"
7600 PRINT "A REMATCH WILL BE NECESSARY!"
7605 PRINT "THANKS FOR PLAYING CONCENTRATION."
7610 PRINT
7615 PRINT "THIS HAS BEEN A GOODSON/TODDMAN PRODUCTION."
7620 END " "

8000 PRINT CLEAR.SCREEN$+CHAR(27)+"&a5r23C"+PUZZLE.TOP$+CHAR(27)+"&a6r23C"+ &
         "|"+CHAR(27)+"&a6r56C"+"|"
8002 PRINT CHAR(27)+"&a7r23C"+PUZZLE.TOP$
8005 PRINT CHAR(27)+"&a6r25C"+A1$
8010 N$=CPAD(NA$(CP)+",",16)
8015 PRINT CHAR(27)+"&a9r32C"+N$
8020 PRINT CHAR(27)+"&a10r27C"+"CAN YOU SOLVE THE PUZZLE?"
8025 PRINT CHAR(27)+"&a13r10C"+"YOUR GUESS ->> "+A1$
8030 RETURN

8100 PRINT CHAR(27)+"&a13r25C":
8110 INPUT IN FORM "R":Q$
8115 PRINT CHAR(27)+"&a13r25C"+Q$+CHAR(27)+"K"
8120 IF Q$<>AN$ THEN
8125   PRINT CHAR(27)+"&a16r26C"+"SORRY, THAT IS INCORRECT."
8130   WAIT -1
8135   WAIT 2
8140   RETURN
8145 ELSE
8146   PRINT CHAR(27)+"&a16r26C"+"YOU ARE CORRECT! YOU WIN!"
8147   WAIT -1
8148   WAIT 2
8150   FOR X=1 TO 10
8155     PRIZE.DESCR$(X,OP)="|XXXXXXXXXXXXXXX|"
8160     CHECKMARK$(X,OP)="XX"
8165   NEXT X
8170   GOSUB 2400
8175   PRINT CHAR(27)+"&a20r27C"+"HERE IS WHAT YOU HAVE WON!"
8180   PRINT CHAR(27)+"&a21r31C"+"CONGRATULATIONS!"
8185   FOR X=1 TO 10
8190     FOR Y=1 TO 10
8195       IF PRIZE.DESCR$(X,CP)=PRIZE.CARD$(Y) THEN V1=V1+PRIZE.VALUE(Y)
8200       IF CHECKMARK$(X,CP)="**" THEN V1=V1+PRIZE.VALUE(Y)
8205     NEXT Y
8210   NEXT X
8215   PRINT CHAR(27)+"&a22r21C":
8220   PRINT IN IMAGE "TOTAL VALUE OF YOUR PRIZES: $%%%%%.%%":V1
8225   GOTO 7605
8230 ENDIF
10000 DATA 4,22,4,42,5,22,5,42,6,22,6,42,7,22,7,42,8,22,8,42,9,22,9,42
10010 DATA 10,22,10,42,11,22,11,42,12,22,12,42,13,22,13,42
10020 DATA 4,19,4,60,5,19,5,60,6,19,6,60,7,19,7,60,8,19,8,60,9,19,9,60
10030 DATA 10,19,10,60,11,19,11,60,12,19,12,60,13,19,13,60
10040 DATA "A","B","C","D","E","F","G","H","I","J","K","L","M","N","O"
10050 DATA "P","Q","R","S","T","U","V","W","X","Y","Z","???"
10060 DATA "WORLD","TOUR",3000,"MOVIE","CAMERA",100,"AUTO","STEREO",250
10070 DATA "MINK","JACKET",750,"VIDEO","RECORDER",2000,"COLOR","TV",500
10080 DATA "SPINET","ORGAN",950,"ICE","CUBES",0,"GARBAGE","CAN",0
10090 DATA "BANANA","PEEL",0
10100 DATA "TRIP TO","PARIS",2000,"HOME","COMPUTER",650,"WASHER &","DRYER",1150
10110 DATA "SPINET","PIANO",1500,"TAPE","RECORDER",250,"GOLF","CLUBS",400
10120 DATA "BEDROOM","SET",2500,"GARTER","BELT",0,"LUMP OF","COAL",0
10130 DATA "AVOCADO","PIT",0
10140 DATA "DATSUN","200 SX",8000,"GAS","RANGE",800,"STEREO","SYSTEM",550
10150 DATA "$1000","CASH",1000,"ROLLTOP","DESK",750,"BOWLING","EQUIP.",125
10160 DATA "VACUUM","CLEANER",300,"CORN","PLASTER",0,"PENCIL","SHAVINGS",0
10170 DATA "SOAP","SCUM",0
10180 DATA "DISH","WASHER",550,"MEXICO","VACATION",1200,"MIC'WAVE","OVEN",475
10190 DATA "DINETTE","SET",1000,"PATIO","SET",300,"HOT","TUB",900
10200 DATA "WATER","BED",750,"DEAD","BATTERY",0,"DIRTY","DIAPER",0
10210 DATA "STALE","DONUT",0
10220 DATA "FORFEIT","1 GIFT","TAKE","1 GIFT","<<WILD>>","<<CARD>>"
10230 DATA "THE GOODBYE GIRL","JUST GOING AROUND IN CIRCLES"
10240 DATA "THE TIJUANA BRASS","FROM HERE TO ETERNITY"
10250 DATA "VICTORIA PRINCIPAL","DAVID COPPERFIELD"
10260 DATA "RETURN OF THE JEDI","BACK TO THE FUTURE"
10270 DATA "CLASSIC COCA COLA","REMINGTON STEELE"
10280 DATA "SAMMY DAVIS JUNIOR","TWELVE OCLOCK HIGH"
10290 DATA "FIRE IN THE VALLEY","HENRY KISSENGER"
10300 DATA "THE TWILIGHT ZONE","ARTIFICIAL INTELLIGENCE"
10310 DATA "SOMEWHERE IN TIME","ONE MORE FOR THE ROAD"
10320 DATA "BOARDWALK AND PARK PLACE","TAKE THIS JOB AND SHOVE IT"
10330 DATA "COMING ATTRACTIONS","THE BLACK CAULDREN"
10340 DATA "INTERNATIONAL VELVET","MIAMI VICE"
10350 DATA "ROBIN WILLIAMS","STEFANIE POWERS"
10360 DATA "MASTERS OF THE UNIVERSE","POP GOES THE WEASEL"
10370 DATA "THE BIG BANG THEORY","FRIDAY THE THIRTEENTH"
    E^