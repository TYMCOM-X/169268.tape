   5 BASE 0
  10 DIM PLAYERS.PICK(80),WINNING.NUMBERS(80),NUMBER.ADDR$(80), &
         PAYOFFS(10,10)
  15 DEF EXTERNAL SIMPL STUFIB(STRING S)
  17 GOSUB 500
  20 GOSUB 1000
  30 GOSUB 2000
  35 GOSUB 2500
  70 IF AMT.LEFT > 0 THEN GOTO 35
  80 GOSUB 10000
  90 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(48) + &
    "Sorry, you're all tapped out.  Maybe next time..."
 100 END ""
 500 PRINT 'Are you running on a ADM 31 (TYM425) terminal':
 510 INPUT RESPONSE$
 520 IF LEFT(RESPONSE$,1) <> 'Y' AND LEFT(RESPONSE$,1) <> 'y' THEN END ''
 530 PRINT CHAR(27)+';'
 540 PRINT &
"This program simulates California's new Keno Lottery game."
 550 PRINT &
'Before this program will run correctly, you must enable the underline'
 560 PRINT &
'capability of the TYM425. To determine if that capability is already enabled'
 570 PRINT &
'in your terminal, place your terminal in local mode by typing <shift-'
 580 PRINT &
'function> <B>, and then typing <ESC> <G> <1>. If the underline character'
 590 PRINT &
'appears on the terminal screen, no modification to your terminal is '
 600 PRINT &
'necessary. If not, turn DIP switch 6 of switch group C-11 off (contained in'
 610 PRINT &
'second switch grouping from the rear of the terminal on the PC board).'
 620 PRINT
 630 PRINT &
'To place your terminal back in character mode, type <shift-function> <C>'
 640 PRINT &
'and type a carriage return to continue.':
 645 CALL STUFIB('.')
 650 INPUT IN IMAGE '#': RESPONSE$
 670 PRINT CHAR(27) + ';'
 680 RETURN
1000 N = 0
1010 FOR X = 5 TO 8
1020   FOR Y = 15 TO 60 STEP 5
1030     N = N + 1
1040     NUMBER.ADDR$(N) = CHAR(27) + '=' + CHAR(31+X) + CHAR(31+Y)
1050     NEXT Y
1060   NEXT X
1070 FOR X = 11 TO 14
1080   FOR Y = 15 TO 60 STEP 5
1090     N = N + 1
1100     NUMBER.ADDR$(N) = CHAR(27) + '=' + CHAR(31+X) + CHAR(31+Y)
1110     NEXT Y
1120   NEXT X
1130 FOR X = 5 TO 14
1140   FOR Y = 15 TO 65 STEP 50
1150     PRINT CHAR(27) + '=' + CHAR(X+31) + CHAR(Y+31):
1160     IF X = 9 THEN
1170       IF Y <> 65 THEN
1180         PRINT CHAR(27) + 'G4'
1190       ELSE
1200         PRINT CHAR(27) + 'G0'
1210       ENDIF
1220     ELSE
1230       IF Y <> 65 THEN
1240         PRINT CHAR(27) + 'G5'
1250       ELSE
1260         PRINT CHAR(27) + 'G0'
1270       ENDIF
1280     ENDIF
1290     NEXT Y
1300   NEXT X
1310 FOR X = 5 TO 8
1320   FOR Y = 20 TO 60 STEP 5
1330     PRINT CHAR(27) + '=' + CHAR(X+31) + CHAR(Y+31) + CHAR(27) + 'G5'
1340     NEXT Y
1350   NEXT X
1360 FOR X = 11 TO 14
1370   FOR Y = 20 TO 60 STEP 5
1380     PRINT CHAR(27) + '=' + CHAR(X+31) + CHAR(Y+31) + CHAR(27) + 'G5'
1390     NEXT Y
1400   NEXT X
1410 PRINT.NUM = 0
1420 FOR X = 1 TO 80
1430   PRINT NUMBER.ADDR$(X):
1435   PRINT CHAR(27) + 'G5':
1440   PRINT IN IMAGE ' %% ': X:
1450   PRINT CHAR(8)
1460   NEXT X
1470 PRINT CHAR(27) + '=' + CHAR(31+9) + CHAR(31+29) + &
          'T  Y  M  S  H  A  R  E'
1480 PRINT CHAR(27) + '=' + CHAR(31+10) + CHAR(31+35) + 'K  E  N  O'
1490 FOR X = 2 TO 17
1500   FOR Y = 66 TO 12 STEP -54
1510     PRINT CHAR(27) + '=' + CHAR(31+X) + CHAR(33+Y) + CHAR(27) + 'G0'
1520     PRINT CHAR(27) + '=' + CHAR(31+X) + CHAR(31+Y) + CHAR(27) + 'G4'
1530     NEXT Y
1540   NEXT X
1550 FOR X = 1 TO 18 STEP 17
1560   FOR Y = 12 TO 68 STEP 56
1570     PRINT CHAR(27) + '=' + CHAR(31+X) + CHAR(31+Y) + CHAR(27):
1580     IF Y = 12 THEN PRINT 'G4' ELSE PRINT 'G0'
1590     NEXT Y
1600   NEXT X
1610 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(47) + 'GAME #' + &
       CHAR(27) + 'G5' + '   ' + CHAR(27) + 'G0'
1620 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(63) + 'NUMBERS BET:' + &
       CHAR(27) + 'G5' + '  ' + CHAR(27) + 'G0'
1630 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(83) + 'WAGER:' + &
       CHAR(27) + 'G5' + '      ' + CHAR(27) + 'G0'
1640 PRINT CHAR(27) + '=' + CHAR(47) + CHAR(47) + 'CURRENT TOTAL' + &
       CHAR(27) + 'G5' + '         ' + CHAR(27) + 'G0'
1650 PRINT CHAR(27) + '=' + CHAR(47) + CHAR(73) + 'WON THIS GAME' + &
       CHAR(27) + 'G5' + '         ' + CHAR(27) + 'G0'
1660 DATA 0,2
1670 DATA 0,0,8
1680 DATA 0,0,2,20
1690 DATA 0,0,1,4,50
1700 DATA 0,0,0,2,10,250
1710 DATA 0,0,0,1,4,25,1000
1720 DATA 0,0,0,1,2,10,100,2500
1730 DATA 0,0,0,0,1,10,40,400,10000
1740 DATA 0,0,0,0,1,4,10,100,2500,25000
1750 DATA 4,0,0,0,0,2,5,50,250,3000,250000
1810 FOR X = 1 TO 10
1820   FOR Y = 0 TO X
1830     READ PAYOFFS(X,Y)
1840     NEXT Y
1850   NEXT X
1860 X = RND(-5)
1870 RETURN
2000 PRINT CHAR(27) + '=' + CHAR(47) + CHAR(61):
2005 LET AMT.LEFT = 20
2010 PRINT IN IMAGE '$%%%%%.%%': AMT.LEFT
2020 LET NEXT.GAME = (INT(RND * 1000) + 1) MOD 1000
2030 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(54):
2040 PRINT IN IMAGE '%%%': NEXT.GAME
2050 RETURN
2500 GOSUB 10000
2510 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(57):
2520 PRINT 'Enter <1> to play a new game'
2530 PRINT CHAR(27) + '=' + CHAR(52) + CHAR(63):
2540 PRINT '<2> to see the payoff charts'
2550 PRINT CHAR(27) + '=' + CHAR(54) + CHAR(57):
2560 PRINT 'Your choice:  ' + CHAR(8) + CHAR(27) + 'T':
2570 INPUT IN FORM '#': MENU.SELECT
2580 IF MENU.SELECT <> 1 AND MENU.SELECT <> 2 THEN GOTO 2550
2590 ON MENU.SELECT GOTO 2600, 2700
2600 GOSUB 3000
2610 GOSUB 4000
2620 GOSUB 5000
2630 RETURN
2700 GOSUB 10000
2710 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(57):
2720 PRINT 'Select payoff chart # (1-10):'
2730 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(87) + CHAR(27) + 'T':
2740 INPUT IN FORM '#': PAYOFF.CHART.NO
2750 IF PAYOFF.CHART.NO <> INT(PAYOFF.CHART.NO) THEN GOTO 2730
2760 IF PAYOFF.CHART.NO < 1 OR PAYOFF.CHART.NO > 10 THEN GOTO 2730
2764 COL.1 = 1
2770 PRINT CHAR(27) + 'J' + CHAR(27) + ';'
2780 PRINT IN IMAGE &
    'Catch        $%%.%% bet        $ 3.00 bet        $ 5.00 bet': COL.1
2790 PRINT
2800 FOR X = 0 TO PAYOFF.CHART.NO
2810   IF PAYOFFS(PAYOFF.CHART.NO, X) <> 0 THEN
2811     LET PAY.1 = MIN(PAYOFFS(PAYOFF.CHART.NO,X) * COL.1, 10000000)
2812     LET PAY.2 = MIN(PAYOFFS(PAYOFF.CHART.NO,X) * 3, 10000000)
2813     LET PAY.3 = MIN(PAYOFFS(PAYOFF.CHART.NO,X) * 5, 10000000)
2820     PRINT IN IMAGE &
    ' %% of %%    $%%%%%%%%         $%%%%%%%%         $%%%%%%%%': &
    X, PAYOFF.CHART.NO, PAY.1, PAY.2, PAY.3
2830   ENDIF
2840   NEXT X
2850 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(51):
2860 PRINT 'Press <enter> when ready...':
2870 CALL STUFIB('.')
2880 INPUT IN IMAGE '#': CARR.RET$
2890 PRINT CHAR(27) + 'J'
2900 GOTO 2500 
3000 GOSUB 10000
3010 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(52):
3020 PRINT 'All bets must be in multiples of $1.00.'
3030 PRINT CHAR(27) + '=' + CHAR(52) + CHAR(41):
3040 PRINT
3050 PRINT CHAR(27) + '=' + CHAR(54) + CHAR(54):
3060 PRINT 'Enter amount to be wagered:'
3070 PRINT CHAR(27) + '=' + CHAR(54) + CHAR(82) + CHAR(27) + 'T':
3080 INPUT IN IMAGE '#': AMT.WAGERED
3090 IF INT(AMT.WAGERED) <> AMT.WAGERED THEN GOTO 3070
3095 IF AMT.LEFT - AMT.WAGERED < 0 THEN GOTO 3070
3100 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(90):
3110 PRINT IN IMAGE '$%%.%%': AMT.WAGERED
3120 GOSUB 10000
3130 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(54):
3140 PRINT 'You may play from 1 to 10 numbers.'
3150 PRINT CHAR(27) + '=' + CHAR(53) + CHAR(54):
3160 PRINT 'How many numbers will you play?'
3170 PRINT CHAR(27) + '=' + CHAR(53) + CHAR(86) + CHAR(27) + 'T':
3180 INPUT IN IMAGE '#': NUMBERS.PLAYED
3190 IF INT(NUMBERS.PLAYED) <> NUMBERS.PLAYED THEN GOTO 3170
3200 IF NUMBERS.PLAYED < 1 OR NUMBERS.PLAYED > 10 THEN GOTO 3170
3210 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(76):
3220 PRINT IN IMAGE '%%': NUMBERS.PLAYED
3222 LET AMT.LEFT = AMT.LEFT - AMT.WAGERED
3224 PRINT CHAR(27) + '=' + CHAR(47) + CHAR(61):
3226 PRINT IN IMAGE '$%%%%%.%%': AMT.LEFT
3230 GOSUB 10000
3240 FOR X = 1 TO NUMBERS.PLAYED
3250   PRINT CHAR(27) + '=' + CHAR(53) + CHAR(58):
3260   PRINT IN IMAGE '(%%) Number selected:' + CHAR(27) + 'T': X:
3270   PRINT CHAR(27) + '=' + CHAR(53) + CHAR(80):
3280   INPUT IN IMAGE '#': SELECTED.NUMBER
3290   IF INT(SELECTED.NUMBER) <> SELECTED.NUMBER THEN GOTO 3250
3300   IF SELECTED.NUMBER < 1 OR SELECTED.NUMBER > 80 THEN GOTO 3250
3310   IF PLAYERS.PICK(SELECTED.NUMBER) = 1 THEN GOTO 3250
3320   LET PLAYERS.PICK(SELECTED.NUMBER) = 1
3330   PRINT NUMBER.ADDR$(SELECTED.NUMBER) + CHAR(27) + 'G7'
3340   NEXT X
3350 RETURN
4000 FOR X = 1 TO 20
4010   Z = INT(RND * 80) + 1
4020   IF WINNING.NUMBERS(Z) = 1 THEN GOTO 4010
4030   LET WINNING.NUMBERS(Z) = 1
4040   PRINT NUMBER.ADDR$(Z) + CHAR(27) + 'G':
4050   IF PLAYERS.PICK(Z) = 1 THEN
4051     PRINT '7': 
4052     LET WINNERS = WINNERS + 1
4053   ELSE
4054     PRINT '5':
4055   ENDIF
4060   PRINT IN IMAGE CHAR(27) + ')' + ' %% ' + CHAR(8): Z
4070   NEXT X
4075 PRINT CHAR(27) + '('
4080 PRINT CHAR(27) + '=' + CHAR(47) + CHAR(87):
4090 PRINT IN IMAGE '$%%%%%.%%': &
    MIN(PAYOFFS(NUMBERS.PLAYED, WINNERS) * AMT.WAGERED, 50000)
4100 LET AMT.LEFT = AMT.LEFT + &
    MIN(PAYOFFS(NUMBERS.PLAYED, WINNERS) * AMT.WAGERED, 50000)
4110 PRINT CHAR(27) + '=' + CHAR(47) + CHAR(61):
4120 PRINT IN IMAGE '$%%%%%.%%': AMT.LEFT
4130 RETURN
5000 GOSUB 10000
5010 PRINT CHAR(27) + '=' + CHAR(51) + CHAR(51):
5020 PRINT 'Press <enter> when ready for the next game...':
5030 CALL STUFIB('.')
5040 INPUT IN IMAGE '#': CARR.RET$
5050 FOR X = 1 TO 80
5060   IF WINNING.NUMBERS(X) = 1 THEN
5070     PRINT NUMBER.ADDR$(X) + CHAR(27) + 'G5':
5080     PRINT IN IMAGE ' %% ' + CHAR(8): X
5081     LET WINNING.NUMBERS(X) = 0
5082     LET PLAYERS.PICK(X) = 0
5090   ELSE
5100     IF PLAYERS.PICK(X) = 1 THEN
5110       PRINT NUMBER.ADDR$(X) + CHAR(27) + 'G5':
5120       PRINT IN IMAGE ' %% ' + CHAR(8): X
5125       LET PLAYERS.PICK(X) = 0
5130     ENDIF
5140   ENDIF
5150   NEXT X
5160 LET NEXT.GAME = NEXT.GAME + 1
5165 IF NEXT.GAME > 999 THEN LET NEXT.GAME = 0
5170 PRINT CHAR(27) + '=' + CHAR(34) + CHAR(54):
5180 PRINT IN IMAGE '%%%': NEXT.GAME
5185 LET WINNERS = 0
5190 RETURN
10000 PRINT CHAR(27) + '=' + CHAR(50) + CHAR(32) + CHAR(27) + 'y'
10010 RETURN
   