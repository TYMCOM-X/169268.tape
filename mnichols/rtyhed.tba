  50 PRINT "RTYHED - Version 1.0 ": DATE
  60 PRINT
  70   !
  80   ! RTYHED.TBA - Version 1.0 - September 3, 1986 - Mark Nichols
  90   !
 100   ! THIS PROGRAM READS A CONTROL FILE CONTAINING A LIST OF ROYALTY
 110   !   FILE NAMES THAT NEED TO HAVE A CONTROL HEADER RECORD INSERTED 
 120   !   BEFORE THE FIRST DATA LINE OF THE CORRESPONDING FILE.  THE CONTROL
 130   !   RECORD IS FORMATTED AS FOLLOWS:
 140   !     COLS  1 -   5: blanks
 150   !     COL         6: "8" (record type to indicate header record)
 160   !     COLS  7 -  10: date of file in format MMDD
 170   !     COLS 11 -  13: host number
 180   !     COLS 14 - 175: blanks
 190   !
 200 OPEN "RTLIST" FOR INPUT AS FILE 1
 210 ON ENDFILE 1 GOTO 700
 220 INPUT FROM 1 IN FORM "R": DATFILE$
 230 OPEN DATFILE$ FOR INPUT AS FILE 2
 240 ON ENDFILE 2 GOTO 500
 245 PRINT "Opening file ": DATFILE$: ":":
 247 INCNT = 0
 250 OPEN "RTOUT.TMP-ANY" FOR OUTPUT AS FILE 3
 260   !
 270   ! NOW WRITE HEADER RECORD AS FIRST RECORD IN FILE
 280   !
 290 PRINT ON 3 IN FORM "5B 1X 4X 3X 162B /": "8", SUBSTR(DATFILE$,3,4), &
                 SUBSTR(DATFILE$,8,3)
 300   !
 310   ! NOW APPEND THE REST OF FILE 2 TO THE HEADER RECORD
 320   !
 330 INPUT FROM 2 IN FORM "R": RECDAT$
 335 INCNT = INCNT + 1
 340 PRINT ON 3 IN FORM "175X /": RECDAT$
 350 GOTO 330
 360   !
 370   ! CONTROL JUMPS TO 500 AT END OF FILE 2
 380   !
 500 CLOSE 2, 3
 505 RENAME "RTOUT.TMP" AS DATFILE$+"-ANY"
 507 PRINT INCNT: " records were read and written."
 510 GOTO 220
 520   !
 530   ! CONTROL JUMPS TO 700 WHEN CONTROL FILE 1 IS EXHAUSTED
 540   !
 700 CLOSE 1
 710 END "End of RTYHED"
