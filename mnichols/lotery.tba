  10 DEF INTEGER LOTTERYNUM
  20   DIM DIGIT$(5)
  30   DIGIT$(1)=CHAR(INT(RND*8)+48)
  40   FOR Q=2 TO 5
  50     DIGIT$(Q)=CHAR(INT(RND*10)+48)
  60   NEXT Q
  70   NUMBER$=""
  80   NUMBER$=NUMBER$+DIGIT$(Q) FOR Q=1 TO 5
  90   EXIT VAL(NUMBER$)
 100 ENDF LOTTERYNUM
 110 DEF STRING SETVAL(INTEGER REPS, STRING WINVAL)
 111   INTEGER X, Y, Z, Z1
 112   FOR X=1 TO REPS
 113     Y=LOTTERYNUM
 114     Z=INT(Y/1000)+1
 115     Z1=Y MOD 1000 + 1
 116     IF SUBSTR(PICKS(Z),Z1,1)='0' &
           THEN PICKS(Z)=SUBSTR(PICKS(Z),1,Z1-1)+ &
                       WINVAL+ &
		       SUBSTR(PICKS(Z),Z1+1) &
	   ELSE GOTO 113
 117     IF WINVAL='6' THEN 
 118       FVTHOUS(X)=Y+1
 119     ORIF WINVAL='5' THEN
 120       THOUS(X)=Y+1
 121     ORIF WINVAL='4' THEN
 122       FVHUNDS(X)=Y+1
 123     ORIF WINVAL='3' THEN
 124       HUNDS(X)=Y+1
 125     ENDIF
 170   NEXT X
 180 ENDF SETVAL
 200 PRINT "Do you want instructions? ":
 210 IIF 'R':RESPONSE$
 220 IF RESPONSE$='y' OR RESPONSE$='Y' THEN GOSUB 5000
 230 ON ESCAPE GOTO 2000
 500 INTEGER WHEEL(10), DRAW, SELECT, AMOUNTS(0:6), X, Y, Z, Z1, AMTWON, BOX, &
             HUNDS(20), FVHUNDS(3), THOUS(2), FVTHOUS(2)
 505 STRING PICKS(80)
 510 DATA 0,2,5,100,500,1000,5000
 515 READ AMOUNTS
 520 X=RND(-1)
 525 PRINT 'Initializing 80,000 lottery tickets...'
 530 PICKS(X)=RPT('0',1000) FOR X=1 TO 80
 535 PRINT 'Selecting 2 $5000.00 tickets...'
 540 CALL SETVAL(2,'6')
 545 PRINT 'Selecting 2 $1000.00 tickets...'
 550 CALL SETVAL(2,'5')
 555 PRINT 'Selecting 3 $500.00 tickets...'
 560 CALL SETVAL(3,'4')
 565 PRINT 'Selecting 20 $100.00 tickets...'
 570 CALL SETVAL(20,'3')
 575 PRINT 'Selecting 960 $5.00 tickets...'
 580 CALL SETVAL(960,'2')
 585 PRINT 'Selecting 8000 $2.00 tickets...'
 590 CALL SETVAL(8000,'1')
1000 PRINT 'Select a number from 1 to 80000: ':
1001 INPUT BOX
1002 IF BOX < 1 OR BOX > 80000 THEN GOTO 1000
1003 BOX=BOX-1
1004 Z=INT(BOX/1000)+1
1005 Z1=BOX MOD 1000 + 1
1010 IF SUBSTR(PICKS(Z),Z1,1)='7' THEN 
1011   PRINT 'That number has already been selected; choose again'
1012   GOTO 1000
1013 ELSE
1014   AMTWON=AMOUNTS(VAL(SUBSTR(PICKS(Z),Z1,1)))
1020   PICKS(Z)=SUBSTR(PICKS(Z),1,Z1-1)+'7'+SUBSTR(PICKS(Z),Z1+1)
1021 ENDIF
1022 PRINT IN IMAGE 'Your winnings: $%%%%': AMTWON
1030 INCOME=INCOME+AMTWON
1035 TICKET=TICKET+1
1040 IF AMTWON<>100 THEN GOTO 1000
1050 PRINT 'Congratulations! You now have a 1 in 625 chance to spin the'
1060 PRINT '  California Lottery Jackpot wheel!'
1070 PRINT
1080 PRINT 'Select a number from 1 to 625: ':
1090 INPUT DRAW
1091 IF DRAW < 1 OR DRAW > 625 THEN GOTO 1080
1100 SELECT=INT(RND*625)+1
1110 IF SELECT=DRAW THEN
1120   PRINT 'We have a winner!! You have won at least $10,000 and have a'
1130   PRINT '  chance to win $2,000,000! Here comes the wheel...'
1140   WHEEL=ZER
1150   X=INT(RND*10)+1
1160   WHEEL(X)=2000000
1170   FOR X=1 TO 2
1180     Y=INT(RND*10)+1
1190     IF WHEEL(Y)=0 THEN WHEEL(Y)=100000 ELSE GOTO 1180
1200   NEXT X
1210   FOR X=1 TO 3
1220     Y=INT(RND*10)+1
1230     IF WHEEL(Y)=0 THEN WHEEL(Y)=50000 ELSE GOTO 1220
1240   NEXT X
1250   IF WHEEL(X)=0 THEN WHEEL(X)=10000  FOR X=1 TO 10
1260   PRINT 
1270   PRINT 'Select a number from 1 to 10: ':
1280   INPUT DRAW
1281   IF DRAW < 1 OR DRAW > 10 THEN GOTO 1270
1290   PRINT 'You have won $':WHEEL(DRAW):' in the California Lottery!'
1300   INCOME=INCOME+WHEEL(DRAW)
1310 ELSE
1320   PRINT 'Sorry, the winning number was':SELECT
1330 ENDIF
1340 GOTO 1000
2000 PRINT
2005 PRINT 'You bought':TICKET:' lottery tickets and'
2010 PRINT 'You won $':INCOME:' for a net total of $':INCOME-TICKET
2020 PRINT
2030 PRINT 'The ticket numbers for $100 winners were:'
2040 PIM ' %%%%%  %%%%%  %%%%%  %%%%%  %%%%%': HUNDS(X) FOR X=1 TO 20
2045 PRINT
2050 PRINT 'The ticket numbers for $500 winners were:'
2060 PIM ' %%%%%  %%%%%  %%%%%': FVHUNDS(X) FOR X=1 TO 3
2065 PRINT
2070 PRINT 'The ticket numbers for $1000 winners were:'
2080 PIM ' %%%%%  %%%%%': THOUS(X) FOR X=1 TO 2
2085 PRINT
2090 PRINT 'The ticket numbers for $5000 winners were:'
2100 PIM ' %%%%%  %%%%%': FVTHOUS(X) FOR X=1 TO 2
2110 END ''
5000 PRINT "This program is a simulation of the new California Instant Winner"
5010 PRINT "Lottery Game.  There will be 400,000,000 Instant Winner Lottery"
5015 PRINT "Tickets printed for the first run of the game, which is estimated"
5016 PRINT "to last about eight weeks.  The odds of winning in the California"
5017 PRINT "Lottery were published in the San Jose Mercury News as follows:"
5030 PRINT
5040 PRINT "Instant Games:"
5050 PRINT "    Prize       Winners       Odds"
5060 PRINT "       $2    40,008,000       1:10"
5070 PRINT "       $5     4,800,960     1:83.3"
5080 PRINT "     $100       100,020    1:4,000"
5090 PRINT "     $500        15,003   1:26,667"
5100 PRINT "   $1,000        10,002   1:40,000"
5110 PRINT "   $5,000        10,002   1:40,000"
5120 PRINT
5130 PRINT "Each one of the $100 ticket winners will be placed in a random"
5140 PRINT "drawing and have about 1 chance in 625 of being selected to spin"
5150 PRINT "the Grand Prize wheel.  The Grand Prize wheel will have 100 slots"
5160 PRINT "with four different prize amounts distributed among the slots."
5161 PRINT "The estimated number of winners for each prize and the final odds"
5162 PRINT "for winning each prize are as follows:"
5170 PRINT
5180 PRINT "     Prize      Slots      Winners        Odds"
5190 PRINT "  $2,000,000      10          16      1:25,000,000"
5200 PRINT "    $100,000      20          32      1:12,500,000"
5210 PRINT "     $50,000      30          48       1:8,333,333"
5220 PRINT "     $10,000      40          64       1:6,250,000"
5230 PRINT
5240 PRINT "It is not possible to exactly duplicate the odds of the Lottery"
5250 PRINT "in this simulation: there is not enough memory or disk space on"
5260 PRINT "this system to create 400,000,000 lottery tickets; however, the"
5270 PRINT "odds can be kept very close by 'printing' (in memory) 80,000"
5280 PRINT "tickets.  All you need to do is pick a ticket number between 1"
5290 PRINT "and 80,000 (inclusive); the value of the ticket will then be"
5300 PRINT "announced and the computer will keep a running total of the total"
5310 PRINT "number of tickets purchased as well as the total winnings.  The"
5320 PRINT "simulation may be ended at any time by typing an 'escape'; at"
5330 PRINT "this time the player's final total will be shown."
5340 PRINT
5350 PRINT "Good luck (you'll need it to come out ahead)!"
5360 PRINT
5370 RETURN
