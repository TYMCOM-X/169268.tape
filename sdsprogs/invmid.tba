5 STRING TYPE,INVNUM1,INVNUM2,F1,F2,F3,F4,F5,F6,F7,FO1,FO2,MONTH
6 STRING CUSTNO,SALESNO,FO3
10 TOT=0  ! INVOICE TOTAL
20 PCOUNT=0  ! # OF LINES PRINTED ON INVOICE
40 KCOUNT=0  ! SEQUENTIAL INVOICE #
50 KOUNT=10000  ! STARTING ITEM # FOR ONE TIME CHARGES
60 INVNUM2='-V24'
80 TYPE='S24'
100 INTEGER ITEM(0:22),D,MSGR
122 STRING DESC(0:22),UNITS(0:22),C,D$
140 REAL QUAN(0:22),RATE(0:22),AMT,TOT
160 D$=SUBSTR(DATE,4,2)
240 F1='ACTSUM.M'+D$
250 F3='SRVCHG.M'+D$
260 F2='CUSADR.M'+D$
320 F5='STOREA.M'+D$
360 FO1='INVMID.M'+D$+'-ANY'
380 FO2='INVS.M'+D$+'-ANY'
390 FO3='BL.M'+D$+'-ANY'
400 INVNUM1=D$+'79-'
420 D=VAL(D$)
440 OPEN 'MONTHS',INPUT,1
460 FOR I=1 TO D
480 INPUT FROM 1: MONTH,DAY
500 NEXT I
520 DATE$=MONTH+' 15 79'
540 CLOSE 1
550 ! INPUT PRODUCT CODES, DESC, RATE, UNITS
560 OPEN 'PROD.COD',INPUT,1
570 ON ENDFILE(1) GO TO 640
580 I = 0
590 I = I+1
600 INPUT FROM 1: ITEM(I),DESC(I),RATE(I),UNITS(I)
620 GO TO 590
640 CLOSE 1
650 OPEN FO1,OUTPUT,15
658 OPEN FO2,OUTPUT,14
659 OPEN FO3,OUTPUT,12
660 ! OPEN SERVICE CHARGE FILE UNTIL FINISHED WITH INVOICES
680 OPEN F3,INPUT,1
700 ON ENDFILE(1) GO TO 20000
710 INPUT FROM 1: ACCT$,QUAN(0)
715 OPEN F1,INPUT,2 !OPEN USAGE FILE
718 ON ENDFILE(2) GO TO 5000
720 INPUT FROM 2 IN FORM '12X 9(6Z.3D) 5(10Z)/': ACCTCODE$,QUAN(1),&
    QUAN(2),QUAN(3),QUAN(4),QUAN(5),QUAN(6),QUAN(7),QUAN(8),QUAN(9),&
    QUAN(10),QUAN(11),QUAN(12),QUAN(13),QUAN(14)
730 IF ACCT$#ACCTCODE$ THEN 720
735 CLOSE 2
740 QUAN(10)=QUAN(10)/1000.  ! MAKE TOTCHLB K-CH
750 QUAN(11)=QUAN(11)/1000.  ! MAKE TOTCHMB K-CH
760 QUAN(12)=QUAN(12)/1000.  ! MAKE TOT CANADIAN CHLB K-CH
770 QUAN(13)=QUAN(13)/1000   ! MAKE TOT CANADIAN CHMB K-CH
780 ! READ CUSTOMER ADDRESS AND PRINT INVOICE HEADER
800 OPEN F2,INPUT,2
820 ON ENDFILE(2) GO TO 10000
840 INPUT FROM 2: ACODE$,PO$,CUSTNO,SALESNO,COMPANY$,ATTN$,ADDR1$,&
    ADDR2$,CITY$,STATE$,ZIP$,COUNTRY$ UNTIL ACODE$=ACCT$
860 KCOUNT=KCOUNT+1
880 CLOSE 2
900 PRINT ON 15 IN FORM '4/ 12X 2/': ACCT$
1125 OPEN 'SRVCHG.COD',INPUT,2
1130 INPUT FROM 2: ITEM(0),DESC(0),RATE(0),UNITS(0)
1135 QUAN(0)=1
1140 I=0
1160 GOSUB 50000
1180 ! PRINT PROD CODES 7210-7219 ON INVOICE
1200 CLOSE 2
1210 FOR I=1 TO 9
1220 IF QUAN(I)#0 THEN GOSUB 50000
1240 NEXT I
1660 ! PRINT CODES 7301-7401 ON INVOICE
1680 FOR I=10 TO 14
1700 IF QUAN(I)#0 THEN GOSUB 50000
1720 NEXT I
1740 ! INPUT STORAGE/K-CH/DAY AND PRINT ON INVOICE
1760 OPEN F5,INPUT,2
1780 ON ENDFILE(2) GO TO 1860
1800 INPUT FROM 2 IN FORM '12X 7Z.3D /': ACODE$,QUAN(15)
1810 IF ACODE$#ACCT$ THEN 1800
1820 I=15
1840 GOSUB 50000
1860 CLOSE 2
2100 ! FINISHED WITH DATA...COMPUTE TOTAL FOR INVOICE & SUMMARY
2140 PRINT ON 15 IN FORM '/ 15B 12X 42B 3$,3$.DDB-': ACCT$,TOT
2150 NTOT=TOT*100
2160 PRINT ON 14 IN FORM '12X 9X 6X 5X 4D 4X 8D 30X /': ACCT$,&
    CUSTNO,SALESNO,INVNUM1,KCOUNT,INVNUM2,NTOT,COMPANY$
2170 TOTALALL=TOTALALL+TOT
2180 TOT=0
2190 NTOT=0
2200 GO TO 710
5000 !CUSTOMER HAD NO USAGE
5010 CLOSE 2
5020 PRINT ' NO USAGE  :ACCOUNT CODE = ':
5030 PRINT ACCT$
5031 FOR I = 1 TO 14
5032 QUAN(I)=0
5033 NEXT I
5040 GO TO 780
10000 ! ADDRESS NOT IN DATA BASE
10005 CLOSE 2
10010 PRINT 'ADDRESS NOT FOUND:  ACCOUNT CODE = ':
10030 PRINT ACCT$
10040 GO TO 710
20000 ! THROUGH WITH INVOICES
20010 PRINT 'EOJ...NO. OF INVOICES CREATED = ':
20020 PRINT KCOUNT
20025 PRINT 'TOTAL AMT BILLED = ':
20030 PRINT TOTALALL
20040 CLOSE 1,14,15
20050 PRINT IN FORM '2/ "FILE TO TYPE IS " 10X /': FO1
20060 END
50000 ! PRINT INVOICE SUBROUTINE
50010 PCOUNT=PCOUNT+1
50020 AMT=ROUN(QUAN(I)*RATE(I)*100.)/100.
50040 PRINT ON 15 IN FORM "5Z B 33X 2B 3Z,3Z.2DY 6Z.DDY 5X 2B &
    3$,3$.2D- /": ITEM(I),DESC(I),QUAN(I),RATE(I),UNITS(I),AMT
50042 NQUAN=QUAN(I)*1000
50044 NRATE=RATE(I)*100
50046 NAMT=AMT*100
50050 PRINT ON 12 IN FORM '5Z 33X 9D 8D 5X 8D/': ITEM(I),DESC(I),NQUAN,&
      NRATE,UNITS(I),NAMT
50060 TOT=TOT+AMT
50080 RETURN
