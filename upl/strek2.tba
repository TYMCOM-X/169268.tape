1 REM ** B3=THE NUMBER OF STARBASES IN THE CURRENT QUADRANT.
2 REM ** C$=STRING CONTAING THE CURRENT CONDITION:
3 REM **  RED, GREEN, YELLOW, DOCKED.
4 REM ** E=AMOUNT OF ENERGY REMAINING.
5 REM ** K3=THE NUMBER OF KLINGON BATTLE CRUISERS IN THE CURRENT QUADRANT.
6 REM ** K9=THE TOTAL NUMBER OF KLINGON SHIPS IN THE GALAXY.
7 REM ** N=THREE DIGIT NUMBER STATING NUMBER OF KLINGONS,
8 REM **  STARBASES AND STARS IN THE QUADRANT.
9 REM ** P=THE NUMBER OF PHOTON TORPEDOES REMAINING.
10 REM ** S=UNITS OF ENERGY CURRENTLY ASSIGNED TO SHIELDS.
11 REM ** S3=THE NUMBER OF STARS IN THE CURRENT QUADRANT.
12 REM ** T=THE CURRENT STARDATE.
13 REM ** T0=THE STARDATE ON WHICH THE GAME BEGAN.
14 REM ** Q1,Q2=SPECIFIES THE X AND Y COORDINATES OF THE CURRENT
15 REM **  QUADRANT WITHIN THE GALAXY.
16 REM ** S1,S2=SPECIFIES THE LOCATION OF THE ENTERPRISE WITHIN THE QUADRANT.
17 REM ** D(1)...D(6)=EACH ELEMENT CONTAINS THE STATE OF REPAIR FOR ITS
18 REM **  RESPECTIVE DEVICE.
19 REM ** G(1,1)...G(8,8)=EACH ELEMENT IS A THREE DIGIT NUMBER CONTAINING
20 REM **  DATA ON A SINGLEQUADRANT OF THE GALAXY.
21 REM ** K(1,1)...K(3,3)=CONTAINS INFORMATION ON THE KLINGONS IN
22 REM **  THE CURRENT QUADRANT.
23 REM ** K(I,1)=THE X COORDINATE OF KLINGON NUMBER I.
24 REM ** K(I,2)=THE Y COORDINATE OF KLINGON NUMBER I.
25 REM ** K(I,3)=THE SHIELD POWER OF KLINGON NUMBER I.
26 REM ** FNA(X)=RETURNS AN INTEGER RANDOM NUMBER IN THE RANGE:
27 REM **  1 <=RANDOM NUMBER <= X.
28 REM ** FND(O)=RETURNS THE DISTANCE FROM THE ENTERPRISE TO
29 REM **  ANOTHER KLINGON.
68 PRINT "PROGRAM START"
69 PRINT
70 DEF FNA(X)=INT(X*RND(0)+1)
80 DIM G(8,8),Z$(0:15),D(6),Q(8,8),K(3,3),C(9,2),N(3)
85 T=RND(-1)
90 T0,T=(FNA(20)+20)*100
100 E=3000
110 P=20
120 S=0
130 X$="------------------------"
140 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
150 Q1,Q2,S1,S2=FNA(8)
160 FOR I1=1 TO 9
161 FOR J1=1 TO 2
162 C(I1,J1)=1
163 NEXT J1
164 NEXT I1
170 C(2,1),C(3,1),C(4,1),C(4,2),C(5,2),C(6,2)=-1
180 C(1,1),C(3,2),C(5,1),C(7,2),C(9,1)=0
190 FOR I1=1 TO 6
191 D(I1)=0
192 NEXT I1
200 B9,K9=0
210 FOR I=1 TO 8
220 FOR J=1 TO 8
230 K3,B3=0
240 IF RND(0)<0.8 THEN 270
250 K3=FNA(3)
260 K9=K9+K3
270 IF RND(0)<0.96 THEN 300
280 B3=1
290 B9=B9+1
300 S3=FNA(5)
310 G(I,J)=K3*100+B3*10+S3
320 NEXT J
330 NEXT I
340 IF K9=0 OR B9=0 THEN 200
345 GOTO 3570
350 GOSUB 7000
360 IF Q1>=1 THEN 380
370 S1,Q1=1
380 IF Q1<=8 THEN 400
390 S1,Q1=8
400 IF Q2>=1 THEN 420
410 S2,Q2=1
420 IF Q2<=8 THEN 440
430 S2,Q2=8
440 X=G(Q1,Q2)/100
450 K3=INT(X)
460 B3=INT((X-K3)*10)
470 S3=G(Q1,Q2)-((B3*10)+(K3*100))
480 IF K3=0 THEN 520
490 PRINT "COMBAT AREA","CONDITION RED"
500 IF S>(K3*100) THEN 520
510 PRINT "  SHIELDS DANGEROUSLY LOW"
520 FOR I1=1 TO 3
521 FOR J1=1 TO 3
522 K(I1,J1)=0
523 NEXT J1
524 NEXT I1
530 FOR I1=1 TO 8
531 FOR J1=1 TO 8
532 Q(I1,J1)=0
533 NEXT J1
534 NEXT I1
540 Q(S1,S2)=1
550 FOR I=1 TO K3
560 GOSUB 3500
570 Q(R1,R2)=2
580 K(I,1)=R1
590 K(I,2)=R2
600 K(I,3)=200
610 NEXT I
620 IF B3=0 THEN 650
630 GOSUB 3500
640 Q(R1,R2)=4
650 FOR I=1 TO S3
660 GOSUB 3500
670 Q(R1,R2)=3
680 NEXT I
690 GOSUB 2600
700 PRINT "COMMAND: ":
710 INPUT A
720 IF A<1 OR A>5 OR A#INT(A) THEN 780
730 R1=A+1
740 IF D(R1)>=0 THEN 780
750 GOSUB 3420
760 PRINT Y$:" NOT OPERATIONAL"
770 GOTO 700
780 IF A<0 OR A>8 THEN 790
781 ON (A+1) GOTO 890,1520,1640,1800,2200,3010,3550,5000,6000
790 PRINT
800 PRINT "0=SET COURSE":TAB(19):"4   3   2"
810 PRINT "1=LONG RANGE SCAN":TAB(21):"\ | /"
820 PRINT "2=PHASER CTRL":TAB(22):"\|/"
830 PRINT "3=TORPEDO CTRL":TAB(18):"5 ------- 1"
840 PRINT "4=SHIELDS":TAB(22):"/|\"
850 PRINT "5=LIBRARY COMPUTER":TAB(21):"/ | \"
860 PRINT "6=RESIGNATION":TAB(19):"6   7   8"
870 PRINT
880 GOTO 700
890 PRINT "COURSE (1-9)":
900 INPUT C1
910 IF C1<1 OR C1>=9 THEN 700
920 PRINT "WARP FACTOR (0-8)":
930 INPUT W1
940 IF W1<=0 OR W1>8 THEN 700
950 IF D(1)>=0 OR W1<=.5 THEN 980
960 PRINT "ENGINES ARE DAMAGED, MAXIMUM SPEED= WARP .5"
970 GOTO 920
980 IF E-(W1*8)>0 THEN 1030
990 IF S<1 THEN 2470
1000 PRINT "YOU ONLY HAVE":E:" UNITS---SUGGEST YOU CROSS-CIRCUIT"
1010 PRINT "FROM SHIELDS, WHICH HAVE":S:" UNITS"
1020 GOTO 700
1029 REM *** REPAIR DAMAGED EQPT, ONE UNIT PER USE OF WARP ENGINES
1030 FOR I=1 TO 6
1040 IF D(I)>=0 THEN 1060
1050 D(I)=D(I)+1
1060 NEXT I
1061 REM *** END REPAIR
1070 IF FNA(10)#5 OR W1<1 THEN 1120
1080 R1=FNA(6)
1090 D(R1)=D(R1)-FNA(5)
1100 GOSUB 3420
1110 PRINT
1111 PRINT "DAMAGE CTRL REPORTS '":Y$:" DAMAGED.'"
1112 PRINT
1120 N1=INT(W1*8)
1130 Q(S1,S2)=0
1140 X=S1
1150 Y=S2
1160 C2=INT(C1)
1170 X1=C(C2,1)+(C(C2+1,1)-C(C2,1))*(C1-C2)
1180 X2=C(C2,2)+(C(C2+1,2)-C(C2,2))*(C1-C2)
1190 FOR I=1 TO N1
1200 S1=S1+X1
1210 S2=S2+X2
1220 IF S1<1 OR S1>8 OR S2<1 OR S2>8 THEN 1360
1230 IF Q(INT(S1),INT(S2))=0 THEN 1270
1240 S1=S1-X1
1250 S2=S2-X2
1260 GOTO 1280
1270 NEXT I
1280 S1=INT(S1)
1290 S2=INT(S2)
1300 Q(S1,S2)=1
1310 E=E-N1
1320 IF W1<1 THEN 690
1330 T=T+1
1340 IF T>T0+25 THEN 2500
1350 GOTO 690
1360 X=Q1*8+X+X1*N1
1370 Y=Q2*8+Y+X2*N1
1380 Q1=INT(X/8)
1390 Q2=INT(Y/8)
1400 S1=INT(X-Q1*8+.5)
1410 S2=INT(Y-Q2*8+.5)
1420 IF S1>0 THEN 1450
1430 Q1=Q1-1
1440 S1=8
1450 IF S2>0 THEN 1480
1460 Q2=Q2-1
1470 S2=8
1480 T=T+1
1490 E=E-N1+5
1500 IF T>T0+25 THEN 2500
1510 GOTO 360
1520 PRINT LEFT(X$,13)
1530 FOR I=Q1-1 TO Q1+1
1540 FOR I1=1 TO 3
1541 N(I1)=0
1542 NEXT I1
1550 FOR J=Q2-1 TO Q2+1
1560 IF I<1 OR I>8 OR J<1 OR J>8 THEN 1580
1570 N(J-Q2+2)=G(I,J)
1580 NEXT J
1590 PRINT IN FORM "':' %%% ':' %%% ':' %%% ':' / ": N(1),N(2),N(3)
1600 PRINT LEFT(X$,13)
1610 NEXT I
1620 GOTO 700
1640 IF K3=0 THEN 2350
1650 PRINT "ENERGY AVAILABLE=":E
1660 PRINT "NUMBER OF UNITS TO FIRE":
1670 INPUT X
1680 IF X<1 THEN 700
1690 IF X-X<0 THEN 1650
1700 E=E-X
1710 FOR I=1 TO 3
1720 IF K(I,3)=0 THEN 1780
1730 H=INT(X/K3/(FND(0)))
1740 K(I,3)=K(I,3)-H
1750 PRINT H:" UNIT HIT ON KLINGON"
1760 IF K(I,3)>0 THEN 1780
1770 GOSUB 2050
1780 NEXT I
1790 GOTO 2030
1800 IF P>0 THEN 1830
1810 PRINT "ALL PHOTON TORPEDOES EXPENDED"
1820 GOTO 700
1830 PRINT "TORPEDO COURSE (1-9)":
1840 INPUT C1
1850 IF C1<1 OR C1>=9 THEN 700
1860 C2=INT(C1)
1870 X1=C(C2,1)+(C(C2+1,1)-C(C2,1))*(C1-C2)
1880 X2=C(C2,2)+(C(C2+1,2)-C(C2,2))*(C1-C2)
1890 X=S1
1900 Y=S2
1910 P=P-1
1920 PRINT
1930 X=X+X1
1940 Y=Y+X2
1950 IF X<1 OR X>8 OR Y<1 OR Y>8 THEN 2180
1960 IF Q(INT(X),INT(Y))=0 AND Q(INT(X+.5),INT(Y+.5))=0 THEN 1930
1970 FOR I=1 TO 3
1980 IF INT(X)=K(I,1) AND INT(Y)=K(I,2) THEN 2020
1990 IF INT(X+.5)=K(I,1) AND INT(Y+.5)=K(I,2) THEN 2020
2000 NEXT I
2010 GOTO 2120
2020 GOSUB 2050
2030 GOSUB 2370
2040 GOTO 700
2050 K(I,3),Q(K(I,1),K(I,2))=0
2060 PRINT "***KLINGON DESTROYED***"
2070 K3=K3-1
2080 K9=K9-1
2090 IF K9=0 THEN 2550
2100 G(Q1,Q2)=G(Q1,Q2)-100
2110 RETURN
2120 IF Q(INT(X),INT(Y))#3 AND Q(INT(X+.5),INT(Y+.5))#3 THEN 2150
2130 PRINT "YOU CAN'T DESTROY STARS!"
2140 GOTO 2180
2150 PRINT "***STARBASE DESTROYED***"
2160 PRINT "YOU ARE HEREBY RELIEVED OF DUTY!!   CONGRATULATIONS!"
2170 GOTO 2530
2180 PRINT "TORPEDO MISSED"
2190 GOTO 2030
2200 PRINT "ENERGY AVAILABLE=":E+S
2210 PRINT "NUMBER OF UNITS TO SHIELDS":
2220 INPUT X
2230 IF X<0 THEN 700
2240 IF E+S-X<0 THEN 2210
2250 E=E+S-X
2260 S=X
2270 GOTO 700
2280 PRINT
2281 PRINT ;"DEVICE","STATE OF REPAIR"
2290 FOR R1=1 TO 6
2300 GOSUB 3420
2310 PRINT Y$,D(R1)
2320 NEXT R1
2330 PRINT
2340 GOTO 700
2350 PRINT "SHORT RANGE SENSORS REPORT NO KLINGONS IN THIS QUANDRANT"
2360 GOTO 700
2370 IF C$="DOCKED" THEN 2460
2380 IF K3=0 THEN 2460
2390 FOR I=1 TO 3
2400 IF K(I,3)=0 THEN 2450
2410 H=INT(K(I,3)/FND(0)+1)
2420 S=S-H
2430 PRINT H:" UNIT HIT ON ENTERPRISE"
2440 IF S<0 THEN 2520
2450 NEXT I
2460 RETURN
2470 PRINT "**FATAL ERROR**"
2480 PRINT "THE ENTERPRISE IS DEAD IN SPACE, AND MUST BE EVACUATED!!"
2490 GOTO 2530
2500 PRINT
2501 PRINT "IT IS STARDATE":T
2510 GOTO 2530
2520 PRINT
2521 PRINT ;"    ***THE ENTERPRISE HAS BEEN DESTROYED***"
2530 PRINT
2531 PRINT ;"THE FEDERATION WILL BE CONQUERED!!!"
2540 GOTO 3560
2550 PRINT
2560 PRINT "THE LAST KLINGON BATTLE CRUISER HAS BEEN DESTROYED!!"
2570 PRINT "THE FEDERATION HAS BEEN SAVED !!"
2580 PRINT
2590 GOTO 3560
2600 FOR I=S1-1 TO S1+1
2610 FOR J=S2-1 TO S2+1
2620 IF I<1 OR I>8 OR J<1 OR J>8 THEN 2640
2630 IF Q(I,J)=4 THEN 2670
2640 NEXT J
2650 NEXT I
2660 GOTO 2730
2670 C$="DOCKED"
2680 E=3000
2690 P=20
2700 MAT D=ZER
2710 S=0
2720 GOTO 2810
2730 PRINT
2740 IF K3>0 THEN 2780
2750 IF E<300 THEN 2800
2760 C$="GREEN"
2770 GOTO 2810
2780 C$="RED"
2790 GOTO 2810
2800 C$="YELLOW"
2810 PRINT X$
2820 Z=0
2830 GOSUB 3330
2840 PRINT
2850 GOSUB 3330
2860 PRINT "STARDATE":"     ":T
2870 GOSUB 3330
2880 PRINT "CONDITION":"     ":C$
2890 GOSUB 3330
2900 PRINT "ENERGY":"       ":E
2910 GOSUB 3330
2920 PRINT "TORPEDOES":"    ":P
2930 GOSUB 3330
2940 PRINT "SHIELDS":"      ":S
2950 GOSUB 3330
2960 PRINT "KLINGONS":"     ":K9
2970 GOSUB 3330
2980 PRINT "QUADRANT":"     ":Q1:",":Q2
2990 PRINT X$
3000 RETURN
3010 PRINT "COMPUTER ACTIVE":
3020 INPUT A
3030 IF A<0 OR A>2 THEN 3040
3031 ON (A+1) GOTO 2280,3100,3310
3040 PRINT "FUNCTIONS AVAILABLE FROM COMPUTER"
3050 PRINT "   0=DAMAGE REPORT"
3060 PRINT "   1=PHOTON TORPEDO DATA"
3070 PRINT "   2=SHORT RANGE SCAN"
3080 PRINT
3090 GOTO 3010
3100 PRINT
3110 FOR I=1 TO 3
3120 IF K(I,3)<=0 THEN 3200
3130 X=K(I,2)-S2
3140 Y=S1-K(I,1)
3150 IF X=0 THEN 3240
3160 A=INT(((57.3*ATN(Y/X))/45+1)*100+.5)/100
3170 IF X>0 AND Y<0 THEN 3290
3180 IF X<0 THEN 3220
3190 PRINT "DIRECTION=":A
3200 NEXT I
3210 GOTO 700
3220 A=A+4
3230 GOTO 3190
3240 IF Y<0 THEN 3270
3250 A=3
3260 GOTO 3190
3270 A=7
3280 GOTO 3190
3290 A=A+8
3300 GOTO 3190
3310 GOSUB 2600
3320 GOTO 700
3330 REM ***** OUTPUT QUANDRANT *****
3340 Z=Z+1
3342 Z$(0)="   "
3343 Z$(1)="<*>"
3344 Z$(2)="+++"
3345 Z$(3)=" * "
3346 Z$(4)=">!<"
3350 FOR I=1 TO 8
3370 PRINT Z$(Q(Z,I)):
3380 NEXT I
3390 IF Z=1 THEN 3410
3400 PRINT "     ":
3410 RETURN
3420 REM ***** PRINTS DEVICE NAME *****
3430 RESTORE
3440 DATA "WARP ENGINES","SCANNERS","PHASER BANKS","TORPEDO TUBES"
3450 DATA "SHIELD CONTROL","COMPUTER"
3460 FOR X=1 TO R1
3470 READ Y$
3480 NEXT X
3490 RETURN
3500 REM **** PLACEMENT IN QUADRANT ARRAY ****
3510 R1=FNA(8)
3520 R2=FNA(8)
3530 IF Q(R1,R2)#0 THEN 3510
3540 RETURN
3550 PRINT
3551 PRINT "YOUR RESIGNATION HAS BEEN *ACCEPTED*"
3560 STOP
3570 PRINT "DO YOU NEED INSTRUCTIONS":
3580 INPUT A$
3590 PRINT
3600 IF LEFT(A$,1)#"Y" THEN 350
3610 PRINT "     <*> = ENTERPRISE"
3620 PRINT "      *  = STAR"
3630 PRINT "     +++ = KLINGON"
3640 PRINT "     >!< = STARBASE"
3650 PRINT
3655 PRINT
3660 PRINT "COMMAND 0 =  WARP ENGINES"
3670 PRINT "COURSE IS IN A CIRCULAR VECTOR AS SHOWN.":TAB(49):"4   3   2"
3680 PRINT "REAL VALUES MAY BE USED. FOR EXAMPLE,":TAB(51):"\ | /"
3690 PRINT "1.5 WOULD BE HALFWAY BETWEEN 1 AND 2.":TAB(52):"\|/"
3700 PRINT TAB(48):"5 ------- 1"
3710 PRINT "A 'WARP FACTOR' IS THE SIZE OF ONE QUADRANT.":TAB(52):"/|\"
3720 PRINT TAB(51):"/ | \"
3730 PRINT TAB(49):"6   7   8"
3735 PRINT
3740 PRINT "COMMAND 1 = LONG RANGE SCAN"
3750 PRINT "CODED IN THE FORM 'KBS', WHERE K=THE NUMBER OF KLINGONS,"
3760 PRINT "B=THE NUMBER OF STARBASES, AND S=THE NUMBER OF STARS."
3770 PRINT
3780 PRINT "COMMAND 2 = PHASERS"
3790 PRINT "YOU MAY DESTROY THE KLINGON BY USING ENOUGH PHASER"
3800 PRINT "POWER AS TO DEPLETE HIS SHIELDS. KEEP IN MIND THAT WHEN"
3810 PRINT "YOU FIRE AT HIM, HE GONNA DO IT TO YOU, TOO!"
3815 PRINT
3820 PRINT "COMMAND 3 = PHOTON TORPEDOES"
3830 PRINT "COURSE IS THE SAME AS WITH WARP ENGINES. THE LIBRARY"
3840 PRINT "COMPUTER CAN COMPUTE TRAJECTORY FOR YOU (OPTION 1)."
3845 PRINT
3850 PRINT "COMMAND 4 = SHIELDS"
3860 PRINT "DEFINES THE AMOUNT OF ENERGY TO BE ASSIGNED TO THE SHIELDS."
3865 PRINT
3870 PRINT "COMMAND 5 = LIBRARY COMPUTER"
3880 PRINT "   OPTION 0 = DAMAGE REPORT"
3890 PRINT "  A STATE OF REPAIR LESS THAN ZERO INDICATES THAT"
3900 PRINT "  THE DEVICE IS TEMPORARILY DISABLED."
3910 PRINT "   OPTION 1 = PHOTON TORPEDO DATA"
3920 PRINT "  GIVES DIRECTION TO ALL KLINGONS IN YOUR QUADRANT."
3930 PRINT "   OPTION 2 = SHORT RANGE SCAN"
3940 PRINT "  ALSO GIVEN AUTOMATICALLY AFTER COMPLETION OF"
3950 PRINT "  WARP ENGINE MANEUVERS."
3955 PRINT
3960 PRINT "COMMAND 6 = RESIGNATION"
3961 PRINT
3962 PRINT
3963 PRINT
3964 PRINT
3965 PRINT
3966 GOTO 350
5000 PAUSE "COMMAND PAUSE. TYPE 'GO' TO CONTINUE."
5010 GOTO 700
6000 GOSUB 7000
6010 GOTO 700
7000 PRINT TAB(10),"C H A R T   O F   G A L A X Y"
7001 PRINT
7002 PRINT "     |  1  |  2  |  3  |  4  |":
7003 PRINT "  5  |  6  |  7  |  8  |"
7004 PRINT "-----|-----|-----|-----|-----|-----|-----|":
7005 PRINT "-----|-----|"
7006 FOR I1=1 TO 8
7007 PRINT I1:"   |     |     |     |     |     |     |":
7008 PRINT "     |     |"
7009 PRINT "-----|-----|-----|-----|-----|-----|-----|":
7010 PRINT "-----|-----|"
7011 NEXT I1
7012 FOR I1=1 TO 10
7013 PRINT
7014 NEXT I1
7015 RETURN
  