100 INTEGER CHANGELOC(128)
110 STRING BLACK,CLEAR,HOME,WPROT,CWPROT,SPROT,CPROT,DSP1(8,8),DSP2(8,8)
111 STRING CRT,CLN
120 CALL INSTRUCTIONS
130 CALL INITGAME
140 CALL PLAYGAME
150 IF ANOTHERGAME='Y' THEN 130 ELSE END 'THANK YOU FOR PLAYING'
160 !
170 !
250 DEF INSTRUCTIONS
260   PRINT "INST"
265   CRT='Y'
270 ENDF INSTRUCTIONS
750 DEF INITGAME
760   CALL SET8X8
770   CALL SETPIECES
780   CALL BLACKORWHITE
790   IF CRT='Y' THEN
800     CALL DRAWSCREEN
810     CALL DRAWPIECES
820   ELSE
830     CALL DRAWBOARD
840   ENDIF
850 ENDF INITGAME
860 !
870 !
1000 DEF PLAYGAME
1010   IF BLACK='Y' THEN
1020     CALL PLAYPERSON
1030   ENDIF
1040   FOR I=1 TO 28
1050     CALL PLAYCOMPUTER
1060     CALL PLAYPERSON
1070   NEXT I
1080   IF BLACK='Y' THEN
1090     CALL PLAYCOMPUTER
1100   ELSE
1110     CALL PLAYCOMPUTER
1120     CALL PLAYPERSON
1130   ENDIF
1135   IF CRT='Y' THEN
1136     WAIT 40
1137     PRINT CHAR(27):"'":CLEAR:
1138   ENDIF
1140 ENDF PLAYGAME
1150 !
1160 !
1400 DEF STRING ANOTHERGAME
1410   PRINT "ANOTHER GAME":
1420   ON YESNO GOTO 1430,1440,1410
1430   EXIT 'Y'
1440   EXIT 'N'
1450 ENDF ANOTHERGAME
1460 !
1470 !
1600 DEF INTEGER YESNO
1610   STRING YORN
1620   INPUT YORN
1630   IF YORN = 'Y' OR YORN = 'YE' OR YORN = 'YES' THEN
1640     EXIT 1
1650   ELSE
1660     IF YORN = 'N' OR YORN = 'NO' THEN
1670       EXIT 2
1680     ELSE
1690       PRINT 'ANSWER YES OR NO PLEASE'
1700       EXIT 3
1710     ENDIF
1720   ENDIF
1730 ENDF YESNO
1740 !
1750 !
2000 DEF SET8X8
2010   FOR I=1 TO 8
2020     FOR J=1 TO 8
2030       DSP1(I,J)= ' '
2040     NEXT J
2050   NEXT I
2060   DSP2=DSP1
2070   FOR I=1 TO 128
2080     CHANGELOC(I) = 0
2090   NEXT I
2100 ENDF SET8X8
2110 !
2120 !
2300 DEF SETPIECES
2310   DSP1(4,4)='O'
2320   DSP1(4,5)='X'
2330   DSP1(5,4)='X'
2340   DSP1(5,5)='O'
2350 ENDF SETPIECES
2360 !
2370 !
2500 DEF BLACKORWHITE
2510 PRINT "DO YOU WANT TO PLAY BLACK":
2520   ON YESNO GO TO 2530,2550,2510
2530   BLACK = 'Y'
2540   EXIT
2550   BLACK = 'N'
2560   EXIT
2570 ENDF BLACKORWHITE
2580 !
2590 !
2700 DEF DRAWBOARD
2705 INTEGER SCR.O,SCR.X
2706 SCR.O,SCR.X=0
2710 PRINT
2720 PRINT
2730 PRINT "   1  2  3  4  5  6  7  8"
2740 PRINT " +                        +"
2750 FOR I=1 TO 8
2760   PIF "DB":I
2770   FOR J=1 TO 8
2780     PIF "B X B":DSP1(I,J)
2790     IF DSP1(I,J)='O' THEN SCR.O = SCR.O+1
2800     IF DSP1(I,J)='X' THEN SCR.X = SCR.X+1
2810   NEXT J
2820 PRINT
2830 NEXT I
2840 PRINT " +                        +"
2850 PRINT
2860 PRINT "   BLACK ":SCR.X;"WHITE ":SCR.O
2870 PRINT
2880 PRINT
2890 ENDF DRAWBOARD
2900 !
2910 !
3000 DEF DRAWSCREEN
3005 LOL 2000
3010   HOME=CHAR(30)
3020   CLEAR=CHAR(27)+';'
3025   CLN=CHAR(27)+'T'
3030   WPROT=CHAR(27)+')'
3040   CWPROT=CHAR(27)+'('
3050   SPROT=CHAR(27)+'&'
3060   CPROT=CHAR(27)+"'"
3080   PRINT CLEAR:WPROT:"                                O T H E L L O":CLN
3090   PRINT WPROT:CLN
3100   PRINT WPROT:"                       1   2   3   4   5   6   7   8":CLN
3110   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3120   PRINT WPROT:"                   1 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3130   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3140   PRINT WPROT:"                   2 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3150   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3160   PRINT WPROT:"                   3 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3170   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3180   PRINT WPROT:"                   4 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3190   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3200   PRINT WPROT:"                   5 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3210   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3220   PRINT WPROT:"                   6 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3230   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3240   PRINT WPROT:"                   7 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3250   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3260   PRINT WPROT:"                   8 : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" : ":CWPROT:" ":WPROT:" :":CLN
3270   PRINT WPROT:"                     :===:===:===:===:===:===:===:===:":CLN
3275   PRINT WPROT:CLN
3280   PRINT WPROT:"                BLACK ":CWPROT:"  ":WPROT:"                      WHITE ":CWPROT:"  ":WPROT:CLN
3290   PRINT WPROT:CLN
3300   PRINT CWPROT:CLN:SPROT
3590 ENDF DRAWSCREEN
3600 DEF DRAWPIECES
3605 INTEGER SCR.O,SCR.X
3606 SCR.O,SCR.X=0
3610 PRINT CLEAR:
3620 FOR I=1 TO 8
3630   FOR J=1 TO 8
3640     PRINT DSP1(I,J):
3650     IF DSP1(I,J)='O' THEN SCR.O = SCR.O + 1
3660     IF DSP1(I,J)='X' THEN SCR.X = SCR.X + 1
3670   NEXT J
3680 NEXT I
3690 PIF "ZD ZD":SCR.X,SCR.O
3700 ENDF DRAWPIECES
3710 !
3720 !
4000 DEF PLAYPERSON
4010   IF CRT='N' THEN PRINT "ENTER ROW, COLUMN"
4020   INPUT A,B
4030   IF A<1 OR B<1 OR A>8 OR B>8 THEN 4010
4040   IF DSP1(A,B)#' ' THEN 4010
4050   CALL MAKEMOVE('P',A,B)
4060   IF CRT='Y' THEN CALL DRAWPIECES ELSE CALL DRAWBOARD
4080 ENDF PLAYPERSON
4090 !
4100 !
5000 DEF PLAYCOMPUTER
5010   INTEGER A(2)
5020   A = LASTMAXLOC
5050   CALL MAKEMOVE('C',A(1),A(2))
5060   IF CRT='Y' THEN CALL DRAWPIECES ELSE CALL DRAWBOARD
5070 ENDF PLAYCOMPUTER
5080 !
5090 !
5500 DEF MAKEMOVE(STRING PLAYER, INTEGER A, INTEGER B)
5510   STRING PIECE
5520   IF PLAYER='P' THEN
5530     IF BLACK='Y' THEN
5540       PIECE='X'
5550     ELSE
5560       PIECE='O'
5570     ENDIF
5580   ELSE
5590     IF BLACK='Y' THEN
5600       PIECE='O'
5610     ELSE
5620       PIECE='X'
5630     ENDIF
5640   ENDIF
5650   DSP2=DSP1
5660   DSP2(A,B)=PIECE
5670   CALL ANALIZE.DSP2(A,B)
5680   DSP1=DSP2
5690 ENDF MAKEMOVE
5700 !
5710 !
6000 DEF INTEGER ANALIZE.DSP2(INTEGER A,B)
6005   INTEGER I,J,CNT
6010   STRING PIECE
6020   PIECE=DSP2(A,B)
6025   CNT=0
6030   FOR I=1 TO 8
6040     IF A-I>=1 THEN
6050       IF DSP2(A-I,B)=' ' OR DSP2(A-I,B)=PIECE THEN
6060         IF DSP2(A-I,B)=PIECE THEN
6070           FOR J=1 TO I-1
6080             DSP2(A-J,B)=PIECE
6090             CNT=CNT+1
6100           NEXT J
6110         ELSE
6120           I=8
6130         ENDIF
6140       ENDIF
6150     ELSE
6160       I=8
6170     ENDIF
6180   NEXT I
6190   FOR I=1 TO 8
6200     IF A+I<=8 THEN
6210       IF DSP2(A+I,B)=' ' OR DSP2(A+I,B)=PIECE THEN
6220         IF DSP2(A+I,B)=PIECE THEN
6230           FOR J=1 TO I-1
6240             DSP2(A+J,B)=PIECE
6250             CNT=CNT+1
6260           NEXT J
6265           I=8
6270         ELSE
6280           I=8
6290         ENDIF
6300       ENDIF
6310     ELSE
6320       I=8
6330     ENDIF
6340   NEXT I
6350   FOR I=1 TO 8
6360     IF B-I>=1 THEN
6370       IF DSP2(A,B-I)=' ' OR DSP2(A,B-I)=PIECE THEN
6380         IF DSP2(A,B-I)=PIECE THEN
6390           FOR J=1 TO I-1
6400             DSP2(A,B-J)=PIECE
6410             CNT=CNT+1
6420           NEXT J
6425           I=8
6430         ELSE
6440           I=8
6450         ENDIF
6460       ENDIF
6470     ELSE
6480       I=8
6490     ENDIF
6500   NEXT I
6510   FOR I=1 TO 8
6520     IF B+I<=8 THEN
6530       IF DSP2(A,B+I)=' ' OR DSP2(A,B+I)=PIECE THEN
6540         IF DSP2(A,B+I)=PIECE THEN
6550           FOR J=1 TO I-1
6560             DSP2(A,B+J)=PIECE
6570             CNT=CNT+1
6580           NEXT J
6590           I=8
6600         ELSE
6610           I=8
6620         ENDIF
6630       ENDIF
6640     ELSE
6650       I=8
6660     ENDIF
6670   NEXT I
6680   FOR I=1 TO 8
6690     IF A-I>=1 AND B-I>=1 THEN
6700       IF DSP2(A-I,B-I)=' ' OR DSP2(A-I,B-I)=PIECE THEN
6710         IF DSP2(A-I,B-I)=PIECE THEN
6720           FOR J=1 TO I-1
6730             DSP2(A-J,B-J)=PIECE
6740             CNT=CNT+1
6750           NEXT J
6760           I=8
6770         ELSE
6780           I=8
6790         ENDIF
6800       ENDIF
6810     ELSE
6820       I=8
6830     ENDIF
6840   NEXT I
6850   FOR I=1 TO 8
6860     IF A+I<=8 AND B-I>=1 THEN
6870       IF DSP2(A+I,B-I)=' ' OR DSP2(A+I,B-I)=PIECE THEN
6880         IF DSP2(A+I,B-I)=PIECE THEN
6890           FOR J=1 TO I-1
6900             DSP2(A+J,B-J)=PIECE
6910             CNT=CNT+1
6920           NEXT J
6930           I=8
6940         ELSE
6950           I=8
6960         ENDIF
6970       ENDIF
6980     ELSE
6990       I=8
7000     ENDIF
7010   NEXT I
7030   FOR I=1 TO 8
7040     IF A-I>=1 AND B+I<=8 THEN
7050       IF DSP2(A-I,B+I)=' ' OR DSP2(A-I,B+I)=PIECE THEN
7060         IF DSP2(A-I,B+I)=PIECE THEN
7070           FOR J=1 TO I-1
7080             DSP2(A-J,B+J)=PIECE
7090             CNT=CNT+1
7100           NEXT J
7110           I=8
7120         ELSE
7130           I=8
7140         ENDIF
7150       ENDIF
7160     ELSE
7170       I=8
7180     ENDIF
7190   NEXT I
7200   FOR I=1 TO 8
7210     IF A+I<=8 AND B+I<=8 THEN
7220       IF DSP2(A+I,B+I)=' ' OR DSP2(A+I,B+I)=PIECE THEN
7230         IF DSP2(A+I,B+I)=PIECE THEN
7240           FOR J=1 TO I-1
7250             DSP2(A+J,B+J)=PIECE
7260             CNT=CNT+1
7270           NEXT J
7280           I=8
7290         ELSE
7300           I=8
7310         ENDIF
7320       ENDIF
7330     ELSE
7340       I=8
7350     ENDIF
7360   NEXT I
7370   EXIT CNT
7380 ENDF ANALIZE.DSP2
7390 !
7400 !
7500 DEF INTEGER LASTMAXLOC (*)
7510   INTEGER A(2),I,LASTMAX,J
7520   FOR I=1 TO 8
7530     FOR J=1 TO 8
7540       IF DSP2(I,J)=' ' THEN
7550         DSP2=DSP1
7551         IF BLACK='Y' THEN DSP2(I,J)='O' ELSE DSP2(I,J)='X'
7560         CHANGELOC((I-1)*8+J)=ANALIZE.DSP2(I,J)
7570         IF CHANGELOC((I-1)*8+J)>=LASTMAX THEN
7580           A(1)=I
7590           A(2)=J
7600           LASTMAX=CHANGELOC((I-1)*8+J)
7610         ENDIF
7620       ENDIF
7630     NEXT J
7640   NEXT I
7650   EXIT A
7660 ENDF LASTMAXLOC
    