10 REM ***********************************************************
15 REM *                                                         *
20 REM *                                                         *
25 REM * program name: mapper                     version: 2.00  *
30 REM * programmed.by: ron vivier        date: march 29, 1985   *
35 REM * translated by: jo ann joels      date: november, 1986   *
40 REM *                                                         *
45 REM * note: version 2.0 has been translated from version 1.0  *
50 REM * to run on the ibm pc.                                   *
55 REM *                                                        *
60 REM **********************************************************
70 REM
80 SCREEN 2,0
90 WIDTH 80
95 DEFINT A-Y
96 KEY 10,""
100 DIM NODE.CENTER(31,1),NUM.POS(31,1),NEIGH.CITY$(31),NEIGH.NUM(31),NEIGH.TYPE$(31)
110 DIM NEIGH.SPEED$(31),NEIGH.STATE$(31),TDBINPUT$(100)
120 DATA 12,02,062,094  ,12,66,577,094  ,02,34,319,015  ,22,34,319,175
130 DATA 02,02,062,015  ,02,66,577,015  ,22,02,062,175  ,22,66,577,175
140 DATA 02,18,190,015  ,02,50,449,015  ,22,18,190,175  ,22,50,449,175
150 DATA 08,02,062,067  ,08,66,577,067  ,15,02,062,121  ,15,66,577,121
160 DATA 05,02,062,041  ,05,66,577,041  ,19,02,062,148  ,19,66,577,148
170 DATA 12,18,190,094  ,12,50,449,094  ,05,34,319,041  ,19,34,319,148
180 DATA 05,18,190,041  ,05,50,449,041  ,19,18,190,148  ,19,50,449,148
190 DATA 08,18,190,067  ,08,50,449,067  ,15,18,190,121  ,15,50,449,121
280 CLS: KEY OFF:  GOTO 1280
290 REM ON ERROR GOTO 1575
300 COMBP=0
310 GOTO 360
320 CLOSE #2
330 COMBP=0
340 FOR I=1 TO 16000
350 NEXT I
360 INPUT "ENTER BAUD RATE: ";BAUDRATE$
370 INPUT "ACCESS NETWORK THROUGH 1)HW OR 2)MODEM: ";ACCESS
380 IF ACCESS = 2 THEN INPUT "ENTER TELEPHONE ACCESS NUMBER: ";ANUM$
385 PRINT: PRINT: PRINT "LOGGING INTO SYSTEM 25..."
390 DIAL$="ATDT "+ANUM$
420 KEY 10,""
430 CFILE$="COM1:"+BAUDRATE$+",M,7,1,CS0,DS0,CD0"
440 OPEN CFILE$ AS #2
450 IF ACCESS = 2 THEN PRINT #2,DIAL$
460 CLOSE #2
470 OPEN CFILE$ AS #2
480 IF ACCESS = 2 THEN PRINT #2,DIAL$
490 IF ACCESS = 1 THEN GOTO 580
500 FOR A = 1 TO 1000: NEXT A
510 A$=INPUT$(1,#2)
520 P=INSTR(1,A$,"x"): IF P = 0 THEN 510
530 PRINT #2,"A";
540 FOR I = 10 TO 500 : NEXT I
550 TS$= INPUT$(1,#2)
560 L = INSTR(1,TS$,"p"):  IF L = 0 THEN 550
570 PRINT #2,"PERSTATS:25;RITE ON"
580 FOR S = 1 TO 1000: NEXT S
590 IF LOC(2)>0 THEN TT$=INPUT$(LOC(2),#2) : GOTO 580
600 FOR L = 1 TO 1000: NEXT L
610 PRINT #2,CHR$(13): PRINT #2,CHR$(13)
620 PRINT #2,"R 1022"
630 GOSUB 1880
640 PRINT #2,"O TYMNOD.ALL TYMLIN.ALL CKTS"
645 PRINT: PRINT "CONNECTION ESTABLISHED": FOR I = 1 TO 3000: NEXT I
647 SCREEN 2,0
650 PRINT #2,"DBSET 1"
660 CLS: LOCATE 1,1
670 INPUT "ENTER NODE NUMBER TO BE MAPPED - (CR TO STOP): ";MNODE
672 IF MNODE = 0 GOTO 1910
675 PRINT : PRINT "ONCE MAP HAS BEEN COMPLETED, PRESS F10 TO CONTINUE"
677 PRINT: PRINT "RETRIEVING DATA....."
690 PRINT #2,"FI NODE "+STR$(MNODE)
700 GOSUB 1880
710 IF LOC(2)>0 THEN TT$=INPUT$(LOC(2),#2) : GOTO 700
720 PRINT #2,"USE NPLOT"
730 TDBLINES=0
740 LINE INPUT #2,TT$
750 TDBINPUT$(TDBLINES)=TT$
760 K=INSTR(1,TDBINPUT$(TDBLINES),"$END$")
770 TDBLINES=TDBLINES+1
790 IF K=0 THEN 740
800 REM ********** BREAK OUT INFORMATION FOR MAPPED NODE
810 FOR LIN.POS = 0 TO TDBLINES
820 K=INSTR(1,TDBINPUT$(LIN.POS),"####")
830 IF K <> 0 THEN 850
840 NEXT LIN.POS
850 GOSUB 1420
860 MNTYPE$=TYPE$: MNCITY$=CITY$: MNSTATE$=STATE$: MNSPEED$="  "
870 NNEXT=LIN.POS: LINES=0
880 FOR LIN.POS= NNEXT TO TDBLINES
890 K=INSTR(1,TDBINPUT$(LIN.POS),"@@@@")
900 IF K = 0 THEN 960
910 GOSUB 1420
920 NEIGH.TYPE$(LINES)=TYPE$: NEIGH.SPEED$(LINES)=SPEED$
930 NEIGH.CITY$(LINES)=CITY$: NEIGH.STATE$(LINES)=STATE$
940 NEIGH.NUM(LINES)=NODE
950 LINES=LINES+1
960 NEXT LIN.POS
965 IF LINES <> 0 THEN GOTO 970
966 CLS: PRINT "UNABLE TO LOCATE NODE: ";MNODE
967 FOR I = 1 TO 3000 : NEXT I
968 GOTO 650
970 PRINT "NODE ";MNODE;" HAS ";LINES;" NEIGHBORS": FOR I = 1 TO 3000: NEXT I
980 CLS
1000 MNODEX=319: MNODEY=94
1010 A=62: B=12
1020 LX1=MNODEX-A: LY1=MNODEY-B: LX2=MNODEX+A: LY2=MNODEY+B
1030 LINE (LX1,LY1)-(LX2,LY2),,B
1032 FOR II = 1 TO 2
1033 LX1=LX1+1: LY1=LY1+1: LX2=LX2-1: LY2=LY2-1
1034 LINE (LX1,LY1)-(LX2,LY2),,B
1035 NEXT II
1040 LOCATE 12,36
1050 PRINT USING "&";MNTYPE$+"  ";
1060 PRINT USING "#####";MNODE;
1080 LOCATE 13,34
1090 PRINT USING "&";MNCITY$;",";MNSTATE$
1100 REM ***** NOW PLOT THE NEIGHBORS
1110 FOR NEIGH = 0 TO LINES-1
1120 LX1=NODE.CENTER(NEIGH,0)-A
1130 LY1=NODE.CENTER(NEIGH,1)-B
1140 LX2=NODE.CENTER(NEIGH,0)+A
1150 LY2=NODE.CENTER(NEIGH,1)+B
1160 LINE (LX1,LY1)-(LX2,LY2),,B
1165 IF LINES > 20 THEN GOTO 1242
1170 X1=NODE.CENTER(NEIGH,0)
1180 Y1=NODE.CENTER(NEIGH,1)
1190 X2=MNODEX
1195 Y2=MNODEY
1200 GOSUB 5000
1210 LX1=MNODEX+IX
1222 LY1=MNODEY+IY
1224 X1=MNODEX
1226 Y1=MNODEY
1228 X2=NODE.CENTER(NEIGH,0)
1230 Y2=NODE.CENTER(NEIGH,1)
1232 GOSUB 5000
1234 LX2=NODE.CENTER(NEIGH,0)+IX
1236 LY2=NODE.CENTER(NEIGH,1)+IY
1240 LINE (LX2,LY2)-(LX1,LY1)
1242 LOCX=NUM.POS(NEIGH,0): LOCY=NUM.POS(NEIGH,1)
1244 LOCATE LOCX,LOCY: PRINT USING "&";NEIGH.TYPE$(NEIGH)+"-";
1246 PRINT USING "#####";NEIGH.NUM(NEIGH);
1248 PRINT USING "&";"-"+NEIGH.SPEED$(NEIGH)
1250 LOCATE LOCX+1,LOCY
1252 PRINT USING "&";NEIGH.CITY$(NEIGH);",";NEIGH.STATE$(NEIGH)
1260 NEXT NEIGH
1266 IKB$=INKEY$
1267 IF IKB$ <> (CHR$(0)+"D") GOTO 1266
1268 GOTO 650
1280 FOR I = 0 TO 31
1290 READ NUM.POS(I,0),NUM.POS(I,1),NODE.CENTER(I,0),NODE.CENTER(I,1)
1300 NEIGH.TYPE$(I)="UK"
1310 NEIGH.SPEED$(I)="UK"
1320 NEIGH.STATE$(I)="UK"
1330 NEIGH.CITY$(I)="UNKNOWN"
1340 NEXT I
1350 GOTO 290
1420 NODE=VAL(MID$(TDBINPUT$(LIN.POS),7,5))
1425 IF NODE < 2000 THEN NODE=NODE+10000
1430 TYPE$=MID$(TDBINPUT$(LIN.POS),13,2)
1440 IF TYPE$="IS" GOTO 1500
1450 IF TYPE$="SW" GOTO 1500
1460 IF TYPE$="CO" THEN TYPE$="CS": GOTO 1500
1470 IF TYPE$="TY" THEN TYPE$="TS": GOTO 1500
1480 IF TYPE$="AT" THEN GOTO 1500
1490 TYPE$="UK"
1500 SPEED$=MID$(TDBINPUT$(LIN.POS),23,5)
1505 IF SPEED$=" " THEN SPEED$=" UK "
1510 CITY$=MID$(TDBINPUT$(LIN.POS),29,10)
1515 IF CITY$=" " THEN CITY$=" UNKNOWN "
1520 STATE$=MID$(TDBINPUT$(LIN.POS),41,2)
1525 IF STATE$=" " THEN STATE$="UK"
1530 RETURN
1540 PRINT #2,"n";NEIG.NUM(I)
1550 DELAY=20000: GOSUB 1600
1560 REM NOD.TYPE(I) = 7
1570 REM IF NEIG.NUM(I) < 2000 GOTO 1590
1580 REM NOD.TYPE(I) = INT(VAL(MID$(PRINPUT$(1),J,8))/10000)+1
1590 RETURN
1600 PRLINES = 0
1610 PRINPUT$(PRLINES)=""
1620 FOR ITTY = 0 TO DELAY
1630 LOC.2 = LOC(2)
1640 IF LOC.2 > 128 THEN LOC.2 = 128
1650 IF LOC.2 = 0 THEN GOTO 1700
1660 PRINPUT$(PRLINES) = PRINPUT$(PRLINES)+ INPUT$(LOC.2,#2)
1670 IF 0 < INSTR(PRINPUT$(PRLINES),CHR$(13)) THEN GOSUB 1720
1680 IF 0 < INSTR(PRINPUT$(PRLINES),":") THEN ITTY = DELAY
1690 IF LEN(PRINPUT$(PRLINES)) > 150 THEN ITTY = DELAY
1700 NEXT ITTY
1710 RETURN
1720 CRPOS=INSTR(PRINPUT$(PRLINES),CHR$(13))
1730  PRINPUT$(PRLINES+1) = RIGHT$(PRINPUT$(PRLINES),LEN(PRINPUT$(PRLINES))-CRPOS)
1740 PRINPUT$(PRLINES) = LEFT$(PRINPUT$(PRLINES),CRPOS)
1750 PRLINES=PRLINES+1
1760 IF 0 < INSTR(PRINPUT$(PRLINES),CHR$(13)) GOTO 1720
1770 RETURN
1830 PRINT "ERROR = ";ERR
1840 IF ERR = 24 THEN RESUME 320
1850 IF ERR = 57 THEN RESUME 320
1860 ON ERROR GOTO 0
1870 STOP
1880 FOR I = 1 TO 1000: NEXT I
1890 IF LOC(2)>0 THEN TT$=INPUT$(LOC(2),#2):  GOTO 1880
1900 RETURN
1910 PRINT: PRINT "BREAKING CONNECTION TO SYSTEM 25"
1915 PRINT #2,"QUIT"
1920 GOSUB 1880
1930 PRINT #2,"LOG"
1940 GOSUB 1880
1950 CLOSE #2
1970 PRINT "FINISHED"
1980 KEY ON
1990 END
5000 ZA=A: ZB=B: Z=ZB/ZA
5010 IF (Y1-Y2) <> 0 THEN GOTO 5025
5020 ZB=0: GOTO 5120
5025 IF (X1-X2) <> 0 THEN GOTO 5030
5026 ZA=0: GOTO 5120
5030 ZSLOPE=(Y1-Y2)/(X1-X2)
5040 ZA1=ABS(X1-X2)
5050 ZB1=ABS(Y1-Y2)
5060 IF ZSLOPE <=Z AND ZSLOPE > -Z THEN GOTO 5100
5070 ZSCALE=ABS(ZB1/ZB)
5080 ZA=ZA1/ZSCALE
5090 GOTO 5120
5100 ZSCALE=ABS(ZA1/ZA)
5110 ZB=ZB1/ZSCALE
5120 IF X1 <= X2 THEN ZA=-ZA
5130 IF Y1 <= Y2 THEN ZB=-ZB
5140 IX=ZA
5150 IY=ZB
5160 RETURN
