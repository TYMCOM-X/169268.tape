70 REM ********************
80 REM ********************
90 REM ********************
100 A=20
110 NNUM=A
120 VNUM=66,ANUM=44,PNUM=7,ADVERB=5
130 DOORNUM=18
140 DIM WORD$(35)
150 DIM DOORS(DOORNUM,6)
160 DIM ADJECT$(ANUM),VERB$(VNUM),VCODE(VNUM)
170 DIM ADVERB$(ADVERB),DIR$(10)
180 DIM MOV(50,10)
190 DIM CHARAC$(6),CHARACTER(82)
200 DIM LOCS(50,10)
210 DIM TEXT$(400),ITEXT(400)
220 DIM PREP$(PNUM)
230 DIM NOUN$(A), NOUN2$(A) 
240 DIM NOUN(A,24)
250 DIM NHITSII(A)
260 DIM DFLAG(100)   ! FLAG LIST ARRAY FOR FLAGGED EVENTS.
270 DIM PRS(15)  ! THIS IS PARSER FINDINGS VNUM,NNUM,ETC..
280 DIM PRSBAK(15)
290 MAT READ MOV
300 MAT READ VERB$
310 MAT READ DIR$
320 MAT READ VCODE
330 MAT READ ADJECT$
340 MAT READ PREP$
350 MAT READ ADVERB$
360 REM A=NOUN-NUM B=VCODE MAX
370 FOR I=1 TO A
380 READ NOUN$(I),NOUN2$(I)
390 FOR J = 1 TO 24
400         READ NOUN(I,J)
410 NEXT J
420 NEXT I
430 FOR I=1 TO A
440 NHITSII(I)=NOUN(I,9)
450 NEXT I
460 CALL READ.DESC  ! GET DESCRIPTIONS
470 FOR I=7 TO 9
480 FOR J=1 TO 50
490 READ LOCS(J,I)
500 NEXT J
510 NEXT I
520 FOR I=1 TO DOORNUM
530 FOR J=1 TO 6
540 READ DOORS(I,J)
550 NEXT J
560 NEXT I
570 CALL STARTUP
580 DEFINE PARSER.SUB
590 REM **********************************************************
600 REM **
610 REM **      SPLIT UP INPUT INTO WORDS YOU TURDS!
620 REM **
630 REM **********************************************************
640 AA=1
650 MAT WORD$=" "
660 CIB
670 PRINT "]":
680 INPUT IN FORM "X":I$
690 REM ****** THAT IS END OF TEST FUNCTIONS!!!! *********
700 IF ASC(I$)=13 AND LENGTH(A1$)<1 AND AA=1 THEN GOTO 660
710 IF I$="," THEN COMMLOC=AA
720 IF I$="," THEN I$=" "
730 IF ASC(I$)<>13 AND I$<>" " AND I$<>"," THEN A1$=A1$+I$
740 IF ASC(I$)=13 THEN GOTO 770
750 IF I$=" " THEN GOTO 770
760 GOTO 680
770 WORD$(AA)=A1$
780 A1$=""
790 IF WORD$(AA)="THE" OR WORD$(AA)="GO" OR WORD$(AA)="A" THEN AA=AA-1
800 AA=AA+1
810 IF ASC(I$)=13 THEN GOTO 830
820 GO TO 680
830 REM DONE
840 MAT PRSBAK=PRS
850 MAT PRS=0
860 WNUM=1
870 IF COMMFLAG=1 THEN
880         COMMFLAG=0,WNUM=COMMLOC+1,COMMLOC=0
890         GOTO 1160
900         ENDIF
910 IF WORD$(1)="TAKE" AND WORD$(2)="INVENTORY" THEN
920         CALL INVENTORY.SUB
930         GOTO 2210
940         ENDIF
950 IF WORD$(1)="PICK" AND WORD$(2)="UP" THEN
960      WNUM=3
970      PRS(1)=27
980      GOTO 1240
990 ENDIF
1000 IF WORD$(1)="PUT" AND WORD$(2)="DOWN" THEN
1010         WNUM=3
1020         PRS(1)=28
1030         GOTO 1240
1040         ENDIF
1050 CALL CHECK.ADVERB
1060 IF PRS(10)>0 AND WORD$(2)="PICK" AND WORD$(3)="UP" THEN 
1070     WNUM=4
1080     PRS(1)=27
1090     GOTO 1240
1100     ENDIF
1110 IF PRS(10)>0 AND WORD$(2)="PUT" AND WORD$(3)="DOWN" THEN
1120         WNUM=4
1130         PRS(1)=28
1140         GOTO 1240
1150         ENDIF
1160 REM *********** SEARCH WORD LISTS ************
1170 FOR I=1 TO VNUM
1180 IF WORD$(WNUM)=VERB$(I) THEN
1190         PRS(1)=I
1200         WNUM=WNUM+1
1210         GOTO 1240
1220         ENDIF
1230         NEXT I
1240 FOR I=1 TO PNUM
1250 IF WORD$(WNUM)=PREP$(I) THEN 
1260         PRS(2)=I
1270         WNUM=WNUM+1
1280         GOTO 1310
1290         ENDIF
1300         NEXT I
1310 FOR I=1 TO ANUM
1320 IF WORD$(WNUM)=ADJECT$(I) THEN
1330         PRS(3)=I
1340         WNUM=WNUM+1
1350         GOTO 1380
1360         ENDIF
1370         NEXT I
1380 FOR I=1 TO ANUM
1390 IF WORD$(WNUM)=ADJECT$(I) THEN
1400         PRS(4)=I
1410         WNUM=WNUM+1
1420         GOTO 1450
1430         ENDIF
1440         NEXT I
1450 FOR I=1 TO NNUM
1460 IF WORD$(WNUM)="IT" THEN PRS(5)=PRSBAK(5),PRS(3)=PRSBAK(3),PRS(4)=PRSBAK(4)
1470 ! IN CASE HE REFERS TO A NOUN IN LAST SENTENCE.
1480 IF WORD$(WNUM)="IT" THEN WNUM=WNUM+1
1490 IF WORD$(WNUM)=NOUN$(I) OR WORD$(WNUM)=NOUN2$(I) THEN
1500         PRS(5)=I
1510         WNUM=WNUM+1
1520         GOTO 1550
1530         ENDIF
1540         NEXT I
1550 FOR I=1 TO PNUM
1560 IF WORD$(WNUM)=PREP$(I) THEN
1570         WNUM=WNUM+1
1580         PRS(6)=I
1590         GOTO 1620
1600         ENDIF
1610         NEXT I
1620 FOR I=1 TO ANUM
1630         IF WORD$(WNUM)=ADJECT$(I) THEN
1640         PRS(7)=I
1650         WNUM=WNUM+1
1660         GOTO 1690
1670         ENDIF
1680         NEXT I
1690 FOR I=1 TO ANUM
1700 IF WORD$(WNUM)=ADJECT$(I) THEN
1710         PRS(8)=I
1720         WNUM=WNUM+1
1730         GOTO 1760
1740         ENDIF
1750         NEXT I
1760 FOR I=1 TO NNUM
1770 IF WORD$(WNUM)="IT" THEN PRS(9)=PRSBAK(5),PRS(7)=PRSBAK(3),PRS(8)=PRSBAK(4)
1780 ! IN CASE HE REFERS TO A NOUN IN LAST SENTENCE
1790 IF WORD$(WNUM)="IT" THEN WNUM=WNUM+1
1800 IF WORD$(WNUM)=NOUN$(I) OR WORD$(WNUM)=NOUN2$(I) THEN
1810         PRS(9)=I
1820         WNUM=WNUM+1
1830         GOTO 1860
1840         ENDIF
1850         NEXT I
1860 REM
1870 IF PRS(1)<1 THEN GOTO 2210
1880 REM ************************
1890 REM * EXECUTE VERBS *******
1900 REM ***********************
1910 PP=P  ! RESET LOCATION HERE TO GIVE MONSTERS A CHANCE
1920 REM *************** NON NOUN-MODIFIERS ********************
1930 IF VCODE(PRS(1))>0 AND VCODE(PRS(1))<11 THEN CALL MOV.SUB
1932 IF VCODE(PRS(1))>0 AND VCODE(PRS(1))<11 THEN ROND=ROND+6
1940 IF VCODE(PRS(1))>0 AND VCODE(PRS(1))<11 THEN GOTO 2210
1950 IF VCODE(PRS(1))=27 THEN CALL CLOSE.SUB
1960 IF VCODE(PRS(1))=26 THEN CALL OPEN.SUB
1970 IF VCODE(PRS(1))=28 THEN CALL LOOK.SUB
1980  IF VCODE(PRS(1))=17 THEN CALL INVENTORY.SUB
1990 REM +++++++ SEARCH STUFF +++++++
2000 IF VCODE(PRS(1))=29 AND PRS(5)<>8 AND PRS(5)<>12 THEN CALL SEARCH.SUB
2010 IF VCODE(PRS(1))=29 AND PRS(9)=12 AND PRS(5)=8 THEN CALL SEARCH.DOOR1
2020 IF VCODE(PRS(1))=29 AND PRS(5)=8 AND PRS(9)<1 THEN PRINT "WHICH DOOR?"
2030 IF VCODE(PRS(1))=29 AND PRS(5)=12 AND PRS(9)<1 THEN PRINT "FOR WHAT?"
2040 IF VCODE(PRS(1))=29 AND PRS(5)=12 AND PRS(9)=8 THEN CALL SEARCH.DOOR2
2050 REM REM *****************   NOUN MODIFIERS ******
2060 IF PRS(1)<1 THEN 2160
2070 IF VCODE(PRS(1))=15 THEN CALL GET.SUB
2075 IF VCODE(PRS(1))=39 THEN CALL ENTER.SUB
2080 IF VCODE(PRS(1))=11 THEN CALL ENGAGE.SUB
2090 IF VCODE(PRS(1))=16 THEN CALL DROP.SUB
2100 IF VCODE(PRS(1))=33 THEN CALL ENGAGE.SUB
2110 IF VCODE(PRS(1))=30 THEN CALL PUT.SUB
2120 IF VCODE(PRS(1))=32 THEN CALL WEAR.SUB
2130 IF VCODE(PRS(1))=31 THEN CALL MOVE.SUB
2140 IF VCODE(PRS(1))=38 THEN CALL DISPLAY
2142 IF VCODE(PRS(1))=27 OR VCODE(PRS(1))=26 OR VCODE(PRS(1))=28 THEN ROND=ROND+4
2144 A=VCODE(PRS(1))
2146 IF A=17 OR A=29 OR A=11 OR A=33 THEN ROND=ROND+4
2148 IF A=15 OR A=16 OR A=30 THEN ROND=ROND+4
2150 IF A=31 THEN ROND=ROND+10
2160 REM
2170 PRINT
2180 IF PRS(1)<1 THEN
2190         PRINT "YOU NEED TO SPECIFY A VERB."
2200         ENDIF
2210 IF COMMLOC<>0 THEN
2220    COMMFLAG=1
2230 GOTO 830
2240 ENDIF
2250 IF PRS(1)<1 THEN
2260         PRINT "I DON'T UNDERSTAND THAT."
2270         GOTO 640
2280         ENDIF
2290 ENDF PARSER.SUB
2300 REM *****************************************
2310 DEFINE READ.DESC
2320 OPEN "TEXT.GME-A" FOR SYMBOLIC IO AS FILE 1
2330 LINE=1,P=1
2340 INPUT FROM 1 IN FORM "R":A$
2350 ON ENDFILE(1) GOTO 2430
2360 IF LENGTH(A$)<4 THEN
2370         P=VAL(A$)
2380         GOTO 2340
2390         ENDIF
2400 TEXT$(LINE)=A$,ITEXT(LINE)=P
2410 LINE=LINE+1
2420 GOTO 2340
2430 ENDF READ.DESC
2440 REM ***************************************
2450 DEFINE MOV.SUB 
2460 PP=P
2470 P=MOV(P,VCODE(PRS(1)))
2480 IF P<1 THEN
2490            IF P=0 THEN Z=30
2500 IF P=0 THEN
2510         FOR I=1 TO DOORNUM
2520        IF DOORS(I,1)=PP AND DOORS(I,2)=VCODE(PRS(1)) OR DOORS(I,3)=PP AND DOORS(I,4)=VCODE(PRS(1)) THEN
2530        IF DOORS(I,5)=2 THEN GOTO 2550
2540         Z=31
2550         ENDIF
2560         NEXT I
2570        ENDIF
2580         P=PP
2590         CALL PRINTER.SUB
2600         ENDIF
2610 REM ** SPECIAL CONSIDERATIONS, ON WALL, INVISO, ETC..
2620 Z=LOCS(P,7)
2630 IF LOCS(P,10)=1 THEN Z=LOCS(P,9)
2632 N=P
2634 CALL LIGHT.MODE
2636 IF L=1 THEN
2638         PRINT "IT'S TOO DARK TO SEE."
2640         GOTO 2670
2642         ENDIF
2649 IF P<>PP THEN CALL PRINTER.SUB
2650 LOCS(P,10)=1
2660 IF P<>PP THEN CALL OBJECT.PRINT
2670 ENDF MOV.SUB
2680 REM ********************
2690 REM * PRINT DESC'S    **
2700 REM ********************
2710 DEFINE PRINTER.SUB
2720 FOR J=1 TO LINE
2730 IF Z=ITEXT(J) THEN PRINT TEXT$(J)
2740 NEXT J
2750 Z=0
2760 ENDF PRINTER.SUB
2770 REM *********************
2780 REM *  START-UP GAME ****
2790 REM *********************
2800 DEFINE STARTUP
2810 CNUM=1
2820 CALL ADDPLAYER
2830 P=1,PP=1
2840 CALL VALUE.MANAGE
2850 Z=1
2860 CALL PRINTER.SUB
2870 IF CHARACTER(67)<>0 AND CHARACTER(67)<>4 THEN
2880         IF CHARACTER(67)=1 THEN PRINT "YOU ARE STUNNED."
2890         IF CHARACTER(67)=2 THEN PRINT "YOU ARE UNCONSCIOUS."
2900         IF CHARACTER(67)=3 THEN PRINT "YOU ARE PARALYZED."
2910         IF CHARACTER(67)=5 THEN PRINT "YOU ARE HELPLESS."
2920         CHARACTER(66)=CHARACTER(66)-1
2930         IF CHARACTER(66)<1 THEN CHARACTER(66)=0
2940         IF CHARACTER(66)=0 THEN CHARACTER(67)=0
2950         GOTO 2990
2960         ENDIF
2965 CALL DFLAG.PRINT UNLESS CHARACTER(69)<>0
2970 CALL PARSER.SUB UNLESS CHARACTER(69)<>0
2980 IF CHARACTER(69)<>0 THEN CALL CPARSER.SUB
2990 CALL MONSTER.SUB
3000 CALL VALUE.MANAGE
3010 GOTO 2870
3020 ENDF STARTUP
3030 DEFINE DOOR.SUB
3040 REM *******************
3050 REM *  OPEN DOORS     *
3060 REM *******************
3070 IF PRS(3)=34 THEN S=1
3080 IF PRS(4)=34 THEN S=1
3090 IF PRS(7)=34 THEN S=1
3100 IF PRS(8)=34 THEN S=1
3110    REM IN CASE HE SAYS OPEN DOOR ON THE LEFT..WE MUST REVERSE
3120       IF PRS(3)<26 OR PRS(3)>33 THEN
3130     IF PRS(4)<26 OR PRS(4)>33 THEN
3140     IF PRS(7)>25 OR PRS(7)<34 THEN
3150      IF PRS(6)=1 THEN PRS(3)=PRS(7)
3160      ENDIF
3170      IF PRS(8)>25 OR PRS(8)<34 THEN
3180     IF PRS(6)=1 THEN PRS(4)=PRS(8)
3190     ENDIF
3200     ENDIF
3210     ENDIF
3220 IF PRS(4)>26 AND PRS(4)<34 THEN GOTO 3270
3230 IF PRS(3)<27 OR PRS(3)>34 THEN
3240    PRINT "YOU MUST SPECIFY WHICH DOOR YOU WISH TO OPEN"
3250 GOTO 3620
3260 ENDIF
3270 IF PRS(3)>26 AND PRS(3)<34 THEN
3280         IF PRS(4)<34 AND PRS(4)>26 THEN
3290                 PRINT "MAKE YOUR MIND? ":ADJECT$(PRS(4)):" OR ":ADJECT$(PRS(3)):"?"
3300                 GOTO 3620
3310                 ENDIF
3320                 ENDIF
3330 IF PRS(4)<34 AND PRS(4)>26 THEN I=PRS(4)-26
3340 IF PRS(3)<34 AND PRS(3)>26 THEN I=PRS(3)-26
3350 IF I=2 THEN FACE=4
3360 IF I=5 THEN FACE=7
3370 IF I=6 THEN FACE=8
3380 IF I=7 THEN FACE=1
3390 IF I=3 THEN FACE=7
3400 IF I=4 THEN FACE=8
3410 IF I=1 THEN FACE=1
3420 FOR I=1 TO DOORNUM
3430         IF DOORS(I,1)=P AND DOORS(I,2)=FACE OR DOORS(I,3)=P AND DOORS(I,4)=FACE THEN
3440                IF DOORS(I,5)=2 AND S<>1 THEN
3450               PRINT "THERE IS NO DOOR THERE TO OPEN."
3460           GOTO 3620
3470               ENDIF
3480                IF DOORS(I,5)=1 THEN
3490                        PRINT "THAT DOOR IS LOCKED."
3500                        GOTO 3590
3510                        ENDIF
3520              IF DOORS(I,6)=1 THEN PRINT "THE DOOR IS ALREADY OPEN."
3530              IF DOORS(I,6)=1 THEN GOTO 3620
3540                 MOV(DOORS(I,1),DOORS(I,2))=DOORS(I,3)
3550                 MOV(DOORS(I,3),DOORS(I,4))=DOORS(I,1)
3560                 PRINT "OK. THE DOOR IS NOW OPEN."
3570             DOORS(I,6)=1
3580                 GOTO 3620
3590                 ENDIF
3600 NEXT I
3610 PRINT "I CAN'T SEE A DOOR THERE."
3620 S=0
3630 ENDF DOOR.SUB
3640 REM *******************
3650 REM *  CLOSE DOORS    *
3660 REM *******************
3670 DEFINE DCLOSE
3680 REM ********************************
3690 REM **** CLOSE DOORS
3700 REM *******************************
3710 REM ** IN CASE HE SAYS CLOSE DOOR ON LEFT..REVERSE
3720 IF PRS(3)<26 OR PRS(3)>33 THEN
3730 IF PRS(4)<26 OR PRS(4)>33 THEN
3740 IF PRS(7)>25 OR PRS(7)<34 THEN
3750         IF PRS(6)=1 THEN PRS(3)=PRS(7)
3760         ENDIF
3770 IF PRS(8)>25 OR PRS(8)<34 THEN
3780         IF PRS(6)=1 THEN PRS(4)=PRS(8)
3790         ENDIF
3800         ENDIF
3810         ENDIF
3820 IF PRS(4)>26 AND PRS(4)<34 THEN GOTO 3870
3830 IF PRS(3)<27 OR PRS(3)>34 THEN
3840    PRINT "YOU MUST SPECIFY WHICH DOOR YOU WISH TO CLOSE."
3850 GOTO 4180
3860 ENDIF
3870 IF PRS(3)>26 AND PRS(3)<34 THEN
3880         IF PRS(4)<34 AND PRS(4)>26 THEN
3890                 PRINT "MAKE YOUR MIND? ":ADJECT$(PRS(4)):" OR ":ADJECT$(PRS(3)):"?"
3900                 GOTO 4180
3910                 ENDIF
3920                 ENDIF
3930 IF PRS(4)<34 AND PRS(4)>26 THEN I=PRS(4)-26
3940 IF PRS(3)<34 AND PRS(3)>26 THEN I=PRS(3)-26
3950 IF I=2 THEN FACE=4
3960 IF I=5 THEN FACE=7
3970 IF I=6 THEN FACE=8
3980 IF I=7 THEN FACE=1
3990 IF I=3 THEN FACE=7
4000 IF I=4 THEN FACE=8
4010 IF I=1 THEN FACE=1
4020 FOR I=1 TO DOORNUM
4030         IF DOORS(I,1)=P AND DOORS(I,2)=FACE OR DOORS(I,3)=P AND DOORS(I,4)=FACE THEN
4040                IF DOORS(I,5)=1 THEN
4050                        PRINT "THAT DOOR IS LOCKED."
4060                        GOTO 4180
4070                        ENDIF
4080              IF DOORS(I,6)=0 THEN PRINT "THE DOOR IS ALREADY CLOSED."
4090              IF DOORS(I,6)=0 THEN GOTO 4180
4100                 MOV(DOORS(I,1),DOORS(I,2))=0
4110                 MOV(DOORS(I,3),DOORS(I,4))=0
4120                 PRINT "OK. THE DOOR IS NOW CLOSED."
4130             DOORS(I,6)=0
4140                 GOTO 4180
4150                 ENDIF
4160 NEXT I
4170 PRINT "I CAN'T SEE A DOOR THERE."
4180 ENDF DCLOSE
4190 DEFINE CHECK.EM
4200 ER=0
4210 REM ** MAKE MATCH **
4220         FOR I=1 TO NNUM
4230         IF NOUN$(I)<>NOUN$(PRS(5)) THEN GOTO 4380
4240         IF PRS(3)>0 AND NOUN(I,6)<>PRS(3) AND NOUN(I,7)<>PRS(3) OR PRS(4)>0 AND NOUN(I,6)<>PRS(4) AND NOUN(I,7)<>PRS(4) THEN
4250                 ER=1
4260                 GOTO 4380
4270                 ENDIF
4280         IF PRS(3)>0 AND PRS(4)>0 AND PRS(3)=PRS(4) THEN
4290                 ER=2
4300                 GOTO 4380
4310                 ENDIF
4320         REM ** CHECK CONDITIONS **
4330        IF E=NOUN(I,1) THEN GOTO 4360
4340         IF E1<>0 AND E1=NOUN(I,1) THEN GOTO 4360
4350       GOTO 4380
4360         PRS(5)=I
4370         GOTO 4410
4380         NEXT I
4390 IF ER=1 THEN PRINT "YOU USED THE WRONG ADJECTIVE(S) FOR A PRESENT SUITABLE OBJECT."
4400 IF ER=1 THEN PRS(5)=0
4410 E=0,E1=0
4420 ENDF CHECK.EM
4430 DEFINE CHECK.EMI
4440 REM ** MAKE MATCH **
4450         FOR I=1 TO NNUM
4460         IF PRS(7)>0 AND NOUN(I,6)<>PRS(7) AND NOUN(I,7)<>PRS(7) OR PRS(8)>0 AND NOUN(I,6)<>PRS(8) AND NOUN(I,7)<>PRS(8) THEN
4470                 ER=1
4480                 GOTO 4610
4490                 ENDIF
4500         IF PRS(7)>0 AND PRS(8)>0 AND PRS(7)=PRS(8) THEN
4510                 ER=2
4520                 GOTO 4610
4530                 ENDIF
4540         IF NOUN$(I)<>NOUN$(PRS(9)) AND NOUN2$(I)<>NOUN2$(PRS(9)) THEN GOTO 4610
4550         ER=0
4560         REM ** CHECK CONDITIONS **
4570         IF E<>NOUN(I,1) THEN GOTO 4610
4580         IF E1<>0 AND E1<>NOUN(I,1) THEN GOTO 4610
4590         PRS(9)=I
4600         GOTO 4620
4610         NEXT I
4620 ENDF CHECK.EMI
4630 DEFINE OBJECT.PRINT
4640 REM **********************
4650 REM * LIST OBJECTS       *
4660 REM **********************
4670 FOR I=1 TO NNUM
4680 REM ** FOR DUPLICATES JUST CALL CHECK.EM AND ADD COUNTER FLAG!!
4690 IF NOUN(I,9)<>0 THEN GOTO 4860
4700 IF NOUN(I,3)<>0 THEN GOTO 4860
4705 IF NOUN(I,18)=1 THEN GOTO 4770
4710 IF NOUN(I,1)=P THEN
4720         Z=NOUN(I,17)
4730 IF P=4 AND I=7 AND DFLAG(1)<>1 THEN GOTO 4860
4740 IF P=4 AND I=7 AND DFLAG(1)=1 THEN Z=9 !BOOK
4750         CALL PRINTER.SUB
4760         ENDIF
4770 IF NOUN(I,3)=0 AND NOUN(I,18)=1 THEN
4780 ! PRINT DEAD MONSTER
4785 IF P<>NOUN(I,1) THEN GOTO 4860
4790        N=I
4800         PRINT "THERE IS A ":
4810         CALL PRINT.NOUN
4820         PRINT " HERE."
4830         ENDIF
4840 !
4850 !
4860 NEXT I
4870 ENDF OBJECT.PRINT
4880 DEFINE GET.SUB
4890 REM ******************
4900 REM * G E T  *********
4910 REM ******************
4920 IF PRS(5)<1 THEN
4930         Z=55
4940         CALL PRINTER.SUB
4950         GOTO 5620
4960         ENDIF
4970 E=P
4980 E1=-1
4990 CALL CHECK.EM
5000 IF PRS(5)<1 THEN GOTO 5670
5010 REM ****** OK, IF HE'S GOT IT,BUT IT IS NOT IN HIS HANDS,THEN
5020 REM ****** TRY TO PUT IT IN HIS HANDS..IT MAY BE IN BAG
5030 IF NOUN(PRS(5),1)=P  AND NOUN(PRS(5),9)<>0 OR NOUN(PRS(5),1)=-1 AND NOUN(PRS(5),9)<>0 THEN
5040         ! CHECK IF IT'S IN PACK OR IF CONTAINER IS CLOSED.
5050        N=PRS(5)
5060        CALL CHECK.BP
5070        IF ER=1 THEN GOTO 5620
5080        ENDIF
5090 IF NOUN(PRS(5),1)=-1 AND CHARACTER(64)<>PRS(5) AND CHARACTER(65)<>PRS(5) THEN GOTO 5330
5100 IF NOUN(PRS(5),1)=-1 THEN
5110         PRINT "YOU ALREADY HAVE THAT!"
5120         GOTO 5620
5130         ENDIF
5140 IF NOUN(PRS(5),1)<>P AND NOUN(PRS(5),1)<>-1 THEN
5150         PRINT "I DON'T SEE IT."
5160         GOTO 5620
5170         ENDIF
5180 IF NOUN(PRS(5),2)<>1 THEN
5190         PRINT "I'M SORRY BUT YOU ARE UNABLE TO PICK THAT UP."
5200         GOTO 5620
5210         ENDIF
5220 REM ** IF HANDS ARE FULL, THEN CAN'T PICK UP
5230 IF CHARACTER(64)<>0 AND CHARACTER(65)<>0 THEN
5240         PRINT "YOUR HANDS ARE FULL."
5250         GOTO 5620
5260         ENDIF
5270 REM ** CHECK WEIGHT **
5280 IF P=4 AND PRS(5)=7 AND DFLAG(1)<>1 THEN
5290         Z=9
5300         CALL PRINTER.SUB
5310         DFLAG(1)=0
5320         ENDIF
5330 NOUN(PRS(5),1)=-1
5340 REM ** SPECIAL STUFF **
5350 REM +++ HANDS STUFF +++
5360 IF CHARACTER(64)=0 THEN
5370      CHARACTER(64)=PRS(5)
5380  GOTO 5410
5390  ENDIF
5400   IF CHARACTER(65)=0 THEN CHARACTER(65)=PRS(5)
5410 REM ** THIS IS A GOTO LINE
5420 IF CHARACTER(64)=PRS(5) THEN NOUN(PRS(5),9)=1
5430 IF CHARACTER(65)=PRS(5) THEN NOUN(PRS(5),9)=2
5440 IF CHARACTER(64)=PRS(5) THEN
5450         N=PRS(5)
5460         PRINT "OK, THE ":
5470         CALL PRINT.NOUN
5480         PRINT :" IS IN YOUR LEFT HAND."
5490         ENDIF
5500 IF CHAR(65)=PRS(5) THEN
5510         N=PRS(5)
5520         PRINT "THE ":
5530         CALL PRINT.NOUN
5540         PRINT " IS IN YOUR RIGHT HAND."
5550         ENDIF
5560 REM ** IF GOTTEN OBJECT IS CONTAINER THE OBJECTS IN CONTAINER
5570 REM *( ALSO EQUAL THAT OF PICKER UPPER.
5580 IF NOUN(PRS(5),5)<>0 THEN
5590         E=-1,N=PRS(5)
5600         CALL PUT.COMPLETE
5610         ENDIF
5620 IF PRS(6)=7 THEN
5630      PRS(5)=PRS(9),PRS(3)=PRS(7),PRS(4)=PRS(8)
5640      PRS(6)=-1   ! SECOND ROUND FLAG FOR GETTING ITEM 2
5650      GOTO 4920
5660      ENDIF
5670 ENDF GET.SUB
5680 REM *********************
5690 REM **** D R O P  *******
5700 IF PRS(5)<1 THEN
5710         Z=55
5720         CALL PRINTER.SUB
5730         GOTO 5980
5740         ENDIF
5750 DEFINE DROP.SUB
5760 E=-1
5770 CALL CHECK.EM
5780 REM ** CHECK IF IN A CONTAINER OR ON BACKPACK,ETC..
5790 IF NOUN(PRS(5),1)=-1 AND NOUN(PRS(5),9)<>0 THEN
5800         N=PRS(5)
5810         CALL CHECK.BP
5820         IF ER=1 THEN GOTO 5980
5830         ENDIF
5840 IF NOUN(PRS(5),1)<>-1 THEN
5850         PRINT "I'M SORRY BUT YOU ARE NOT CARRYING THAT."
5860        GOTO 5980
5870        ENDIF
5880 PRINT "OK."
5890 IF CHARACTER(64)=PRS(5) THEN CHARACTER(64)=0
5900 IF CHARACTER(65)=PRS(5) THEN CHARACTER(65)=0
5910 NOUN(PRS(5),9)=0
5920 NOUN(PRS(5),1)=P
5930 REM **++**++**++ PUT ANYTHING IN A CONTAINER ON THE GROUND TOO.
5940 IF NOUN(PRS(5),5)<>0 THEN
5950         E=P,N=PRS(5)
5960         CALL PUT.COMPLETE
5970         ENDIF
5980 ENDF DROP.SUB
5990 DEFINE INVENTORY.SUB
6000 REM **********************
6010 REM * INVENTORY
6020 REM **********************
6030 PRINT 
6040 PRINT "YOU ARE CARRYING THE FOLLOWING:"
6050 PRINT
6060 FOR I=1 TO NNUM
6070         IF NOUN(I,1)=-1 THEN
6080           IF NOUN(I,9)<1 THEN GOTO 6100
6090            IF NOUN(NOUN(I,9),5)=2 THEN GOTO 6200
6100         N=I
6110         CALL PRINT.NOUN
6120         IF NOUN(I,9)<>0 THEN
6130          IF NOUN(I,9)=1 OR NOUN(I,9)=2 OR NOUN(I,9)=3 THEN GOTO 6170
6140                 PRINT " INSIDE THE ":
6150                 N=NOUN(I,9)
6160                 CALL PRINT.NOUN
6170                 IF NOUN(I,5)=2 THEN PRINT ". IT IS CLOSED." ELSE PRINT "."
6180                 ENDIF
6190                 ENDIF
6200 NEXT I
6210 PRINT
6220 REM ** EXPLAIN WHERE EVERYTHING IS **
6230 IF CHARACTER(64)<>0 THEN
6240         N=CHARACTER(64)
6250         PRINT "THE ":
6260         CALL PRINT.NOUN
6270         PRINT " IS IN YOUR LEFT HAND."
6280         ENDIF
6290 IF CHARACTER(65)<>0 THEN
6300         N=CHARACTER(65)
6310         PRINT "THE ":
6320         CALL PRINT.NOUN
6330         PRINT " IS IN YOUR RIGHT HAND."
6340         ENDIF
6350 FOR I=1 TO NNUM
6360 IF NOUN(I,9)=3 THEN
6370         N=I
6380         PRINT "YOU ARE WEARING THE ":
6390         CALL PRINT.NOUN
6400         PRINT "."
6410         ENDIF
6420 NEXT I
6430 ENDF INVENTORY.SUB
6440 DEFINE SEARCH.SUB
6450 REM ****************
6460 REM *  SEARCH     **
6470 REM ****************
6480 IF PRS(5)<1 THEN CALL LOOK.SUB
6485 IF PRS(5)<1 THEN GOTO 6720
6490 REM +++ SEARCH ASHES +++
6500 IF PRS(5)=11 AND P<>4 THEN
6510         PRINT "ASHES? WHAT ASHES?"
6520         GOTO 6720
6530         ENDIF
6540 IF DFLAG(1)=1 AND P=4 AND PRS(5)=11 THEN
6550         Z=49
6560         CALL PRINTER.SUB
6570         GOTO 6720
6580         ENDIF
6590 IF DFLAG(1)=0 AND P=4 AND PRS(5)=11 AND NOUN(11,1)=4 THEN
6600         Z=9
6610         DFLAG(1)=1
6620         CALL PRINTER.SUB
6630         GOTO 6720
6640         ENDIF
6650 REM ++++++++++++++++++++
6660 REM ^^ THAT WAS THE BOOK STUFF IN LOC 4 ^^^
6670 E=P,E1=-1
6680 CALL CHECK.EM
6690 IF NOUN(PRS(5),1)=P OR NOUN(PRS(5),1)=-1 THEN CALL SEARCH.OBJ
6700 IF NOUN(PRS(5),1)=P OR NOUN(PRS(5),1)=-1 THEN GOTO 6720
6710 PRINT "YOU FIND NOTHING SPECIAL."
6720 ENDF SEARCH.SUB
6730 DEFINE SEARCH.DOOR1
6740 REM ***********************
6750 REM * SEARCH FOR SECRET DOOR ON NORTH WALL
6760 REM ***********************
6770 IF PRS(2)<>6 OR PRS(6)<>1 THEN
6780         Z=50
6790         CALL PRINTER.SUB
6800         GOTO 7140
6810         ENDIF
6820 IF PRS(3)<>34 AND PRS(3)<>40 AND PRS(3)<>41 AND PRS(3)<>42 AND PRS(4)<>34 AND PRS(4)<>40 AND PRS(4)<>41 AND PRS(4)<>42 THEN S=0 ELSE S=1
6830 IF PRS(7)<27 OR PRS(7)>33 THEN
6840 IF PRS(8)<27 OR PRS(8)>33 THEN
6850         PRINT "YOU MUST SPECIFY WHICH WALL YOU ARE SEARCHING."
6860         GOTO 7140
6870         ENDIF
6880 IF PRS(3)>26 AND PRS(3)<34 AND PRS(4)>26 AND PRS(4)<34 THEN
6890         PRINT "MAKE UP YOUR MIND? ":ADJECT$(PRS(7)):" OR ":ADJECT$(PRS(8)):"?"
6900         GOTO 7140
6910         ENDIF
6920 ENDIF
6930 IF PRS(7)>26 THEN I=PRS(7)-26
6940 IF PRS(8)>26 THEN I=PRS(8)-26
6950 IF I=1 THEN FACE=1
6960 IF I=2 THEN FACE=4
6970 IF I=3 THEN FACE=7
6980 IF I=4 THEN FACE=8
6990 IF I=5 THEN FACE=7
7000 IF I=6 THEN FACE=8
7010 IF I=7 THEN FACE=1
7020 FOR I=1 TO DOORNUM
7030         IF DOORS(I,1)=P AND DOORS(I,2)=FACE OR DOORS(I,3)=P AND DOORS(I,4)=FACE THEN
7040         IF DOORS(I,5)=2 AND S=0 THEN GOTO 7100
7050         IF DOORS(I,5)=2 THEN Z=51
7060         IF DOORS(I,5)<>2 THEN Z=52
7070         CALL PRINTER.SUB
7080         GOTO 7140
7090         ENDIF
7100 NEXT I
7110 Z=54
7120 IF S=1 THEN Z=53
7130 CALL PRINTER.SUB
7140 ENDF SEARCH.DOOR1
7150 DEFINE SEARCH.DOOR2
7160 REM ***********************
7170 REM * SEARCH FOR SECRET DOOR ON NORTH WALL
7180 REM ***********************
7190 IF PRS(6)<>6 THEN
7200         Z=50
7210         CALL PRINTER.SUB
7220         GOTO 7560
7230         ENDIF
7240 IF PRS(7)<>34 AND PRS(7)<>40 AND PRS(7)<>41 AND PRS(7)<>42 AND PRS(8)<>34 AND PRS(8)<>40 AND PRS(8)<>41 AND PRS(8)<>42 THEN S=0 ELSE S=1
7250 IF PRS(3)<27 OR PRS(3)>33 THEN
7260 IF PRS(4)<27 OR PRS(4)>33 THEN
7270         PRINT "YOU MUST SPECIFY WHICH WALL YOU ARE SEARCHING."
7280         GOTO 7560
7290         ENDIF
7300 IF PRS(7)>26 AND PRS(7)<34 AND PRS(8)>26 AND PRS(8)<34 THEN
7310         PRINT "MAKE UP YOUR MIND? ":ADJECT$(PRS(3)):" OR ":ADJECT$(PRS(8)):"?"
7320         GOTO 7560
7330         ENDIF
7340 ENDIF
7350 IF PRS(3)>26 THEN I=PRS(3)-26
7360 IF PRS(4)>26 THEN I=PRS(4)-26
7370 IF I=1 THEN FACE=1
7380 IF I=2 THEN FACE=4
7390 IF I=3 THEN FACE=7
7400 IF I=4 THEN FACE=8
7410 IF I=5 THEN FACE=7
7420 IF I=6 THEN FACE=8
7430 IF I=7 THEN FACE=1
7440 FOR I=1 TO DOORNUM
7450         IF DOORS(I,1)=P AND DOORS(I,2)=FACE OR DOORS(I,3)=P AND DOORS(I,4)=FACE THEN
7460         IF DOORS(I,5)=2 AND S=0 THEN GOTO 7520
7470         IF DOORS(I,5)=2 THEN Z=51
7480         IF DOORS(I,5)<>2 THEN Z=52
7490         CALL PRINTER.SUB
7500         GOTO 7560
7510         ENDIF
7520 NEXT I
7530 Z=54
7540 IF S=1 THEN Z=53
7550 CALL PRINTER.SUB
7560 ENDF SEARCH.DOOR2
7570 DEFINE CHECK.ADVERB
7580 REM **********************
7590 REM * SEARCH FOR ADVERBS *
7600 REM **********************
7610 FOR I=1 TO ADVERB
7620         IF WORD$(WNUM)=ADVERB$(I) THEN
7630                 PRS(10)=I
7640                 WNUM=WNUM+1
7650                 GOTO 7680
7660                 ENDIF
7670                 NEXT I
7680 ENDF CHECK.ADVERB
7690 DEFINE LOOK.SUB
7700 REM *********************
7710 REM *  L O O K   ********
7720 REM *********************
7722 N=P
7724 CALL LIGHT.MODE
7726 IF L=1 THEN
7728         PRINT "IT'S TOO DARK TO SEE."
7730         GOTO 7800
7732         ENDIF
7740 !
7750 Z=LOCS(P,7)
7760 CALL PRINTER.SUB
7770 CALL OBJECT.PRINT
7780 !
7790 !
7800 ENDF LOOK.SUB
7810 DEFINE MONSTER.SUB
7820         FOR M=1 TO NNUM
7830         IF NOUN(M,3)=0 THEN GOTO 8100
7840 IF NOUN(M,19)>0 AND NOUN(M,19)<6 AND NOUN(M,1)=P THEN
7850         PRINT "THE ":
7860         N=M
7870         CALL PRINT.NOUN
7880        IF NOUN(M,19)=1 THEN PRINT " IS STUNNED."
7890        IF NOUN(M,19)=2 THEN PRINT " IS UNCONSCIOUS."
7900        ENDIF
7910         IF NOUN(M,18)>0 THEN 
7920        NOUN(M,18)=NOUN(M,18)-1
7930         IF NOUN(M,18)=0 AND NOUN(M,19)<>0 AND NOUN(M,1)=P THEN
7940         ! TELL PLAYER IT'S BETTER!
7950         PRINT "THE ":
7960         N=M
7970         CALL PRINT.NOUN
7980         PRINT " IS COMING AROUND."
7990         ENDIF
8000         GOTO 8100
8010         ENDIF
8020         NOUN(M,19)=0
8030         IF NOUN(M,1)<1 THEN GOTO 8100
8040         IF NOUN(M,3)=0 THEN GOTO 8100
8050         IF NOUN(M,14)=6  OR NOUN(M,14)=7  THEN CALL UNDEAD.MOV
8060         IF NOUN(M,14)=9 THEN CALL HOBBIT.SUB
8070 !
8080 !
8090 !
8100 NEXT M
8110 ENDF MONSTER.SUB
8120 DEFINE UNDEAD.MOV
8130 ! DON'T PRINT OUR DESCRIPTION ALL THE TIME..IT'S BORIN!
8140 IF NOUN(M,20)<>0 THEN GOTO 8180
8150  IF NOUN(M,1)=P THEN
8160 Z=NOUN(M,17)
8170 CALL PRINTER.SUB
8180 PRINT "THE ":
8190 N=M
8200 CALL PRINT.NOUN
8210 PRINT " ATTACKS YOU!"
8220 CHARACTER(69)=M
8230 IF CHARACTER(67)<>0 THEN MODE=1  !SO HE WILL ATTACK TO KILL
8240 IF MODE=1 THEN CALL MKILL.SUB
8250 IF MODE=2 THEN CALL MGRAPPL.SUB
8260 IF MODE=3 THEN CALL MPUMMEL.SUB
8270 IF MODE<1 OR MODE>3 THEN CALL MPUMMEL.SUB
8280         ! ATTACK
8290         ! ATTACK
8300 GOTO 8640
8310 ENDIF
8320   ! MOVE THE MONSTER
8330   ! FIRST DECIDE DIRECTION
8340     A=INT(RND(1)*9)+1
8350 FOR I=1 TO 10
8360         IF MOV(NOUN(M,1),I)=P THEN
8370         A=I
8380        GOTO 8410
8390         ENDIF
8400         NEXT I
8410 ! CHEC IF MONSTER IS BY NEARBYE DOOR
8420  FOR I=1 TO DOORNUM
8430         IF DOORS(I,1)=NOUN(M,1) AND DOORS(I,3)=P AND DOORS(I,6)=0 OR DOORS(I,3)=NOUN(M,1) AND DOORS(I,1)=P AND DOORS(I,6)=0 THEN
8440 IF DOORS(I,1)=NOUN(M,1) AND DOORS(I,3)=P AND DOORS(I,6)=0 THEN Z=DOORS(I,2)
8450 IF DOORS(I,3)=NOUN(M,1) AND DOORS(I,1)=P AND DOORS(I,6)=0 THEN Z=DOORS(I,4)
8460 IF Z=1 THEN J=4
8470 IF Z=4 THEN J=1
8480 IF Z=8 THEN J=7
8490 IF Z=7 THEN J=8
8500          PRINT "YOU HEAR A LOUD BANGING ON THE DOOR ON THE ":DIR$(J):" WALL!"
8510                 GOTO 8640
8520                 ENDIF
8530                 NEXT I
8540 A1=MOV(NOUN(M,1),A)
8550 IF A1<1 THEN GOTO 8640
8560 NOUN(M,1)=A1
8570 IF P=A1 THEN
8580 PRINT "A ":
8590 N=M
8600 CALL PRINT.NOUN
8610    IF P=PP THEN PRINT " IS APPROACHING!"
8620     IF P<>PP THEN PRINT " FOLLOWS YOU!"
8630     ENDIF
8640 ENDF UNDEAD.MOV
8650 DEFINE DFLAG.PRINT
8660 REM ******************
8670 REM * SPECIAL THINGS ARE PRINTED HERE
8680 REM * AFTER PERSON MOVES AND BEFORE MONSTER 
8690 REM * MOVES, DICTATED BY DFLAG.
8700 REM *******************
8710 IF P=22 AND DFLAG(2)=1 THEN
8712         PRINT "THE STATUE ON THE SOUTHERN WALL HAS BEEN"
8714         PRINT "MOVED ASIDE, EXPOSING A SOUTHERN EXIT."
8716         ENDIF
8730 !
8740 !
8750 !
8760 !
8770 !
8780 !
8790 !
8800 !
8810 !
8820 !
8830 !
8840 !
8850 !
8860 ENDF DFLAG.PRINT
8870 DEFINE ADDPLAYER
8880 REM *******************
8890 REM * ADD PLAYER NUM **
8900 REM * BER CNUM      ***
8910 REM *******************
8920 PRINT
8930 PRINT "ENTER THE NAME OF THE CHARACTER YOU WISH TO ENTER:":
8940 INPUT IN FORM "R":F$
8950 IF LENGTH(F$)>5 THEN F$=LEFT(F$,6)
8960 CLOSE 1
8970 OPEN F$+".GME-A" FOR SYMBOLIC IO AS FILE 1
8980 FOR I=1 TO 6
8990 INPUT FROM 1 IN FORM "R":CHARAC$(I)
9000 NEXT I
9010 INPUT FROM 1 IN FORM "DDDDD":CHARACTER(I) FOR I=1 TO 82
9020 CLOSE (1)
9030 ENDF ADDPLAYER
9040 DEFINE OPEN.SUB
9050 REM *****************
9060 REM * OPEN SUB CONTROL*
9070 REM *****************
9080 IF PRS(5)<1 THEN
9090         Z=55
9100         CALL PRINTER.SUB
9110         GOTO 9390
9120         ENDIF
9130 IF PRS(5)=8 THEN CALL DOOR.SUB
9140 IF PRS(5)=8 THEN GOTO 9390
9150 E=P
9160 E1=-1
9170 CALL CHECK.EM
9180 ! CHECK IF IT IS INSIDE SOMETHING THAT IS CLOSED, ETC..
9190 IF NOUN(PRS(5),1)=P AND NOUN(PRS(5),9)<>0 OR NOUN(PRS(5),1)=-1 AND NOUN(PRS(5),9)<>0 THEN
9200         N=PRS(5)
9210         CALL CHECK.BP
9220         IF ER=1 THEN GOTO 9390
9230         ENDIF
9240 IF NOUN(PRS(5),1)<>P AND NOUN(PRS(5),1)<>-1 THEN
9250         PRINT "I DON'T SEE IT HERE."
9260         GOTO 9390
9270         ENDIF
9280 IF NOUN(PRS(5),3)<>0 THEN GOTO 9390
9290 IF NOUN(PRS(5),5)=0 THEN
9300         PRINT "THAT CANNOT BE OPENED."
9310         GOTO 9390
9320         ENDIF
9330 IF NOUN(PRS(5),5)=1 THEN
9340         PRINT "IT IS ALREADY OPEN."
9350         GOTO 9390
9360         ENDIF
9370 PRINT "OK. IT IS NOW OPEN."
9380 NOUN(PRS(5),5)=1
9390 ENDF OPEN.SUB
9400 REM *********************=*=*=*=*=*=*=*****************
9410 DEFINE CLOSE.SUB
9420 IF PRS(5)<1 THEN
9430         Z=55
9440         CALL PRINTER.SUB
9450         GOTO 9730
9460         ENDIF
9470 IF NOUN(PRS(5),3)<>0 THEN GOTO 9730
9480 IF PRS(5)=8 THEN CALL DCLOSE
9490 IF PRS(5)=8 THEN GOTO 9730
9500 E=P
9510 E1=-1
9520 CALL CHECK.EM
9530 ! CHECK IF IN PACK OR IN CLOSED OBJECT..ETC..
9540 IF NOUN(PRS(5),1)=P AND NOUN(PRS(5),9)<>0 OR NOUN(PRS(5),1)=-1 AND NOUN(PRS(5),9)<>0 THEN
9550         N=PRS(5)
9560         CALL CHECK.BP
9570         IF ER=1 THEN GOTO 9730
9580         ENDIF
9590 IF NOUN(PRS(5),1)<>P AND NOUN(PRS(5),1)<>-1 THEN
9600         PRINT "I DON'T SEE IT HERE."
9610         GOTO 9730
9620         ENDIF
9630 IF NOUN(PRS(5),5)=2 THEN
9640         PRINT "IT IS CLOSED."
9650         GOTO 9730
9660         ENDIF
9670 IF NOUN(PRS(5),5)<>1 THEN
9680         PRINT "THAT IS NOT OPEN."
9690         GOTO 9730
9700         ENDIF
9710 PRINT "OK, IT IS CLOSED."
9720 NOUN(PRS(5),5)=2
9730 ENDF CLOSE.SUB
9740 DEFINE PUT.SUB
9750 REM ************************
9760 REM * PUT THINGS INSIDE  ***
9770 REM * OF OTHER THINGS OR ***
9780 REM * WHATEVER ESE       ***
9790 REM *************************
9800 REM +*+*+?+ IN CASE HE SAYS PUT THE BOX DOWN
9810 IF WORD$(3)="DOWN" OR WORD$(4)="DOWN" OR WORD$(5)="DOWN" THEN
9820     CALL DROP.SUB
9830     GOTO 10920
9840     ENDIF
9850 IF PRS(5)<1 THEN
9860         Z=55
9870         CALL PRINTER.SUB
9880         GOTO 10920
9890         ENDIF
9900 IF PRS(6)=1 THEN GOTO 10830
9910 REM ***********
9920 REM ++ PUT <BLANK> IN THE <BLANK>
9930 REM ***********
9940 E=-1
9950 CALL CHECK.EM
9960 ! CHECK IF IN PACK ETCETERA
9970 IF NOUN(PRS(5),1)=-1 AND NOUN(PRS(5),9)<>0 THEN
9980         N=PRS(5)
9990         CALL CHECK.BP
10000         IF ER=1 THEN GOTO 10920
10010         ENDIF
10020 IF PRS(6)<>1 AND PRS(6)<>2 AND PRS(6)<>3 THEN
10030         Z=50
10040         CALL PRINTER.SUB
10050         GOTO 10920
10060         ENDIF
10070 IF NOUN(PRS(5),1)<>-1 THEN
10080         PRINT "YOU DON'T HAVE THE ":
10090         N=PRS(5)
10100         CALL PRINT.NOUN
10110         PRINT "."
10120         GOTO 10920
10130         ENDIF
10140 IF PRS(9)<1 THEN
10150         PRINT "YOU NEED TO SPECIFY AND INDIRECT NOUN."
10160         GOTO 10920
10170         ENDIF
10180 IF PRS(9)=18 THEN ! FOR PUT SOMETHING IN RIGHT HAND
10190         CALL PUT.HAND
10200         GOTO 10920
10210         ENDIF
10220 E=P
10230 CALL CHECK.EMI
10240 E=-1
10250 CALL CHECK.EMI
10260 ! CHECK IF THE INDIRECT NOUN IS IN CONTAINER,ETC..
10270 IF NOUN(PRS(9),1)=-1 AND NOUN(PRS(9),9)<>0 OR NOUN(PRS(9),1)=P AND NOUN(PRS(9),9)<>0 THEN
10280         N=PRS(9)
10290         CALL CHECK.BP
10300        IF ER=1 THEN GOTO 10920
10310        ENDIF
10320 IF PRS(5)=PRS(9) THEN
10330         PRINT "THAT WILL NOT WORK."
10340         GOTO 10920
10350         ENDIF
10360 IF NOUN(PRS(9),1)<>P AND NOUN(PRS(9),1)<>-1 THEN
10370         PRINT "I DON'T SEE THE ":
10380         N=PRS(9)
10390         CALL PRINT.NOUN
10400         PRINT "."
10410         GOTO 10920
10420         ENDIF
10430 IF NOUN(PRS(9),5)=0 THEN
10440         PRINT "THAT IS NOT A CONTAINER!"
10450         GOTO 10920
10460         ENDIF
10470 IF NOUN(PRS(9),5)<>1 THEN
10480         PRINT "THE ":
10490         N=PRS(9)
10500         CALL PRINT.NOUN
10510         PRINT " IS NOT OPEN."
10520         GOTO 10920
10530         ENDIF
10540 REM ++ CHECK HOW MUCH WEIGHT CONTAINER ALREADY HAS
10550 A=0
10560 FOR I=1 TO NNUM
10570         IF NOUN(I,9)=PRS(9) THEN A=A+NOUN(I,13)
10580 NEXT I
10590 IF (A+NOUN(PRS(5),13))>NOUN(PRS(9),8) THEN
10600         PRINT "THE ":
10610         N=PRS(9)
10620         CALL PRINT.NOUN
10630         PRINT " CAN HOLD NOTHING MORE."
10640         GOTO 10920
10650         ENDIF
10660 REM ** CHECK IF TOO BULKY **
10670 IF NOUN(PRS(5),15)>NOUN(PRS(9),15) THEN
10680         PRINT "THE ":NOUN$(PRS(5)):" WON'T FIT. IT'S TOO BIG."
10690         GOTO 10920
10700         ENDIF
10710 PRINT "OK, THE ":NOUN$(PRS(5)):" IS NOW INSIDE THE ":NOUN$(PRS(9)):"."
10720 NOUN(PRS(5),9)=PRS(9)
10730 REM ++++ CLEAR HAND REGISTERS +++++
10740 IF CHARACTER(64)=PRS(5) THEN CHARACTER(64)=0
10750 IF CHARACTER(65)=PRS(5) THEN CHARACTER(65)=0
10760 REM ***** MAKE OBJECTS LOC SAME AS IT'S CONTAINER!
10770 NOUN(PRS(5),1)=NOUN(PRS(9),1)
10780 IF NOUN(PRS(5),5)<>0 THEN
10790         E=NOUN(PRS(9),1),N=PRS(5)
10800         CALL PUT.COMPLETE
10810         ENDIF
10820 REM *********+=+=+=***** THAT WAS PUT INSIDE
10830 IF PRS(6)<>1 THEN GOTO 10920
10840 REM ***** OK, NOW FOR PUT ON ******
10850 CALL DROP.SUB
10860 !
10870 !
10880 !
10890 !
10900 !
10910 !
10920 ENDF PUT.SUB
10930 DEFINE PUT.COMPLETE
10940 REM *************************
10950 REM * WE MUST CENTRALIZE  ***
10960 REM * THE ROUTINES THAT   ***
10970 REM * CHECK THINGS THAT   ***
10980 REM * ARE DROPPPED OR PICKED*
10990 REM * UP OR PUT,ETC IF THEY *
11000 REM * ARE CONTAINERS,IF SO THEN WE PUT EVERYIHTHING UP,DOWN
11010 REM * OR WHATEVER OK? DAMN, THIS IS LONG.
11020 REM *************************
11030 REM ++ E=LOC WE ARE PUTTING TO
11040 REM ++ N="PRS(5)"  WE KNOW IT IS A CONTAINER
11050 FOR I=1 TO NNUM
11060         IF NOUN(I,9)=N THEN NOUN(I,1)=E
11070         REM ** CHECK IF I IS ALSO CONTAINER
11080         IF NOUN(I,5)<>0 THEN
11090                 FOR J=1 TO NNUM
11100                 IF NOUN(J,9)=I THEN NOUN(J,1)=NOUN(I,1)
11110                 NEXT J
11120                  ENDIF
11130         NEXT I
11140 ENDF PUT.COMPLETE
11150 DEFINE PRINT.NOUN
11160 REM ***************
11170 REM * PRINT NOUN IN FORM *
11180 REM * NOUN$ ADJECT$ ADJECT$
11190 REM ***************
11200 REM +++ N=NOUN IN QUESTION
11210 IF NOUN(N,6)<1 THEN GOTO 11230
11220         PRINT ADJECT$(NOUN(N,6)):" ":
11230 IF NOUN(N,7)<1 THEN GOTO 11250
11240         PRINT ADJECT$(NOUN(N,7)):" ":
11250 PRINT NOUN$(N):
11260 ENDF PRINT.NOUN
11270 DEFINE WEAR.SUB
11280 REM ********************
11290 REM * PUT ON CLOTHES,SWORDBELTS,POUCHES,BACKPACKS
11300 REM * ETCETERA
11310 REM ********************
11320 IF PRS(1)=65 THEN
11330 IF PRS(2)<>1 AND PRS(6)<>1 THEN
11340         PRINT "THAT DOESN'T MAKE SENSE TO ME."
11350         GOTO 11910
11360         ENDIF
11370         ENDIF
11380 IF PRS(5)<1 THEN
11390         Z=55
11400         CALL PRINTER.SUB
11410         GOTO 11910
11420         ENDIF
11430 E=-1
11440 CALL CHECK.EM
11450 IF NOUN(PRS(5),1)<>-1 AND CHARACTER(64)<>PRS(5) AND CHARACTER(65)<>PRS(5) THEN
11460         PRINT "YOU DON'T HAVE THE ":
11470         N=PRS(5)
11480         CALL PRINT.NOUN
11490         PRINT " IN HAND."
11500         GOTO 11910
11510         ENDIF
11520 IF NOUN(PRS(5),14)<>1 AND NOUN(PRS(5),14)<>2 AND NOUN(PRS(5),14)<>3 AND NOUN(PRS(5),14)<>9 THEN
11530         PRINT "YOU CAN'T WEAR A ":
11540         N=PRS(5)
11550         CALL PRINT.NOUN
11560         PRINT "!"
11570         GOTO 11910
11580         ENDIF
11590 REM ** DO ARMOR, IT MUST BE RIGHT SIZE. **
11600 E=0
11610 IF NOUN(PRS(5),14)=1 THEN
11615  ROND=ROND+10
11620 IF CHARAC$(4)="ELF" AND NOUN(PRS(5),11)<>2 THEN E=1
11630 IF CHARAC$(4)="HUMAN" AND NOUN(PRS(5),11)<>1 THEN E=1
11640 IF CHARAC$(4)="HOBBIT" AND NOUN(PRS(5),11)<>4 AND NOUN(PRS(5),11)<>3 THEN E=1
11650 IF CHARAC$(4)="DWARF"  AND NOUN(PRS(5),11)<>4 AND NOUN(PRS(5),11)<>3 THEN E=1
11660 IF CHARAC$(4)="ORC" AND NOUN(PRS(5),11)<>5 THEN E=1
11670 IF E=1 THEN
11680         PRINT "THE ARMOR DOESN'T FIT YOU."
11690         GOTO 11910
11700         ENDIF
11710 IF E<>1 THEN
11720         Z=63
11730         CALL PRINTER.SUB
11740         ENDIF
11750    ENDIF
11760 REM ** CHECK FOR DUPLICATES **
11770 FOR I=1 TO NNUM
11780         IF NOUN(I,14)=NOUN(PRS(5),14) AND NOUN(I,9)=3 THEN
11790         PRINT "YOU ARE ALREADY WEARING A ":NOUN$(PRS(5)):"."
11800         GOTO 11910
11810         ENDIF
11820         NEXT I
11830 REM ++ CLEAR HAND REGISTERS ++
11840 IF CHARACTER(64)=PRS(5) THEN CHARACTER(64)=0
11850 IF CHARACTER(65)=PRS(5) THEN CHARACTER(65)=0
11860 NOUN(PRS(5),9)=3
11870 IF NOUN(PRS(5),14)=2 THEN Z=64
11880 IF NOUN(PRS(5),14)=3 THEN Z=65
11890 IF NOUN(PRS(5),14)=9 THEN Z=66
11900 CALL PRINTER.SUB
11910 ENDF WEAR.SUB
11920 DEFINE CHECK.BP
11930 REM *****************
11940 REM * CHECK TO SEE IF ITEM IS IN A CONTAINER WHICH IS
11950 REM * IN THE WORN PACK OR CLOSED.
11960 REM *****************
11970 REM ** IF MF=1 THEN WE ARE NOT TO PRINT ERR MESSAGES.
11980 ER=0
11990 REM ** ER IS ERRCD FOR ROUTINE, N=PRS(5)
12000 REM ** CHECK FOR BACKPACK FIRST.
12010 IF NOUN(NOUN(N,9),14)=9 AND NOUN(NOUN(N,9),9)=3 THEN
12020         Z=67
12025         IF MF=1 THEN GOTO 12040
12030         CALL PRINTER.SUB
12040         ER=1
12050         GOTO 12240
12060         ENDIF
12070 ! CHECK IF ITEM IS CLOSED
12080 IF NOUN(NOUN(N,9),5)=2 THEN
12085         IF MF=1 THEN GOTO 12100
12090         PRINT "THE ":NOUN$(NOUN(N,9)):" IS CLOSED."
12100         ER=1
12110         GOTO 12240
12120         ENDIF
12130 ! CHECK IF IT IS INSIDE SOMETHING WHICH IS INSIDE SOMETHING CLOSED.
12140 IF NOUN(NOUN(N,9),9)=0 THEN GOTO 12240
12150         IF NOUN(NOUN(NOUN(N,9),9),5)=2 THEN
12160         IF MF=1 THEN GOTO 12210
12170         PRINT "THE ":
12180         N=NOUN(NOUN(N,9),9)
12190         CALL PRINT.NOUN
12200          PRINT " IS CLOSED,SO YOU CANNOT REACH IT."
12210         ER=1
12220         GOTO 12240
12230         ENDIF
12240 MF=0
12250 ENDF CHECK.BP
12260 DEFINE MOVE.SUB
12270 REM ***********************
12280 REM * MOVE THINGS  ********
12290 REM ***********************
12300 IF PRS(5)<1 THEN
12310         Z=55
12320         CALL PRINTER.SUB
12330         GOTO 12710
12340         ENDIF
12350 E=P
12360 CALL CHECK.EM
12370 IF NOUN(PRS(5),1)=-2 THEN GOTO 12420
12380 IF NOUN(PRS(5),1)=P AND NOUN(PRS(5),1)<>-1 THEN
12390         PRINT "I DON'T SEE ONE HERE."
12400         GOTO 12710
12410         ENDIF
12420 IF NOUN(PRS(5),1)=-2 THEN   ! <* MAIN *>
12430 ! SPECIAL STUFF !
12440 ! IF WE ARE IN LOC 22 AND PRS(5)=17 (STATUE) THEN WE'LL GO.
12442 IF PRS(5)=17 AND P<>22 THEN
12444         PRINT "I DON'T SEE IT HERE."
12446         GOTO 12710
12448         ENDIF
12459 IF PRS(5)=17 AND P=22 AND DFLAG(2)=0 THEN
12460         IF CHARACTER(1)<8 THEN
12470         Z=58
12480         CALL PRINTER.SUB
12490         GOTO 12710
12500         ENDIF
12510 ! FIX MOVE DATA FOR EXPOSED EXIT
12520 MOV(22,4)=10,MOV(10,1)=22
12530 Z=59
12535 DFLAG(2)=1
12540 CALL PRINTER.SUB
12550 GOTO 12710
12560 ENDIF
12570 REM ** IF IT IS OPEN THEN CLOSE IT **
12580 IF PRS(5)=17 AND P=22 AND DFLAG(2)=1 THEN
12590         IF CHARACTER(1)<8 THEN
12600        Z=58
12610        CALL PRINTER.SUB
12620        GOTO 12710
12630        ENDIF
12640 ! FIX MOVE DATA TOO. FOR EXPOSED PASSAGE.
12650 MOV(22,4)=0,MOV(10,1)=0
12660 Z=84
12665 DFLAG(2)=0
12670 CALL PRINTER.SUB
12680 GOTO 12710
12690 ENDIF
12700     ENDIF    ! <* MAIN *>
12710 ENDF MOVE.SUB
12720 DEFINE CPARSER.SUB
12730 REM **********************************************************
12740 REM **
12750 REM **      SPLIT UP INPUT INTO WORDS YOU TURDS!
12760 REM **
12770 REM **********************************************************
12780 AA=1
12790 MAT WORD$=" "
12800 CIB
12810 PRINT "*":
12820 INPUT IN FORM "X":I$
12830 REM ****** THAT IS END OF TEST FUNCTIONS!!!! *********
12840 IF ASC(I$)=13 AND LENGTH(A1$)<1 AND AA=1 THEN GOTO 12800
12850 IF I$="," THEN COMMLOC=AA
12860 IF I$="," THEN I$=" "
12870 IF ASC(I$)<>13 AND I$<>" " AND I$<>"," THEN A1$=A1$+I$
12880 IF ASC(I$)=13 THEN GOTO 12910
12890 IF I$=" " THEN GOTO 12910
12900 GOTO 12820
12910 WORD$(AA)=A1$
12920 A1$=""
12930 IF WORD$(AA)="THE" OR WORD$(AA)="GO" OR WORD$(AA)="A" THEN AA=AA-1
12940 AA=AA+1
12950 IF ASC(I$)=13 THEN GOTO 12970
12960 GO TO 12820
12970 REM DONE
12980 MAT PRSBAK=PRS
12990 MAT PRS=0
13000 WNUM=1
13010 IF COMMFLAG=1 THEN
13020         COMMFLAG=0,WNUM=COMMLOC+1,COMMLOC=0
13030         GOTO 13260
13040         ENDIF
13050 IF WORD$(1)="PICK" AND WORD$(2)="UP" THEN
13060      WNUM=3
13070      PRS(1)=27
13080      GOTO 13340
13090 ENDIF
13100 IF WORD$(1)="PUT" AND WORD$(2)="DOWN" THEN
13110         WNUM=3
13120         PRS(1)=28
13130         GOTO 13340
13140         ENDIF
13150 CALL CHECK.ADVERB
13160 IF PRS(10)>0 AND WORD$(2)="PICK" AND WORD$(3)="UP" THEN 
13170     WNUM=4
13180     PRS(1)=27
13190     GOTO 13340
13200     ENDIF
13210 IF PRS(10)>0 AND WORD$(2)="PUT" AND WORD$(3)="DOWN" THEN
13220         WNUM=4
13230         PRS(1)=28
13240         GOTO 13340
13250         ENDIF
13260 REM *********** SEARCH WORD LISTS ************
13270 FOR I=1 TO VNUM
13280 IF WORD$(WNUM)=VERB$(I) THEN
13290         PRS(1)=I
13300         WNUM=WNUM+1
13310         GOTO 13340
13320         ENDIF
13330         NEXT I
13340 FOR I=1 TO PNUM
13350 IF WORD$(WNUM)=PREP$(I) THEN 
13360         PRS(2)=I
13370         WNUM=WNUM+1
13380         GOTO 13410
13390         ENDIF
13400         NEXT I
13410 FOR I=1 TO ANUM
13420 IF WORD$(WNUM)=ADJECT$(I) THEN
13430         PRS(3)=I
13440         WNUM=WNUM+1
13450         GOTO 13480
13460         ENDIF
13470         NEXT I
13480 FOR I=1 TO ANUM
13490 IF WORD$(WNUM)=ADJECT$(I) THEN
13500         PRS(4)=I
13510         WNUM=WNUM+1
13520         GOTO 13550
13530         ENDIF
13540         NEXT I
13550 FOR I=1 TO NNUM
13560 IF WORD$(WNUM)="IT" THEN PRS(5)=PRSBAK(5),PRS(3)=PRSBAK(3),PRS(4)=PRSBAK(4)
13570 ! IN CASE HE REFERS TO A NOUN IN LAST SENTENCE.
13580 IF WORD$(WNUM)="IT" THEN WNUM=WNUM+1
13590 IF WORD$(WNUM)=NOUN$(I) OR WORD$(WNUM)=NOUN2$(I) THEN
13600         PRS(5)=I
13610         WNUM=WNUM+1
13620         GOTO 13650
13630         ENDIF
13640         NEXT I
13650 FOR I=1 TO PNUM
13660 IF WORD$(WNUM)=PREP$(I) THEN
13670         WNUM=WNUM+1
13680         PRS(6)=I
13690         GOTO 13720
13700         ENDIF
13710         NEXT I
13720 FOR I=1 TO ANUM
13730         IF WORD$(WNUM)=ADJECT$(I) THEN
13740         PRS(7)=I
13750         WNUM=WNUM+1
13760         GOTO 13790
13770         ENDIF
13780         NEXT I
13790 FOR I=1 TO ANUM
13800 IF WORD$(WNUM)=ADJECT$(I) THEN
13810         PRS(8)=I
13820         WNUM=WNUM+1
13830         GOTO 13860
13840         ENDIF
13850         NEXT I
13860 FOR I=1 TO NNUM
13870 IF WORD$(WNUM)="IT" THEN PRS(9)=PRSBAK(5),PRS(7)=PRSBAK(3),PRS(8)=PRSBAK(4)
13880 ! IN CASE HE REFERS TO A NOUN IN LAST SENTENCE
13890 IF WORD$(WNUM)="IT" THEN WNUM=WNUM+1
13900 IF WORD$(WNUM)=NOUN$(I) OR WORD$(WNUM)=NOUN2$(I) THEN
13910         PRS(9)=I
13920         WNUM=WNUM+1
13930         GOTO 13960
13940         ENDIF
13950         NEXT I
13960 REM
13970 IF PRS(1)<1 THEN 14170
13980 REM ************************
13990 REM * EXECUTE VERBS *******
14000 REM ***********************
14010 REM *************** NON NOUN-MODIFIERS ********************
14020 REM REM *****************   NOUN MODIFIERS ******
14030 IF PRS(1)<1 THEN 14170
14040 IF VCODE(PRS(1))=30 THEN CALL PUT.SUB
14050 IF VCODE(PRS(1))=26 THEN CALL OPEN.SUB
14060 IF VCODE(PRS(1))=28 THEN CALL CLOSE.SUB
14070 IF VCODE(PRS(1))=15 THEN CALL GET.SUB
14080 IF VCODE(PRS(1))=35 THEN CALL CPUMMEL.SUB
14090 IF VCODE(PRS(1))=36 THEN CALL CGRAPPL.SUB
14100 IF VCODE(PRS(1))=11 THEN CALL KILL.SUB
14110 IF VCODE(PRS(1))=34 THEN CALL DISENGAGE.SUB
14120 IF VCODE(PRS(1))=16 THEN CALL DROP.SUB
14130 IF VCODE(PRS(1))=35 THEN MODE=2
14140 IF VCODE(PRS(1))=36 THEN MODE=3
14150 IF VCODE(PRS(1))=11 THEN MODE=1
14160 A=PRS(1)
14170 REM
14180 REM
14190 IF PRS(1)<1 THEN
14200         PRINT "YOU CAN'T DO THAT!"
14210         ENDIF
14220 REM ****
14270 ENDF CPARSER.SUB
14280 REM *****************************************
14290 DEFINE KILL.SUB
14300 REM ********************
14310 REM * GET READY TO ATTACK *
14320 REM ********************
14330 REM ** DETERMINE GOOD HAND AND BAD HAND **
14340 IF PRS(9)>0 AND CHARACTER(HAND)<>PRS(9) AND CHARACTER(BHAND)<>PRS(9) THEN 
14350         PRINT "YOU DON'T HAVE THAT IN HAND TO FIGHT WITH."
14360         GOTO 15070
14370         ENDIF
14380 IF CHARACTER(HAND)<1 AND CHARACTER(BHAND)<1 THEN
14390         PRINT "YOUR HANDS ARE EMPTY."
14400         PRINT "YOU MUST HAVE A WEAPON TO FIGHT WITH."
14410         GOTO 15070
14420         ENDIF
14430 IF CHARACTER(HAND)<1 THEN WHAND=BHAND ELSE WHAND=HAND
14440 REM **** OK. BY NOW HE HAS SOMNETHING TO FIGHT WITH WETHER
14450 REM **** IT IS IN HIS GOOD HAND OR BAD HAND, NOW.. FIGHT!
14460 IF CHARACTER(70)<>0 THEN PRINT "ALTHOUGH YOU ARE IN A HELD POSITION, YOU ATTACK ANYWAY."
14470 REM **** ROLL !!!
14480 A=INT(RND(0)*99)+1
14490 TH=INT(RND(0)*19)+1
14500 PTH=TH
14510 IF CHARACTER(1)>8 THEN TH=TH+CHARACTER(1)-8
14520 IF CHARACTER(2)>8 THEN TH=TH+CHARACTER(2)-8
14530 IF CHARACTER(7)>6 THEN TH=TH+CHARACTER(7)-6
14540 IF NOUN(CHARACTER(69),19)=2 OR NOUN(CHARACTER(69),19)=3 THEN TH=TH+30
14550 IF NOUN(CHARACTER(69),19)=1 THEN TH=TH+4
14560 TH=TH+CHARACTER(58)/10
14570 REM *** CHECK FOR FUMBLE
14580 IF PTH<3 THEN
14590 PRINT "YOU SERIOUSLY FUMBLED THAT ATTACK!"
14600 IF PTH=1 THEN
14610         PRINT "YOU DROPPED THE ":
14620         N=CHARACTER(WHAND)
14630         CALL PRINT.NOUN
14640         PRINT "ON THE GROUND!"
14650         IF PTH=1 THEN NOUN(CHARACTER(WHAND),1)=P,CHARACTER(WHAND)=0
14660         CHARACTER(66)=1
14670           GOTO 15070
14680         ENDIF
14690         ENDIF
14700 IF TH <(12+(10-NOUN(CHARACTER(69),5))-(CHARACTER(58)/10)) THEN
14710         PRINT "YOUR ATTACK MISSES."
14720         GOTO 15070
14730         ENDIF
14740 IF TH>=(12+(10-NOUN(CHARACTER(69),5))-(CHARACTER(58)/10)) THEN
14750 REM *** WE'VE HIT, BUT IS TERE DAMAGE?
14760 IF TH-(12+(10-NOUN(CHARACTER(69),5)))<3 THEN
14770         PRINT "YOUR BLOW GLANCES OFF OF IT'S TARGET."
14780         GOTO 15070
14790         ENDIF
14800 IF TH=(12+(10-NOUN(CHARACTER(69),5))) THEN
14810         PRINT "THE MONSTER BARELY DODGES YOUR ATTACK!"
14820         GOTO 15070
14830         ENDIF
14840 REM ** WE DID DAMAGE, FIGURE IT **
14850 PRINT "YOUR ATTACK DAMAGED YOUR OPPONENT."
14860 DAM=INT(RND(0)*NOUN(CHARACTER(WHAND),4))+1
14870 IF NOUN(CHARACTER(69),19)=2 OR NOUN(CHARACTER(69),19)=3 THEN DAM=DAM+100
14880 IF NOUN(CHARACTER(69),19)=1 THEN DAM=DAM+4
14890 IF CHARACTER(1)>8 THEN DAM=DAM+CHARACTER(2)-8
14900 IF WHAND=BHAND THEN TH=TH-4
14910 IF CHARACTER(7)>8 THEN DAM=DAM+CHARACTER(7)-8
14920 DAM=DAM+CHARACTER(58)/10
14930 NOUN(CHARACTER(69),8)=NOUN(CHARACTER(69),8)-1
14940 IF NOUN(CHARACTER(69),8)<1 THEN
14950 REM ** IT DIED
14960         PRINT "THE ":
14970         N=CHARACTER(69)
14980         CALL PRINT.NOUN
14990         PRINT " SLUMPS TO THE GROUND, DEAD."
15000          CHARACTER(69)=0
15010         NOUN(N,7)=NOUN(N,6),NOUN(N,6)=17,NOUN(N,3)=0
15020         NOUN(N,19)=0,NOUN(N,20)=0,NOUN(N,21)=0,NOUN(N,18)=1
15030         NOUN(N,18)=1,NOUN(N,9)=0
15040         GOTO 15070
15050 ENDIF
15060 ENDIF
15070 ENDF KILL.SUB
15080 DEFINE CPUMMEL.SUB
15090 REM ****************
15100 REM * HAND AND PUNCHING 
15110 REM *  COMBAT
15120 REM ****************
15130 DAM=0
15140 REM *** CHECK STUFF ***
15150 REM *** IS HE HELD ? ***
15160 IF CHARACTER(70)<>0 THEN
15170         CALL CROLL.SUB
15180         IF A<CHARACTER(70) THEN
15190         PRINT "YOU FAIL TO BREAK THE HOLD YOUR OPPONENT HAS YOU IN."
15200         GOTO 15860
15210         ENDIF
15220         PRINT "YOU BREAK YOUR OPPONENTS HOLD AND ATTACK."
15230         CHARACTER(70)=0
15240         ENDIF
15250 IF CHARACTER(HAND)<>0 THEN
15260         PRINT "YOU DROP THE ":
15270         N=CHARACTER(HAND)
15280         CALL PRINT.NOUN
15290         PRINT " THAT WAS IN YOUR HAND."
15300         NOUN(CHARACTER(HAND),1)=P
15310         ENDIF
15320 REM *** OK ***
15330         CALL CROLL.SUB
15340         TH=A
15350 IF TH<1 THEN
15360         PRINT "YOUR BLOW MISSES."
15370         GOTO 15720
15380         ENDIF
15390         IF TH<21 THEN
15400         PRINT "INEFFECTIVE BLOW, YOU MAY STRIKE AGAIN."
15410         NOUN(CHARACTER(69),18)=1
15420         GOTO 15720
15430         ENDIF
15440 IF TH<41 THEN
15450         PRINT "YOU MANAGE A GLANCING BLOW, THROWING YOURSELF OFF BALANCE."
15460         DAM=2
15470         GOTO 15720
15480         ENDIF
15490 IF TH<61 THEN
15500         PRINT "YOU SCORE A GLANCING BLOW."
15510        NOUN(CHARACTER(69),18)=1
15520         DAM=4
15530         GOTO 15720
15540         ENDIF
15550 IF TH<81 THEN
15560         PRINT "YOU SCORE A SOLID PUNCH, BUT THROW YOURSELF OFF BALANCE."
15570         DAM=6
15580         GOTO 15720
15590         ENDIF
15600 IF TH<101 THEN
15610         PRINT "YOU LAND A VERY SOLID PUNCH."
15620         DAM=8
15630         NOUN(CHARACTER(69),18)=1
15640         GOTO 15720
15650      ENDIF
15660 IF TH>100 THEN
15670         PRINT "YOU STRIKE YOUR TARGET WITH A CRUSHING BLOW, STUNNING IT."
15680         DAM=10
15690         NOUN(CHARACTER(69),18)=3
15700         NOUN(CHARACTER(69),19)=1
15710         ENDIF
15720 REM ****  D A M A G E  ****
15730 IF DAM<1 THEN GOTO 15860
15740 IF CHARACTER(1)>6 THEN DAM=DAM+CHARACTER(1)-6
15750 IF CHARACTER(7)>6 THEN DAM=DAM+CHARACTER(7)-6
15760 NOUN(CHARACTER(69),8)=NOUN(CHARACTER(69),8)-DAM
15770 IF NOUN(CHARACTER(69),8)<1 THEN
15780        CHARACTER(70)=0,NOUN(CHARACTER(69),20)=0
15790         PRINT "THE ":
15800         N=CHARACTER(69)
15810         CALL PRINT.NOUN
15820         PRINT " FALLS TO THE GROUND, UNCONSCIOUS."
15830         NOUN(N,8)=1
15840         NOUN(N,19)=2,NOUN(N,18)=3+INT(RND(0)*4)+2
15850         ENDIF
15860 ENDF CPUMMEL.SUB
15870 DEFINE CGRAPPL.SUB
15880 REM **************
15890 REM * HEY! IT'S BIG TIME WRESTLING!
15900 REM **************
15910 DAM=0
15920 REM *** CHECK ***
15930 REM *** ARE WE HELD ? ***
15940 IF CHARACTER(70)<>0 THEN 
15950         CALL CROLL.SUB
15960         IF A<CHARACTER(70) THEN
15970         PRINT "YOUR OPPONENT HAS YOU IN A HOLD, AND YOU'VE FAILED TO BREAK IT."
15980         GOTO 16530
15990         ENDIF
16000         PRINT "YOU BREAK THE HOLD YOU OPPONENT HAS YOU IN, ALLOWING AN ATTACK."
16010         CHARACTER(70)=0
16020         ENDIF
16030 IF CHARACTER(HAND)<>0 THEN
16040         N=CHARACTER(HAND)
16050         PRINT "YOU DROP THE ":
16060         CALL PRINT.NOUN
16070         PRINT " THAT YOU HAD IN YOUR HAND."
16080         NOUN(CHARACTER(HAND),1)=P,CHARACTER(HAND)=0
16090         ENDIF
16100 REM *** O.K ***
16110 CALL CROLL.SUB
16120 TH=INT(A)
16130 IF TH<1 THEN
16140         PRINT "YOU GET YOUR OPPONENT IN A LIGHT WAIST CLINCH."
16150         ENDIF
16160 IF TH>20 AND TH<41 THEN
16170 PRINT "YOU GET YOUR OPPONENT IN AN ARMLOCK."
16180         DAM=1
16190         ENDIF
16200 IF TH>40 AND TH<56 THEN
16210         PRINT "YOU GET YOUR TARGET IN A FINGER LOCK."
16220         DAM=2
16230         ENDIF
16240 IF TH>55 AND TH<71 THEN
16250         PRINT "YOU HAVE WRANGLED YOUR ENEMY INTO A BEAR HUG."
16260         DAM=3
16270         ENDIF
16280 IF TH>70 AND TH<86 THEN
16290         PRINT "YOU HAVE YOUR OPPONENT IN A HEAD LOCK."
16300         DAM=5
16310         ENDIF
16320 IF TH>85 AND TH<96 THEN
16330         PRINT "YOU HAVE YOUR OPPONENT IN A STRANGLE-HOLD."
16340         DAM=6
16350         ENDIF
16360 IF TH>95 THEN
16370         PRINT "YOU KICK YOUR OPPONENT DOWN, STUNNING IT."
16380         DAM=8
16390         NOUN(CHARACTER(69),18)=2,NOUN(CHARACTER(69),19)=1
16400         ENDIF
16410 NOUN(CHARACTER(69),20)=TH
16420 N=CHARACTER(69)
16430 IF CHARACTER(1)>6 THEN DAM=DAM+CHARACTER(1)-6
16440 IF CHARACTER(7)>6 THEN DAM=DAM+CHARACTER(7)-6
16450 NOUN(N,8)=NOUN(N,8)-DAM
16460 IF NOUN(N,8)<1 THEN
16470        CHARACTER(70)=0,NOUN(CHARACTER(69),20)=0
16480         PRINT "THE ":
16490         CALL PRINT.NOUN
16500         PRINT " SLUMPS TO THE GROUND, UNCONSCIOUS."
16510         NOUN(N,8)=INT(NHITSII(N)/2),NOUN(N,19)=2,NOUN(N,18)=3+INT(RND(0)*4)
16520         ENDIF
16530 ENDF CGRAPPL.SUB
16540 DEFINE CROLL.SUB
16550 REM *****************
16560 REM * ROLL THE DICE
16570 REM ******************
16580 A=INT(RND(0)*99)+1
16590 A=INT(RND(0)*99)+1
16600 A=A+INT(CHARACTER(63)/2)
16610 A=A+CHARACTER(1)
16620 A=A+CHARACTER(2)
16630 A=A+CHARACTER(1)/2
16640 A=A+CHARACTER(7)/2
16650 IF NOUN(CHARACTER(69),19)=4 THEN A=A+10
16660 IF NOUN(CHARACTER(69),19)=1 THEN A=A+20
16670 IF NOUN(CHARACTER(69),20)<>0 THEN A=A+40
16680 IF NOUN(CHARACTER(69),19)<>1 AND NOUN(CHARACTER(69),19)<>4 THEN
16690         A=A-(NOUN(CHARACTER(69),23)-6)
16700         A=A-(NOUN(CHARACTER(69),22)-6)
16710         IF NOUN(CHARACTER(69),5)<6 THEN A=A-10
16720         IF NOUN(CHARACTER(69),5)<4 THEN A=A-20
16730         IF NOUN(CHARACTER(69),5)<2 THEN A=A-30
16740 ENDIF
16750 ENDF CROLL.SUB
16760 DEFINE PUT.HAND
16770 REM ************
16780 REM * PUT <  > IN RIGHT OR LEFT HAND..OK?
16790 REM *************
16800 A=0
16810 IF PRS(7)<>31 AND PRS(7)<>32 AND PRS(8)<>31 AND PRS(8)<>32 THEN
16820         PRINT "WHICH HAND?"
16830         GOTO 17070
16840         ENDIF
16850 IF PRS(7)>30 AND PRS(7)<33 AND PRS(8)>30 AND PRS(8)<33 THEN
16860         PRINT "WHICH HAND?"
16870         GOTO 17070
16880         ENDIF
16890 IF PRS(7)=PRS(8) THEN
16900         PRINT "WHICH HAND?"
16910         GOTO 17070
16920         ENDIF
16930 IF PRS(7)>30 AND PRS(7)<33 THEN A=PRS(7)
16940 IF PRS(8)>30 AND PRS(8)<33 THEN A=PRS(8)
16950 IF A=31 THEN A=64
16960 IF A=32 THEN A=65
16970 IF CHARACTER(A)<>0 THEN
16980         PRINT "THERE IS SOMETHING ALREADY IN THAT HAND!"
16990         GOTO 17070
17000         ENDIF
17010  IF CHARACTER(64)=PRS(5) THEN CHARACTER(64)=0
17020  IF CHARACTER(65)=PRS(5) THEN CHARACTER(65)=0
17030 PRINT "OK."
17040 CHARACTER(A)=PRS(5)
17050 IF A=31 THEN NOUN(PRS(5),9)=1
17060 IF A=32 THEN NOUN(PRS(5),9)=2
17070 ENDF PUT.HAND
17080 DEFINE ENGAGE.SUB
17090 REM *****************
17100 REM * ENGAGE <MONSTER>
17110 REM *****************
17120 IF PRS(5)<1 THEN
17130         Z=55
17140         CALL PRINTER.SUB
17150         GOTO 17310
17160         ENDIF
17170 E=P
17180 CALL CHECK.EM
17190 IF NOUN(PRS(5),1)<>P THEN
17200         PRINT "I DON'T SEE IT HERE."
17210         GOTO 17310
17220         ENDIF
17230 IF NOUN(PRS(5),3)=0 THEN
17240         PRINT "YOU CAN'T ATTACK THAT!"
17250         GOTO 17310
17260         ENDIF
17270 PRINT "OK."
17271 NOUN(PRS(5),15)=1
17280 PRINT "YOU ARE NOW IN COMBAT MODE."
17289 CHARACTER(69)=PRS(5)
17299 NOUN(PRS(5),21)=1
17300 IF INT(RND(-1)*99)+1<51 THEN
17302         PRINT "THE ":
17304         N=PRS(5)
17306         CALL PRINT.NOUN
17308         PRINT " GETS THE FIRST ADVANTAGE!"
17309         CHARACTER(66)=1
17310 ENDIF
17311 ENDF ENGAGE.SUB
17320 DEFINE DISENGAGE.SUB
17330 REM ****************
17340 REM * BREAK COMBAT *
17350 REM ****************
17360 A=INT(RND(0)*99)+1
17370 A=A-NOUN(CHARACTER(69),22)
17380 A=A-NOUN(CHARACTER(69),23)
17390 IF NOUN(CHARACTER(69),19)=2 THEN A=A+100
17400 IF A<60 THEN
17410         PRINT "YOU FAIL TO BREAK COMBAT!"
17420         GOTO 17490
17430         ENDIF
17440 PRINT "YOU MANAGE TO BREAK FROM COMBAT!"
17450 NOUN(CHARACTER(69),18)=2
17460 N=CHARACTER(69)
17470 CHARACTER(69)=0
17480 NOUN(N,21)=0
17490 ENDF DISENGAGE.SUB
17500 DEFINE VALUE.MANAGE
17510 REM ***************
17520 REM * MANAGE CHARACTER VALUES AND STUFF
17530 REM ***************
17540 HAND=CHARACTER(68)
17550 IF HAND=64 THEN BHAND=65
17560 IF HAND=65 THEN BHAND=64
17570 IF CHARACTER(64)>0 THEN IF NOUN(CHARACTER(64),1)<>-1 THEN CHARACTER(64)=0
17580 IF CHARACTER(65)>0 THEN IF NOUN(CHARACTER(65),1)<>-1 THEN CHARACTER(65)=0
17590 !
17600 A=INT(RND(-1)*99)+1
17605 TURN=INT(ROND/10)
17606 REM *!*!*  FIGURE  A R M O R  C L A S S !*!*
17608 AC=10
17610 IF CHARACTER(2)>8 THEN AC=AC-CHARACTER(2)-8
17612 ! CHECK FOR ARMOR WORN
17614 FOR I=1 TO NNUM
17616         IF NOUN(I,14)=1 AND NOUN(I,9)=3 THEN
17618         AC=AC-NOUN(I,20)
17620         GOTO 17624
17622         ENDIF
17623         NEXT I
17624 CHARACTER(71)=AC
17625 ! CHECK FOR MAGICAL STUFF TOO
17630 ENDF VALUE.MANAGE
17640 DEFINE MKILL.SUB
17650 REM ***************
17660 REM * MONSTER ATTACK-TO-KILL ROUTINE
17670 REM ***************
17680 REM ** M=MONSTER
17690 IF NOUN(M,20)<>0 THEN PRINT "EVEN THOUGH YOU HAVE YOUR OPPONENT IN A HOLD, HE ATTACKS YOU."
17700 REM ** ATTACK
17710 TH=INT(RND(0)*19)+1
17720 IF TH<3 THEN
17730         PRINT "THE MONSTER FUMBLES IT'S ATTACK!"
17740         NOUN(M,18)=1
17750         GOTO 18110
17760          ENDIF
17770 IF NOUN(M,22)>8 THEN TH=TH+NOUN(M,22)-8
17780 IF NOUN(M,21)>8 THEN TH=TH+NOUN(M,21)-8
17790 TH=TH+INT(NOUN(M,24)/10)
17800 TB=(12+(10-CHARACTER(71))-INT(NOUN(M,24)/10))
17810 IF TH<TB THEN
17820         PRINT "THE MONSTERS ATTACK MISSES."
17830         GOTO 18110
17840         ENDIF
17850 IF TH>TB THEN
17860 IF TH-TB<3 THEN
17870         PRINT "YOU EVADE THE MONSTERS ATTACK."
17880         GOTO 18110
17890         ENDIF
17900 IF TH=TB THEN
17910         PRINT "THE MONSTERS BLOW MERELY GLANCES AWAY, WITHOUT HARMING YOU."
17920         GOTO 18110
17930         ENDIF
17940 IF NOUN(M,13)<>0 THEN DAM=INT(RND(0)*NOUN(NOUN(M,13),4))+1
17950 IF NOUN(M,13)=0 THEN
17960         DAM=0
17970         FOR I=1 TO NOUN(M,4)
17980                 DAM=DAM+INT(RND(0)*NOUN(M,10))+1
17990         NEXT I
18000         ENDIF
18010 ENDIF
18020 CHARACTER(8)=CHARACTER(8)-DAM
18030 IF CHARACTER(8)<1 THEN
18040         PRINT "YOU ARE DEAD."
18050         FOR I=1 TO NNUM
18060         IF NOUN(I,1)=-1 THEN NOUN(I,1)=P
18070         NEXT I
18080         CHARACTER(I)=0 FOR I=1 TO 80
18090         END "SORRY CHUCK."
18100         ENDIF
18110 ENDF MKILL.SUB
18120 DEFINE MPUMMEL.SUB
18130 REM *** CHECK STUFF ***
18140 REM   * IS HE HELD   **
18150 IF NOUN(M,20)<>0 THEN
18160         CALL MROLL.SUB
18170         IF A<NOUN(M,18) THEN
18180         PRINT "THE MONSTER FAILS TO BREAK YOUR HOLD."
18190         GOTO 18690
18200         ENDIF
18210 PRINT "THE MONSTER BREAKS FREE OF YOUR HOLD."
18220         NOUN(M,20)=0
18230         ENDIF
18240 REM ***    OK   ***
18250 CALL MROLL.SUB
18260 TH=A
18270 IF TH<1 THEN
18280         PRINT "YOUR OPPONENT'S ATTACK MISERABLY MISSES."
18290         GOTO 18690
18300         ENDIF
18310 IF TH>0 AND TH<21 THEN
18320         PRINT "THE ATTACK WAS INEFFECTIVE."
18330         CHARACTER(66)=1
18332         GOTO 18690
18340         ENDIF
18350 IF TH>20 AND TH<41 THEN
18360         PRINT "YOU RECEIVE A GLANCING BLOW."
18370         DAM=2
18380         ENDIF
18390 IF TH>40 AND TH<61 THEN
18400         PRINT "YOU RECEIVE A GLANCING BLOW."
18410         DAM=4
18420         CHARACTER(66)=1
18430         ENDIF
18440 IF TH>60 AND TH<81 THEN
18450         PRINT "A SOLID PUNCH HITS YOU AND STARTS YOU TO WORRY!"
18460         DAM=6
18470         ENDIF
18480 IF TH>80 AND TH<101 THEN
18490         PRINT "YOU RECEIVE A VERY SOLID PUNCH!"
18500         DAM=8
18510         CHARACTER(66)=1
18520         ENDIF
18530 IF TH>100 THEN
18540         PRINT "YOU ARE THE VICTM OF A SERIOUSLY CRUSHING BLOW!"
18550         DAM=10
18560         CHARACTER(67)=1,CHARACTER(66)=2
18570         ENDIF
18580 REM ** DAMAGE **
18590 IF NOUN(M,22)>6 THEN DAM=DAM+NOUN(M,22)-6
18600 IF NOUN(M,23)>6 THEN DAM=DAM+NOUN(M,23)-6
18610 CHARACTER(8)=CHARACTER(8)-DAM
18612 IF DAM>=8 AND CHARACTER(HAND)<>0 THEN
18614         NOUN(CHARACTER(HAND),1)=P
18618         PRINT "YOU DROP THE ":
18620         N=CHARACTER(HAND)
18622         CALL PRINT.NOUN
18624         IF HAND=64 THEN PRINT " THAT WAS IN YOUR LEFT HAND."
18626         IF HAND=65 THEN PRINT " THAT WAS IN YOUR RIGHT HAND."
18628         CHARACTER(HAND)=0
18629         ENDIF
18630          IF CHARACTER(8)<1 THEN
18632         CHARACTER(8)=INT(CHARACTER(9)/2)
18640         CHARACTER(66)=(10-CHARACTER(4)-6)
18650         IF CHARACTER(66)<1 THEN CHARACTER(66)=2
18660         CHARACTER(67)=2
18670         CHARACTER(70)=0,NOUN(M,18)=0
18680         ENDIF
18690 ENDF MPUMMEL.SUB
18700 DEFINE MGRAPPL.SUB
18710 REM ***************
18720 REM * IT'S EVEN BIGGER TIME WRESTLING
18730 REM ***************
18740 DAM=0
18750 REM *** CHECK ***
18760 REM *** IS HE HELD ***
18770 IF NOUN(M,20)<>0 THEN
18780         CALL MROLL.SUB
18790         IF A<NOUN(M,18) THEN
18800         PRINT "YOUR OPPONENT FAILS TO BREAK FREE OF YOUR HOLD."
18810         GOTO 19260
18820         ENDIF
18830 PRINT "YOUR OPPONENT BREAKS FREE OF YOUR HOLD, ALLOWING HIM TO ATTACK."
18840 NOUN(M,20)=0
18850 ENDIF
18860 REM *** OK ***
18870 CALL MROLL.SUB
18880 TH=A
18890 IF TH<21 THEN
18900         PRINT "YOUR OPPONENT BARELY GET'S YOU INTO A WAIST-CLINCH"
18910         ENDIF
18920 IF TH>20 AND TH<41 THEN
18930         PRINT "YOUR OPPONENT GET'S YOU INTO AN ARMLOCK."
18940         DAM=1
18950         ENDIF
18960 IF TH>40 AND TH<56 THEN
18970         PRINT "YOUR OPPONENT HOLDS YOU IN A HAND LOCK."
18980         DAM=2
18990         ENDIF
19000 IF TH>55 AND TH<71 THEN
19010         PRINT "YOUR ENEMY HAS MANAGED TO GET YOU INTO A BEAR HUG."
19020         DAM=3
19030         ENDIF
19040 IF TH>70 AND TH<86 THEN
19050         PRINT "YOUR OPPONENT WRANGLES YOU INTO A HEADLOCK."
19060         DAM=5
19070         ENDIF
19080 IF TH>85 AND TH<96 THEN
19090         PRINT "YOUR ENEMY HAS YOU IN A STRANGLE-HOLD."
19100         DAM=6
19110         ENDIF
19120 IF TH>95 THEN 
19130         PRINT "YOUR ENEMY KICKS YOU, KNOCKING YOU DOWN, AND STUNNING YOU."
19140         DAM=8
19150         CHARACTER(66)=2,CHARACTER(67)=1
19160         ENDIF
19170 REM *** DAMAGE ***
19180 CHARACTER(70)=TH
19190 CHARACTER(8)=CHARACTER(8)-DAM
19200 IF CHARACTER(8)<1 THEN
19210         PRINT "YOU SLIP INTO UNCONSCIOUSNESS."
19220         CHARACTER(67)=2,CHARACTER(66)=INT(RND(0)*4)+2
19230         CHARACTER(70)=0
19240         NOUN(M,18)=0
19250         ENDIF
19260 ENDF MGRAPPL.SUB
19270 DEFINE MROLL.SUB
19280 REM ********
19290 REM ROLL DICE
19300 REM ********
19310 A=INT(RND(-1)*99)+1
19330 A=A+NOUN(M,23)
19340 A=A+NOUN(M,22)
19350 A=A+INT(NOUN(M,22)/5)
19355 A=A+CHARACTER(71)
19360 REM !
19370 REM !
19380 REM !
19390 IF CHARACTER(67)<>1 AND CHARACTER(67)<>2 THEN
19400 A=A-CHARACTER(1)-6
19410 A=A-CHARACTER(2)-6
19420 A=A-CHARACTER(7)-6
19430 ENDIF
19440 ENDF MROLL.SUB
19450 DEFINE DISPLAY
19460 PRINT
19470 PRINT
19480 PRINT "*********************************************************************"
19490 PRINT CHARAC$(1):"  ":CHARAC$(6):" ":CHARAC$(4):"-":CHARAC$(2)
19500 PRINT "STRENGTH:":CHARACTER(1):TAB(25):"HITS:":CHARACTER(8)
19510 PRINT "DEXTERITY:":CHARACTER(2):TAB(25):"HIT MAX:":CHARACTER(9)
19520 PRINT "PERSONALITY:":CHARACTER(3):TAB(25):"SKILLS:"
19530 PRINT "ENDURANCE:":CHARACTER(4):TAB(25):"FIGHTING:":CHARACTER(58)
19540 PRINT "INTELIGENCE:":CHARACTER(5):TAB(25):"THIEVING:":CHARACTER(59)
19550 PRINT "MANA:":CHARACTER(6):TAB(25):"TRADING:":CHARACTER(60)
19560 PRINT "LUCK:":CHARACTER(7):TAB(25):"MAGIC:":CHARACTER(61)
19570 PRINT "GOLD:":CHARACTER(10):TAB(25):"MECHANICS:":CHARACTER(62)
19580 PRINT "ARMOR CLASS:":CHARACTER(71):TAB(25):"HAND TO HAND:":CHARACTER(68)
19590 PRINT
19600 IF CHARACTER(68)=64 THEN PRINT "YOU ARE LEFT HANDED."
19610 IF CHARACTER(68)=65 THEN PRINT "YOU ARE RIGHT HANDED."
19615 PRINT "THIS IS TURN ":TURN
19620 PRINT "*********************************************************************"
19630 PRINT
19640 ENDF DISPLAY
19650 DEFINE HOBBIT.SUB
19660 REM ******************
19670 REM * MOVE ROUTINE FOR *
19680 REM * HOBBITS,OR OTHER *
19690 REM * DEMI-HUMANS LIKE *
19700 REM * DWARVES,ELVES,ETC *
19710 REM *********************
19720 FACE=0
19730 IF NOUN(M,1)=P AND CHARACTER(67)=0 AND NOUN(M,21)=0 THEN
19740         Z=NOUN(M,17)
19750         CALL PRINTER.SUB
19760         ENDIF
19770 REM ****+++ +?+?+? COMBAT 
19780 IF NOUN(M,1)=P AND NOUN(M,15)=1 AND NOUN(M,21)=0 AND CHARACTER(69)=0 AND CHARACTER(67)=0 THEN
19790         PRINT "THE ":
19800         N=M
19810         CALL PRINT.NOUN
19820         PRINT " ATTACKS YOU!"
19830         CHARACTER(69)=M,NOUN(M,21)=1
19840         ENDIF
19850 IF CHARACTER(67)<>0 AND CHARACTER(69)=M THEN CHARACTER(69)=0 
19860 IF CHARACTER(69)=0 THEN NOUN(M,21)=0
19870 IF NOUN(M,21)=1 AND CHARACTER(69)=M THEN
19872 A=INT(RND(-1)*99)+1
19874 IF A<45 THEN GOTO 19880
19875 IF NOUN(M,15)=1 THEN CALL MKILL.SUB
19876 IF NOUN(M,15)=1 THEN GOTO 19930
19880         IF NOUN(M,13)<>0 AND MODE=1 THEN CALL MKILL.SUB
19890         A=INT(RND(0)*99)+1
19900         IF NOUN(M,13)=0 AND A<60 THEN CALL MPUMMEL.SUB
19910         IF NOUN(M,13)=0 AND A>59 THEN CALL MGRAPPL.SUB
19920         IF NOUN(M,13)<>0 AND MODE<>1 THEN CALL MPUMMEL.SUB
19930         GOTO 20770
19940         ENDIF
19950 REM **** SEARCH DEAD OR UNCONSCIOUS BODIES ****
19960 IF P=NOUN(M,1) THEN
19970         IF CHARACTER(67)=2 OR CHARACTER(67)=3 THEN
19980 IF CHARACTER(67)=3 THEN
19990         PRINT "THE ":
20000         N=M
20010         CALL PRINT.NOUN
20020         PRINT " RAVAGES YOUR PARALYZED BODY FOR ANYTHING VALUABLE."
20030         ENDIF
20040 REM *** SEARCH THE BODY ***
20050 FOR I=1 TO NNUM
20060         IF NOUN(I,1)=-1 THEN
20070                 NOUN(I,1)=-M
20080                 N=I,E=-M   ! FOR PUT.COMPLETE SUBROUTINE
20090                 CHARACTER(64)=0,CHARACTER(65)=0
20100                 IF NOUN(I,9)=3 THEN NOUN(I,9)=0
20110                 ENDIF
20120         NEXT I
20130       GOTO 20590
20135           NOUN(M,15)=3
20140 ENDIF
20150 ENDIF
20155 IF CHARACTER(67)<>0 THEN GOTO 20590
20160 REM *** OK, BUT CAN WE PICK SOMETHING UP OFF OF THE GROUND? ***
20170 FOR I=1 TO NNUM
20180 N1=I
20190 IF NOUN(M,1)=NOUN(I,1) AND NOUN(I,2)=1 THEN
20200         IF NOUN(I,9)<>0 THEN
20210         ! CHECK IF IT'S IN A CONTAINER
20220         N=I,MF=1   !TELL CHECK.BP THAT WE ARE NOT PLAYER
20230         CALL CHECK.BP
20240         IF ER=1 THEN GOTO 20390
20250         ENDIF
20260         NOUN(I,1)=-M,NOUN(I,9)=0
20270        N=N1,E=-M
20280         CALL PUT.COMPLETE
20290         IF P=NOUN(M,1) THEN
20300                 PRINT "THE ":
20310                 N=M
20320                 CALL PRINT.NOUN
20330                 PRINT " PICKS UP THE ":
20340                 N=N1
20350                 CALL PRINT.NOUN
20360                 PRINT "."
20370                 ENDIF
20380         ENDIF
20390 NEXT I
20400 IF INT(RND(0)*99)+1<51 THEN
20410 REM ** LET'S OPEN A DOOR. **
20420         FOR I=1 TO DOORNUM
20430         IF DOORS(I,1)=NOUN(M,1) AND DOORS(I,6)<>1 AND DOORS(I,5)<>1 AND DOORS(I,5)<>2 OR DOORS(I,3)=NOUN(M,1) AND DOORS(I,6)<>1 AND DOORS(I,5)<>1 AND DOORS(I,5)<>2 THEN
20440                 MOV(DOORS(I,1),DOORS(I,2))=DOORS(I,3)
20450                 MOV(DOORS(I,3),DOORS(I,4))=DOORS(I,1)
20460                 DOORS(I,6)=1
20470                 IF DOORS(I,1)=NOUN(M,1) THEN FACE=DOORS(I,2)
20480                 IF DOORS(I,3)=NOUN(M,1) THEN FACE=DOORS(I,4)
20490                 IF NOUN(M,1)=P THEN
20500                         PRINT "THE ":
20510                         N=M
20520                         CALL PRINT.NOUN
20530                         PRINT " OPENS THE DOOR ON THE ":DIR$(FACE):" WALL."
20540                         GOTO 20590
20550                         ENDIF
20560                         ENDIF
20570                 NEXT I
20580                 ENDIF
20590 REM ! GOTO LINE
20600         NOUN(M,12)=NOUN(M,1)
20610 FOR A=1 TO 10
20612       IF FACE<>0 THEN A=FACE
20614       IF FACE=0 AND INT(RND(-1)*99)+1<25 THEN A=INT(RND(0)*9)+1,FACE=1
20650         A1=MOV(NOUN(M,1),A)
20660         IF A1<1 THEN GOTO 20750
20670         NOUN(M,1)=A1
20680         IF P=NOUN(M,12) THEN
20690         N=M
20700         PRINT "THE ":
20710         CALL PRINT.NOUN
20720         PRINT " JUST WENT ":DIR$(A):"."
20730         ENDIF
20740   GOTO 20755
20750         IF FACE<>0 THEN A=10
20752         NEXT A
20755 REM ** GOTO LINE
20770 A=NOUN(M,10),A1=0
20780 FOR I=1 TO NNUM
20790 IF NOUN(I,1)=-M AND NOUN(I,4)>A THEN A1=I
20800 NEXT I
20810 NOUN(M,13)=A1
20815 FACE=0
20820 ENDF HOBBIT.SUB
20830 DEFINE SEARCH.OBJ
20840 REM *********************
20850 REM * SEARCH OBJECTS ****
20860 REM *********************
20862 IF NOUN(PRS(5),3)<>0 AND NOUN(PRS(5),19)=0 THEN
20864         PRINT "THE ":
20866         N=PRS(5)
20868         CALL PRINT.NOUN
20870         PRINT " WON'T LET YOU DO THAT!"
20872         GOTO 21060
20874         ENDIF
20879 IF NOUN(PRS(5),1)=P THEN E=P
20880 IF NOUN(PRS(5),1)=-1 THEN E=-1
20890 A=0
20900 PRINT "YOU FIND THE FOLLOWING:"
20910 FOR I=1 TO NNUM
20920 IF NOUN(I,1)=-PRS(5) THEN
20930         NOUN(I,1)=P
20940         A=A+1
20950         N=I
20960        IF NOUN(I,9)<>0 THEN GOTO 20990
20970         CALL PRINT.NOUN
20980       PRINT "."
20990        IF E=P THEN NOUN(I,1)=P
21000       E=P,N=I
21010       IF NOUN(I,5)<>0 THEN CALL PUT.COMPLETE
21020         ENDIF
21030 NEXT I
21040 PRINT
21050 IF A=0 THEN PRINT "NOTHING."
21060 ENDF SEARCH.OBJ
22000 DEFINE LIGHT.MODE
22010 REM ************************
22020 REM * DETERMINE IS A SOURCE*
22030 REM * OF LIGHT IS OPERATING*
22040 REM * IN LOC N             *
22050 REM ************************
22060 L=0  ! L=0 THEN LIT L=1 THEN DARK
22070      ! N= LOCATION
22080 IF L<36 THEN L=0
22085 IF L>35 THEN L=1
22090 !
22100 !
22110 FOR I=1 TO NNUM
22112 IF NOUN(I,9)<>0 THEN
22114         N=I
22116         MF=1
22118         CALL CHECK.BP
22120         IF ER=1 THEN GOTO 22500
22122         ENDIF
22140         IF NOUN(I,1)=-1 AND NOUN(I,14)=5 AND NOUN(I,11)=1 THEN L=0
22150         IF NOUN(I,1)=N  AND NOUN(I,14)=5 AND NOUN(I,11)=1 THEN L=0
22160         FOR J=1 TO NNUM
22170         IF NOUN(J,1)=N AND NOUN(I,1)=-J AND NOUN(I,11)=1  AND NOUN(I,14)=5 THEN L=0
22180         NEXT J
22190 NEXT I
22500 ENDF LIGHT.MODE
23000 DEFINE LIGHT.IT
23010 REM *****************
23020 REM * LIGHT SOMETHING*
23030 REM ******************
23040 REM ******************
23050 IF PRS(1)<1 THEN
23060         Z=55
23070         CALL PRINTER.SUB
23080         GOTO 23800
23090         ENDIF
23100 E=-1
23110 CALL CHECK.EM
23120 IF PRS(5)<1 THEN GOTO 23800
23130 IF NOUN(PRS(5),1)<>-1 THEN
23140         PRINT "YOU DON'T HAVE THE ":
23150         N=PRS(5)
23160         CALL PRINT.NOUN
23170         PRINT "."
23180         GOTO 23800
23190         ENDIF
23200 IF NOUN(PRS(5),9)<>0 THEN
23210         N=PRS(5)
23220         CALL CHECK.BP
23230         IF ER=1 THEN GOTO 23800
23240         ENDIF
23250 ! IS IT LIGHT-ABLE?
23260 IF NOUN(PRS(5),14)<>5 THEN
23270         PRINT "YOU CAN'T LIGHT THAT!"
23280         GOTO 23800
23290         ENDIF
23300 PRINT "IT IS NOW ALIGHT."
23310 NOUN(PRS(5),9)=1
23800 ENDF LIGHT.IT
24000 DEFINE ENTER.SUB
24010 REM ********************
24020 REM * HE WANTS ENTER?
24030 REM * OK!
24040 REM ********************
24050 !
24060 !
24070 FOR I=1 TO 10
24080         IF MOV(P,I)>0 THEN
24090         P=MOV(P,I)
24100         Z=LOCS(P,7)
24110         CALL PRINTER.SUB
24120         CALL OBJECT.PRINT
24130         GOTO 24500
24140         ENDIF
24150         NEXT I
24160 PRINT "ENTER WHAT?"
24500 ENDF ENTER.SUB
30000 REM ********************************
30010 REM *                           *
30020 REM *         DATA              *
30030 REM *                           *
30040 REM ********************************
30050 REM +++ MOVE DATA +++
30060 DATA 2,0,0,37,49,38,49,0,0,0
30070 DATA 3,0,0,1,0,0,0,0,0,0
30080 DATA 6,0,0,2,0,0,0,0,0,0
30090 DATA 0,0,0,0,0,0,0,0,0,0
30100 DATA 0,0,0,0,0,0,8,0,22,0
30110 DATA 7,0,0,3,0,0,0,0,0,0
30120 DATA 16,0,0,6,0,0,0,0,0,0
30130 DATA 0,0,0,0,0,0,0,5,0,0
30140 DATA 0,0,0,0,0,0,0,0,0,0
30150 DATA 0,0,0,0,0,0,0,0,0,0
30160 DATA 0,0,0,0,0,0,0,0,0,0
30170 DATA 0,0,0,0,0,0,0,0,0,0
30180 DATA 0,0,0,0,0,0,0,0,0,0
30190 DATA 0,0,0,0,0,0,0,0,0,0
30200 REM +++ THAT WAS 14 +++
30210 DATA 0,0,0,0,0,0,0,0,0,0
30220 DATA 0,0,0,7,0,0,0,0,0,0
30230 DATA 0,0,0,0,0,0,0,0,0,0
30240 DATA 0,0,0,0,0,0,0,0,0,0
30250 DATA 0,0,0,0,0,0,0,0,32,0
30260 DATA 0,0,0,0,0,0,0,0,0,0
30270 DATA 0,0,0,0,0,0,0,0,0,0
30280 REM ++ THAT WAS 21 +++
30290 DATA 0,0,0,0,0,0,0,0,0,5 
30300 DATA 0,0,0,0,0,0,24,28,0,0
30310 DATA 0,0,0,25,0,0,0,23,0,0
30320 DATA 24,0,0,0,0,0,0,0,0,0
30330 DATA 0,0,0,0,0,0,0,0,0,0
30340 DATA 0,0,0,0,0,0,0,0,0,0
30350 REM ** THAT WAS 27
30360 DATA 0,0,0,0,0,0,23,0,0,0
30370 DATA 0,0,0,0,0,0,0,0,0,0
30380 DATA 0,0,0,0,0,0,0,0,0,0
30390 DATA -1,-1,-1,33,-1,0,-1,32,0,0
30400 DATA -1,-1,-1,0,0,0,31,-1,0,19
30410 DATA 31,-1,0,-1,-1,-1,-1,34,0,0
30420 DATA 32,0,-1,-1,-1,-1,33,-1,0,0
30430 REM THAT WAS 34
30440 DATA 0,0,0,0,0,0,0,0,0,0
30450 DATA 0,0,0,0,0,0,0,0,0,0
30460 DATA 1,49,38,39,0,40,49,38,00,0
30470 DATA 1,1,0,40,39,0,37,40,0,0
30480 DATA 37,50,38,42,48,41,49,40,0,0
30490 DATA 38,37,0,41,42,0,39,0,0,0
30500 DATA 40,39,40,-1,44,-1,43,0,0,0
30510 DATA 39,49,40,43,47,44,48,41,0,0
30520 DATA 42,48,41,-1,-1,45,47,44,0,0
30530 DATA 41,42,41,45,-1,-1,34,-1,0,0
30540 DATA 44,43,41,-1,-1,-1,43,41,0,0
30550 DATA 52,52,15,0,0,0,0,0,0,0
30560 DATA 48,48,42,47,0,43,47,43,0,0
30570 DATA 49,49,39,47,47,42,48,42,0,0
30580 REM ***  THAT WAS 48 ***
30585 DATA 50,50,1,48,48,42,0,39,0,0
30590 DATA 0,0,0,49,1,0,0,1,0,0
31000 REM ++ VERB DATA ++
31010 DATA N,NORTH,NW,NORTHWEST,NE,NORTHEAST
31020 DATA SOUTH,S,SOUTHWEST,SW,SOUTHEAST,SE
31030 DATA WEST,W,EAST,E,UP,U,DOWN,D
31040 DATA ATTACK,KILL,SLAY,EAT,DRINK,FUCK,GET,DROP,RELEASE,INVENTORY
31050 DATA LIGHT,EXTINGUISH,BREAK,DESTROY,QUIT,SNEAK,RUN,WALK
31060 DATA FORWARD,BACK,LEFT,RIGHT,OPEN,CLOSE,SHUT,SLAM
31070 DATA LOOK,SEARCH,EXPLORE,INVESTIGATE
31080 DATA PUT,TAKE,MOVE,WEAR,DON
31085 DATA ENGAGE,DISENGAGE,PUMMEL,GRAPPLE,HELP
31086 DATA CHARACTER,VIEW,LIST,STATUS,STRAP
31087 DATA ENTER
31090 REM ***   DIR$ ***
31100 DATA NORTH,NORTHWEST,NORTHEAST,SOUTH,SOUTHWEST,SOUTHEAST,WEST,EAST,UP,DOWN
31110 DATA 1,1,2,2,3,3,4,4,5,5,6,6,7,7,8,8,9,9,10,10
31120 DATA 11,11,11,12,13,14,15,16,16,17,18,19,20,20,21
31130 DATA 22,23,24,1,25,7,8,26,27,27,27
31140 DATA 28,29,29,29,30,15
31150 DATA 31,32,32
31155 DATA 33,34,35,36,37
31156 DATA 38,38,38,38,32
31157 DATA 39
31160 REM ++
31170 REM ++ ADJECTIVE DATA 
31180 REM ++
31190 DATA BIG,LITTLE,UGLY,HEAVY,BROWN,GOLDEN,JEWELED,IVORY,IRON,LONG,STEEL,BRONZE,BEUTIFUL,DARK,YOUNG
31200 DATA OLD,DEAD,BROKEN,SOFT,HARD,GIANT,ROBED,ANIMATED,DECREPID,MAGIC,BRASS
31210 DATA NORTH,SOUTH,WEST,EAST,LEFT,RIGHT,FORWARD,SECRET
31220 DATA CHAIN,WOODEN,LEATHER,PLATE,ELVEN
31230 DATA HIDDEN,CONCEALED,UNSEEN,WHITE,ROUND
31240 REM ++ PREPOSITIONS
31250 DATA ON,IN,INSIDE,OUT,WITH,FOR,AND
31260 REM ********** A D V E R B S ! *****************
31270 DATA QUICKLY,SLOWLY,SNEAKILY,CAREFULLY,QUIETLY
31280 REM ++ NOUNS ++ ++ NOUNS ++
31290 DATA ARMOR,*,26,1,0,12,0,39,35,0,0,25,2,1,25,1,8,0,42,0,0,4,0,0,0,0
31300 DATA SWORD,BLADE,01,1,0,12,0,10,12,0,0,25,0,1,20,2,6,0,43,0,0,0,0,0,0,0
31310 DATA BELL,* ,-2,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
31320 DATA POUCH,BAG,14,1,0,0,2,19,37,10,5,2,0,1,0,3,4,0,44,0,0,0,0,0,0,0
31330 DATA TABLE,*,-2,0,0,0,0,5,0,0,0,0,0,1,0,5,0,0,0,0,0,0,0,0,0,0
31340 DATA STICK,STAFF,14,1,0,3,0,10,36,0,5,0,0,1,10,4,7,0,45,0,0,0,0,0,0,0
31350 DATA BOOK,*,4,1,0,0,0,24,16,0,0,5,0,0,8,5,3,0,46,0,0,0,0,0,0,0
31360 DATA DOOR,DOORS,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
31370 DATA LAMP,LANTERN,1,1,0,0,0,26,0,0,0,5,0,1,40,5,5,0,47,0,0,0,0,0,0,0
31380 DATA GHOUL,*,16,0,1,6,5,2,3,14,1,0,0,1,0,6,0,0,48,0,0,0,0,10,5,35
31390 DATA ASHES,ASH,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
31400 DATA WALL,WALLS,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
31410 DATA ZOMBIE,MONSTER,10,0,1,1,8,24,0,13,1,8,0,1,13,7,1,0,57,0,0,0,0,8,4,20
31420 DATA ROCK,STONE,14,1,0,0,0,2,43,0,0,0,0,1,2,8,1,0,61,0,0,0,0,0,0,0
31430 DATA BACKPACK,PACK,1,1,0,0,2,37,0,50,0,5,0,1,30,9,8,0,62,0,0,0,0,0,0,0
31440 DATA BACKPACK,PACK,1,1,0,0,2,39,0,50,0,5,0,1,30,9,8,0,69,0,0,0,0,0,0,0
31450 DATA STATUE,*,-2,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
31460 DATA HAND,HANDS,-2,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
31465 DATA HOBBIT,HALFLING,4,0,1,1,7,2,0,16,1,6,7,0,0,9,3,0,70,0,0,0,0,9,10,60
31466 DATA DWARF,*,42,0,1,1,4,2,16,30,1,8,0,42,0,9,1,0,85,0,0,0,0,6,8,30
31470 REM *****************
31480 REM * LOCS DATA
31490 REM ******************
31500 DATA 1,4,6,8,11,25,15,16,13,71
31510 DATA 0,0,0,17,0,19,0,23,24,27
31520 DATA 28,32,34,37,38,39,40,36,56,39
31525 DATA 73,74,73,73,0,0,75,75,93,94
31527 DATA 80,82,80,78,76,86,88,89,89,91
31530 REM ++ NOW FOR NIGHTTIME DESC'S ++
31540 DATA 2,4,6,8,11,25,15,16,13,71
31550 DATA 0,0,0,17,0,21,0,23,24,27
31560 DATA 28,32,34,37,38,39,40,36,56,39
31562 DATA 73,74,73,73,0,0,75,75,90,94
31564 DATA 80,82,80,78,76,86,88,90,90,92
31570 REM ++ NOW FOR BRIEF DESC'S ++
31580 DATA 3,5,7,10,12,26,15,16,14,72
31590 DATA 0,0,0,18,0,22,0,23,24,27
31600 DATA 29,33,35,37,38,39,40,36,56,39
31602 DATA 73,74,73,73,0,0,75,75,90,94
31604 DATA 81,83,81,79,77,87,88,89,89,92
31610 REM +++++++++++++++++
31620 REM + DOOR DATA     +
31630 REM +++++++++++++++++
31640 REM ++ DOOR BY DOOR NOT ITEM BY ITEM...LIKE NOUNS.
31650 DATA 2,1,2,4,0,0
31660 DATA 4,8,3,7,0,0
31670 DATA 20,7,3,8,0,0
31680 DATA 4,1,5,4,0,0
31690 DATA 5,1,9,4,2,0
31700 DATA 5,8,6,7,0,0
31710 DATA 9,1,14,4,2,0
31720 DATA 0,0,0,0,0,0
31730 DATA 16,7,17,8,1,0
31740 DATA 16,8,18,7,0,0
31750 DATA 20,8,21,7,0,0
31760 DATA 18,8,19,7,2,0
31770 DATA 22,1,23,4,0,0
31780 DATA 23,1,35,4,1,0
31790 DATA 25,4,26,1,0,0
31800 DATA 28,4,29,1,0,0
31810 DATA 29,8,30,7,0,0
31820 DATA 32,4,27,1,2,0
  liv