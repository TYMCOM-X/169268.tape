100 RESTORE
200 LEVEL=1
300 A=INT(RND(-1)*99)+1
400 DIM ABIL(6),X(6)
500 DIM ORDER(4,6)
600 DIM TSTAT(5)
700 MAT READ ORDER
800 MAT READ TSTAT
900 DIM TBIL(5)
1000 REM ******************************************************
1100 REM **
1200 REM **       MAKE CHARACTERS
1300 REM **
1400 REM **
1500 REM ******************************************************
1600 PRINT TAB(15):"ROLL-UP CHARACTERS"
1700 PRINT "FIRST OF ALL, WHAT IS THE NAME OF YOU CHARCTER TO BE":
1800 INPUT NAME$
1900 PRINT "WHAT PASSWORD WOULD YOU LIKE YOU CHARACTER TO BE LOCKED UNDER"
2000 PRINT "(HIT RETURN FOR NONE)":
2100 INPUT IN FORM "R":PASS$
2200 PRINT "WHAT RACE DO YOU WISH TO BE :"
2300 PRINT "ELF / DWARF / GNOME / HUMAN / HOBBIT "
2400 INPUT RACE$
2500 IF RACE$<>"ELF" AND RACE$<>"DWARF" AND RACE$<>"HUMAN" AND RACE$<>"GNOME" AND RACE$<>"HOBBIT" THEN GOTO 2200
2600 PRINT
2700 PRINT "WHAT ALIGNMENT WOULD YOU LIKE YOUR CHARACTER TO BE :"
2800 PRINT "GOOD / NEUTRAL / EVIL ":
2900 INPUT ALIGN$
3000 IF ALIGN$<>"GOOD" AND ALIGN$<>"NEUTRAL" AND ALIGN$<>"EVIL" THEN GOTO 2700
3100 PRINT
3200 I4=0
3300 PRINT "NOW, YOU MAY CHOOSE WHICH OF THE BELOW CLASSES YOU WOULD LIKE TO BE."
3400 PRINT "AND THE COMPUTER WILL GENERATE YOUR ABILITIES AND ARANGE THEM "
3500 PRINT "TO FIT THE CLASS DESIRED.YOU CAN BE :"
3600 PRINT "MAGIC-USER / WARRIOR / THIEF / OR MONK "
3700 PRINT 
3800 PRINT "WHICH WOULD YOU LIKE":
3900 INPUT CLASS$
4000 IF CLASS$<>"MAGIC-USER" AND CLASS$<>"MONK" AND CLASS$<>"THIEF" AND CLASS$<>"WARRIOR" THEN GOTO 3700
4100 IF CLASS$="PRIEST" AND ALIGN$="NEUTRAL" THEN I4=-1
4200 IF CLASS$="THIEF" AND ALIGN$="GOOD" THEN I4=-2
4300 IF I4=-1 THEN PRINT "I'M SORRY, BUT PRIESTS CAN'T BE NEUTRAL, YOU ARE."
4400 IF I4=-2 THEN PRINT "COME NOW! YOU CANNOT BE A *GOOD* THIEF!"
4500 IF I4<0 THEN PRINT "WOULD YOU LIKE TO START OVER? ":
4600 IF I4<0 THEN INPUT IN FORM "R":Y$
4700 IF I4<0 AND Y$="Y" OR Y$="YES" THEN GOTO 1600
4800 IF I4<0 AND Y$<>"YES" AND Y$<>"Y" THEN GOTO 3300
4900 PRINT "PLEASE WAIT ONE MOMENT, THANK YOU."
5000 REM ****** GENERATE ABILITY SCORES  *********
5100 REM C=CONSTITUTION  *  S=STRENTGTH * D=DEXTERITY
5200 REM CH=CHARISMA * II=INTELIGENCE
5300 REM AND W=WISDOM
5400 FOR I=1 TO 6
5500 ABIL(I)=INT(RND(-1)*15)+4
5600 WAIT .5
5700 IF ABIL(I)>18 THEN GOTO 5500
5800 NEXT I
5900 MAT X=ABIL
6000 REM *** SORT NUMBERS FROM HIGHEST TO LOWEST...
6100 FOR I=1 TO 5
6200 FOR J=I+1 TO 6
6300 IF X(I)>=X(J) THEN 6700
6400 Z=X(J)
6500 X(J)=X(I)
6600 X(I)=Z
6700 NEXT J
6800 NEXT I
6900 MAT ABIL=X
7000 IF CLASS$="WARRIOR" THEN I=1
7100 IF CLASS$="MAGIC-USER" THEN I=2
7200 IF CLASS$="PRIEST" THEN I=3
7300 IF CLASS$="THIEF" THEN I=4
7305 IF CLASS$="MONK" THEN I=4
7400 FOR I1=1 TO 6
7500 IF ORDER(I,I1)=1 THEN S=ABIL(I1)
7600 IF ORDER(I,I1)=2 THEN D=ABIL(I1)
7700 IF ORDER(I,I1)=3 THEN C=ABIL(I1)
7800 IF ORDER(I,I1)=4 THEN II=ABIL(I1)
7900 IF ORDER(I,I1)=5 THEN W=ABIL(I1)
8000 IF ORDER(I,I1)=6 THEN CH=ABIL(I1)
8100 NEXT I1
8200 PRINT "STRENGTH :":S
8300 PRINT "CON ":C
8400 PRINT "DEX ":D
8500 PRINT "INT ":II
8600 PRINT "WIS ":W
8700 PRINT "CHA ":CH
8800 REM ******** ADJUST ABILITIES FOR RACE AND FOR CLASS **
8900 PRINT "RACE ADJUSTMENTS ON STAND BY ..."
9000 IF RACE$="GNOME" THEN D=D+1
9100 IF RACE$="GNOME" THEN C=C+1
9200 IF RACE$="DWARF" THEN C=C+1
9300 IF RACE$="DWARF" THEN S=S+1
9400 IF RACE$="DWARF" THEN CH=CH-1
9500 IF RACE$="ELF" THEN D=D+1
9600 IF RACE$="ELF" THEN C=C-1
9700 IF RACE$="ELF" THEN II=II+1
9800 IF RACE$="HOBBIT" THEN S=S-1
9900 IF RACE$="HOBBIT" THEN D=D+2
10000 REM ****** DATA FOR BEST ORDER OF ABILITIES FOR CLASSES***
10100 REM ORDER IN THIS WAY,
10200 REM STR  DEX  CON  INT WIS CHA
10300 REM FIGHTER ::: MAGE ::: PRIEST ::: THIEF
10400 DATA 1,3,2,6,4,5
10500 DATA 4,3,2,1,5,6
10600 DATA 5,6,1,2,3,4
10700 DATA 2,3,1,4,5,6
10800 REM *************************************************
10900 REM *************************************************
11000 REM ***    
11100 REM ***
11200 REM ***      NOW, FINISH THE CHARACTER
11300 REM ***
11400 REM ***
11500 REM *************************************************
11600 REM *************************************************
11700 IF CLASS$="THIEF" THEN RANK$="ROGUE"
11800 IF CLASS$="WARRIOR" THEN RANK$="VETERAN"
11900 IF CLASS$="MAGIC-USER" THEN RANK$="PRESTIDIGITATOR"
12000 IF CLASS$="THIEF" THEN CALL THIEF
12001 IF CLASS$="MONK" THEN CALL THIEF
12005 IF CLASS$="MONK" THEN RANK$="NOVICE"
12100 REM FIRST DO HIS HIT POINTS
12200 PRINT 
12300 IF CLASS$="WARRIOR" THEN HIT=10
12400 IF CLAS$="PRIEST" THEN HIT=8
12500 IF CLASS$="MAGIC-USER" THEN HIT=4
12600 IF CLASS$="THIEF" THEN HIT=6
12605 IF CLASS$="MONK" THEN HIT=4
12700 HP=0
12800 IF C>14 THEN HP=HP+(C-14)
12900 IF C<6 THEN HP=HP-C
12905 IF CLASS$="MONK" THEN A=2 ELSE A=1
13000 HP=HP+INT(RND(0)*HIT+1) FOR I = 1 TO A
13100 IF HP<2 THEN HP=2
13200 REM ******* DO HIS ARMOR CLASS NEXT
13300 AC=10
13400 IF D>14 THEN AC=AC-(D-14)
13500 IF CLASS$="MONK" THEN AC=10
13600 REM ***** DO GOLD NEXT
13700 IF CLASS$="WARRIOR" THEN NUM=5 AND AM=4
13800 IF CLASS$="PRIEST" THEN NUM=3 AND AM=6
13900 IF CLASS$="THIEF" THEN NUM=2 AND AM=6
14000 IF CLASS$="MAGIC-USER" THEN NUM=2 AND AM=4
14100 GOLD = 127
14200 GOLD=10*(NUM*(INT(RND(0)*AM+1)))
14300 GOLD=10*(NUM*(INT(RND(0)*AM)+1))
14400 PRINT "YOUR CHARACTER IS COMPLETE, WOULD YOU LIKE TO SEE IT ":
14500 INPUT Y$
14600 IF CLASS$="MAGIC-USER" THEN CALL MAGE
14700 IF CLASS$="PRIEST" THEN CALL CLERIC
14800 IF Y$<>"YES" AND Y$<>"Y" THEN GOTO 15900
14900 PRINT NAME$:" ":RACE$:" ":ALIGN$:"-":CLASS$
15000 PRINT FOR I=1 TO 2
15100 PRINT "STRENGTH:":S:TAB(35):"EXP":EX
15200 PRINT "DEXTERITY:":D:TAB(35):"GOLD":GOLD
15300 PRINT "CONSTITUTION:":C
15400 PRINT "INTELIGENCE:":II
15500 PRINT "WISDOM:":W
15600 PRINT "CHARISMA:":CH
15700 PRINT
15800 PRINT "HIT POINTS :":HP:TAB(25):"ARMOR CLASS :":AC
15900 PRINT "DO YOU WISH TO SAVE THIS CHARACTER:"
16000 INPUT Y$
16100 IF Y$<>"Y" AND Y$<>"YES" THEN GOTO 4900
16200 P=INT(RND(0)*26)+1
16300 PP=P
16400 REM *******************************************************
16500 REM **
16600 REM **
16700 REM **
16800 REM **   SAVE CHARACTER
16900 REM **
17000 REM **
17100 REM ** FORMAT IS AS FOLLOWS
17200 REM ** STR DEX CON INT WIS CHA
17300 REM ** HP  GOLD AC       (ALL NUMBERS ARE LISTED AS FIVE DIGITS
17400 REM ** NAME FOLLOWED BY A "@"
17500 REM ** PASSWORD FOLLOWED BY A "@"
17600 REM ** END
17700 OPEN NAME$+".ADV-A" FOR SYMBOLIC RANDOM IO AS FILE 1 WITH PROTECTION "UPD NO NO"
17800 NUM$="DDDDDDD"
17900 WRITE ON 1 IN FORM NUM$:S
18000 WRITE ON 1 IN FORM NUM$:D
18100 WRITE ON 1 IN FORM NUM$:C
18200 WRITE ON 1 IN FORM NUM$:II
18300 WRITE ON 1 IN FORM NUM$:W
18400 WRITE ON 1 IN FORM NUM$:CH
18500 WRITE ON 1 IN FORM NUM$:HP
18600 WRITE ON 1 IN FORM NUM$:HP
18700 WRITE ON 1 IN FORM NUM$:AC
18800 WRITE ON 1 IN FORM NUM$:GOLD
18900 WRITE ON 1 IN FORM NUM$:P
19000 WRITE ON 1 IN FORM NUM$:PP
19100 WRITE ON 1 IN FORM NUM$:EXPERIENCE
19200 WRITE ON 1 IN FORM NUM$:LEVEL
19300 FOR I=1 TO 8
19400 WRITE ON 1 IN FORM NUM$:HOLDING
19500 NEXT I
19600 WRITE ON 1 IN FORM NUM$:INT(II/2)
19700 WRITE ON 1 IN FORM NUM$:INT(II/2)
19800 FOR I=1 TO 5
19900 WRITE ON 1 IN FORM NUM$:TBIL(I)
20000 NEXT I
20100 WRITE ON 1 IN FORM NUM$:0
20200 REM ^^^^^^^^^^^^^^^GONE(CNUM2)^^^^^^^^^^^^^^
20300 WRITE ON 1 IN FORM NUM$:0
20400 REM ***^^^*** THAT WAS THE FLAG FOR HIM IN THE POOL **^^**
20500 WRITE ON 1 IN FORM NUM$:0
20600 REM ^^^^^^^^^^^^*********<<<>>> THAT WAS HIS WEAPON IN HAND
20700 WRITE ON 1 IN FORM "R":NAME$+"@"
20800 WRITE ON 1 IN FORM "R":RACE$+"@"
20900 WRITE ON 1 IN FORM "R":CLASS$+"@"
21000 WRITE ON 1 IN FORM "R":ALIGN$+"@"
21100 WRITE ON 1 IN FORM "R":PASS$+"@"
21200 WRITE ON 1 IN FORM "R":RANK$+"@"
21300 WRITE ON 1 IN FORM "R":"*"
21400 CLOSE 1
21500 PRINT "SAVED"
21600 DEFINE MAGE
21700 REM ********************************************
21800 REM **
21900 REM **    PICK SPELLS FOR MAGIC USER
22000 REM **
22100 REM **
22200 REM ********************************************
22300 REM ** FIRST LEVEL SPELLS ARE, MAGIC MISSILE
22400 REM ** SLEEP. SHIELD . LIGHT . IDENTIFY
22500 REM ** BUT THEY ONLY GET SLEEP AND MISSILE
22600 SPELL1=1
22700 SPELL2=2
22800 ENDF MAGE
22900 DEFINE CLERIC
23000 REM PICK CLERICAL SPELLS
23100 REM THERE ARE : CURE LIGHT WOUNDS
23200 REM LIGHT
23300 REM DETECT EVIL
23400 REM DETECT GOOD
23500 REM BUT ONLEY GETS CURE LIGHT WOUNDS AND LIGHT
23600 SPELL1=1
23700 SPELL2=2
23800 ENDF CLERIC
23900 DATA 30,25,15,85,5
24000 DEFINE THIEF
24100 FOR I=1 TO 5
24200 TBIL(I)=TBIL(I)+(D-15)*5
24300 NEXT I
24400 FOR I=1 TO 5
24500 TBIL(I)=TBIL(I)+TSTAT(I)
24600 NEXT I
24700 ENDF THIEF
     