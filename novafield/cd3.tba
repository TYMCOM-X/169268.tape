10 REM **********************************************************************
20 REM *                                                                    *
30 REM *                       'little black book'                          *
40 REM *                          version 1.00                              *
50 REM *                                                                    *
60 REM **********************************************************************
70 STRING NUMB(11,1000),SEAR,TEMP(11,1000)
80 DIM TEMP4$(7,1000)
90 DIM TEMP9(1000)
100 REN1=0
110 INTEGER NUM,CNT,SM,L,N,LEG,LEGG,CHNG,EN,WE,W1,H,H1,W
120 FLA=0
130 H1=3
140 BZ=2
150 R2=0
160 WE1 = 1
170 X4$=""
180 X=0
190 X$=""
200 BB$=CHAR(27)+CHAR(72)+CHAR(27)+CHAR(74)
210 PRINT
220 PRINT
230 R2=R2+1
240 PRINT '              enter PASSWORD : ':
250 INPUT IN FORM "R":PASS$
260 IF PASS$="ENFEW" THEN 360
270 IF LENGTH(PASS$)=5 THEN 280 ELSE 340
280 FOR X=1 TO 5
290 X$=RIGHT(STR(ASC(SUBSTR(PASS$,X,1))),2)
300 X4$=X4$+X$
310 NEXT X
320 IF X4$="827170706588" THEN 360 
330 X4$=""
340 IF R2=3 THEN 4660
350 GOTO 230
360 PRINT
370 PRINT 'Are you using a TYMSHARE 444 or a HONEYWELL?  (Y/N) ':
380 INPUT IN FORM "R":YN$
390 YN$=LEFT(YN$,1)
400 IF YN$="y" THEN YN$="Y"
410 GOSUB 3900
420 PRINT
430 PRINT
440 PRINT
450 FILE$="CD.DAT"
460 PRINT
470 PRINT '                             please wait...'
480 PRINT '                           loading DATA file...'
490 OPEN FILE$ FOR SYMBOLIC SEQUENTIAL INPUT AS FILE 1
500 ON ENDFILE 1 GOTO 560
510 CNT=CNT+1
520 FOR W=1 TO 7
530 INPUT FROM 1 IN FORM "R":NUMB(W,CNT)
540 NEXT W
550 GOTO 510
560 CLOSE 1
570 A$=""
580 B$=""
830 CNT=CNT-1
840 IF YN$="Y" THEN 850 ELSE 900
850 PRINT BB$;
860 PRINT
870 PRINT 
880 PRINT
890 PRINT
900 PRINT
910 CNTR=0
920 PC=0
930 PRINT '                  DISPATCH QUICK LIST'
940 PRINT
950 PRINT '               1. ADD NEW SYSTEM DATA'
960 PRINT '               2. CHANGE SYSTEM INFO'
970 PRINT '               3. DELETE DATA FROM FILE'
980 PRINT '               4. SEARCH FOR SYSTEM INFO'
990 PRINT '               5. QUICK LIST OF ALL SYSTEMS'
1000 PRINT '               6. END'
1010 PRINT
1020 PRINT '                    choose (1 - 6) ':
1030 INPUT L1
1040 IF L1<0 OR L1>6 THEN 840
1050 GOSUB 3900
1060 ON L1 GOTO 1100,2470,2290,1550,1520,3800
1070 REM *********************************************************************
1080 REM *                                 ADD                               *
1090 REM *********************************************************************
1100 CNT=CNT+1
1106 PRINT'ENTER NODE NUMBER AS IT APPEARS ON DISPATCH'
1107 PRINT'I.E., NODE 2660 WOULD BE ENTERED AS 0102660 ETC.'
1108 PRINT'OR ENTER "**" TO RETURN TO MENU.'
1109 PRINT
1110 PRINT 'ENTER SYSTEM I.D. : ':
1120 INPUT IN FORM "R":NUMB(1,CNT)                                !last name
1130 IF NUMB(1,CNT)="" THEN NUMB(1,CNT)=" "
1140 IF NUMB(1,CNT)="**" THEN 1150 ELSE 1170
1150 CNT=CNT-1
1160 GOTO 840                                                     !back to menu
1170 PRINT'      SYSTEM TYPE    : ':
1180 INPUT IN FORM "R":NUMB(2,CNT)
1190 IF NUMB(2,CNT)="" THEN NUMB(2,CNT)=" "
1200 FLA=1
1210 PRINT'    CUSTOMER NAME    : ':
1220 INPUT IN FORM "R":NUMB(3,CNT)
1230 IF NUMB(3,CNT)="" THEN NUMB(3,CNT)=" "
1240 PRINT'   STREET ADDRESS    : ':
1250 INPUT IN FORM "R":NUMB(4,CNT)
1260 IF NUMB(4,CNT)="" THEN NUMB(4,CNT)=" "
1270 PRINT'   CITY AND STATE    : ':
1280 INPUT IN FORM "R":NUMB(5,CNT)
1290 IF NUMB(5,CNT)="" THEN NUMB(5,CNT)=" "
1300 PRINT'  SYSTEM SERIAL #    : ':
1310 INPUT IN FORM "R":NUMB(6,CNT)
1320 IF NUMB(6,CNT)="" THEN NUMB(6,CNT)=" "
1330 PRINT'      ASSET TAG #    : ':
1340 INPUT IN FORM "R":NUMB(7,CNT)
1350 IF NUMB(7,CNT)="" THEN NUMB(7,CNT)=" "
1360 PRINT
1370 PRINT
1380 PRINT'      ADDITTIONAL INFORMATION'
1390 PRINT'      ----------- -----------'
1400 PRINT '          (four (4) lines of comments only, or hit <C/R> on the)'
1410 PRINT '          (remaining lines to terminate.                       )'
1420 PRINT
1430 FOR T=8 TO 11
1440 PRINT '   ->':
1450 INPUT IN FORM "R":NUMB(T,CNT)
1460 IF NUMB(T,CNT)="" THEN NUMB(T,CNT)=" "
1470 NEXT T
1480 GOSUB 3900
1490 GOTO 1100
1500 REM *********************************************************************
1510 REM *                             CHANGE                                *
1520 REM *********************************************************************
1530 IF L1<>5 THEN 1550
1540 GOTO 1640
1550 PRINT
1560 PRINT
1570 PRINT ' ENTER SEARCH STRING >':
1580 INPUT IN FORM "R":SEAR$
1590 CNTR=0
1600 LEG=LENGTH(SEAR$)                             !length of search string
1610 GOSUB 3900
1620 PRINT
1630 K=0
1640 FOR L=1 TO CNT
1650 IF L1=5 THEN 1870
1660 FOR Y=1 TO 11
1670 IF Y=3 OR Y=4 OR Y=5 OR Y=8 OR Y=9 OR Y=10 OR Y=11 THEN 1750
1680 IF NUMB(Y,L)=SEAR$ THEN 1870
1690 LEGG=LENGTH(NUMB(Y,L))
1700 IF LEGG<LEG THEN 1750
1710 LEGG=LEGG-LEG
1720 FOR R=1 TO LEGG
1730 IF SEAR$=SUBSTR(NUMB(Y,L),R,LEG) THEN 1870
1740 NEXT R
1750 NEXT Y
1760 NEXT L
1770 IF K=0 THEN 840
1780 PRINT
1790 PRINT
1800 IF L1=4 THEN 1810 ELSE 1830
1810 PRINT '                  enter (R)eturn, or (L)ook :':
1820 GOTO 2200
1830 PRINT '                     enter a <C/R> to continue ':
1840 INPUT IN FORM "R":DD$
1850 K=0
1860 GOTO 840
1870 K=K+1
1880 IF K>1 THEN 1950
1890 IF L1=4 THEN 1900 ELSE 1930
1900 PRINT 'NO.  SYSTEM I.D      TYPE            SERIAL #         ASSET TAG #'
1910 CNTR=0
1920 GOTO 1940
1930 PRINT '   SYSTEM I.D        TYPE            SERIAL #         ASSET TAG #'
1940 PRINT '*********************************************************************'
 1950 TEMP$=NUMB(1,L)+"........"+NUMB(2,L)+"............................."
1960 IF L1=4 THEN 1970 ELSE 1990
1970 TEMP$="   "+LEFT(TEMP$,30)+" "
1980 GOTO 2000
1990 TEMP$="      "+LEFT(TEMP$,30)+" "
2000 TEMP$=TEMP$+NUMB(6,L)+" ........................."
2010 TEMP$=LEFT(TEMP$,54)+" "
2020 TEMP$=TEMP$+NUMB(7,L)+" ......................."
2030 TEMP$=LEFT(TEMP$,69)
2040 PC=PC+1
2050 CNTR=CNTR+1
2060 IF L1=4 THEN 2070 ELSE 2150
2070 !
2080 PC$=RIGHT(STR(PC),LENGTH(STR(PC))-1)
2090 PC$=RIGHT("   "+PC$,3)
2100 TEMP9(PC)=L
2110 PRINT PC$:TEMP$
2120 IF CNTR>12 THEN 2130 ELSE 2250
2130 PRINT
2140 GOTO 2170
2150 PRINT TEMP$
2160 GOTO 2250
2170 !
2180 PRINT
2190 PRINT '                 enter (R)eturn, (C)ontinue, or (L)ook :':
2200 INPUT IN FORM "R":CNTR$
2210 IF LEFT(CNTR$,1)="R" THEN 840
2220 IF LEFT(CNTR$,1)="L" THEN 2730          !look at a name
2230 GOSUB 3900
2240 K=0
2250 GOTO 1760
2260 REM *******************************************************************
2270 REM *                              DELETE                             *
2280 REM *******************************************************************
2290 GOTO 2470
2300 PRINT '             Is this the correct record to DELETE (Y/N) :':
2310 INPUT IN FORM "R":DELL$
2320 IF LEFT(DELL$,1)="Y" THEN 2350
2330 IF LEFT(DELL$,1)="N" THEN 2680
2340 GOTO 2300
2350 GOSUB 3900
2360 PRINT
2370 PRINT '                           DELETING...'
2380 FOR TU=L+1 TO CNT
2390 FOR TU1=1 TO 11
2400 NUMB(TU1,TU-1)=NUMB(TU1,TU)
2410 NEXT TU1
2420 NEXT TU
2430 CNT=CNT-1
2440 FLA=1
2450 GOTO 840
2460 !
2470 GOSUB 3900
2480 PRINT
2490 Y=0
2500 K=0
2510 L=0
2520 NUM=0
2530 PRINT '  ENTER SEARCH STRING >':
2540 INPUT IN FORM "R":SEAR$
2550 LEG=LENGTH(SEAR$)
2560 GOSUB 3900
2570 CHNG=0
2580 FOR L=1 TO CNT
2590 FOR Y=1 TO 11
2600 IF SEAR$=NUMB(Y,L) THEN 2700
2610 LEGG=LENGTH(NUMB(Y,L))
2620 IF LEG>LEGG THEN 2670
2630 LEGG=LEGG-LEG
2640 FOR R=1 TO LEGG
2650 IF SEAR$=SUBSTR(NUMB(Y,L),R,LEG) THEN 2700
2660 NEXT R
2670 NEXT Y
2680 NEXT L
2690 IF K=0 THEN 840
2700 GOSUB 3900
2710 PRINT '   record : '+CHAR(39)+NUMB(1,L)+CHAR(39)
2720 GOTO 2800
2730 PRINT '      what is the number of the record :':
2740 INPUT IN FORM "R":REN$
2750 IF REN$="0" THEN 1050
2760 REN1=VAL(REN$)
2770 IF REN1>TEMP9(PC) THEN 1050
2780 L=TEMP9(REN1)
2790 GOSUB 3900
2800 PRINT
2810 IF L1=2 THEN 2820 ELSE 2830
2820 PRINT '1 ':
2830 PRINT'   SYSTEM I.D.      : '+NUMB(1,L)
2840 IF L1=2 THEN 2850 ELSE 2860
2850 PRINT '2 ':
2860 PRINT'   SYSTEM TYPE      : '+NUMB(2,L)
2870 IF L1=2 THEN 2880 ELSE 2890
2880 PRINT '3 ':
2890 PRINT'   CUSTOMER NAME    : '+NUMB(3,L)
2900 IF L1=2 THEN 2910 ELSE 2920
2910 PRINT '4 ':
2920 PRINT'   STREET ADDRESS   : '+NUMB(4,L)
2930 IF L1=2 THEN 2940 ELSE 2950
2940 PRINT '5 ':
2950 PRINT'   CITY AND STATE   : '+NUMB(5,L)
2960 IF L1=2 THEN 2970 ELSE 2980
2970 PRINT '6 ':
2980 PRINT'   SYSTEM SERIAL #  : '+NUMB(6,L)
2990 IF L1=2 THEN 3000 ELSE 3010
3000 PRINT '7 ':
3010 PRINT'   ASSET TAG #      : '+NUMB(7,L)
3020 PRINT
3030 PRINT'        ADDITTIONAL INFORMATION'
3040 PRINT
3050 IF L1=2 THEN 3060 ELSE 3070
3060 PRINT '8 ':
3070 PRINT '    '+NUMB(8,L)
3080 IF L1=2 THEN 3090 ELSE 3100
3090 PRINT '9 ':
3100 PRINT '    '+NUMB(9,L)
3110 IF L1=2 THEN 3120 ELSE 3130
3120 PRINT '10':
3130 PRINT '    '+NUMB(10,L)
3140 IF L1=2 THEN 3150 ELSE 3160
3150 PRINT '11':
3160 PRINT '    '+NUMB(11,L)
3170 PRINT
3180 PRINT
3190 IF L1=4 THEN 3200 ELSE 3230
3200 PRINT '                          enter a <C/R> to return :':
3210 INPUT IN FORM "R":CR$
3220 GOTO 840
3230 IF CHNG>0 THEN RETURN
3240 IF L1=3 THEN 2300
3250 PRINT '         (C)hange, (N)ext, or (R)eturn :':
3260 INPUT IN FORM "R":CNR$
3270 IF CNR$="R" OR CNR$="r" THEN 840
3280 IF CNR$="N" OR CNR$="n" THEN 2680
3290 IF CNR$="C" OR CNR$="c" THEN 3310
3300 GOTO 3250
3310 PRINT '   WHICH FIELD NEEDS CHANGING?  (1 - 11) :':
3320 INPUT CHNG
3330 IF CHNG<1 OR CHNG>11 THEN 3310
3340 IF CHNG=1 THEN 3350 ELSE 3360
3350 FIL$="SYSTEM I.D."
3360 IF CHNG=2 THEN 3370 ELSE 3380
3370 FIL$="SYSTEM TYPE"
3380 IF CHNG=3 THEN 3390 ELSE 3400
3390 FIL$="CUSTOMER NAME"
3400 IF CHNG=4 THEN 3410 ELSE 3420
3410 FIL$="STREET ADDRESS"
3420 IF CHNG=5 THEN 3430 ELSE 3440
3430 FIL$="CITY AND STATE"
3440 IF CHNG=6 THEN 3450 ELSE 3460
3450 FIL$="SYSTEM SERIAL"
3460 IF CHNG=7 THEN 3470 ELSE 3480
3470 FIL$="ASSET TAG #"
3480 IF CHNG=8 THEN 3490 ELSE 3500
3490 FIL$="1ST COMMENT LINE"
3500 IF CHNG=9 THEN 3510 ELSE 3520
3510 FIL$="2ND COMMENT LINE"
3520 IF CHNG=10 THEN 3530 ELSE 3540
3530 FIL$="3RD COMMENT LINE"
3540 IF CHNG=11 THEN 3550 ELSE 3560
3550 FIL$="4TH COMMENT LINE"
3560 GOSUB 3900
3570 PRINT
3580 PRINT '  The '+FIL$+' field has been picked'
3590 PRINT
3600 IF CHNG<8 THEN 3610 ELSE 3640
3610 PRINT '     OLD '+FIL$+' - '+NUMB(CHNG,L)
3620 PRINT '     NEW '+FIL$+' - ':
3630 GOTO 3680
3640 PRINT '     OLD '+FIL$+' :'
3650 PRINT '>'+NUMB(CHNG,L)
3660 PRINT '     NEW '+FIL$+' :'
3670 PRINT '>':
3680 INPUT IN FORM "R":NUMB(CHNG,L)
3690 IF NUMB(CHNG,L)="" THEN NUMB(CHNG,L)=" "
3700 FLA=1
3710 GOSUB 2700
3720 PRINT '    Is this correct   (Y/N) :':
3730 INPUT IN FORM "R":COR$
3740 COR$=LEFT(COR$,1)
3750 IF COR$="Y" THEN 840                 !main menu
3760 IF COR$="N" THEN 3770 ELSE 3720
3770 CHNG=0
3780 GOTO 3310
3790 !
3800 REM ********************************************************************
3810 REM *                                END                               *
3820 REM ********************************************************************
3830 GOSUB 3900
3840 PRINT
3850 PRINT
3860 PRINT
3870 IF FLA=1 THEN 3880 ELSE 4620
3880 PRINT '                            SAVING DATA FILE....'
3890 GOTO 3970
3900 IF YN$="Y" THEN 3910 ELSE 3950
3910 PRINT BB$;
3920 PRINT
3930 PRINT
3940 PRINT
3950 PRINT
3960 RETURN
3970 !
4550 OPEN FILE$+'-A' FOR SYMBOLIC SEQUENTIAL OUTPUT AS FILE 1
4560 FOR EN=1 TO CNT
4570 FOR W=1 TO 7
4580 WRITE ON 1 IN FORM "R":NUMB(W,EN)
4590 NEXT W
4600 NEXT EN
4610 CLOSE 1
4620 PRINT
4630 PRINT
4640 PRINT
4650 PRINT '                             END!'
4660 PRINT
4670 PRINT
    