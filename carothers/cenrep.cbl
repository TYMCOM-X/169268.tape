IDENTIFICATION DIVISION.
PROGRAM-ID. CENREP.
DATE-WRITTEN. 1-APR-79.
DATE-COMPILED.
REMARKS. PRODUCE REPORTS FROM CENTREX DATABASE.

ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. PDP-10.
OBJECT-COMPUTER. PDP-10.
INPUT-OUTPUT SECTION.
FILE-CONTROL.

    SELECT DATA-FILE ASSIGN TO DSK DSK
        RECORDING MODE IS SIXBIT
        ACCESS MODE IS INDEXED
        SYMBOLIC KEY IS ACC-KEY, RECORD KEY IS REC-KEY.

    SELECT SORT-FILE ASSIGN TO DSK DSK DSK
        RECORDING MODE IS SIXBIT.

    SELECT REPORT-FILE ASSIGN TO DSK
        RECORDING MODE IS ASCII.

DATA DIVISION.
FILE SECTION.
FD DATA-FILE
    LABEL RECORD IS STANDARD
    DATA RECORD IS TEL-REC, FIRST-REC
    BLOCK CONTAINS 20 RECORDS
    VALUE OF IDENTIFICATION IS 'CENTRXIDX'.

01  TEL-REC.
    03  REC-KEY.
        05 FILLER                      PIC X(8).
        05 TEL-EXT                     PIC X(4).
    03  PERSON-NAME                    PIC X(20).
    03  SERVICE-TYPE                   PIC X(12).
    03  LOC                            PIC X(6).
    03  LONG-DISTANCE-CURR             PIC S9(5)V99     USAGE COMP.
    03  LONG-DISTANCE-1BAK             PIC S9(5)V99     USAGE COMP.
    03  LONG-DISTANCE-2BAK             PIC S9(5)V99     USAGE COMP.
    03  MONTHLY-COST                   PIC S9(5)V99     USAGE COMP.

01  FIRST-REC.
    03  REC-1-KEY                      PIC X(12).
    03  TOTAL-RECORDS                  PIC S9(6)        USAGE COMP.
FD REPORT-FILE
    LABEL RECORD IS STANDARD
    DATA RECORD IS REPORT-REC
    VALUE OF IDENTIFICATION IS REPORT-NAME.

01  REPORT-REC                         PIC X(105)       USAGE DISPLAY-7.

SD SORT-FILE.

01  SORT-REC.
    03  TEL-EXT-S                      PIC X(12).
    03  PERSON-NAME-S                  PIC X(20).
    03  SERVICE-TYPE-S                 PIC X(12).
    03  LOC-S                          PIC X(6).
    03  FILLER                         PIC X(36).

WORKING-STORAGE SECTION.

    77 T-R-HOLD                        PIC S9(6)        USAGE COMP.
    77 INV-KEY-SW                      PIC X            VALUE ZERO.
        88 INVKEY                      VALUE IS '1'.
    77 MORE-REPORTS                    PIC X            VALUE ZERO.
        88 NO-MORE                     VALUE IS '1'.
    77 NAME-6                          PIC X(9)         USAGE DISPLAY-6.
    77 ANS-SPACE                       PIC S9           USAGE COMP.
        88  ANS-YES                    VALUE IS 1.
    77 REPORT-CHOICE                   PIC S9.
    77 SORT-CHOICE                     PIC S9.
    77 LOW-KEY                         PIC X(12)        VALUE '000000000001'.
    77 PHON-PREF                       PIC X(8)         VALUE '415-446-'.
    77 REPORT-NAME                     PIC X(9).

01  FN-OUT.
    03  FN.
        05 FN-1                         PIC X(6).
        05 FILLER                       PIC X(4).
    03  FN1 REDEFINES FN               PIC X(10)        JUSTIFIED RIGHT.
    03  FN2 REDEFINES FN1.
        05  FILLER                     PIC X(6).
        05  DOT                        PIC X.
        05  FILLER                     PIC XXX.

01  IN-NAME.
    03  IN-CHAR                        PIC X            OCCURS 20 TIMES
        INDEXED BY I-IND.
01  NAME-OUT.
    03  OUT-CHAR                       PIC X            OCCURS 9 TIMES
        INDEXED BY O-IND.
01  ACC-KEY.
    03  FILLER                         PIC X(8).
    03  ACCESS-KEY                     PIC X(4).

01  DETAIL-LINE       USAGE DISPLAY-7.
    03  FILLER                         PIC X(5)         VALUE SPACES.
    03  NAME-O                         PIC X(20).
    03  FILLER                         PIC X(5)         VALUE SPACES.
    03  TYPE-O                         PIC X(5).
    03  FILLER                         PIC X(5)         VALUE SPACES.
    03  LOC-O                          PIC X(6).
    03  FILLER                         PIC X(5)         VALUE SPACES.
    03  LD-CURR-O                      PIC $(7).99.
    03  LD-1BAK-O                      PIC $(7).99.
    03  LD-2BAK-O                      PIC $(7).99.
    03  MONTHLY-O                      PIC $(7).99.
PROCEDURE DIVISION.

    OPEN I-O DATA-FILE.
    MOVE LOW-KEY TO ACC-KEY.
    READ DATA-FILE INVALID KEY 
        DISPLAY ' ' DISPLAY 'NO CONTROL RECORD.'
        CLOSE DATA-FILE
        STOP RUN.
    DISPLAY ' ' DISPLAY 'CENTREX REPORT GENERATOR,   VERSION 1.0'
    DISPLAY 'TOTAL RECORDS = ', TOTAL-RECORDS, '.'.
    MOVE TOTAL-RECORDS TO T-R-HOLD.
    PERFORM GET-REPORT-CHOICE.
    PERFORM GET-SORT-CHOICE.
    PERFORM GET-OUT-FILE.
    OPEN OUTPUT REPORT-FILE.
    PERFORM MAIN-SEC UNTIL NO-MORE.
    CLOSE DATA-FILE.
    STOP RUN.

GET-REPORT-CHOICE.
    DISPLAY ' ' DISPLAY 'REPORT TYPE :' DISPLAY ' '.
    DISPLAY '1. ALL,  2. LONG DISTANCE > 0,  3. DISCONNECTED.'.
    DISPLAY 'ENTER NUMBER : ' WITH NO ADVANCING.
    ACCEPT REPORT-CHOICE.
    IF REPORT-CHOICE = (1 OR 2 OR 3) NEXT SENTENCE
        ELSE DISPLAY 'BAD CHOICE, TRY AGAIN.'
             GO TO GET-REPORT-CHOICE.

GET-SORT-CHOICE.
    DISPLAY ' ' DISPLAY 'SORT BY :' DISPLAY ' '.
    DISPLAY '1. EXTENTION,  2. NAME,  3. LOCATION,  4. LOCATION/NAME.'.
    DISPLAY 'ENTER NUMBER : ' WITH NO ADVANCING.
    ACCEPT SORT-CHOICE.
    IF SORT-CHOICE = (1 OR 2 OR 3 OR 4) NEXT SENTENCE
        ELSE DISPLAY ' ' DISPLAY 'BAD CHOICE, TRY AGAIN.'
             GO TO GET-SORT-CHOICE.

GET-OUT-FILE.
    MOVE SPACE TO NAME-OUT.
    SET O-IND TO ZERO.
    DISPLAY ' ' DISPLAY 'OUTPUT FILE NAME : ' WITH NO ADVANCING.
    ACCEPT IN-NAME.
    EXAMINE IN-NAME TALLYING ALL '('.
    IF TALLY NOT = ZERO DISPLAY 'NO USERNAMES PLEASE.'
        GO TO GET-OUT-FILE.
    PERFORM GF1 VARYING I-IND FROM 1 BY 1 UNTIL
        I-IND > 6 OR IN-CHAR (I-IND) = ('.' OR ' ').
    IF IN-CHAR (I-IND) = '.'
        PERFORM GF3 UNTIL O-IND = 6
        PERFORM GF2 3 TIMES
    ELSE IF IN-CHAR (I-IND) NOT = ' '
        PERFORM GF4 UNTIL IN-CHAR (I-IND) = ('.' OR ' ') OR I-IND = 19
        IF I-IND = 19 NEXT SENTENCE
            ELSE PERFORM GF2 UNTIL O-IND = 9 OR I-IND = 19.
    MOVE NAME-OUT TO NAME-6.
    ENTER MACRO LOOKIT USING ANS-SPACE, NAME-6.
    IF ANS-YES PERFORM NAME-DIS
               DISPLAY '  EXISTING FILE.' WITH NO ADVANCING
        ENTER MACRO ANSWER USING ANS-SPACE
        IF ANS-YES MOVE NAME-OUT TO REPORT-NAME
        ELSE GO TO GET-OUT-FILE
    ELSE PERFORM NAME-DIS
         DISPLAY '  NEW FILE.' WITH NO ADVANCING
        ENTER MACRO ANSWER USING ANS-SPACE
        IF ANS-YES MOVE NAME-OUT TO REPORT-NAME
        ELSE GO TO GET-OUT-FILE.

GF1.
    SET O-IND UP BY 1.
    MOVE IN-CHAR (I-IND) TO OUT-CHAR (O-IND).
GF2.
    SET I-IND, O-IND UP BY 1.
    MOVE IN-CHAR (I-IND) TO OUT-CHAR (O-IND).
GF3.
    SET O-IND UP BY 1.
    MOVE SPACE TO OUT-CHAR (O-IND).
GF4.
    SET I-IND UP BY 1.
NAME-DIS.
    MOVE NAME-OUT TO FN1.
    MOVE NAME-OUT TO FN-1.
    MOVE '.' TO DOT.
    DISPLAY FN-OUT WITH NO ADVANCING.
MAIN-SEC.
    IF SORT-CHOICE = 1 PERFORM SORT-1
    ELSE IF SORT-CHOICE = 2 PERFORM SORT-2
    ELSE IF SORT-CHOICE = 3 PERFORM SORT-3
    ELSE IF SORT-CHOICE = 4 PERFORM SORT-4.
    DISPLAY ' ' DISPLAY 'MORE' WITH NO ADVANCING.
    ENTER MACRO ANSWER USING ANS-SPACE.
    IF ANS-YES
        PERFORM GET-REPORT-CHOICE
        PERFORM GET-SORT-CHOICE
        DISPLAY ' ' DISPLAY 'APPEND TO SAME FILE.' WITH NO ADVANCING
        ENTER MACRO ANSWER USING ANS-SPACE
        IF ANS-YES NEXT SENTENCE
        ELSE CLOSE REPORT-FILE
            PERFORM GET-OUT-FILE
            OPEN OUTPUT REPORT-FILE
    ELSE MOVE '1' TO MORE-REPORTS.

SORT-1.
    SORT SORT-FILE ASCENDING KEY TEL-EXT-S
        INPUT PROCEDURE IS INP-PROC
        OUTPUT PROCEDURE IS OUT-PROC.
SORT-2.
    SORT SORT-FILE ASCENDING KEY PERSON-NAME-S
        INPUT PROCEDURE IS INP-PROC
        OUTPUT PROCEDURE IS OUT-PROC.
SORT-3.
    SORT SORT-FILE ASCENDING KEY LOC-S, TEL-EXT-S
        INPUT PROCEDURE IS INP-PROC
        OUTPUT PROCEDURE IS OUT-PROC.
SORT-4.
    SORT SORT-FILE ASCENDING KEY LOC-S, PERSON-NAME-S
        INPUT PROCEDURE IS INP-PROC
        OUTPUT PROCEDURE IS OUT-PROC.
INP-PROC SECTION.
IN-1.
    MOVE LOW-KEY TO ACC-KEY.
    PERFORM READ-FILE.
    MOVE LOW-VALUES TO ACC-KEY.
    PERFORM READ-FILE.
    PERFORM FEED-SORT UNTIL INVKEY.
    GO TO EXIT-IN-SEC.

READ-FILE.
    MOVE ZERO TO INV-KEY-SW.
    READ DATA-FILE INVALID KEY MOVE '1' TO INV-KEY-SW.

FEED-SORT.

    RELEASE SORT-REC FROM TEL-REC.
    PERFORM READ-FILE.

EXIT-IN-SEC.  EXIT.

OUT-PROC SECTION.
OUT-1.
    PERFORM GET-SORT.
    PERFORM PRINT-REP UNTIL INVKEY.
    GO TO EXIT-OUT-SEC.

GET-SORT.
    MOVE ZERO TO INV-KEY-SW.
    RETURN SORT-FILE AT END MOVE '1' TO INV-KEY-SW.

PRINT-REP.
    DISPLAY TEL-EXT-S.
    DISPLAY PERSON-NAME-S.
    DISPLAY SERVICE-TYPE-S.
    DISPLAY LOC-S.
    DISPLAY ' '.
    PERFORM GET-SORT.

EXIT-OUT-SEC.  EXIT.
 