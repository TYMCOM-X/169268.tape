100 !  THIS PROGRAM WILL KEEP A SUPMOVE DATABASE AND CREATE REPORT FILES OF:
105 !       (1) DAILY SUPMOVE LOG
110 !       (2) NON-SCHEDULE SUPMOVES ON TTY
115 !  DATA INPUT ROUTINE BY CONTROLLER COMPLETED.
120 !  DATA OUTPUT AFTER ORIGINAL ENTRY COMPLETED.
125 !  CREATION OF TAKEOVER TIME ROUNTINE COMPLETED (SUB 2000).
130 !  DATA FILE INPUT ROUTINE COMPLETED (SUB 3000).
135 !  ADDRESSING AND STORING NON-SCHEDULED SUPMOVE DATABASE (SUB 4000)
140 !  COMPLETED.
145 !     -ADDRESSING DONE USING GMT, YEAR, AND TSLEEP COMBINED.
150 !  GENERATING SUPLOG REPORT COMPLETED (SUB 10000).
155 !  GENERATING NONSCHEDULED SUPMOVE REPORT FOR TTY COMPLETED (SUB 11000)
160 !  GENERATING SUP TAKEOVER TIME REPORT COMPLETED.
165 !
170 !  THIS VERSION GENERATES ASTAT REPORT (SUB 14000)
175 !               GENERATES ALL REPORTS TO FILES
180 !
181 PRINT
182 PRINT "                      VERSION 12"
183 PRINT
184 PRINT
185 ! DATA FIELDS
190 !
195 STRING DATAIO
200 DATAIO="3% 1% 2(3% 3(2%)) 2(4%) 3(2%) 3% 4(3(2%)) 2(2%) 3(2%) 3(4%) /"
205 !
210 STRING CONTNAME(1200,2)  ! INTIALS + SCHEDULED/NON-SCHEDULED MOVE
215 !
220 INTEGER DATES(1200,4), TSLEEP(1200,3), TWAKE(1200,3), LOGINS(1200,3)
225 INTEGER TOTIME(1200,2), NODCNTRL(1200,1:3)
230 INTEGER SUPNO(1200,8), NODES(1200,2)
235 INTEGER ASTAT(1200,6)  ! TIME / TOT FIAL / TOT REBUILD
240 INTEGER PERIOD(2,2),RPTTYPE
245 !
250 ! SUPBINDER FIELDS (SUB 1000)
255 !
260 STRING SUPBIND(9)
265 SUPBIND(1)="20B 10('-') 1B 'SUPMOVE'1B 10('-') /// 20B 'OLD SUP NUMBER (3 DIGITS):' 1B 3% //"
270 SUPBIND(2)="20B 'NEW SUP NUMBER (3 DIGITS):' 1B 3% ///"
275 SUPBIND(3)="5B 'SCHEDULED MOVE? (Y OR N):' 3B 1% ///"
280 SUPBIND(4)="22B 'DATE/TIME (DDD:HH:MM:SS)' / 22B 25('-') // 21B 'OLD SUP SLEPT:'"
285 SUPBIND(5)="18B 'NEW SUP AWAKENED:'"
290 SUPBIND(6)="17B 'PROCESSING LOGINS:'"
295 SUPBIND(7)="13B 'IN CONTROL OF ALL NET:'"
300 SUPBIND(8)="56B 'CONTROLLER:' 2B 3% /"
305 !
310 SUPBIND(9)="2B 3D ':' 2D 2(':' 2D) ///"
315 !
320 ! SUPLOG FIELDS
325 !
330 STRING SUPLOGPRT, SUPLOGPRT2, SUPLOGTITLE, SUPLOGTITLE2, SUPLOGTITLE3, SUPLOGTITLE4
335 SUPLOGTITLE="40B 'SUPLOG FOR ' 1B 3% '/19' 2% //"
340 SUPLOGTITLE2="15B 'SUP' 10B 'NODES' /"
345 SUPLOGTITLE3="5B 'DATE/' 5B 'OLD/' 8B 'BEFORE/' 1B 'TSLEEP/' 11B 'CONTROL NET/' 3B 'ASTAT' 3B 'TOT' 2B 'TOT' 4B 'PEAK' /"
350 SUPLOGTITLE4="1B 'INI' 2B 'GMT' 2B 'SCH' 1B 'NEW' 1B 'TIME CMD' 1B 'AFTER' 3B 'TWAKE' 3B 'LOGINS' 2B 'TAKEOVER TIME' 4B 'TIME' 3B 'REQ' 1B 'FAILS' 1B 'REBUILD' //"
355 SUPLOGPRT="1B 3% 1B 2D '/' 2D 2B 1% 2B 3D 1B 2D ':' 2D ':' 2D 1B 4D 2B 2(2D ':' 2D ':' 2D 1B) 2(3B 2D ':' 2D ':' 2D) 1B 2(4% 1B) 2B 4% /"
360 SUPLOGPRT2="6B 3D 6B 3D 1B 2D ':' 2D  ':' 2D 1B 4D 2B 2D ':' 2D ':' 2D 16B 2D ':' 2D /"
365 INTEGER MTHCHG(2)
370 !
375 !  TAKEOVER TIME FIELDS
377 !  ***ENTER GO TO STATEMENT HERE BACK TO RREPORT COMMANDS ***
380 !
385 STRING TOTIMETTY
390 TOTIMETTY="'TAKEOVER TIME WAS' 2B 2% 1B 'MINS.' 2B 2% 1B 'SECS.' /"
395 INTEGER TWAKESEC, LOGINSEC
400 !
405 !  NON-SCHEDULED SUPMOVE FIELDS
410 !
415 STRING NOSCHEDINFO(500),NOSCHEDDAT,NOSCHEDOP
420 NOSCHEDDAT="3% 4(2%) 60% /"
425 NOSCHEDOP="5B 60% /"
430 INTEGER NONENTTOT, NOSCHEDADD(500,5)
435 !
440 !  NON-SCHEDULED SUPMOVE REPORT FIELDS
445 !
450 STRING NSCHRPT1,NSCHRPT2,NSCHRPT3,NSCHRPT4,NSCHRPTOP1,NSCHRPTOP2
455 NSCHRPT1="40(/) 12B 'UNSCHEDULED SUPMOVES FOR' 2B 2% '/' 2% 1B 'TO' 1B 2% '/' 2% //"
460 NSCHRPT2="11B 'SUP' 10B 'NODES' /"
465 NSCHRPT3="6B 'DATE' 1B 'OLD/' 8B 'BEFORE/' 1B 'TSLEEP/' 11B 'CONTROL NET/' /"
470 NSCHRPT4="1B 'INI' 2B 'GMT' 2B 'NEW' 1B 'TIME CMD' 1B 'AFTER' 3B 'TWAKE' 3B 'LOGINS' 2B 'TAKEOVER TIME' /"
475 NSCHRPTOP1="/ 1B 3% 1B 2D '/' 2D 1B 3D 1B 2D ':' 2D ':' 2D 1B 4D 2B 2(2D ':' 2D ':' 2D 1B) 3B 2D':' 2D ':' 2D /"
480 NSCHRPTOP2="6B 3D 2B 3D 1B 2D ':' 2D ':' 2D 1B 4D 2B 2D ':' 2D ':' 2D 16B 2D ':' 2D /"
485 !
490 !  SUPMOVE TAKEOVER SUMMARY FIELDS
495 !
500 INTEGER SUPMOVARR(100,27), TOTMMSET(1,2), MMCNT
505 !
510 !  SUP TAKEOVER TIMES REPORT FIELDS
515 !
520 STRING SUPTORPT1,SUPTORPT2,SUPTORPT3,SUPTORPT4,SUPTORPT5,SUPTORPT6
525 SUPTORPT1="25B '121' 4B '122' 4B '123' 4B '124' 4B '125' / 25B 5('---' 4B) //"
530 SUPTORPT2="5B 2% '/' 2% 2B 'TOT MOVES' 5B 5(2D 5B) /"
535 SUPTORPT3="12B 'TOT TIME' 4B 5(2D ':' 2D 2B) /"
540 SUPTORPT4="12B 'AVG TIME' 4B 5(2D ':' 2D 2B) //"
545 SUPTORPT5="40(/) 12B 'SUP TAKEOVER TIME REPORT FOR' 2B 2% '/' 2% 1B 'TO' 1B 2% '/' 2% //"
550 SUPTORPT6="5B 2% '/' 2% 18B '********** NONE **********' //"
555 !
560 !  ASTAT REPORT FIELDS
565 !
570 STRING ASTATTITLE, ASTATTITLE2, ASTATTITLE3, ASTATOP
575 ASTATTITLE="26B 'ASTAT REPORT FOR' 1B 3% '/19' 2% //"
580 ASTATTITLE2="36B 'TOTAL' 6B 'TOT' 8B 'PEAK' /"
585 ASTATTITLE3="11B 'DATE' 5B 'GMT' 5B 'SUP' 6B 'REQS' 5B 'FAILS' 5B 'REBUILD' //"
590 ASTATOP="10B 2% '/' 2% 5B 3% 5B 3% 6B 4% 6B 4% 7B 4% /"
595 !
600 REMOVE "SUPLOG.TMP-A"
605 REMOVE "SUPLOG.RPT-A"
610 REMOVE "ASTAT.RPT-A"
615 REMOVE "SUPNON.RPT-A"
620 REMOVE "SUPTO.RPT-A"
625 !
630 OPEN "SUPMOV.DAT-A" FOR RANDOM IO AS FILE 1  ! SUPMOVE DATA FILE
635 OPEN "SUPLOG.TMP-A" FOR RANDOM IO AS FILE 2  ! PRINTOUT FOR BINDER
640 OPEN "SUPNON.DAT-A" FOR RANDOM IO AS FILE 3
645 OPEN "SUPLOG.RPT-A" FOR RANDOM IO AS FILE 4  ! SUPLOG REPORT FILE
650 OPEN "ASTAT.RPT-A" FOR RANDOM IO AS FILE 5  ! ASTAT REPORT FILE
655 OPEN "SUPNON.RPT-A" FOR RANDOM IO AS FILE 6
660 OPEN "SUPTO.RPT-A" FOR RANDOM IO AS FILE 7
665 STRING ALPMM  ! DATA RECEIVED BACK ON ALPMTH SUBPROGRAM
670 !
675 DEF STRING EXTERNAL TBA ALPMTH(INTEGER MTH) ! SUBPROG NUMBER TO ALPHABETIC
680 !
685 B=SIZE(1)
690 LOCATE B+1 ON 1
695 !
700 B=SIZE(3)
705 LOCATE B+1 ON 3
710 IIM "DO YOU WISH TO INPUT DATA? #": Y$
715 IF Y$#"Y" AND Y$#"N" THEN
720      PRINT "ENTER Y OR N.  TRY AGAIN."
725      GO TO 710
730 ENDIF
735 IF Y$="Y" THEN GOSUB 1000  ! GENERATE BINDER REPORT AND O/P TO DB
740 !
745 PIF "2(/)"
750 PRINT "TYPE OF REPORT TO PRINT (PICK A NUMBER):"
755 PRINT
760 PRINT "   (1)  SUPMOVE LOG                    FILE SUPLOG.RPT IS GENERATED"
765 PRINT "   (2)  UNSCHEDULED SUPMOVE REPORT     FILE SUPNON.RPT IS GENERATED"
770 PRINT "   (3)  SUPMOVE TAKEOVER REPORT        FILE SUPTO.RPT IS GENERATED"
775 PRINT "   (4)  ASTAT REPORT                   FILE ASTAT.RPT IS GENERATED"
776 PRINT "   (5)  ALL REPORTS"
780 PRINT "   (6)  EXIT"
785 PRINT
790 IIM "INPUT NUMBER: #": RPTTYPE
795 PRINT
800 IF RPTTYPE=6 THEN 895
801 IF RPTTYPE=7 THEN
802      PRINT "100 = TEST ROUTINE FOR TOTIME CALCULATION"
803      PRINT "101 = CONVERTS TSLEEP/LOGINS TO TWAKE/LOGINS"
804      PRINT "102 = SORTS SUPMOVE DATA FILE"
805      PRINT "103 = CONVERTS SUPMOVE DATABASE PDP-10 TO UNIX"
806      IIM "ENTER 100-103: #": RPTTYPE
808 ENDIF
809 IF RPTTYPE<6 THEN
810      IIM "SPECIFIC PERIOD (Y OR N)? #": YYY$
815      IF YYY$#"N" AND YYY$#"Y" THEN
820           PRINT "REENTER PLEASE."
825           GO TO 810
830      ENDIF
835      IF YYY$="N" THEN 850
840      IIM "   INPUT BEGINNING MONTH/YEAR (IE. 12,87): # #": PERIOD(1,1:2)
845      IIM "   INPUT ENDING MONTH/YEAR (IE. 3,88): # #": PERIOD(2,1:2)
850 ENDIF
851 IF RPTTYPE=5 THEN
852      RPTTYPE=2
853      GOSUB 3000
854      GOSUB 11000  ! GENERATING SUPNON REPORT
855      RPTTYPE=1
856      GOSUB 3000
857      GOSUB 10000  ! GENERATING SUPLOG REPORT
858      RPTTYPE=3
859      GOSUB 12000  ! GENERATING SUPTO REPORT
860      RPTTYPE=4
861      GOSUB 14000  ! GENERATING ASTAT REPORT
862      GO TO 895
863 ENDIF
865 GOSUB 3000
868 IF RPTTYPE=1 THEN GOSUB 10000  ! GENERATE SUPLOG REPORT
869 IF RPTTYPE=2 THEN GOSUB 11000  ! GENERATE UNSCHEDULE SUPMOVE REPORT
870 IF RPTTYPE=3 THEN GOSUB 12000  ! GENERATE SUP TAKEOVER TIME REPORT
875 IF RPTTYPE=4 THEN GOSUB 14000  ! GENERATE ASTAT REPORT
880 IF RPTTYPE=100 THEN GO TO 20000  ! TEST ROUTINE FOR TOTIME CALCULATION
885 IF RPTTYPE=101 THEN GO TO 21000  ! CONVERTS TSLEEP/LOGINS TO TWAKE/LOGINS
886 IF RPTTYPE=102 THEN  ! SORTS SUPMOV.DAT FILE
887      YYY$="N"
888      GOSUB 3000  ! INPUT DATA FILES
889      GO TO 6000  ! SORT DATA FILES
890 ENDIF
891 IF RPTTYPE=103 THEN GOSUB 22000  !  CONVERTS SUPMOVE DB PDP-10 TO UNIX
894 GO TO 745
895 CLOSE 1,2,3,4,5,6,7
900 STOP ""
905 END
1000 !
1010 !  ***********************************************************
1020 !  *** INPUT DATA FOR BINDER REPORT AND OUTPUT TO DATABASE ***
1030 !  ***********************************************************
1040 !
1050 IIM "INPUT DATE (IE: 11,14,87): # # #": DATES(1,1:3)
1060 IIM "      JULIAN DAY (IE: 318): #": DATES(1,4)
1070 IIM "      OLD SUP (IE: 125): #": SUPNO(1,1)
1080 IIM "      SUP TIME COMMAND: # # #": SUPNO(1,2:4)
1090 IIM "      NEW SUP: #": SUPNO(1,5)
1100 IIM "      SUP TIME COMMAND: # # #": SUPNO(1,6:8)
1110 ! 
1120 IF SUPNO(1,1)=SUPNO(1,5) THEN
1130      PRINT "SUP NUMBERS ARE THE SAME. TRY AGAIN."
1140      GO TO 1070
1150 ENDIF
1160 !
1170 IIM "      NODES BEFORE: #": NODES(1,1)
1180 IIM "      NODES AFTER: #": NODES(1,2)
1190 PRINT
1200 IIM "SCHEDULED? #": Y$
1210 PRINT
1220 PRINT "ENTER FOLLOWING TIMES IN FORM 11,00,08 (DO NOT USE COLONS)."
1230 PRINT
1240 IIM "SLEEP TIME: # # #": TSLEEP(1,1:3)
1250 IIM "TWAKE TIME : # # #": TWAKE(1,1:3)
1260 IIM "LOGINS: # # #": LOGINS(1,1:3)
1270 IIM "CONTROL OF NODES: # # #": NODCNTRL(1,1:3)
1280 !
1290 GOSUB 2000  ! ROUTINE TO HAVE TAKEOVER TIME CALCULATED
1300 !
1310 PRINT "NOW ENTER ASTAT INFORMATION:"
1320 PRINT
1330 IIM "TIME ASTAT WAS RUN: # # #": ASTAT(1,1:3)
1340 IIM "TOTAL REQUEST: #": ASTAT(1,4)
1350 IIM "TOTAL FAILURE: #": ASTAT(1,5)
1360 IIM "PEAK REBUILD: #": ASTAT(1,6)
1370 IF Y$="NO" OR Y$="N" THEN GOSUB 4000 ELSE CONTNAME(1,2)="Y"
1380 PRINT
1390 !
1400 IIM "ENTER YOUR INTIALS: #": CONTNAME(1,1)
1410 IIM "ALL ENTRIES CORRECT? #": Y$
1420 IF Y$="NO" OR Y$="N" THEN 1050
1430 PRINT
1440 GOSUB 13010  ! GENERATE PRINTOUT FOR BINDER
1450 PRINT ON 1 IN FORM DATAIO: CONTNAME(1,1:2),SUPNO(1,1:8),NODES(1,1:2),DATES(1,1:4),TSLEEP(1,1:3),TWAKE(1,1:3),LOGINS(1,1:3),NODCNTRL(1,1:3),TOTIME(1,1:2),ASTAT(1,1:6)
1460 IF COUNTER>0 THEN
1470      PRINT ON 3 IN FORM NOSCHEDDAT: NOSCHEDADD(T,1:5),NOSCHEDINFO(T)  FOR T=1 TO COUNTER
1480 ENDIF
1490 !
1500 ! STOP PROGRAM HERE IF DATA ENTERED FOR BINDER PRINTOUT BY CONTROLLER
1510 !
1520 CLOSE 1,2,3,4
1530 STOP "END OF RUN"
1540 END
2000 !
2010 !  **********************************
2020 !  *** GENERATE SUP TAKEOVER TIME ***
2030 !  **********************************
2040 !
2050 ! SETTING X FOR CALCULATION WHEN INPUTTING FOLLOWING SUPMOVE
2060 X=1
2070 !
2080 LOGINSEC=0
2090 TWAKESEC=0
2100 TOTIME(X,1:2)=0
2110 !
2120 LOGINSEC=(LOGINS(X,2)*60)+LOGINS(X,3)  ! LOGIN TIME MINS TO SECS
2130 !
2140 IF TWAKE(X,1)#LOGINS(X,1) THEN 2240  ! TWAKE BEFORE HOUR/TWAKE AFTER HOUR
2150 !
2160 !  TWAKE / TWAKE HOUR SAME
2170 !
2180 TWAKESEC=(TWAKE(X,2)*60)+TWAKE(X,3)  ! CONVERTS TWAKE TO SECONDS
2190 GO TO 2310
2200 !
2210 !  TWAKE/TWAKE DIFFERENT HOUR
2220 !
2230 !  NEXT LINE CONVERTS HOURS (IE 1457 BECOMES 3 MINUTES AFTER HOUR)
2240 TWAKESEC=60-TWAKE(X,2)
2250 IF TWAKE(X,3)#0 THEN
2260      TWAKESEC=((TWAKESEC-1)*60)+(60-TWAKE(X,3))
2270    ELSE
2280      TWAKESEC=TWAKESEC*60
2290 ENDIF
2300 !
2310 IF TWAKE(X,1)#LOGINS(X,1) THEN
2320      LOGINSEC=LOGINSEC+TWAKESEC  ! CHANGE OF SUP STARTED BEFORE AND ENDED AFTER HOUR
2330    ELSE
2340      LOGINSEC=LOGINSEC-TWAKESEC  ! CHANGE OF SUP OCCURRED TOTALLY AFTER HOUR
2350 ENDIF
2360 !
2370 IF RPTTYPE=3 THEN RETURN  ! BACK TO SUP TAKEOVER SUMMARY REPORT
2380 !
2390 !  CONVERT TAKEOVER TIME TO MINUTES FROM SECONDS
2400 !
2410 IF LOGINSEC<60 THEN 2450
2420 LOGINSEC=LOGINSEC-60
2430 TOTIME(X,1)=TOTIME(X,1)+1
2440 GO TO 2410
2450 TOTIME(X,2)=LOGINSEC  ! LESS THAN A MINUTE REMAINING
2460 PIF TOTIMETTY: TOTIME(X,1:2)
2470 !
2480 RETURN
3000 !
3010 !  ************************
3020 !  *** INPUT DATA FILES ***
3030 !  ************************
3040 !
3050 INTEGER ENTTOT
3060 !
3070 ENTTOT=1
3080 LOCATE 1 ON 1
3090 ON ENDFILE(1) GO TO 3420
3100      INPUT FROM 1 IN FORM DATAIO: CONTNAME(ENTTOT,1:2),SUPNO(ENTTOT,1:8),NODES(ENTTOT,1:2),DATES(ENTTOT,1:4),TSLEEP(ENTTOT,1:3),TWAKE(ENTTOT,1:3),LOGINS(ENTTOT,1:3),NODCNTRL(ENTTOT,1:3),TOTIME(ENTTOT,1:2),ASTAT(ENTTOT,1:6)
3110 ! ROUTINE WILL ALLOW ONLY NON-SCHEDULED DATA INPUTTED
3120 !
3130 IF RPTTYPE=2 THEN
3140      IF CONTNAME(ENTTOT,2)="Y" THEN 3400
3150 ENDIF
3160 !
3170 ! THIS ELIMATES DATA NOT IN SPECIFIED TIME PERIOD
3180 !
3190 IF YYY$="N" THEN 3390
3200 !
3210 IF PERIOD(1,2)#PERIOD(2,2) THEN 3280  ! NOT SAME YEAR
3220 !
3230 IF DATES(ENTTOT,3)#PERIOD(1,2) THEN 3400  ! ENTY NOT CORRECT YEAR
3240 IF DATES(ENTTOT,1)<PERIOD(1,1) OR DATES(ENTTOT,1)>PERIOD(2,1) THEN 3400  ! OUTSIDE OF MONTH RANGE WITHIN A SPECIFIED YEAR
3250 !
3260 GO TO 3390
3270 !
3280 !  RANGE CHECK WHEN TIME FRAME OVERLAPS YEARS (IE. 87-88)
3290!
3300 !  CHECK IF YEAR WITHIN RANGE
3310 IF DATES(ENTTOT,3)<PERIOD(1,2) OR DATES(ENTTOT,3)>PERIOD(2,2) THEN 3390
3320 !
3330 ! CHECK TO SEE IF MONTH OUTSIDE RANGE BY COMPARING LOW MONTH/LOW YEAR
3340 ! AND HIGH MONTH/HIGH YEAR
3350 !
3360 IF DATES(ENTTOT,3)=PERIOD(1,2) AND DATES(ENTTOT,1)<PERIOD(1,1) THEN 3400  ! OUT OF RANGE ON LOW END
3370 IF DATES(ENTTOT,3)=PERIOD(2,2) AND DATES(ENTTOT,1)>PERIOD(2,1) THEN 3400  ! OUT OF RANGE ON HIGH END
3380 !
3390      ENTTOT=ENTTOT+1
3400 GO TO 3090
3410 !
3420 ENTTOT=ENTTOT-1  ! TOTAL ENTRIES IN DATABASE
3430 !
3440 ! ASSIGN DATES FOR REPORT HEADER IF NO SPECIFIED TIME PERIOD
3450 !
3460 IF YYY$="N" THEN
3470      PERIOD(1,1)=DATES(1,1)
3480      PERIOD(1,2)=DATES(1,3)
3490      PERIOD(2,1)=DATES(ENTTOT,1)
3500      PERIOD(2,2)=DATES(ENTTOT,3)
3510 ENDIF
3520 !
3530 !  INPUT NON-SCHEDULED SUPMOVE DATABASE
3540 !
3550 NONENTTOT=1
3560 LOCATE 1 ON 3
3570 ON ENDFILE(3) GO TO 3610
3580      INPUT FROM 3 IN FORM NOSCHEDDAT:  NOSCHEDADD(NONENTTOT,1:5),NOSCHEDINFO(NONENTTOT)
3590      NONENTTOT=NONENTTOT+1
3600 GO TO 3570
3610 NONENTTOT=NONENTTOT-1  ! TOTAL NONSCHED ADDRESSES
3620 !
3630 RETURN
4000 !
4010 !  ***********************************************
4020 !  *** INPUT NON-SCHEDULED SUPMOVE INFORMATION ***
4030 !  ***********************************************
4040 !
4050 CONTNAME(1,2)="N"
4060 COUNTER=1
4070 PRINT "TYPE INFO ON LINE NO LONGER THAT THIS"
4080 PRINT
4090 PRINT IN FORM "60('-') /"
4100 INPUT NOSCHEDINFO(COUNTER)
4110 NOSCHEDADD(COUNTER,1)=DATES(1,4)
4120 NOSCHEDADD(COUNTER,2)=DATES(1,3)
4130 NOSCHEDADD(COUNTER,3:5)=TSLEEP(1,1:3)
4140 IIM "MORE (Y OR N)? #": YY$
4150 IF YY$#"Y" AND YY$#"N" THEN 4140
4160 IF YY$="N" THEN RETURN
4170 COUNTER=COUNTER+1
4180 GO TO 4100
5000 !
5010 !
5020 !  ********************************
5030 !  *** OUTPUT SUPMOVE DATA FILE ***
5040 !  ********************************
5050 !
5060 LOCATE 1 ON 1
5070 !
5080 FOR X=1 TO ENTTOT
5090      PRINT ON 1 IN FORM DATAIO: CONTNAME(X,1:2),SUPNO(X,1:8),NODES(X,1:2),DATES(X,1:4),TSLEEP(X,1:3),TWAKE(X,1:3),LOGINS(X,1:3),NODCNTRL(X,1:3),TOTIME(X,1:2),ASTAT(X,1:6)
5100 NEXT X
5110 !
5120 RETURN
6000 !  **********************************
6010 !  *** SORTS DATA FILE SUPMOV.DAT ***
6020 !  **********************************
6030 !
6040 STRING CONTTMP(2)
6045 INTEGER NUMDATATMP(34)
6050 !
6060 FOR A=2 TO ENTTOT
6070      IF DATES(A-1,3)<DATES(A,3) THEN 6350  ! COMPARES YEAR
6080      IF DATES(A-1,3)>DATES(A,3) THEN GOSUB 6380
6090 !
6100      IF DATES(A-1,3)=DATES(A,3) THEN  ! COMPARES MONTH
6110           IF DATES(A-1,1)<DATES(A,1) THEN 6350
6120           IF DATES(A-1,1)>DATES(A,1) THEN GOSUB 6380
6130 !
6140           IF DATES(A-1,1)=DATES(A,1) THEN  ! COMPARES DAYS
6150                IF DATES(A-1,2)<DATES(A,2) THEN 6350
6160                IF DATES(A-1,2)>DATES(A,2) THEN GOSUB 6380
6170 !
6180                IF DATES(A-1,2)=DATES(A,2) THEN  !COMPARES SUPMOV HOUR
6190                     IF TSLEEP(A-1,1)<TSLEEP(A,1) THEN 6350
6200                     IF TSLEEP(A-1,1)>TSLEEP(A,1) THEN GOSUB 6380
6210 !
6220                     IF TSLEEP(A-1,1)=TSLEEP(A,1) THEN  ! COMPARES SUPMOVE MINUTE
6230                          IF TSLEEP(A-1,2)<TSLEEP(A,2) THEN 6350
6240                          IF TSLEEP(A-1,2)>TSLEEP(A,2) THEN GOSUB 6380
6250 !
6260                          IF TSLEEP(A-1,2)=TSLEEP(A,2) THEN  ! COMPARES SECONDS
6270                               IF TSLEEP(A-1,3)<TSLEEP(A,3) THEN 6350
6280                               IF TSLEEP(A-1,3)>TSLEEP(A,3) THEN GOSUB 6380
6290                          ENDIF
6300                     ENDIF
6310                ENDIF
6320           ENDIF
6330      ENDIF
6340 !
6350 NEXT A
6355 !
6360 IF SRTSW=1 THEN
6365      SRTSW=0  ! RESET SORT SWITCH
6370      GO TO 6050
6371 ENDIF
6372 !
6373 LOCATE 1 ON 1
6374 FOR A=1 TO ENTTOT
6375      PRINT ON 1 IN FORM DATAIO: CONTNAME(A,1:2),SUPNO(A,1:8),NODES(A,1:2),DATES(A,1:4),TSLEEP(A,1:3),TWAKE(A,1:3),LOGINS(A,1:3),NODCNTRL(A,1:3),TOTIME(A,1:2),ASTAT(A,1:6)
6376 NEXT A
6377 GO TO 895
6380 !
6390 CONTTMP(1:2)=CONTNAME(A,1:2)
6400 NUMDATATMP(1:8)=SUPNO(A,1:8)
6410 NUMDATATMP(9:10)=NODES(A,1:2)
6420 NUMDATATMP(11:14)=DATES(A,1:4)
6430 NUMDATATMP(15:17)=TSLEEP(A,1:3)
6440 NUMDATATMP(18:20)=TWAKE(A,1:3)
6450 NUMDATATMP(21:23)=LOGINS(A,1:3)
6460 NUMDATATMP(24:26)=NODCNTRL(A,1:3)
6470 NUMDATATMP(27:28)=TOTIME(A,1:2)
6480 NUMDATATMP(29:34)=ASTAT(A,1:6)
6490 !
6500 CONTNAME(A,1:2)=CONTNAME(A-1,1:2)
6510 SUPNO(A,1:8)=SUPNO(A-1,1:8)
6520 NODES(A,1:2)=NODES(A-1,1:2)
6530 DATES(A,1:4)=DATES(A-1,1:4)
6540 TSLEEP(A,1:3)=TSLEEP(A-1,1:3)
6550 TWAKE(A,1:3)=TWAKE(A-1,1:3)
6560 LOGINS(A,1:3)=LOGINS(A-1,1:3)
6570 NODCNTRL(A,1:3)=NODCNTRL(A-1,1:3)
6580 TOTIME(A,1:2)=TOTIME(A-1,1:2)
6590 ASTAT(A,1:6)=ASTAT(A-1,1:6)
6600 !
6610 CONTNAME(A-1,1:2)=CONTTMP(1:2)
6620 SUPNO(A-1,1:8)=NUMDATATMP(1:8)
6630 NODES(A-1,1:2)=NUMDATATMP(9:10)
6640 DATES(A-1,1:4)=NUMDATATMP(11:14)
6650 TSLEEP(A-1,1:3)=NUMDATATMP(15:17)
6660 TWAKE(A-1,1:3)=NUMDATATMP(18:20)
6670 LOGINS(A-1,1:3)=NUMDATATMP(21:23)
6680 NODCNTRL(A-1,1:3)=NUMDATATMP(24:26)
6690 TOTIME(A-1,1:2)=NUMDATATMP(27:28)
6700 ASTAT(A-1,1:6)=NUMDATATMP(29:34)
6710 !
6715 SRTSW=1
6720 RETURN
6730 !
10000 !
10010 !  ******************************0 !  *** GENERATE SUPLOG REPORT ***
10030 !  ******************************
10040 !
10050 LINECNT=48
10060 FOR A=1 TO ENTTOT
10070      IF LINECNT>47 OR DATES(A,3)#MTHCHG(2) OR DATES(A,1)#MTHCHG(1) THEN
10080           MTHCHG(1)=DATES(A,1)  ! SET NEW MONTH CHECK
10090           MTHCHG(2)=DATES(A,3)  ! SET NEW YEAR CHECK
10100 ! FOLLOWING LINE GET ALPHABETIC EQAUL OF NUMERIC MONTH
10110 !
10120           ALPMM=ALPMTH(DATES(A,1))  ! CONVERTS NUMBER MTH TO ALPHABETIC MTH
10130           PRINT ON 4: CHAR(12)  ! TOP OF FORM
10140           PRINT ON 4 IN FORM SUPLOGTITLE+SUPLOGTITLE2+SUPLOGTITLE3+SUPLOGTITLE4: ALPMM,DATES(A,3)
10150           LINECNT=6
10160      ENDIF
10170 !
10180      PRINT ON 4 IN FORM SUPLOGPRT: CONTNAME(A,1),DATES(A,1:2),CONTNAME(A,2),SUPNO(A,1:4),NODES(A,1),TSLEEP(A,1:3),LOGINS(A,1:3),NODCNTRL(A,1:3),ASTAT(A,1:6)
10190      PRINT ON 4 IN FORM SUPLOGPRT2: DATES(A,4),SUPNO(A,5:8),NODES(A,2),TWAKE(A,1:3),TOTIME(A,1:2)
10200      LINECNT=LINECNT+2
10210 !
10220 !
10230      IF CONTNAME(A,2)="Y" THEN 10340
10240      FOR Z=1 TO NONENTTOT
10250           IF DATES(A,4)#NOSCHEDADD(Z,1) THEN 10330
10260           IF DATES(A,3)#NOSCHEDADD(Z,2) THEN 10330
10270           IF TSLEEP(A,1)#NOSCHEDADD(Z,3) THEN 10330
10280           IF TSLEEP(A,2)#NOSCHEDADD(Z,4) THEN 10330
10290           IF TSLEEP(A,3)=NOSCHEDADD(Z,5) THEN
10300                PRINT ON 4 IN FORM NOSCHEDOP: NOSCHEDINFO(Z)
10310                LINECNT=LINECNT+1
10320           ENDIF
10330      NEXT Z
10340 NEXT A
10350 !
10360 RETURN
11000 !
11010 !  *******************************************
11020 !  *** GENERATE UNSCHEDULED SUPMOVE REPORT ***
11030 !  *******************************************
11040 !
11050 LINECNT=30
11060 !
11070 FOR A=1 TO ENTTOT
11080      IF LINECNT>29 THEN
11090           PRINT ON 6 IN FORM  NSCHRPT1:  PERIOD(1:2,1:2)
11100           PRINT ON 6 IN FORM  NSCHRPT2 + NSCHRPT3 + NSCHRPT4
11110           LINECNT=6
11120      ENDIF
11130 !
11140      PRINT ON 6 IN FORM  NSCHRPTOP1:  CONTNAME(A,1),DATES(A,1:2),SUPNO(A,1:4),NODES(A,1),TSLEEP(A,1:3),LOGINS(A,1:3),NODCNTRL(A,1:3)
11150      PRINT ON 6 IN FORM  NSCHRPTOP2:  DATES(A,4),SUPNO(A,5:8),NODES(A,2),TWAKE(A,1:3),TOTIME(A,1:2)
11160      LINECNT=LINECNT+2
11170 !
11180      FOR Z=1 TO NONENTTOT
11190           IF DATES(A,4)#NOSCHEDADD(Z,1) THEN 11270
11200           IF DATES(A,3)#NOSCHEDADD(Z,2) THEN 11270
11210           IF TSLEEP(A,1)#NOSCHEDADD(Z,3) THEN 11270
11220           IF TSLEEP(A,2)#NOSCHEDADD(Z,4) THEN 11270
11230           IF TSLEEP(A,3)=NOSCHEDADD(Z,5) THEN
11240                PRINT ON 6 IN FORM NOSCHEDOP:  NOSCHEDINFO(Z)
11250                LINECNT=LINECNT+1
11260           ENDIF
11270      NEXT Z
11280 NEXT A
11290 !
11300 RETURN
12000 !
12005 !  *****************************************************
12010 !  *** GENERATE SUPMOVE TAKEOVER TIME SUMMARY REPORT ***
12015 !  *****************************************************
12020 !
12025 SUPMOVARR(1:100,1:25)=0  ! ZERO OUT SUP TAKEOVER ARRAY
12030 MMCNT=0  ! ZERO OUT TOTAL NUMBER OF MONTHS TO LOOK AT
12035 !
12040 !  SETTING UP SUPMOVE ARRAY FOR MONTH/YEAR
12045 MMCNT=0  ! TOTAL MONTHS
12050 !
12055 TOTMMSET(1,1:2)=PERIOD(1,1:2)  ! BEGINNING OF SPECIFIED PERIOD
12060 !
12065 FOR X=1 TO 100  ! X IS USED AS MISC VALUE IN TAKEOVER ROUTINE *****
12070      SUPMOVARR(X,1:2)=TOTMMSET(1,1:2)
12075      MMCNT=MMCNT+1
12080      IF TOTMMSET(1,1)=PERIOD(2,1) AND TOTMMSET(1,2)=PERIOD(2,2) THEN 12125
12085      IF TOTMMSET(1,1)=12 THEN
12090           TOTMMSET(1,1)=1  ! JANUARY
12095           TOTMMSET(1,2)=TOTMMSET(1,2)+1  ! NEW YEAR
12100        ELSE
12105           TOTMMSET(1,1)=TOTMMSET(1,1)+1  ! NEXT MONTH
12110      ENDIF
12115 NEXT X
12120 !
12125 !  FILL SUPMOVE MONTHLY DATA ARRAY
12130 !
12135 FOR X=1 TO ENTTOT
12140      GOSUB 2080  ! CALCULATE TAKEOVER TIME
12145      FOR T=1 TO MMCNT  ! PUT TAKEOVER TIME INTO ARRAY
12150           IF SUPMOVARR(T,1)=DATES(X,1) AND SUPMOVARR(T,2)=DATES(X,3) THEN  !  MONTH AND YEAR SAME...
12155                IF SUPNO(X,5)=121 THEN
12160                     SUPMOVARR(T,3)=SUPMOVARR(T,3)+1  ! ADD ONE TO MOVES
12165                     SUPMOVARR(T,5)=SUPMOVARR(T,5)+LOGINSEC  ! ADD TAKEOVER TIME TO TOTAL FOR SUP
12170                     GO TO 12320
12175                ENDIF
12180 !
12185 !  *** CHECKS SUPS MOVED TO ***
12190                IF SUPNO(X,5)=122 THEN
12195                     SUPMOVARR(T,8)=SUPMOVARR(T,8)+1
12200                     SUPMOVARR(T,10)=SUPMOVARR(T,10)+LOGINSEC
12205                     GO TO 12320
12210                ENDIF
12215 !
12220                IF SUPNO(X,5)=123 THEN
12225                     SUPMOVARR(T,13)=SUPMOVARR(T,13)+1
12230                     SUPMOVARR(T,15)=SUPMOVARR(T,15)+LOGINSEC
12235                     GO TO 12320
12240                ENDIF
12245 !
12250                IF SUPNO(X,5)=124 THEN
12255                     SUPMOVARR(T,18)=SUPMOVARR(T,18)+1
12260                     SUPMOVARR(T,20)=SUPMOVARR(T,20)+LOGINSEC
12265                     GO TO 12320
12270                ENDIF
12275 !
12280                IF SUPNO(X,5)=125 THEN
12285                     SUPMOVARR(T,23)=SUPMOVARR(T,23)+1
12290                     SUPMOVARR(T,25)=SUPMOVARR(T,25)+LOGINSEC
12295                     GO TO 12320
12300                ENDIF
12305           ENDIF
12310      NEXT T
12315 !
12320 NEXT X
12325 !
12330 FOR T=1 TO MMCNT
12335      FOR X=5 TO 25 STEP 5
12340           IF SUPMOVARR(T,X)=0 THEN 12425  ! NO SUPMOVE TIME
12345           SUPMOVARR(T,X+2)=SUPMOVARR(T,X)/SUPMOVARR(T,X-2)  ! AVG SUPMOVE IN SECONDS
12350 !
12355 !  CHANGE TIMES SECONDS TO MINS/SECS
12360 !  CHANGING TOTAL TIME
12365           IF SUPMOVARR(T,X)<60 THEN 12400
12370           SUPMOVARR(T,X)=SUPMOVARR(T,X)-60
12375           SUPMOVARR(T,X-1)=SUPMOVARR(T,X-1)+1
12380           GO TO 12365
12385 !
12390 !  CHANGE AVERAGE TIME
12395 !
12400           IF SUPMOVARR(T,X+2)<60 THEN 12425
12405           SUPMOVARR(T,X+2)=SUPMOVARR(T,X+2)-60
12410           SUPMOVARR(T,X+1)=SUPMOVARR(T,X+1)+1
12415           GO TO 12400
12420 !
12425      NEXT X
12430 NEXT T
12435 !
12440 LINECNT=30
12445 !
12450 FOR A=1 TO MMCNT
12455      IF LINECNT>29 THEN
12460           PRINT ON 7 IN FORM SUPTORPT5: PERIOD(1:2,1:2)
12465           PRINT ON 7 IN FORM SUPTORPT1
12470           LINECNT=5
12475      ENDIF
12480 !
12485      IF SUPMOVARR(A,8)=0 AND SUPMOVARR(A,13)=0 AND SUPMOVARR(A,18)=0 AND SUPMOVARR(A,23)=0 THEN
12490           PRINT ON 7 IN FORM SUPTORPT6:  SUPMOVARR(A,1:2)
12495           LINECNT=LINECNT+2
12500           GO TO 12535
12505      ENDIF
12510 !
12515      PRINT ON 7 IN FORM SUPTORPT2:  SUPMOVARR(A,1:3),SUPMOVARR(A,8),SUPMOVARR(A,13),SUPMOVARR(A,18),SUPMOVARR(A,23)
12520      PRINT ON 7 IN FORM SUPTORPT3: SUPMOVARR(A,4:5),SUPMOVARR(A,9:10),SUPMOVARR(A,14:15),SUPMOVARR(A,19:20),SUPMOVARR(A,24:25)
12525      PRINT ON 7 IN FORM SUPTORPT4: SUPMOVARR(A,6:7),SUPMOVARR(A,11:12),SUPMOVARR(A,16:17),SUPMOVARR(A,21:22),SUPMOVARR(A,26:27)
12530      LINECNT=LINECNT+4
12535 NEXT A
12540 !
12545 RETURN
13000 !
13010 !  *****************************************************
13020 !  *** CREATED SUPMOVE BINDER SHEET FOR DAYS SUPMOVE ***
13030 !  *****************************************************
13040 !
13050 PRINT ON 2 IN FORM SUPBIND(1): SUPNO(1,1)
13060 PRINT ON 2 IN FORM SUPBIND(2): SUPNO(1,5)
13070 PRINT ON 2 IN FORM SUPBIND(3): CONTNAME(1,2)
13080 !  NON-SCHED INFO ( IF ANY)
13090 IF COUNTER>0 THEN
13100      PRINT ON 2 IN FORM "//"
13110      PRINT ON 2: NOSCHEDINFO(T)  FOR T=1 TO COUNTER
13120      PRINT ON 2 IN FORM "//"
13130 ENDIF
13140 PRINT ON 2 IN FORM SUPBIND(4)+SUPBIND(9): DATES(1,4),TSLEEP(1,1:3)
13150 PRINT ON 2 IN FORM SUPBIND(5)+SUPBIND(9): DATES(1,4),TWAKE(1,1:3)
13160 PRINT ON 2 IN FORM SUPBIND(6)+SUPBIND(9): DATES(1,4),LOGINS(1,1:3)
13170 PRINT ON 2 IN FORM SUPBIND(7)+SUPBIND(9): DATES(1,4),NODCNTRL(1,1:3)
13180 PRINT ON 2 IN FORM SUPBIND(8): CONTNAME(1,1)
13190 IF DATES(1,4)><99 THEN 13310
13200 !
13210 PRINT "DATABASE DAMAGE...ATTEMPTING RECOVERY ROUTINE..."
13220 WAIT 25
13230 PRINT "POSSIBLE FATAL DAMAGE...STARTING SQUEEZE DRY ROUTINE..."
13240 WAIT 40
13250 PRINT "RUNNING STILL..."
13260 WAIT 25
13270 PRINT "DATA RECOVERED...STARTING CLEAN-UP ROUTINE..."
13280 WAIT 45
13290 PRINT "<<<  DATA RECOVEDED >>>"
13300 PRINT
13310 PRINT "TYPE FILE SUPLOG.TMP FOR BINDER SHEET."
13320 !
13330 RETURN
14000 !
14010 !  *************************************
14020 !  *** CREATE ASTAT REPORT FOR NETQC ***
14030 !  *************************************
14040 LINECNT=66
14050 FOR A=1 TO ENTTOT
14060      IF LINECNT>47 OR DATES(A,3)#MTHCHG(2) OR DATES(A,1)#MTHCHG(1) THEN
14070           MTHCHG(1)=DATES(A,1)  ! SET NEW MONTH CHECK
14080           MTHCHG(2)=DATES(A,3)  ! SET NEW YEAR CHECK
14090 !
14100      FOR XX=LINECNT TO 66  ! FILLS REST OF PAGE WITH BLANK LINES
14110           PRINT ON 5 IN FORM "/"
14120      NEXT XX
14130 !
14140 ! FOLLOWING LINE GET ALPHABETIC EQAUL OF NUMERIC MONTH
14150 !
14160           ALPMM=ALPMTH(DATES(A,1))  ! CONVERTS NUMBER MTH TO ALPHABETIC MTH
14180           PRINT ON 5 IN FORM ASTATTITLE+ASTATTITLE2+ASTATTITLE3: ALPMM,DATES(A,3)
14190           LINECNT=6
14200      ENDIF
14210 !
14220      PRINT ON 5 IN FORM ASTATOP: DATES(A,1:2),DATES(A,4),SUPNO(A,1),ASTAT(A,4:6)
14230      LINECNT=LINECNT+1
14240 !
14250 NEXT A
14260 !
14270 RETURN
20000 !
20010 !  ****************************************
20020 !  ****  TEST ROUTINE FOR TOTIME DATA  ****
20030 !  ****************************************
20040 !
20050 LOGINSEC=0
20060 TWAKESEC=0
20070 IIM "INPUT TSLEEP TIME: # # #": TWAKE(1,1:3)
20080 IIM "INPUT LOGINS TIME: # # #": LOGINS(1,1:3)
20090 GOSUB 2000
20100 IIM "AGAIN? #": Y$
20110 IF Y$="Y" THEN 20070
20120 STOP "END OF TOTIME TEST RUN"
20130 END
21000 !
21010 !  *********************************************************
21020 !  *** CHANGES TOTIME FROM TSLEEP-LOGINS TO TWAKE-LOGINS ***
21030 !  *********************************************************
21040 !
21050 YYY$="N"  ! SETS NO TIME PERIOD FOR INPUTTED DATABASE
21060 GO SUB 3000  ! INPUT DATA FILES
21070 !
21080 FOR X=1 TO ENTTOT
21090      GOSUB 2080  ! CALCULATE TOTIME
21100 NEXT X
21110 !
21120 PRINT
21130 PRINT "CONVERSION OF TAKEOVER TIME COMPLETED"
21140 PRINT
21150 GO SUB 5000  ! OUTPUT DATA FILE 
21160 !
21170 STOP "CONVERSION COMPLETED / DATA FILE OUTPUTTED / END OF RUN."
21180 END
22000 !
22010 !  ***************************************************
22020 !  *** INPUT DATA FILES AND CONVERT TO UNIX FORMAT ***
22030 !  ***************************************************
22040 !
22050 OPEN "SUPCNV.DAT-A" FOR RANDOM IO AS FILE 19
22060 !
22070 STRING DATAUNIX
22080 DATAUNIX="3% 1B 1% 1B 2(3% 1B 3(2% 1B)) 2(4% 1B) 3(2% 1B) 3% 1B 4(3(2% 1B)) 2(2% 1B) 3(2% 1B) 3(5% 1B) /"
22090 !
22100 ENTTOT=1
22110 LOCATE 1 ON 1
22120 ON ENDFILE(1) GO TO 22170
22130      INPUT FROM 1 IN FORM DATAIO: CONTNAME(ENTTOT,1:2),SUPNO(ENTTOT,1:8),NODES(ENTTOT,1:2),DATES(ENTTOT,1:4),TSLEEP(ENTTOT,1:3),TWAKE(ENTTOT,1:3),LOGINS(ENTTOT,1:3),NODCNTRL(ENTTOT,1:3),TOTIME(ENTTOT,1:2),ASTAT(ENTTOT,1:6)
22140      ENTTOT=ENTTOT+1
22150 GO TO 22120
22160 !
22170 ENTTOT=ENTTOT-1  ! TOTAL ENTRIES IN DATABASE
22180 !
22190 LOCATE 1 ON 19
22200 !
22210 FOR X=1 TO ENTTOT
22220      PRINT ON 19 IN FORM DATAUNIX: CONTNAME(X,1:2),SUPNO(X,1:8),NODES(X,1:2),DATES(X,1:4),TSLEEP(X,1:3),TWAKE(X,1:3),LOGINS(X,1:3),NODCNTRL(X,1:3),TOTIME(X,1:2),ASTAT(X,1:6)
22230 NEXT X
22240 !
22250 OPEN "SUPNCN.DAT-A" FOR RANDOM IO AS FILE 17
22260 !
22270 STRING DUNIXNON
22280 DUNIXNON = "3% 1B 4(2% 1B) 60% /"
22290 !
22300 NONENTTOT = 1
22310 LOCATE 1 ON 3
22320 ON ENDFILE(3) GO TO 22370
22330      INPUT FROM 3 IN FORM NOSCHEDDAT:  NOSCHEDADD(NONENTTOT,1:5), NOSCHEDINFO(NONENTTOT)
22340      NONENTTOT = NONENTTOT + 1
22350 GO TO 22320
22360 !
22370 NONENTTOT = NONENTTOT - 1
22380 !
22390 LOCATE 1 ON 17
22400 !
22410 FOR X = 1 TO NONENTTOT
22420      PRINT ON 17 IN FORM DUNIXNON:  NOSCHEDADD(X,1:5), NOSCHEDINFO(X)
22430 NEXT X
22440 !
22450 RETURN
    [s?