100 !  THIS PROGRAM IS TO LIST LINE OUTAGES THAT ARE NOT LONGER TO BE
105 !  PERSUED AND LIST THEM IN NUMERIC ORDER BY LINE...
110 !
115 !  SECTION ON ADDING DATA FUNCTIONING
120 !     -SECTION NOW SET UP FOR ADDING EITHER LINES OR NODES
125 !  SECTION ON SORTING DATA FUNCTIONING
130 !     -SECTION NOW SET UP FOR SORTING BOTH TYPE DATABASES
135 !  SECTION ON OUTPUTTING DATA TO DATA FILE FUNCTIONING
140 !  SECTION ON GENERATING REPORT FILE FUNCTIONING
145 !
150 !
155 STRING STRDATA(500,5), LNEDATA, COMMAND, IODATA
160 STRING TITLE1, TITLE2, TITLE3, OPLINE1A, OPLINE1B, OPLINE2
165 STRING NODDATA, TITLE4, TITLE5, TITLE6, OPNODE, OPNODE2
170 TITLE1="7B 'ENTRY' 11B 'LINE' 6B 'PROJECT' 30B 'TMCS' /"
175 TITLE2="8B 'DATE' 3B 'INIT' 1B 'NODE1' 1B 'NODE2' 3B 'NUMBER' 11B 'COMMENTS' 10B 'RES' 1B 'IGN' //"
180 TITLE4="7B 'ENTRY' 17B 'PROJECT' 32B 'TMCS' /"
185 TITLE5="7B 'DATE' 4B 'INIT' 3B 'NODE' 4B 'NUMBER' 12B 'COMMENTS' 11B 'RES' 1B 'IGN' //"
190 OPLINE1A="16B"
195 OPLINE1B="5B 2% '/' 2% '/' 2% 3B"
200 OPLINE2="3% 2(1B 5%) 1B 10% 1B 25% 2(1B 3%) /"
205 OPNODE="16B 3% 2B 5% 2B 10% 2B 25% 2B 2(1B 3%) /"
210 OPNODE2="5B 2% '/' 2% '/' 2% 3B 3% 2B 5% 2B 10% 2B 25% 2B 2(1B 3%) /"
215 LNEDATA="3(2%) 2(5%) 3% 10% 25% 2(2%) /"
220 NODDATA="3(2%) 5% 3% 10% 25% 2(2%) /"
225 !
230 INTEGER NUMDATA(500,7), LNECNT, CNT1, CNT2, FILENUM
235 !
240 OPEN "LNEDSC.DAT-A" FOR RANDOM IO AS FILE 1
245 OPEN "NODDSC.DAT-A" FOR RANDOM IO AS FILE 2
250 !
255 COMMAND="HELP"  ! TO PRINT OUT COMMANDS WHEN ENTERING PROGRAM
260 GO TO 505
265 !
270 IIM "INPUT COMMAND:  #":  COMMAND
275 IF COMMAND="ADD LINE IGNORE" THEN
280      PRINT
285      PRINT "ENTER DATA AS MM,DD,YY,INITIALS,NODE1,NODE2,PROJECT #,COMMENTS,RESTORED,IGNORED"
290      PRINT "FOR EXAMPLE: 4,8,87,SPC,4356,7742,43897C.A3,DISCONNECT,-,Y"
295      PRINT "ENTER A DASH IF NO PROJECT NUMBER"
300      PRINT "ENTER A ZERO IF NO SECOND NODE NUMBER"
305      PRINT
310      FILENUM=1  ! SELECT LINE DISC DATABASE
315      W=5  ! INCLUDE 2ND FIGURE IN LINE NUMBER
320      IODATA=LNEDATA
325      GOSUB 1000  ! ADD TO DATABASE
330      GO TO 270
335 ENDIF
340 IF COMMAND="ADD NODE IGNORE" THEN
345      PRINT
350      PRINT "ENTER DATA AS MM,DD,YY,INITIALS,NODE,PROJECT #,COMMENTS,RESTORED,IGNORED"
355      PRINT "ENTER A DASH IF NO PROJECT NUMBER"
360      PRINT
365      FILENUM=2  ! SELECT NODE DISC DATABASE
370      W=4  ! NO SECOND FIGURE FOR LINE DATABASE
375      IODATA=NODDATA
380      GOSUB 1000  ! ADD TO DATABASE
385      GO TO 270
390 ENDIF
395 IF COMMAND="GENERATE LINE REPORT" OR COMMAND="GENERATE REPORTS" THEN
400      FILENUM=1  ! SELECT LINE DATABASE
405      W=5
410      IODATA=LNEDATA
415      GOSUB 3000  ! INPUTS DATABASE
420      GOSUB 2000  ! SORTS DATABASE
425      GOSUB 4000  ! OUTPUTS ARRAY TO DATABASE
430      GOSUB 10000
435      IF COMMAND="GENERATE REPORTS" THEN 445
440      GO TO 270
445 ENDIF
450 IF COMMAND="GENERATE NODE REPORT" OR COMMAND="GENERATE REPORTS" THEN
455      FILENUM=2
460      W=4
465      IODATA=NODDATA
470      GOSUB 3000
475      GOSUB 2000
480      GOSUB 4000
485      GOSUB 11000  ! GENERATE NODE REPORT
490      GO TO 270
495 ENDIF
500 IF COMMAND="QUIT" THEN 585
505 IF COMMAND="HELP" OR COMMAND="?" THEN
510      PRINT
515      PRINT "USE ONE OF THE FOLLOWING COMMANDS:"
520      PRINT
525      PRINT "     ADD LINE IGNORE"
530      PRINT "     ADD NODE IGNORE"
535      PRINT "     GENERATE REPORTS"
540      PRINT "     GENERATE LINE REPORT"
545      PRINT "     GENERATE NODE REPORT"
550      PRINT "     QUIT"
555      PRINT "     HELP"
560      PRINT
565      GO TO 270
570 ENDIF
575 GO TO 255  ! WILL PRINT OUT COMMANDS DUE TO INCORRECT COMMAND ENTRY
580 !
585 CLOSE 1,2
590 STOP "END OF RUN"
595 END
1000 !
1005 !  ***************************************************************
1010 !  *** INPUTTING OF DISCONNECT DATA AND OUTPUTTING TO DATABASE ***
1015 !  ***************************************************************
1020 !
1030 IIM "HOW MANY ENTRIES?  #": CNT2
1040 IF CNT2=0 THEN RETURN
1100 !
1110 B=SIZE(FILENUM)
1120 LOCATE B+1 ON FILENUM
1130 !
1140 FOR A=1 TO CNT2
1150      INPUT NUMDATA(1,1:3),STRDATA(1,1),NUMDATA(1,4:W),STRDATA(1,2:5)
1160      PRINT ON FILENUM IN FORM IODATA:  NUMDATA(1,1:W),STRDATA(1,1:5)
1170 NEXT A
1180 !
1190 RETURN
2000 !
2005 !  ************************
2010 !  *** SORT OF DATABASE ***
2015 !  ************************
2020 !
2030 STRING STRTMP(5)
2040 INTEGER NUMTMP(7)
2050 !
2060 PRINT "BEGINNING SORT OF DATABASE"
2070 !
2080 SRT=0
2090 !
2100 FOR A=1 TO CNT1-1
2110 !
2120      IF W#4 THEN 2160
2130 !
2140      IF NUMDATA(A,4)>NUMDATA(A+1,4)  THEN 2180 ELSE 2250
2150 !
2160      IF NUMDATA(A,4)>NUMDATA(A+1,4) OR NUMDATA(A,4)=NUMDATA(A+1,4) AND NUMDATA(A,5)>NUMDATA(A+1,5) THEN 2180 ELSE 2250
2170 !
2180           NUMTMP(1:W)=NUMDATA(A,1:W)
2190           NUMDATA(A,1:W)=NUMDATA(A+1,1:W)
2200           NUMDATA(A+1,1:W)=NUMTMP(1:W)
2210           STRTMP(1:5)=STRDATA(A,1:5)
2220           STRDATA(A,1:5)=STRDATA(A+1,1:5)
2230           STRDATA(A+1,1:5)=STRTMP(1:5)
2240           SRT=1
2250 NEXT A
2260 !
2270 IF SRT=1 THEN 2080
2280 !
2290 PRINT "COMPLETED SORT OF DATABASE"
2300 !
2310 RETURN
3000 !
3010 !  ***********************************
3020 !  *** INPUT DATA FILE INTO ARRAYS ***
3030 !  ***********************************
3040 !
3050 !
3060 CNT1=1
3070 B=SIZE (FILENUM)
3080 IF B=0 THEN RETURN
3090 LOCATE 1 ON FILENUM
3100 !
3110 PRINT "INPUTTING DATABASE"
3120 ON ENDFILE(FILENUM) GO TO 3170
3130      INPUT FROM FILENUM IN FORM IODATA:  NUMDATA(CNT1,1:W),STRDATA(CNT1,1:5)
3140      CNT1=CNT1+1
3150 GO TO 3120
3160 !
3170 CNT1=CNT1-1  ! TOTAL NUMBER OF ENTRIES
3180 !
3190 PRINT "DATABASE INPUTTED"
3200 !
3210 RETURN
4000 !
4010 !  **********************************
4020 !  *** OUTPUT ARRAY INTO DATABASE ***
4030 !  **********************************
4040 !
4050 PRINT "OUTPUTTING SORTED DATA INTO DATABASE"
4060 LOCATE 1 ON FILENUM
4070 FOR A=1 TO CNT1
4080      PRINT ON FILENUM IN FORM IODATA:  NUMDATA(A,1:W),STRDATA(A,1:5)
4090 NEXT A
4100 !
4110 PRINT "OUTPUTTING COMPLETED"
4120 !
4130 RETURN
10000 !
10010 !  **************************************
10020 !  *** GENERATES LINE REPORT FOR FILE ***
10030 !  **************************************
10040 !
10050 PRINT "GENERATING LINE REPORT"
10060 !
10070 OPEN "LNEDSC.RPT-A" FOR RANDOM IO AS FILE 3
10080 !
10090 LNECNT=60  ! SET TOP OF FORM
10100 !
10110 FOR A=1 TO CNT1
10120      IF LNECNT=60 THEN
10130           PRINT ON 3: CHAR(12)  ! TOP OF FORM
10140           PRINT ON 3 IN FORM TITLE1 + TITLE2 + TITLE3
10150           LNECNT=4
10160      ENDIF
10170 !
10180      IF NUMDATA(A,1)=0 THEN PRINT ON 3 IN FORM OPLINE1A ELSE PRINT ON 3 IN FORM OPLINE1B:  NUMDATA(A,1:3)
10190      PRINT ON 3 IN FORM OPLINE2:  STRDATA(A,1), NUMDATA(A,4:5), STRDATA(A,2:5)
10200      LNECNT=LNECNT+1
10210 NEXT A
10220 !
10230 CLOSE 3
10240 !
10250 PRINT "LINE REPORT GENERATED"
10260 !
10270 RETURN
11000 !
11010 !  ****************************
11020 !  *** GENERATE NODE REPORT ***
11030 !  ****************************
11040 !
11050 PRINT "GENERATING NODE REPORT"
11060 !
11070 OPEN "NODDSC.RPT-A" FOR RANDOM IO AS FILE 4
11080 !
11090 LNECNT=60
11100 !
11110 FOR A=1 TO CNT1
11120      IF LNECNT=60 THEN
11130           PRINT ON 4:  CHAR(12)  ! TOP OF FORM
11140           PRINT ON 4 IN FORM TITLE4 + TITLE5 + TITLE6
11150           LNECNT=4
11160      ENDIF
11170 !
11180      IF NUMDATA(A,1)=0 THEN
11190           PRINT ON 4 IN FORM OPNODE:   STRDATA(A,1), NUMDATA(A,4), STRDATA(A,2:5)
11200        ELSE
11210           PRINT ON 4 IN FORM OPNODE2:  NUMDATA(A,1:3),STRDATA(A,1),NUMDATA(A,4),STRDATA(A,2:5)
11220      ENDIF
11230      LNECNT=LNECNT+1
11240 !
11250 NEXT A
11260 !
11270 CLOSE 4
11280 !
11290 PRINT "NODE REPORT GENERATED"
11300 !
11310 RETURN
     