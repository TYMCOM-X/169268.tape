100 !  THIS PROGRAM WILL KEEP A SUPMOVE DATABASE AND CREATE REPORT FILES OF:
105 !       (1) DAILY SUPMOVE LOG
110 !       (2) NON-SCHEDULE SUPMOVES ON TTY
115 !  DATA INPUT ROUTINE BY CONTROLLER COMPLETED.
120 !  DATA OUTPUT AFTER ORIGINAL ENTRY COMPLETED.
125 !  CREATION OF TAKEOVER TIME ROUNTINE COMPLETED (SUB 2000).
130 !  DATA FILE INPUT ROUTINE COMPLETED (SUB 3000).
135 !  ADDRESSING AND STORING NON-SCHEDULED SUPMOVE DATABASE (SUB 4000)
140 !  COMPLETED.
145 !     -ADDRESSING DONE USING GMT, YEAR, AND TSLEEP COMBINED.
150 !  GENERATING SUPLOG REPORT COMPLETED (SUB 10000).
155 !  GENERATING NONSCHEDULED SUPMOVE REPORT FOR TTY COMPLETED (SUB 11000)
160 !  GENERATING SUP TAKEOVER TIME REPORT COMPLETED.
165 !
170 !  THIS VERSION GENERATES ASTAT REPORT (SUB 14000)
175 !               GENERATES ALL REPORTS TO FILES
180 !
181 PRINT
182 PRINT "                      VERSION 12"
183 PRINT
184 PRINT
185 ! DATA FIELDS
190 !
195 STRING DATAIO
200 DATAIO="3% 1% 3(2%) 3% 3% 4% 3(2%) 3(4%) 7% %.% /"
205 !
210 STRING CONTNAME(1200,2)  ! INTIALS + SCHEDULED/NON-SCHEDULED MOVE
215 !
220 INTEGER DATES(1200,4), TSLEEP(1200,3)
230 INTEGER SUPNO(1200), NODES(1200)
235 INTEGER ASTAT(1200,3)  ! TIME / TOT FIAL / TOT REBUILD
240 INTEGER PERIOD(2,2),RPTTYPE
241 !
243 REAL LSTMIN (1200,2)
245 !
365 INTEGER MTHCHG(2)
400 !
405 !  NON-SCHEDULED SUPMOVE FIELDS
410 !
415 STRING NOSCHEDINFO(500),NOSCHEDDAT,NOSCHEDOP
420 NOSCHEDDAT="3% 4(2%) 50% /"
425 NOSCHEDOP="50% /"
430 INTEGER NONENTTOT, NOSCHEDADD(500,5)
435 !
440 !  NON-SCHEDULED SUPMOVE REPORT FIELDS
445 !
450 STRING NSCHRPT1,NSCHRPT2,NSCHRPT3,NSCHRPT4,NSCHRPTOP1,NSCHRPTOP2
455 NSCHRPT1="40(/) 12B 'UNSCHEDULED SUPMOVES FOR' 2B 2% '/' 2% 1B 'TO' 1B 2% '/' 2% //"
465 NSCHRPT2="6B 'DATE' 1B 'GMT' 1B 'SUP NO' 2B 'NODE' 9B 'REASON FOR UNSCHEDULED SUPERVISOR MOVE' //"
475 NSCHRPTOP1="5B 2% '/' 2% 1B 3% 3B 3% 3B 4% 2B"
485 !
560 !  ASTAT REPORT FIELDS
565 !
570 STRING ASTATTITLE, ASTATTITLE2, ASTATTITLE3, ASTATOP
575 ASTATTITLE="23B 'ASTAT/LSTMIN REPORT FOR' 1B 3% '/19' 2% //"
580 ASTATTITLE2="31B 'TOTAL' 6B 'TOT' 8B 'PEAK' /"
585 ASTATTITLE3="6B 'DATE' 5B 'GMT' 5B 'SUP' 6B 'REQS' 5B 'FAILS' 5B 'REBUILD' 5B 'LOGINS' 5B 'ALU' //"
590 ASTATOP="5B 2% '/' 2% 5B 3% 5B 3% 6B 4% 6B 4% 7B 4% 5B 7% 5B %.% /"
595 !
600 REMOVE "SUPLOG.TMP-A"
610 REMOVE "ASTAT.RPT-A"
615 REMOVE "SUPNON.RPT-A"
625 !
630 OPEN "SUPMOV.DAT-A" FOR RANDOM IO AS FILE 1  ! SUPMOVE DATA FILE
640 OPEN "SUPNON.DAT-A" FOR RANDOM IO AS FILE 3
650 OPEN "ASTAT.RPT-A" FOR RANDOM IO AS FILE 5  ! ASTAT REPORT FILE
655 OPEN "SUPNON.RPT-A" FOR RANDOM IO AS FILE 6
665 STRING ALPMM  ! DATA RECEIVED BACK ON ALPMTH SUBPROGRAM
670 !
675 DEF STRING EXTERNAL TBA ALPMTH(INTEGER MTH) ! SUBPROG NUMBER TO ALPHABETIC
680 !
685 B=SIZE(1)
690 LOCATE B+1 ON 1
695 !
700 B=SIZE(3)
705 LOCATE B+1 ON 3
710 IIM "DO YOU WISH TO INPUT DATA? #": Y$
715 IF Y$#"Y" AND Y$#"N" THEN
720      PRINT "ENTER Y OR N.  TRY AGAIN."
725      GO TO 710
730 ENDIF
735 IF Y$="Y" THEN GOSUB 1000  ! GENERATE BINDER REPORT AND O/P TO DB
740 !
745 PIF "2(/)"
750 PRINT "TYPE OF REPORT TO PRINT (PICK A NUMBER):"
755 PRINT
765 PRINT "   (1)  UNSCHEDULED SUPMOVE REPORT     FILE SUPNON.RPT IS GENERATED"
775 PRINT "   (2)  ASTAT REPORT                   FILE ASTAT.RPT IS GENERATED"
776 PRINT "   (5)  ALL REPORTS"
780 PRINT "   (6)  EXIT"
785 PRINT
790 IIM "INPUT NUMBER: #": RPTTYPE
795 PRINT
800 IF RPTTYPE=6 THEN 895
801 IF RPTTYPE=7 THEN
802      PRINT "100 = TEST ROUTINE FOR TOTIME CALCULATION"
803      PRINT "101 = CONVERTS TSLEEP/LOGINS TO TWAKE/LOGINS"
804      PRINT "102 = SORTS SUPMOVE DATA FILE"
805      PRINT "103 = CONVERTS SUPMOVE DATABASE PDP-10 TO UNIX"
806      IIM "ENTER 100-103: #": RPTTYPE
808 ENDIF
809 IF RPTTYPE<6 THEN
810      IIM "SPECIFIC PERIOD (Y OR N)? #": YYY$
815      IF YYY$#"N" AND YYY$#"Y" THEN
820           PRINT "REENTER PLEASE."
825           GO TO 810
830      ENDIF
835      IF YYY$="N" THEN 850
840      IIM "   INPUT BEGINNING MONTH/YEAR (IE. 12,87): # #": PERIOD(1,1:2)
845      IIM "   INPUT ENDING MONTH/YEAR (IE. 3,88): # #": PERIOD(2,1:2)
850 ENDIF
851 IF RPTTYPE=5 THEN
852      RPTTYPE=1
853      GOSUB 3000
854      GOSUB 11000  ! GENERATING SUPNON REPORT
860      RPTTYPE=2
861      GOSUB 14000  ! GENERATING ASTAT REPORT
862      GO TO 895
863 ENDIF
865 GOSUB 3000
869 IF RPTTYPE=1 THEN GOSUB 11000  ! GENERATE UNSCHEDULE SUPMOVE REPORT
875 IF RPTTYPE=2 THEN GOSUB 14000  ! GENERATE ASTAT REPORT
886 IF RPTTYPE=102 THEN  ! SORTS SUPMOV.DAT FILE
887      YYY$="N"
888      GOSUB 3000  ! INPUT DATA FILES
889      GO TO 6000  ! SORT DATA FILES
890 ENDIF
894 GO TO 745
895 CLOSE 1,3,5,6
900 STOP ""
905 END
1000 !
1010 !  ***********************************************************
1020 !  *** INPUT DATA FOR BINDER REPORT AND OUTPUT TO DATABASE ***
1030 !  ***********************************************************
1040 !
1050 IIM "INPUT DATE (IE: DD,MM,YY): # # #": DATES(1,2), DATES(1,1), DATES(1,3)
1060 IIM "      GMT DAY (IE: 318): #": DATES(1,4)
1070 IIM "      SUPERVISOR PUT TO SLEEP (IE: 122): #": SUPNO(1)
1170 IIM "      NODES BEFORE: #": NODES(1)
1175 IIM "     TSLEEP TIME: # # #": TSLEEP(1,1:3)
1190 PRINT
1200 IIM "SCHEDULED? #": Y$
1210 PRINT
1300 !
1310 PRINT "NOW ENTER ASTAT INFORMATION:"
1320 PRINT
1340 IIM "TOTAL REQUEST: #": ASTAT(1,1)
1350 IIM "TOTAL FAILURE: #": ASTAT(1,2)
1360 IIM "PEAK REBUILD: #": ASTAT(1,3)
1361 PRINT
1362 PRINT "NOW ENTER LSTMIN INFORMATION:"
1363 PRINT
1364 IIM "TOTAL LOGINS: #": LSTMIN(1,1)
1365 IIM "ALU: #": LSTMIN(1,2)
1366 PRINT
1370 IF Y$="NO" OR Y$="N" THEN GOSUB 4000 ELSE CONTNAME(1,2)="Y"
1380 PRINT
1390 !
1400 IIM "ENTER YOUR INTIALS: #": CONTNAME(1,1)
1410 IIM "ALL ENTRIES CORRECT? #": Y$
1420 IF Y$="NO" OR Y$="N" THEN 1050
1430 PRINT
1450 PRINT ON 1 IN FORM DATAIO: CONTNAME(1,1:2), DATES(1,1:4), SUPNO(1), NODES(1), TSLEEP(1,1:3), ASTAT(1,1:3), LSTMIN(1,1:2)
1460 IF COUNTER>0 THEN
1470      PRINT ON 3 IN FORM NOSCHEDDAT: NOSCHEDADD(T,1:5),NOSCHEDINFO(T)  FOR T=1 TO COUNTER
1480 ENDIF
1490 !
1500 ! STOP PROGRAM HERE IF DATA ENTERED FOR BINDER PRINTOUT BY CONTROLLER
1510 !
1520 CLOSE 1,3
1530 STOP "END OF RUN"
1540 END
3000 !
3010 !  ************************
3020 !  *** INPUT DATA FILES ***
3030 !  ************************
3040 !
3050 INTEGER ENTTOT
3060 !
3070 ENTTOT=1
3080 LOCATE 1 ON 1
3090 ON ENDFILE(1) GO TO 3420
3100      INPUT FROM 1 IN FORM DATAIO: CONTNAME(ENTTOT,1:2), DATES(ENTTOT,1:4), SUPNO(ENTTOT), NODES(ENTTOT), TSLEEP(ENTTOT,1:3), ASTAT(ENTTOT,1:3), LSTMIN(ENTTOT,1:2)
3110 ! ROUTINE WILL ALLOW ONLY NON-SCHEDULED DATA INPUTTED
3120 !
3130 IF RPTTYPE=1 THEN
3140      IF CONTNAME(ENTTOT,2)="Y" THEN 3400
3150 ENDIF
3160 !
3170 ! THIS ELIMATES DATA NOT IN SPECIFIED TIME PERIOD
3180 !
3190 IF YYY$="N" THEN 3390
3200 !
3210 IF PERIOD(1,2)#PERIOD(2,2) THEN 3280  ! NOT SAME YEAR
3220 !
3230 IF DATES(ENTTOT,3)#PERIOD(1,2) THEN 3400  ! ENTY NOT CORRECT YEAR
3240 IF DATES(ENTTOT,1)<PERIOD(1,1) OR DATES(ENTTOT,1)>PERIOD(2,1) THEN 3400  ! OUTSIDE OF MONTH RANGE WITHIN A SPECIFIED YEAR
3250 !
3260 GO TO 3390
3270 !
3280 !  RANGE CHECK WHEN TIME FRAME OVERLAPS YEARS (IE. 87-88)
3290!
3300 !  CHECK IF YEAR WITHIN RANGE
3310 IF DATES(ENTTOT,3)<PERIOD(1,2) OR DATES(ENTTOT,3)>PERIOD(2,2) THEN 3390
3320 !
3330 ! CHECK TO SEE IF MONTH OUTSIDE RANGE BY COMPARING LOW MONTH/LOW YEAR
3340 ! AND HIGH MONTH/HIGH YEAR
3350 !
3360 IF DATES(ENTTOT,3)=PERIOD(1,2) AND DATES(ENTTOT,1)<PERIOD(1,1) THEN 3400  ! OUT OF RANGE ON LOW END
3370 IF DATES(ENTTOT,3)=PERIOD(2,2) AND DATES(ENTTOT,1)>PERIOD(2,1) THEN 3400  ! OUT OF RANGE ON HIGH END
3380 !
3390      ENTTOT=ENTTOT+1
3400 GO TO 3090
3410 !
3420 ENTTOT=ENTTOT-1  ! TOTAL ENTRIES IN DATABASE
3430 !
3440 ! ASSIGN DATES FOR REPORT HEADER IF NO SPECIFIED TIME PERIOD
3450 !
3460 IF YYY$="N" THEN
3470      PERIOD(1,1)=DATES(1,1)
3480      PERIOD(1,2)=DATES(1,3)
3490      PERIOD(2,1)=DATES(ENTTOT,1)
3500      PERIOD(2,2)=DATES(ENTTOT,3)
3510 ENDIF
3520 !
3530 !  INPUT NON-SCHEDULED SUPMOVE DATABASE
3540 !
3550 NONENTTOT=1
3560 LOCATE 1 ON 3
3570 ON ENDFILE(3) GO TO 3610
3580      INPUT FROM 3 IN FORM NOSCHEDDAT:  NOSCHEDADD(NONENTTOT,1:5),NOSCHEDINFO(NONENTTOT)
3590      NONENTTOT=NONENTTOT+1
3600 GO TO 3570
3610 NONENTTOT=NONENTTOT-1  ! TOTAL NONSCHED ADDRESSES
3620 !
3630 RETURN
4000 !
4010 !  ***********************************************
4020 !  *** INPUT NON-SCHEDULED SUPMOVE INFORMATION ***
4030 !  ***********************************************
4040 !
4050 CONTNAME(1,2)="N"
4060 COUNTER=1
4070 PRINT "TYPE INFO ON LINE NO LONGER THAT THIS"
4080 PRINT
4090 PRINT IN FORM "50('-') /"
4100 INPUT NOSCHEDINFO(COUNTER)
4110 NOSCHEDADD(COUNTER,1)=DATES(1,4)
4120 NOSCHEDADD(COUNTER,2)=DATES(1,3)
4130 NOSCHEDADD(COUNTER,3:5)=TSLEEP(1,1:3)
4140 IIM "MORE (Y OR N)? #": YY$
4150 IF YY$#"Y" AND YY$#"N" THEN 4140
4160 IF YY$="N" THEN RETURN
4170 COUNTER=COUNTER+1
4180 GO TO 4100
5000 !
5010 !
5020 !  ********************************
5030 !  *** OUTPUT SUPMOVE DATA FILE ***
5040 !  ********************************
5050 !
5060 LOCATE 1 ON 1
5070 !
5080 FOR X=1 TO ENTTOT
5090      PRINT ON 1 IN FORM DATAIO: CONTNAME(X,1:2), DATES(X,1:4), SUPNO(X), NODES(X), TSLEEP(X,1:3), ASTAT(X,1:3), LSTMIN(X,1:2)
5100 NEXT X
5110 !
5120 RETURN
6000 !  **********************************
6010 !  *** SORTS DATA FILE SUPMOV.DAT ***
6020 !  **********************************
6030 !
6040 STRING CONTTMP(2)
6045 INTEGER NUMDATATMP(34)
6047 REAL NUMREALTMP(2)
6050 !
6060 FOR A=2 TO ENTTOT
6070      IF DATES(A-1,3)<DATES(A,3) THEN 6350  ! COMPARES YEAR
6080      IF DATES(A-1,3)>DATES(A,3) THEN GOSUB 6380
6090 !
6100      IF DATES(A-1,3)=DATES(A,3) THEN  ! COMPARES MONTH
6110           IF DATES(A-1,1)<DATES(A,1) THEN 6350
6120           IF DATES(A-1,1)>DATES(A,1) THEN GOSUB 6380
6130 !
6140           IF DATES(A-1,1)=DATES(A,1) THEN  ! COMPARES DAYS
6150                IF DATES(A-1,2)<DATES(A,2) THEN 6350
6160                IF DATES(A-1,2)>DATES(A,2) THEN GOSUB 6380
6170 !
6180                IF DATES(A-1,2)=DATES(A,2) THEN  !COMPARES SUPMOV HOUR
6190                     IF TSLEEP(A-1,1)<TSLEEP(A,1) THEN 6350
6200                     IF TSLEEP(A-1,1)>TSLEEP(A,1) THEN GOSUB 6380
6210 !
6220                     IF TSLEEP(A-1,1)=TSLEEP(A,1) THEN  ! COMPARES SUPMOVE MINUTE
6230                          IF TSLEEP(A-1,2)<TSLEEP(A,2) THEN 6350
6240                          IF TSLEEP(A-1,2)>TSLEEP(A,2) THEN GOSUB 6380
6250 !
6260                          IF TSLEEP(A-1,2)=TSLEEP(A,2) THEN  ! COMPARES SECONDS
6270                               IF TSLEEP(A-1,3)<TSLEEP(A,3) THEN 6350
6280                               IF TSLEEP(A-1,3)>TSLEEP(A,3) THEN GOSUB 6380
6290                          ENDIF
6300                     ENDIF
6310                ENDIF
6320           ENDIF
6330      ENDIF
6340 !
6350 NEXT A
6355 !
6360 IF SRTSW=1 THEN
6365      SRTSW=0  ! RESET SORT SWITCH
6370      GO TO 6050
6371 ENDIF
6372 !
6373 LOCATE 1 ON 1
6374 FOR A=1 TO ENTTOT
6375      PRINT ON 1 IN FORM DATAIO: CONTNAME(A,1:2), DATES(A,1:4), SUPNO(A), NODES(A), TSLEEP(A,1:3), ASTAT(A,1:3), LSTMIN(A,1:2)
6376 NEXT A
6377 GO TO 895
6380 !
6390 CONTTMP(1:2)=CONTNAME(A,1:2)
6400 NUMDATATMP(1)=SUPNO(A)
6410 NUMDATATMP(9)=NODES(A)
6420 NUMDATATMP(11:14)=DATES(A,1:4)
6430 NUMDATATMP(15:17)=TSLEEP(A,1:3)
6480 NUMDATATMP(29:31)=ASTAT(A,1:3)
6485 NUMREALTMP(1:2)=LSTMIN(A,1:2)
6490 !
6500 CONTNAME(A,1:2)=CONTNAME(A-1,1:2)
6510 SUPNO(A)=SUPNO(A-1)
6520 NODES(A)=NODES(A-1)
6530 DATES(A,1:4)=DATES(A-1,1:4)
6540 TSLEEP(A,1:3)=TSLEEP(A-1,1:3)
6590 ASTAT(A,1:3)=ASTAT(A-1,1:3)
6595 LSTMIN(A,1:2)=LSTMIN(A-1,1:2)
6600 !
6610 CONTNAME(A-1,1:2)=CONTTMP(1:2)
6620 SUPNO(A-1)=NUMDATATMP(1)
6630 NODES(A-1)=NUMDATATMP(9)
6640 DATES(A-1,1:4)=NUMDATATMP(11:14)
6650 TSLEEP(A-1,1:3)=NUMDATATMP(15:17)
6700 ASTAT(A-1,1:3)=NUMDATATMP(29:31)
6705 LSTMIN(A-1,1:2)=NUMREALTMP(1:2)
6710 !
6715 SRTSW=1
6720 RETURN
6730 !
11000 !
11010 !  *******************************************
11020 !  *** GENERATE UNSCHEDULED SUPMOVE REPORT ***
11030 !  *******************************************
11040 !
11050 LINECNT=30
11055 SW=0
11060 !
11070 FOR A=1 TO ENTTOT
11080      IF LINECNT>29 THEN
11090           PRINT ON 6 IN FORM  NSCHRPT1:  PERIOD(1:2,1:2)
11100           PRINT ON 6 IN FORM  NSCHRPT2
11110           LINECNT=6
11120      ENDIF
11130 !
11140      PRINT ON 6 IN FORM  NSCHRPTOP1: DATES(A,2), DATES(A,1), DATES(A,4), SUPNO(A), NODES(A)
11160      LINECNT=LINECNT+2
11170 !
11180      FOR Z=1 TO NONENTTOT
11190           IF DATES(A,4)#NOSCHEDADD(Z,1) THEN 11270
11200           IF DATES(A,3)#NOSCHEDADD(Z,2) THEN 11270
11210           IF TSLEEP(A,1)#NOSCHEDADD(Z,3) THEN 11270
11220           IF TSLEEP(A,2)#NOSCHEDADD(Z,4) THEN 11270
11230           IF TSLEEP(A,3)=NOSCHEDADD(Z,5) THEN
11235                IF SW=1 THEN PRINT ON 6 IN FORM "29B"
11240                PRINT ON 6 IN FORM NOSCHEDOP:  NOSCHEDINFO(Z)
11250                LINECNT=LINECNT+1
11255                SW=1
11260           ENDIF
11270      NEXT Z
11275      SW=0
11276      PRINT ON 6 IN FORM "/"
11277      LINECNT=LINECNT+1
11280 NEXT A
11290 !
11300 RETURN
14000 !
14010 !  *************************************
14020 !  *** CREATE ASTAT REPORT FOR NETQC ***
14030 !  *************************************
14040 LINECNT=66
14050 FOR A=1 TO ENTTOT
14060      IF LINECNT>47 OR DATES(A,3)#MTHCHG(2) OR DATES(A,1)#MTHCHG(1) THEN
14070           MTHCHG(1)=DATES(A,1)  ! SET NEW MONTH CHECK
14080           MTHCHG(2)=DATES(A,3)  ! SET NEW YEAR CHECK
14090 !
14100      FOR XX=LINECNT TO 66  ! FILLS REST OF PAGE WITH BLANK LINES
14110           PRINT ON 5 IN FORM "/"
14120      NEXT XX
14130 !
14140 ! FOLLOWING LINE GET ALPHABETIC EQAUL OF NUMERIC MONTH
14150 !
14160           ALPMM=ALPMTH(DATES(A,1))  ! CONVERTS NUMBER MTH TO ALPHABETIC MTH
14180           PRINT ON 5 IN FORM ASTATTITLE+ASTATTITLE2+ASTATTITLE3: ALPMM,DATES(A,3)
14190           LINECNT=6
14200      ENDIF
14210 !
14220      PRINT ON 5 IN FORM ASTATOP: DATES(A,2), DATES(A,1),  DATES(A,4), SUPNO(A), ASTAT(A,1:3), LSTMIN(A,1:2)
14230      LINECNT=LINECNT+1
14240 !
14250 NEXT A
14260 !
14270 RETURN
 