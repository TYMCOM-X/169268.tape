!            PROGRAM:       LNKRPT.DMC
!            AUTHOR         Rob Nielsen
!            DATE:          September 24, 1986
!            PURPOSE:       To produce reports of Deluti link statistics for
!                 the trend analysis project. The reports will get their
!                 data from the data base (MSNTRACK)LNKSUM.DMS and, at this
!                 initial writing will produce 3 reports:

!             1. (MSNTRACK)LNKRPT.1, a report of link counts for various
!                    Utilization ranges.
!             2. (MSNTRACK)LNKRPT.2, a report of percentile points for the
!                    Bandwidth utilization distribution.

!             DESCRIPTION:   To facilitate additional reports in the future
!                 the data base data is read into arrays
!                 at the start of the program (This way new attribute names
!                 only have to be added to 1 subroutine). 

!                 The algorithms for each report writer are very simple:

!                      Write Heading
!                      Write Title for Table 1
!                      Write Table 1 data
!                      Write Title for Table 2, if needed
!                      Write Table 2 data, if needed
!                      Write Footer
!             NOTATION:
!                 ALL CAPS: File names, attribute names, & DPL reserved words
!                 Mixed:    Variables, labels, and subroutines.


!            ******************  MAIN PROGRAM **********************

             OPEN (MSNTRACK)LNKSUM.

             DPL START.
             DBSET LNKSUM.
             LET SYSCASE EQ 1.

!                 Note:  Arrays for link speeds and ranges of utilization 
!            values are both dimensioned at 10. The actual count is set 
!            by the programmer at the start of the main program.

             DEFINE INTEGER Blanks UtlzPrcent(10,7) MeanUtlzn(10) I MaxUrange &
                            Dait(2) Speeds Lynks(10).
             DEFINE REAL UtlzRange(10,10,2) TotPercent(10).
             DEFINE TEXT 7 LinkSpeed(10).
             DEFINE TEXT 5 URange(10).
             DEFINE TEXT 3 Percents (7).


!                 Set the No of link speeds and utilization ranges to report
             LET MaxUrange EQ 5.
             LET Speeds EQ 7.

!                 To report periods other than the most recen
!            period, change the assignments to Mindate and Maxdate below

             DBSET LNKSUM.
             FIND ALL.
             EVALUATE Dait(1) MAX MINDATE.
                 SEARCH MINDATE GE Dait(1).
             EVALUATE Dait(2) MAX MAXDATE.

Reports:     CALL ReadData.
             CALL Report1.
             CALL Report2.
             CALL Report3.

             GOTO TheEnd.



!            ***************  SUBROUTINE READDATA *******************

!                 This subroutine will read all report data from the 1022
!            data base Nodsum.DMS into arrays and will also set arrays of
!            ranges, range values, and link speeds.

ReadData:    DBSET LNKSUM.

!            Assign all link speeds
                  LET LinkSpeed(1) EQ "ALL LN ".
                  LET LinkSpeed(2) EQ "9.6 KB ".
                  LET LinkSpeed(3) EQ "14.4 KB".
                  LET LinkSpeed(4) EQ "19.2 KB".
                  LET LinkSpeed(5) EQ "28.8 KB".
                  LET LinkSpeed(6) EQ "38.4 KB".
                  LET LinkSpeed(7) EQ " 56  KB".

             LET I EQ 1.
             LET J EQ 1.

             FIND MINDATE GE Dait(1) AND MAXDATE LE Dait(2).
NextRecord:  GETREC LastRecord.
                  IF SPEED CT "ALL" GOTO All.
                  IF SPEED CT "9.6" GOTO Speed96.
                  IF SPEED CT "14.4" GOTO Speed144.
                  IF SPEED CT "19.2" GOTO Speed192.
                  IF SPEED CT "28.8" GOTO Speed288.
                  IF SPEED CT "38.4" GOTO Speed384.
                  IF SPEED CT "56" GOTO Speed56.

LastRecord:  GOTO EndReadata.



All:         LET I EQ 1.
             CALL AssignData.
             GOTO NextRecord.

Speed96:     LET I EQ 2.
             CALL AssignData.
             GOTO NextRecord.

Speed144:    LET I EQ 3.
             CALL AssignData.
             GOTO NextRecord.

Speed192:    LET I EQ 4.
             CALL AssignData.
             GOTO NextRecord.

Speed288:    LET I EQ 5.
             CALL AssignData.
             GOTO NextRecord.

Speed384:    LET I EQ 6.
             CALL AssignData.
             GOTO NextRecord.

Speed56:     LET I EQ 7.
             CALL AssignData.
             GOTO NextRecord.

EndReadata:  RETURN.


!            **************  SUBROUTINE ASSIGNDATA ******************

AssignData:  LET Percents(1) EQ "Min".
             LET Percents(2) EQ "10%".
             LET Percents(3) EQ "25%".
             LET Percents(4) EQ "50%".
             LET Percents(5) EQ "75%".
             LET Percents(6) EQ "90%".
             LET Percents(7) EQ "Max".

             LET URange(1) EQ " 0-20".
             LET URange(2) EQ "21-40".
             LET URange(3) EQ "41-60".
             LET URange(4) EQ "61-80".
             LET URange(5) EQ "81-99".

             LET UtlzPrcent(I,1) EQ UTILMIN.
             LET UtlzPrcent(I,2) EQ UTIL10PCT.
             LET UtlzPrcent(I,3) EQ UTIL25PCT.
             LET UtlzPrcent(I,4) EQ UTIL50PCT.
             LET UtlzPrcent(I,5) EQ UTIL75PCT.
             LET UtlzPrcent(I,6) EQ UTIL90PCT.
             LET UtlzPrcent(I,7) EQ UTILMAX.

             LET UtlzRange(I,1,1) EQ REAL(UTIL20).
             LET UtlzRange(I,1,2) EQ REAL(UTIL20) / REAL(LINKS).
             LET UtlzRange(I,2,1) EQ REAL(UTIL40).
             LET UtlzRange(I,2,2) EQ REAL(UTIL40) / REAL(LINKS).
             LET UtlzRange(I,3,1) EQ REAL(UTIL60).
             LET UtlzRange(I,3,2) EQ REAL(UTIL60) / REAL(LINKS).
             LET UtlzRange(I,4,1) EQ REAL(UTIL80).
             LET UtlzRange(I,4,2) EQ REAL(UTIL80) / REAL(LINKS).
             LET UtlzRange(I,5,1) EQ REAL(UTIL100).
             LET UtlzRange(I,5,2) EQ REAL(UTIL100) / REAL(LINKS).

             LET K EQ 1.
NextK:           LET TotPercent(I) EQ (TotPercent(I) + UtlzRange(I,K,2)).
                 LET K EQ (K+1).
             IF K LE Speeds GOTO NextK.
             LET Lynks(I) EQ LINKS.
             LET MeanUtlzn(I) EQ UTILMEAN.

             RETURN.


!            *****************************************************
!            *******                                       *******
!            *******         SUBROUTINE REPORT1            *******
!            *******                                       *******
!            *****************************************************


Report1:     INIT 1 (MSNTRACK)LNKRPT.1.

!            *******  Print Heading for the Report  *********

!            Lines 1 and 2 of Header
                  PRINT ON 1 FORMAT // END.

!            Lines 3 and 4 of Header
                  LET Blanks EQ 24.
                  CALL PrntBlanks.
                  PRINT ON 1 FORMAT "L I N K    S U M M A R Y" / END.

!            Lines 5 and 6-8 of Header
                  LET Blanks EQ 16.
                  CALL PrntBlanks.
                  PRINT ON 1 &
                  FORMAT "Samples Taken Between: " $ END.
                  PRINT ON 1 Dait(1) FORMAT I6  " AND "  $ END.
                  PRINT ON 1 Dait(2) FORMAT I6 / END.
                  LET Blanks EQ 12.
                  CALL PrntBlanks.
                  PRINT ON 1 &
                  FORMAT "Source:  Delay and Utilization Program (Deluti)" END.
                  PRINT ON 1 FORMAT /// END.



!            ******** Print Heading for Utilization table ***********

!            Lines 1 and 2 of header
                  LET Blanks EQ 25.
                  CALL PrntBlanks.
                  PRINT ON 1 FORMAT "BANDWIDTH UTILIZATION" / END.

!            Lines 3 and 4 of table header
                  PRINT ON 1 FORMAT " RANGE " 1X END.
                  PRINT ON 1 FORMAT "%BW.UT " 1X $ END.
                  LET J EQ 1.
NextLin11:             PRINT ON 1 LinkSpeed(J) FORMAT 3X A7 $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin11.
                  PRINT ON 1 FORMAT END.

!            Line 5 of table header
                  PRINT ON 1 FORMAT "MX(I,O)" 1X $ END.
                  LET J EQ 1.
NextLin12:             PRINT ON 1 FORMAT 3X " No   %"  $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin12.
                  PRINT ON 1 FORMAT END.

!            Lines 6 through 8 of table header 
                  PRINT ON 1 FORMAT "-------" 2X $ END.
                  LET J EQ 1.
NextLin13:             PRINT ON 1 FORMAT 2X "--- ----"  $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin13.
                  PRINT ON 1 FORMAT END.




!             ************* Print Body of Table ********************

             LET I EQ 1.
             LET J EQ 1.
NextRow11:        PRINT ON 1 URange(I) FORMAT /, 1X A5 3X $ END.
NextCol11:        PRINT ON 1 UtlzRange(J,I,1) (UtlzRange(J,I,2) * 100.0) &
                  FORMAT 2X F3 1X F3 "%" $ END.
                  LET J EQ (J+1).
             IF J LE Speeds GOTO NextCol11.
                  PRINT ON 1 FORMAT END.
                  LET I EQ (I+1).
                  LET J EQ 1.
             IF I LE MaxUrange GOTO NextRow11.

!            Print Class Means and Totals

                  PRINT ON 1 FORMAT "-------" 2X $ END.
                  LET J EQ 1.
NextLin17:             PRINT ON 1 FORMAT 2X "--- ----" $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin17.
                  PRINT ON 1 FORMAT END.

             LET J EQ 1.
                  PRINT ON 1 FORMAT "Totals:" 2X $ END.
NextCol12:        PRINT ON 1 Lynks(J) (TotPercent(J) * 100) &
                  FORMAT 1X I4 1X I3 "%" $ END.
                  LET J EQ (J+1).
             IF J LE Speeds GOTO NextCol12.
                  PRINT ON 1 FORMAT / END.

             LET J EQ 1.
                  PRINT ON 1 FORMAT "Class Mean:" $ END.
NextCol13:        PRINT ON 1 MeanUtlzn(J) FORMAT 4X I4 2X $ END.
                  LET J EQ (J+1).
             IF J LE (Speeds-1) GOTO NextCol13.
                  PRINT ON 1 MeanUtlzn(J) FORMAT 4X I4 $ END.
                  PRINT ON 1 FORMAT /// END.




!            *****  Print Report Footer ***********

             PRINT ON 1 &
             FORMAT / "KEY:  No--Number of Links in Given Range" END.
             PRINT ON 1 &
             FORMAT   "      % --Per Cent of Links in Given Range" END.

             PRINT ON 1 FORMAT /, "NOTES:  "  &
"1)Utilization data comes from daily XRAY observations. Each link is" END.
             PRINT ON 1 FORMAT &
"  represented by its maximum observed utilization for the study period." END.
             PRINT ON 1 FORMAT /, 8X, &
"2)ALL LN total includes bandwidths besides those shown in table." END.

!            ****** End of Subroutine ******

             RELEASE 1.
             RETURN.


!            *****************************************************
!            *******                                       *******
!            *******         SUBROUTINE REPORT2            *******
!            *******                                       *******
!            *****************************************************


Report2:     INIT 1 (MSNTRACK)LNKRPT.2.

!            *******  Print Heading for the Report  *********

!            Lines 1 and 2 of Header
                  PRINT ON 1 FORMAT // END.

!            Lines 3 and 4 of Header
                  LET Blanks EQ 24.
                  CALL PrntBlanks.
                  PRINT ON 1 FORMAT "L I N K    S U M M A R Y" / END.

!            Lines 5 and 6-8 of Header
                  LET Blanks EQ 15.
                  CALL PrntBlanks.
                  PRINT ON 1 &
                  FORMAT "SAMPLES TAKEN BETWEEN: " $ END.
                  PRINT ON 1 Dait(1) FORMAT I6  " AND "  $ END.
                  PRINT ON 1 Dait(2) FORMAT I6 /// END.



!            ******** Print Heading for Utilization table ***********

!            Lines 1 and 2 of header
                  LET Blanks EQ 24.
                  CALL PrntBlanks.
                  PRINT ON 1 FORMAT "BANDWIDTH UTILIZATION" / END.

!            Lines 3 and 4 of table header
                  PRINT ON 1 FORMAT "Percentile " 1X END.
                  PRINT ON 1 FORMAT " Point for " 1X $ END.
                  LET J EQ 1.
NextLin21:             PRINT ON 1 LinkSpeed(J) FORMAT 2X A7 $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin21.
                  PRINT ON 1 FORMAT END.

!            Line 5 of table header
                  PRINT ON 1 FORMAT "Utilization" 1X $ END.
                  LET J EQ 1.
NextLin22:             PRINT ON 1 FORMAT 2X "Utilztn"  $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin22.
                  PRINT ON 1 FORMAT END.

!            Lines 6 through 8 of table header 
                  PRINT ON 1 FORMAT "-----------" 1X $ END.
                  LET J EQ 1.                   
NextLin23:             PRINT ON 1 FORMAT 2X "-------"  $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin23.
                  PRINT ON 1 FORMAT END.




!             ************* Print Body of Table ********************

             LET I EQ 1.
             LET J EQ 1.
NextRow21:        PRINT ON 1 Percents(I) FORMAT /, 5X A3 1X " :" $ END.
NextCol21:        PRINT ON 1 UtlzPrcent(J,I)  &
                  FORMAT 2X 3X 1X F3 $ END.
                  LET J EQ (J+1).
             IF J LE Speeds GOTO NextCol21.
                  PRINT ON 1 FORMAT END.
                  LET I EQ (I+1).
                  LET J EQ 1.
             IF I LE 7 GOTO NextRow21.

!            Print Class Means and Totals

                  PRINT ON 1 FORMAT "-----------" 1X $ END.
                  LET J EQ 1.
NextLin27:             PRINT ON 1 FORMAT 2X "-------" $ END.
                       LET J EQ (J+1).
                  IF J LE Speeds GOTO NextLin27.
                  PRINT ON 1 FORMAT END.


             LET J EQ 1.
                  PRINT ON 1 FORMAT "Mean Utlztn:" $ END.
NextCol23:        PRINT ON 1 MeanUtlzn(J) FORMAT 5X I3 1X $ END.
                  LET J EQ (J+1).
             IF J LE Speeds GOTO NextCol23.
                  PRINT ON 1 FORMAT / END.

             LET J EQ 1.
                  PRINT ON 1 FORMAT "Totals:    " $ END.
NextCol22:        PRINT ON 1 Lynks(J) FORMAT 6X I3 $ END.
                  LET J EQ (J+1).
             IF J LE Speeds GOTO NextCol22.
                  PRINT ON 1 FORMAT /// END.

!            *****  Print Report Footer ***********

             PRINT ON 1 FORMAT ////, 2X,  &
"NOTE:  Utilization data comes from daily XRAY observations. Each link is", &
"  represented by its maximum observed utilization for the study period." END.



!            ****** End of Subroutine ******

             RELEASE 1.
             RETURN.

!            **************  SUBROUTINE REPORT3 ******************

Report3:     RETURN.

!            **************  SUBROUTINE PRNTBLANKS ******************

PrntBlanks:  LET K EQ 1.
NextBlank:      PRINT ON 1 FORMAT " " $ END.
                LET K EQ (K+1).
             IF K LE Blanks GOTO NextBlank.

             RETURN.

!            **************  END OF PROGRAM ******************

Theend:      DPL STOP.
             DPL END.
 