!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! 
! PROGRAM:	(NTRACKDATA:28)DLSUM.DPL
! PROJECT:	Trend Analysis - Delay/ALU 
! AUTHOR:	Jason L. Ma
! DATE:		September 30, 1986
! 
! INPUT FILES:	1.  DLAY.DMS, containing Delay output in a database.  
!
! OUTPUT FILES:	1.  DLSUM.RPT, the Delay/ALU report.
!		2.  DLSUM.DMI, to be appended to DLSUM.DMS after this
!	    	    program.
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


	O (NTRACKDATA)DLAY.
	INIT 1 DLSUM.RPT.
	INIT 2 DLSUM.DMI.
	DPL START.
	DEFINE INTEGER CUMCNT DAT TCOUNT DLOWER DUPPER HLOWER HUPPER HPS.
	DEFINE REAL DAVE DMED HAVE HMED CUMPER DLWM DHWM HLWM HHWM.
	DEFINE REAL TDAVE TDMED TDLWM TDHWM THAVE THMED THLWM THHWM.


!!!!!!!!!!
!!! Assign all-circuits values.
!!!!!!!!!!

	F ALL.
	IF SYSNREC EQ 0 THEN END.
	LET TCOUNT EQ SYSNREC.
	EV TDAVE EQ MEAN DELAY TDLWM EQ MIN DELAY TDHWM EQ MAX DELAY &
		THAVE EQ MEAN HOPS THLWM EQ MIN HOPS THHWM EQ MAX HOPS &
                DAT EQ MAX CDATE.
	F LAST.
	SOR DELAY.
	GETR END SYSNREC/2.
	LET TDMED EQ DELAY.
	F LAST.
	SOR HOPS.
	GETR END SYSNREC/2.
	LET THMED EQ HOPS.

	

!!!!!!!!!!
!!! Process each Delay class individually.
!!!!!!!!!!

	P ON 1 DAT FORMAT / 28X 'DELAY/ALU PERFORMANCE OF' / &
	26X 'TOP 40 ORIG-NODES BY SESSIONS' // 34X 'DATE:  ' I6 // END.

	P ON 1 FORMAT &
'                           NUMBER         CUMULATIVE %          MEAN' / &
'     R-T DELAY [ms]     OF CIRCUITS     OF ALL CIRCUITS     HOPS/CIRCUIT' / &
'     --------------     -----------     ---------------     ------------' &
END.

	LET CUMCNT EQ 0.
	LET DLOWER EQ 0 DUPPER EQ 100.
	CALL DCRANK.
	LET DLOWER EQ 101 DUPPER EQ 200.
	CALL DCRANK.

DLOOP:	IF DLOWER GE 901 GOTO D1001.
	LET DLOWER EQ DLOWER + 100 DUPPER EQ DUPPER + 100.
	CALL DCRANK.
	GOTO DLOOP.
D1001:	LET DLOWER EQ 1001 DUPPER EQ 1500.
	CALL DCRANK.
	LET DLOWER EQ 1501 DUPPER EQ 2000.
	CALL DCRANK.
	LET DLOWER EQ 9999 DUPPER EQ 9999.
	CALL DCRAN2.
	GOTO HSTART.

DCRANK:	F DELAY GE DLOWER AND LE DUPPER.
	LET CUMCNT EQ (CUMCNT+SYSNREC).
	LET CUMPER EQ 100.*CUMCNT/TCOUNT.
	EV HAVE EQ MEAN HOPS.
	P ON 1 DLOWER DUPPER SYSNREC CUMPER HAVE FORMAT &
	6X I4 ' - ' I4 11X I5 14X F3.0 15X F2.1 END.
	P ON 2 DAT DLOWER DUPPER HAVE SYSNREC FORMAT I6 1X I4 1X I4 &
	1X F2.1 1X I5 END.
	RETURN.

DCRAN2:	F DELAY GE 2001.
	LET CUMCNT EQ (CUMCNT+SYSNREC).
	LET CUMPER EQ 100.*CUMCNT/TCOUNT.
	EV HAVE EQ MEAN HOPS.
	P ON 1 SYSNREC CUMPER HAVE FORMAT &
	6X ' Over 2000 ' 11X I5 14X F3.0 15X F2.1 END.
	P ON 2 DAT DLOWER DUPPER HAVE SYSNREC FORMAT I6 1X I4 1X I4 &
	1X F2.1 1X I5 END.
	RETURN.


!!!!!!!!!!
!!! Process each Hop class individually.
!!!!!!!!!!

HSTART:	P ON 1 TCOUNT FORMAT / 20X 'Total: ' I4 END.
	P ON 1 FORMAT // &
'                           NUMBER         CUMULATIVE %          MEAN' / &
'      HOPS/CIRCUIT      OF CIRCUITS     OF ALL CIRCUITS    R-T DELAY [ms]' / &
'      ------------      -----------     ---------------    --------------' &
END.

	LET CUMCNT EQ 0.
	LET HPS EQ 0.
	CALL HCRANK.

HLOOP:	IF HPS GE 12 GOTO H99.
	LET HPS EQ HPS + 1.
	CALL HCRANK.
	GOTO HLOOP.
H99:	LET HPS EQ 99.
	CALL HCRAN2.
	GOTO FINAL.
	
HCRANK:	F HOPS EQ HPS.
	LET CUMCNT EQ (CUMCNT+SYSNREC).
	LET CUMPER EQ 100.*CUMCNT/TCOUNT.
	EV DAVE EQ MEAN DELAY.
	P ON 1 HPS SYSNREC CUMPER DAVE FORMAT &
	11X I2 15X I5 14X F3.0 13X F5.0 END.
	P ON 2 DAT SYSNREC HPS DAVE FORMAT I6 16X I5 1X I2 1X F5.0 END.
	RETURN.

HCRAN2:	F HOPS GE 13.
	LET CUMCNT EQ (CUMCNT+SYSNREC).
	LET CUMPER EQ 100.*CUMCNT/TCOUNT.
	EV DAVE EQ MEAN DELAY.
	P ON 1 SYSNREC CUMPER DAVE FORMAT &
	8X 'Over 12' 13X I5 14X F3.0 13X F5.0 END.
	P ON 2 DAT SYSNREC HPS DAVE FORMAT I6 16X I5 1X I2 1X F5.0 END.
	RETURN.


FINAL:	P ON 1 TCOUNT FORMAT / 20X 'Total: ' I5 END.
	P ON 1 TDAVE THAVE TDMED THMED TDLWM THLWM TDHWM THHWM FORMAT // &
	26X '       ALL CIRCUITS         ' / &
	26X 'R-T DELAY       HOPS/CIRCUIT' / &
	26X '---------       ------------' / &
	18X 'MEAN    ' F5.0 ' ms' 11X F2.1 / &
	18X 'MEDIAN  ' F5.0 ' ms' 11X F2.1 // &
	18X 'LOW     ' F5.0 ' ms' 11X F2.1 / &
	18X 'HIGH    ' F5.0 ' ms' 11X F2.1 // END.

	P ON 2 DAT THAVE TCOUNT TDAVE TDMED THMED TDLWM THLWM TDHWM &
	THHWM FORMAT I6 1X '9999' 1X '9999' 1X F2.1 1X I3 1X '99' 1X &
	F5.0 3(1X F5.0 1X F2.1) END.


END:	RELEASE 1.
	RELEASE 2.
	DPL STOP.
	DPL END.
